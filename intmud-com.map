[com _comum]
# Contém os comandos que os jogadores podem digitar
#
# Cada classe:
#
indiceobj comando # Contém o nome do comando
const nivel = 0 # Nível mínimo para usar o comando
const posic = 8 # Posição mínima para usar o comando; vide [comum_perso]
func iniclasse # Cria um objeto do comando
 criar(arg0)
# func ini # Acerta a variável comando
#  comando = "com_abc" # É sempre com_ seguido do nome do comando
func exec # Executa o comando
# arg0=personagem
# arg1=o que escreveu
  arg0.msg("Você digitou " + arg1)

[com encerrar]
herda com__comum
func ini
  comando = "com_encerrar"
func exec
  msg("Encerrando o MUD")
  terminar

[com falar]
herda com__comum
const posic = 5
func ini
  comando = "com_falar"
func exec
  listaitem l
  txt300 texto # O que o usuário digitou
  txt20 ini
  se sala.semsom
    arg0.msg("Você tenta falar alguma coisa mas não sai nenhum som")
    ret
  fimse
  texto = txttroca(arg1, "'", "")
  ini = txt(texto, inttotal(texto)-1)
  se ini = ""
    arg0.msg("Falar o quê?")
    ret
  senão ini = "?"
    ini = " pergunta '"
  senão ini = "!"
    ini = " exclama '"
  senão
    ini = " fala '"
  fimse
  l = arg0.sala.dentro.ini
  enquanto l
    se l.obj = arg0
      l.obj.msg("Você" + ini + texto + "'")
    senão l.obj.invisver >= arg0.invis
      l.obj.msg(arg0.nome + ini + texto + "'")
    senão
      l.obj.msg("Alguém" + ini + texto + "'")
    fimse
    l.depois
  efim

[com fim]
herda com__comum
indiceobj comando2
func ini
  comando = "com_fim"
  comando2 = "com_quit"
func exec
  apagar(arg0)

[com move]
# Movimenta-se em uma direção
const nivel = 0
const posic = 8
indiceobj comando1
indiceobj comando2

func iniclasse
  criar(arg0)

func exec # Executa o comando
# arg0=personagem
# arg1=o que escreveu
  ref destino
  destino = arg0.sala.[dir]
  se destino = nulo
    arg0.msg("Impossível seguir nessa direção")
  senão arg0.sala.[dir]_agora >= 2
    arg0.msg("A porta está trancada")
  senão destino = arg0.sala
    arg0.msg("Você tenta, mas acaba voltando no mesmo lugar")
  senão
    $mens.p(arg0)
    $mens.todos(m_sair)
    arg0.msg(m_perso)
    arg0.mudasala(destino)
    arg0.descrsala(arg0.sala)
    $mens.todos(m_chegar)

[com move_norte]
herda com_move
const dir = "norte"
const m_perso = "norte"
const m_sair = "$p foi para o norte"
const m_chegar = "$p chegou do sul"
func ini
  comando1 = "com_n"
  comando2 = "com_norte"

[com move_sul]
herda com_move
const dir = "sul"
const m_perso = "sul"
const m_sair = "$p foi para o sul"
const m_chegar = "$p chegou do norte"
func ini
  comando1 = "com_s"
  comando2 = "com_sul"

[com move_leste]
herda com_move
indiceobj comando3
const dir = "leste"
const m_perso = "leste"
const m_sair = "$p foi para o leste"
const m_chegar = "$p chegou do oeste"
func ini
  comando1 = "com_l"
  comando2 = "com_leste"
  comando3 = "com_e"

[com move_oeste]
herda com_move
indiceobj comando3
const dir = "oeste"
const m_perso = "oeste"
const m_sair = "$p foi para o oeste"
const m_chegar = "$p chegou do leste"
func ini
  comando1 = "com_o"
  comando2 = "com_oeste"
  comando3 = "com_w"

[com move_cima]
herda com_move
const dir = "cima"
const m_perso = "cima"
const m_sair = "$p subiu"
const m_chegar = "$p chegou de baixo"
func ini
  comando1 = "com_c"
  comando2 = "com_cima"

[com move_baixo]
herda com_move
const dir = "baixo"
const m_perso = "baixo"
const m_sair = "$p desceu"
const m_chegar = "$p chegou de cima"
func ini
  comando1 = "com_b"
  comando2 = "com_baixo"

[com ver]
herda com__comum
const posic = 5
func ini
  comando = "com_ver"
func exec
  nomeobj nomes
  listaitem l
  se arg1 = ""
    arg0.descrsala(arg0.sala)
    ret
  fimse
  nomes.ini(arg1, 10)
  l = arg0.sala.dentro.ini
  enquanto l
    se l.obj.invisver >= arg0.invis & nomes.nome(l.obj.alias)
      se l.obj = arg0
        arg0.msg("Você tenta olhar para si mesmo.")
      senão
        $mens.p(arg0, l.obj)
        $mens.todos2("$p olha para $a")
        $mens.perso("$p olha para $a")
        $mens.alvo2("$p olha para $a")
        arg0.msg(l.obj.dver)
      fimse
      ret
    fimse
    l.depois
  efim
  arg0.msg("Não há nenhum " + arg1 + " aqui.")
