classe adm_rezou
herda comando_adm
const objcmd = arg1.c_rezou ? este
const txtajuda = "\b\c3Rezou\b\n\
Sintaxe: REZOU <jogador> <texto>\n\
Envia mensagem para o jogador com cópia para todos os administradores.\n\
Digitando apenas Rezou, mostra as últimas mensagens."
textovar var

func escr
  se !arg1
    ret !var.historico, $mens.mp("Nenhuma mensagem Rezou", arg0)
    refvar t = arg0.sock.txtmais
    t.limpar
    t.addfim("\b\c6Últimas mensagens Rezou:\b")
    t.addfim(var.historico)
    arg0.sock.txtmostra
    ret
  senao !txt2(arg1)
    ret arg0.msg("Digite REZOU seguido do nome do personagem jogador e a mensagem")
  fimse
  indiceitem item
  item.ini("pn " + txts(txtnome(txt1(arg1))))
  ret !item.obj, arg0.msg("Jogador inexistente ou ausente: " + txt1(arg1))
  $misclog.rezou("rezou " + arg0.nome + " para " + item.obj.nome + ": " + txt2(arg1))
  $mens.p(arg0, item.obj)
  $mens.mens = txt2(arg1)
  $mens.msg(config:rezou2msg, item.obj)
  epara item.ini("un "), txt1(item.txt) == "un", item.depois
    continuar !$perm_[item.obj.jogperm].c_rezou || item.obj.perso.var.u_[nome]
    $mens.msg(config:rezou1msg, item.obj.perso)
  efim
  refvar m = var.historico
  m = $mens.txt("$R para $c: $m") + (m ? "\n") + txtsublin(m, 0, 29)
