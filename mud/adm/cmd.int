classe adm_cmd
herda comando_adm
const m_ajuda = "-t\n\
-s <comando>\n\
Executa comando na linguagem do IntMUD e mostra o resultado.\n\
O objeto \"este\" é sempre o seu personagem."

func escr
  prog p
  refvar erro = p.classe(este, "func f\n" + arg1)
  se erro != "0" && erro != "1"
    ret arg0.msg("Erro em: " + arg1 + "\n" + txt2(erro))
  senao txtsepara(arg1, "L+DLEOV,DLEOV+L", "\b") != arg1
    cmdlog(arg0, "cmd", arg1)
  fimse
  debug d
  arg0.msg(">" + d.cmd(arg0, "ret (" + arg1 + ")"))


classe adm_cmdj
herda comando_adm
const m_ajuda = "-t\n\
-s <personagem jogador> <comando>\n\
-s <personagem jogador>.<número do animal> <comando>\n\
Executa comando na linguagem do IntMUD e mostra o resultado."

func escr
  se !txt2(arg1)
    ret arg0.msg("Digite CMDJ seguido do nome do personagem jogador e o comando")
  fimse
  refvar nome = txttroca(txt1(arg1), ".", " ")
  indiceitem item
  item.ini("pn " + txts(txtnome(txt1(nome))))
  ref r = item.obj
  ret !r, arg0.msg("Jogador inexistente ou ausente: " + nome)
  se txt2(nome)
    ref r2 = misc:personum(r, txt2(nome))
    $mens.mens = txt2(nome)
    ret !r2, $mens.mp("Personagem $r não tem animal de número $m", arg0, r)
    r = r2
  fimse
  prog p
  refvar erro = p.classe(este, "func f\n" + txt2(arg1))
  se erro != "0" && erro != "1"
    ret arg0.msg("Erro em: " + txt2(arg1) + "\n" + txt2(erro))
  senao txtsepara(txt2(arg1), "L+DLEOV,DLEOV+L", "\b") != txt2(arg1)
    cmdlog(arg0, "cmdj", arg1)
  fimse
  debug d
  arg0.msg(txt(r.descnome2) + ">" + d.cmd(r, "ret " + txt2(arg1)))


classe adm_cmdp
herda comando_adm
const m_ajuda = "-t\n\
-s <personagem da sala> <comando>\n\
-s <personagem da sala>.<número do animal> <comando>\n\
Executa comando na linguagem do IntMUD e mostra o resultado."

func escr
  se !txt2(arg1)
    ret arg0.msg("Digite CMDP seguido do nome do personagem e o comando")
  fimse
  refvar nome = txttroca(txt1(arg1), ".", " ")
  ref r = misc:persodono(arg0, txt1(nome))
  ret !r, arg0.msg("Não há nenhum personagem chamado " + nome)
  se txt2(nome)
    ref r2 = misc:personum(r, txt2(nome))
    $mens.mens = txt2(nome)
    ret !r2, $mens.mp("Personagem $r não tem animal de número $m", arg0, r)
    r = r2
  fimse
  prog p
  refvar erro = p.classe(este, "func f\n" + txt2(arg1))
  se erro != "0" && erro != "1"
    ret arg0.msg("Erro em: " + txt2(arg1) + "\n" + txt2(erro))
  senao txtsepara(txt2(arg1), "L+DLEOV,DLEOV+L", "\b") != txt2(arg1)
    cmdlog(arg0, "cmdp", arg1)
  fimse
  debug d
  arg0.msg(txt(r.descnome2) + ">" + d.cmd(r, "ret " + txt2(arg1)))


classe adm_cmdi
herda comando_adm
const m_ajuda = "-t\n\
-s <item seu ou da sala> <comando>\n\
Executa comando na linguagem do IntMUD e mostra o resultado."

func escr
  se !txt2(arg1)
    ret arg0.msg("Digite CMDI seguido do nome do item e o comando")
  fimse
  refvar nome = txt1(arg1)
  ref r = misc:itemdentro(arg0, nome) ?? misc:itemdono(arg0, nome)
  ret !r, arg0.msg("Não há nenhum item chamado " + nome)
  prog p
  refvar erro = p.classe(este, "func f\n" + txt2(arg1))
  se erro != "0" && erro != "1"
    ret arg0.msg("Erro em: " + txt2(arg1) + "\n" + txt2(erro))
  senao txtsepara(txt2(arg1), "L+DLEOV,DLEOV+L", "\b") != txt2(arg1)
    cmdlog(arg0, "cmdi", arg1)
  fimse
  debug d
  arg0.msg(txt(r.descnome2) + ">" + d.cmd(r, "ret " + txt2(arg1)))
