classe adm_hab
const iniclasse = !$[arg0] && criar(arg0)
const txtajuda = "\b\c3Habilidades e magias\b\n\
  \c2LHAB\b      Lista habilidades existentes\n\
  \c2LMAG\b      Lista magias existentes\n\
  \c2CHAB\b      Cria uma habilidade\n\
  \c2CMAG\b      Cria uma magia\n\
  \c2EHAB\b      Edita uma habilidade\n\
  \c2EMAG\b      Edita uma magia\n\
  \c2AHAB\b      Apaga uma ou mais habilidades\n\
  \c2AMAG\b      Apaga uma ou mais magias\n\
  \c2DHAB\b      Calcula danos causados por uma habilidade\n\
  \c2DMAG\b      Calcula danos causados por uma magia"


classe adm_lhab
herda comando_adm
const txtajuda = "\b\c3Lhab\b\n\
Sintaxe: LHAB [nome]\n\
Lista as habilidades que começam com o nome especificado.\n\
Se o nome for omitido, lista todas as habilidades."

func escr
  prog p
  se !p.iniclasse("cmd_" + arg1)
    ret arg0.msg("\bNenhuma habilidade encontrada.")
  fimse
  textotxt t
  txt100 lin
  epara nulo, p.lin && t.linhas < 499, p.depois
    continuar !$[p.texto].cmdmenu # Continua se não é uma habilidade
    lin = txtcopiamai(txt2(txte(p.texto)), "A") + ", "
    se $[p.texto].tipoalvo < 6
      lin += txtsublin(config:atknomes, $[p.texto].tipoatk, 1)
    fimse
    casovar $[p.texto].tipoalvo
    casose "1"
      lin += ", Outros"
      sair
    casose "2"
      lin += ", Todos dividindo ataque"
      sair
    casose "3"
      lin += ", Outros dividindo ataque"
      sair
    casose "4"
      lin += ", Todos sem dividir ataque"
      sair
    casose "5"
      lin += ", Outros sem dividir ataque"
      sair
    casose "6"
      lin += "Não agressivo"
      sair
    casose "7"
      lin += "Não agressivo, O próprio"
      sair
    casose "8"
      lin += "Automático"
      sair
    casose "9"
      lin += "Nenhum"
      sair
    casofim
    t.addfim(lin)
  efim
  se t.linhas
    arg0.msg2("\b\c6Habilidade, Tipo, Alvo\b\n" + t.remove(500))
  senao
    arg0.msg("\bNenhuma habilidade encontrada.")
  fimse


classe adm_lmag
herda comando_adm
const txtajuda = "\b\c3Lmag\b\n\
Sintaxe: LMAG [nome]\n\
Lista as magias que começam com o nome especificado.\n\
Se o nome for omitido, lista todas as magias."

func escr
  prog p
  se !p.iniclasse("magia_" + arg1)
    ret arg0.msg("\bNenhuma magia encontrada.")
  fimse
  textotxt t
  txt100 lin
  epara nulo, p.lin && t.linhas < 499, p.depois
    continuar !$[p.texto].cmdmenu # Continua se não é uma magia editável
    lin = txtcopiamai(txt2(txte(p.texto)), "A") + ", "
    se $[p.texto].tipoalvo < 6
      lin += txtsublin(config:atknomes, $[p.texto].tipoatk, 1)
    fimse
    casovar $[p.texto].tipoalvo
    casose "1"
      lin += ", Outros"
      sair
    casose "2"
      lin += ", Todos dividindo ataque"
      sair
    casose "3"
      lin += ", Outros dividindo ataque"
      sair
    casose "4"
      lin += ", Todos sem dividir ataque"
      sair
    casose "5"
      lin += ", Outros sem dividir ataque"
      sair
    casose "6"
      lin += "Não agressivo"
      sair
    casose "7"
      lin += "Não agressivo, O próprio"
      sair
    casose "8"
      lin += "Automático"
      sair
    casose "9"
      lin += "Nenhum"
      sair
    casofim
    t.addfim(lin)
  efim
  se t.linhas
    arg0.msg2("\b\c6Magia, Tipo, Alvo\b\n" + t.remove(500))
  senao
    arg0.msg("\bNenuma magia encontrada.")
  fimse


classe adm_chab
herda comando_adm
const txtajuda = "\b\c3Chab\b\n\
Sintaxe: CHAB <nome>\n\
Cria uma habilidade com o nome especificado.\n\
O nome deve ser composto de letras, mas pode ter espaços no meio."
const tipo = "habilidade"

func escr
  prog p
# Checa se digitou nome do ataque
  se arg1 == ""
    ret arg0.msg("Tecle o nome da habilidade após CHAB")
  fimse
# Checa se nome válido
  txt80 nome = txte(txtremove(arg1, "EMDSA"))
  se txtproc(txtsepara(txttroca(nome, " ", ""), "O+DOL,DOL+O"), " ") >= 0
    ret arg0.msg("Nome inválido para habilidade: " + nome)
  senao inttotal(nome) > 40
    ret arg0.msg("Nome deve ter até 40 caracteres: " + nome)
  senao p.existe("cmd_" + nome)
    ret arg0.msg("Já existe o comando: " + nome)
  senao p.existe("soc_" + nome)
    ret arg0.msg("Já existe o comando social: " + nome)
  fimse
# Pergunta que tipo de habilidade criar
  arg0.sock.var.opcclasse = "cmd_" + txts(txtmin(nome))
  arg0.sock.var.opcvar = "h_" + txtcopiamai(txte(nome), "A")
  passo(arg0.sock, "")

func passo # Escolher o tipo de habilidade ou magia
  txt40 deriv
  casovar txtmin(arg1)
  casose
    arg0.msg("Opção inválida")
  casose "\b"
  casose ""
    arg0.passo = este
    arg0.msg("Entre com o tipo de " + tipo + ", O mostra as opções, Z cancela")
    ret 1
  casose "z"
    arg0.msg("Retornando ao jogo")
    arg0.passo = ""
    arg0.var.opcclasse = ""
    arg0.var.opcnome = ""
    ret 1
  casose "o"
    arg0.txtmais.limpar
    arg0.txtmais.addfim("C Habilidade ou magia comum")
    arg0.txtmais.addfim("I Identificar")
    arg0.txtmais.addfim("R Retornar")
    arg0.txtmostra
    ret 1
  casose "c"
    deriv = "comando_ataque"
    sair
  casose "i"
    deriv = "comando_identificar"
    sair
  casose "r"
    deriv = "comando_retornar"
    sair
  casofim
# Cria habilidade ou magia
  txt512 lin
  lin = arg0.var.opcclasse + "\nconfig/hab\nherda " + deriv
  lin += "\nconst nomevar = \"" + arg0.var.opcvar + "\""
  prog p
  p.criar(lin)
  criar("admordena", "config/hab")
# config:salvar
  arg0.msg("\bCriou " + tipo + ": " + txt(arg0.var.opcvar, 2))
  admlog(arg0, "criou " + tipo + " " + txt(arg0.var.opcvar, 2))
  arg0.passo = ""
  arg0.var.opcclasse = ""
  arg0.var.opcnome = ""
  ret 1


classe adm_cmag
herda adm_chab
const txtajuda = "\b\c3Cmag\b\n\
Sintaxe: CMAG <nome>\n\
Cria uma magia com o nome especificado.\n\
O nome deve ser composto de letras, mas pode ter espaços no meio."
const tipo = "magia"

func escr
  prog p
# Checa se digitou nome do ataque
  se arg1 == ""
    ret arg0.msg("Tecle o nome da magia após CMAG")
  fimse
# Checa se nome válido
  txt80 nome = txte(txtremove(arg1, "EMDSA"))
  se txtproc(txtsepara(txttroca(nome, " ", ""), "O+DOL,DOL+O"), " ") >= 0
    ret arg0.msg("Nome inválido para magia: " + nome)
  senao inttotal(nome) > 40
    ret arg0.msg("Nome deve ter até 40 caracteres: " + nome)
  senao p.existe("magia_" + nome)
    ret arg0.msg("Já existe a magia: " + nome)
  fimse
# Pergunta que tipo de magia criar
  arg0.sock.var.opcclasse = "magia_" + txts(txtmin(nome))
  arg0.sock.var.opcvar = "m_" + txtcopiamai(txte(nome), "A")
  passo(arg0.sock, "")


classe adm_ahab
herda comando_adm
const txtajuda = "\b\c3Ahab\b\n\
Sintaxe: AHAB <nomes das habilidades>\n\
Apaga uma ou mais habilidades, com os nomes especificados.\n\
Para apagar uma habilidade cujo nome contém espaços no meio, use\n\
sublinhado no lugar dos espaços. Exemplo, se o nome da magia for\n\
Chute Forte, você deve teclar: ahab Chute_Forte"

func escr
  prog p
  se arg1 == ""
    ret arg0.msg("Tecle um ou mais nomes de habilidades após AHAB")
  fimse
  uint16 x
  enquanto txtsub(arg1, x, 1)
    se !p.existe("cmd_" + txtsub(arg1, x, 1))
      ret arg0.msg("Habilidade não existe: " + txtsub(arg1, x, 1))
    senao !$cmd_[txtsub(arg1, x, 1)].cmdmenu
      ret arg0.msg("Comando não é uma habilidade: " + txtsub(arg1, x, 1))
    fimse
    x++
  efim
  arg0.sock.tpasso = arg1
  passo(arg0.sock, "")

func passo # Confirmar que quer apagar habilidade
  arg0.passo = ""
  se arg1 == "n"
    arg0.msg("Cancelado")
  senao arg1 == "s"
    prog p
    uint16 x
    enquanto txtsub(arg0.tpasso, x, 1)
      p.apagar("cmd_" + txtsub(arg0.tpasso, x, 1))
      x++
    efim
    config:salvar
    se x < 2
      arg0.msg("Habilidade apagada: " + arg0.tpasso)
      admlog(arg0, "apagou habilidade " + arg0.tpasso)
    senao
      arg0.msg("Habilidades apagadas: " + arg0.tpasso)
      admlog(arg0, "apagou habilidades " + arg0.tpasso)
    fimse
  senao
    arg0.passo = este
    refvar m1 = txt2(arg0.tpasso) ? "habilidades" : "habilidade"
    arg0.msg("Apagar " + m1 + " " + arg0.tpasso + "? Tecle S ou N")
  fimse
  ret 1


classe adm_amag
herda comando_adm
const txtajuda = "\b\c3Amag\b\n\
Sintaxe: AMAG <nomes das magias>\n\
Apaga uma ou mais magias, com os nomes especificados.\n\
Para apagar uma magia cujo nome contém espaços no meio, use sublinhado\n\
no lugar dos espaços. Exemplo, se o nome da magia for Curar Cegueira,\n\
você deve teclar: amag Curar_Cegueira"

func escr
  prog p
  se arg1 == ""
    ret arg0.msg("Tecle um ou mais nomes de magias após AMAG")
  fimse
  uint16 x
  enquanto txtsub(arg1, x, 1)
    se !p.existe("magia_" + txtsub(arg1, x, 1))
      ret arg0.msg("Magia não existe: " + txtsub(arg1, x, 1))
    senao !$magia_[txtsub(arg1, x, 1)].cmdmenu
      ret arg0.msg("Magia não pode ser apagada: " + txtsub(arg1, x, 1))
    fimse
    x++
  efim
  arg0.sock.tpasso = arg1
  passo(arg0.sock, "")

func passo # Confirmar que quer apagar magia
  arg0.passo = ""
  se arg1 == "n"
    arg0.msg("Cancelado")
  senao arg1 == "s"
    prog p
    uint16 x
    enquanto txtsub(arg0.tpasso, x, 1)
      p.apagar("magia_" + txtsub(arg0.tpasso, x, 1))
      x++
    efim
    config:salvar
    se x < 2
      arg0.msg("Magia apagada: " + arg0.tpasso)
      admlog(arg0, "apagou magia " + arg0.tpasso)
    senao
      arg0.msg("Magias apagadas: " + arg0.tpasso)
      admlog(arg0, "apagou magias " + arg0.tpasso)
    fimse
  senao
    arg0.passo = este
    refvar m1 = txt2(arg0.tpasso) ? "magias" : "magia"
    arg0.msg("Apagar " + m1 + " " + arg0.tpasso + "? Tecle S ou N")
  fimse
  ret 1


classe adm_ehab
herda comando_adm
const txtajuda = "\b\c3Ehab\b\n\
Sintaxe: EHAB <nome>\n\
Edita a habilidade com o nome especificado."

func escr
  prog p
  se arg1 == ""
    arg0.msg("Tecle o nome da habilidade após EHAB")
  senao !p.existe("cmd_" + arg1)
    arg0.msg("Habilidade não existe: " + arg1)
  senao !$cmd_[arg1].cmdmenu
    arg0.msg("Comando não é uma habilidade: " + arg1)
  senao
    arg0.sock.var.classe1 = "cmd_" + txts(txtmin(arg1))
    arg0.sock.var.menufim = "menu_hab_" + $cmd_[arg1].cmdmenu
    arg0.sock.m_menu(arg0.sock.var.menufim)
  fimse


classe adm_emag
herda comando_adm
const txtajuda = "\b\c3Emag\b\n\
Sintaxe: EMAG <nome>\n\
Edita a magia com o nome especificado."

func escr
  prog p
  se arg1 == ""
    arg0.msg("Tecle o nome da magia após EMAG")
  senao !p.existe("magia_" + arg1)
    arg0.msg("Magia não existe: " + arg1)
  senao !$magia_[arg1].cmdmenu
    arg0.msg("Comando não é uma magia editável: " + arg1)
  senao
    arg0.sock.var.classe1 = "magia_" + txts(txtmin(arg1))
    arg0.sock.var.menufim = "menu_magia_" + $magia_[arg1].cmdmenu
    arg0.sock.m_menu(arg0.sock.var.menufim)
  fimse


classe adm_dhab
herda comando_adm
const txtajuda = "\b\c3Dhab\b\n\
Sintaxe: DHAB <habilidade> <alvo>\n\
         DHAB <personagem> <habilidade> <alvo>\n\
Calcula os danos que uma habilidade faria no personagem alvo.\n\
Se for fornecido apenas a habalidade e o alvo, calcula os danos que\n\
o seu personagem faria."
const prefixo = "cmd_"
const naoencontrado = "Habilidade não encontrada: "
const naoataque = "Habilidade não é ataque: "

func escr
  txt100 nome
  ref perso
  ref alvo
  ref obj
  se intsub(arg1) < 2
    ret arg0.msg(txtmaimin(txt(este, 4)) + " requer 2 ou 3 argumentos")
  senao intsub(arg1) >= 3
    perso = misc:persodono(arg0, txt1(arg1))
    nome = txtsub(arg1, 1, 1)
    alvo = misc:persodono(arg0, txtsub(arg1, 2))
    ret !perso, arg0.msg("Personagem não existe: " + txt1(arg1))
    ret !alvo, arg0.msg("Alvo não existe: " + txtsub(arg1, 2))
  senao
    perso = arg0
    nome = txt1(arg1)
    alvo = misc:persodono(arg0, txt2(arg1))
    ret !alvo, arg0.msg("Alvo não existe: " + txt2(arg1))
  fimse
  enquanto 1
    refvar perm = $perm_[jogperm]
    obj = $[prefixo + nome] # Checa comando exato
    sair obj.cmdmenu
    refvar tam = inttotal(prefixo)
    prog p
    nomeobj n
    n.ini(txt1(arg0))
    epara p.iniclasse(prefixo + txt1(txte(nome))), p.lin, p.depois
      obj = $[p.texto]
      sair obj.cmdmenu && !n.nome(txt(obj, tam))
    efim
    sair p.lin
    ret arg0.msg(naoencontrado + nome)
  efim
  ret obj.cmdmenu != "ataque", arg0.msg(naoataque + txt(obj.nomevar, 2))
  ret obj.tipoalvo > 5 && obj.tipoalvo < 8, arg0.msg(naoataque + txt(obj.nomevar, 2))
  textotxt t
  obj.danosgolpe(perso, alvo, t)
  arg0.msg2(t.remove(1000))


classe adm_dmag
herda adm_dhab
const txtajuda = "\b\c3Dmag\b\n\
Sintaxe: DMAG <magia> <alvo>\n\
         DMAG <personagem> <magia> <alvo>\n\
Calcula os danos que uma magia faria no personagem alvo.\n\
Se for fornecido apenas a magia e o alvo, calcula os danos que\n\
o seu personagem faria."
const prefixo = "magia_"
const naoencontrado = "Magia não encontrada: "
const naoataque = "Magia não é ataque: "


classe menu_hab_custo
herda comando_menu
const titulo = "Consome"
const menufim = var.menufim
const colunas = 25
#
const nome1 = "Tempo de espera"
const info1 = "Tempo mínimo que deve esperar, em décimos de segundo"
const tipo1 = "opc_numero"
const clas1 = var.classe1
const vari1 = "c_espera"
const extr1 = "5 150"
#
const nome2 = "Vida iniciante"
const info2 = "Quantos pontos de vida consome no nível iniciante"
const tipo2 = "opc_numero"
const clas2 = var.classe1
const vari2 = "c_vida1"
#
const nome3 = "Vida perito"
const info3 = "Quantos pontos de vida consome no nível perito"
const tipo3 = "opc_numero"
const clas3 = var.classe1
const vari3 = "c_vida2"
#
const nome4 = "Mana iniciante"
const info4 = "Quantos pontos de mana consome no nível iniciante"
const tipo4 = "opc_numero"
const clas4 = var.classe1
const vari4 = "c_mana1"
#
const nome5 = "Mana perito"
const info5 = "Quantos pontos de mana consome no nível perito"
const tipo5 = "opc_numero"
const clas5 = var.classe1
const vari5 = "c_mana2"
#
const nome6 = "Vigor iniciante"
const info6 = "Quantos pontos de vigor consome no nível iniciante"
const tipo6 = "opc_numero"
const clas6 = var.classe1
const vari6 = "c_move1"
#
const nome7 = "Vigor perito"
const info7 = "Quantos pontos de vigor consome no nível perito"
const tipo7 = "opc_numero"
const clas7 = var.classe1
const vari7 = "c_move2"


classe menu_hab_alvo
herda comando_menu
const titulo = "Afeta o alvo"
const menufim = var.menufim
const colunas = 25
#
const nome1 = "Condição"
const tipo1 = "opc_opcao"
const clas1 = var.classe1
const vari1 = "a_cond"
const extr1 = "Um ou mais pontos de vida\nNenhum ponto de vida\nNenhuma condição"
#
const nome2 = "Vida"
const info2 = "Quantos pontos de vida ganha"
const tipo2 = "opc_numero"
const clas2 = var.classe1
const vari2 = "a_vida1"
#
const nome3 = "Vida por nível"
const info3 = "Quantos pontos de vida ganha a cada nível"
const tipo3 = "opc_numero"
const clas3 = var.classe1
const vari3 = "a_vida2"
#
const nome4 = "Mana"
const info4 = "Quantos pontos de mana ganha"
const tipo4 = "opc_numero"
const clas4 = var.classe1
const vari4 = "a_mana1"
#
const nome5 = "Mana por nível"
const info5 = "Quantos pontos de mana ganha a cada nível"
const tipo5 = "opc_numero"
const clas5 = var.classe1
const vari5 = "a_mana2"
#
const nome6 = "Vigor"
const info6 = "Quantos pontos de vigor ganha"
const tipo6 = "opc_numero"
const clas6 = var.classe1
const vari6 = "a_move1"
#
const nome7 = "Vigor por nível"
const info7 = "Quantos pontos de vigor ganha a cada nível"
const tipo7 = "opc_numero"
const clas7 = var.classe1
const vari7 = "a_move2"
#
const nome8 = "Nota vida/mana/vigor"
const tipo8 = "opc_ajuda"
const extr8 = "Os valores de vida, mana e vigor são para o nível itermediário.\n\
Eles podem diminuir em até 50% (iniciante) ou aumentar em até 50% (perito)."
#
const nome9 = "Efeito 1"
const info9 = "Nome do efeito"
const tipo9 = "opc_linha"
const clas9 = var.classe1
const vari9 = "a_efeito1"
#
const nome10 = "Precisão 1"
const info10 = "Probabilidade de criar o efeito, 100=não falha"
const tipo10 = "opc_numero"
const clas10 = var.classe1
const vari10 = "a_prob1"
const extr10 = "0 100"
#
const nome11 = "Efeito 2"
const info11 = "Nome do efeito"
const tipo11 = "opc_linha"
const clas11 = var.classe1
const vari11 = "a_efeito2"
#
const nome12 = "Precisão 2"
const info12 = "Probabilidade de criar o efeito, 100=não falha"
const tipo12 = "opc_numero"
const clas12 = var.classe1
const vari12 = "a_prob2"
const extr12 = "0 100"
#
const nome13 = "Efeito 3"
const info13 = "Nome do efeito"
const tipo13 = "opc_linha"
const clas13 = var.classe1
const vari13 = "a_efeito3"
#
const nome14 = "Precisão 3"
const info14 = "Probabilidade de criar o efeito, 100=não falha"
const tipo14 = "opc_numero"
const clas14 = var.classe1
const vari14 = "a_prob3"
const extr14 = "0 100"
#
const nome15 = "Remover efeitos"
const info15 = "Efeitos que serão removidos; uma linha por efeito"
const tipo15 = "opc_texto"
const clas15 = var.classe1
const vari15 = "a_remove"


classe menu_hab_opcataque
herda comando_menu
const titulo = "Ataque"
const menufim = var.menufim
const colunas = 25
#
const nome1 = "Fórmula"
const tipo1 = "opc_ajuda--"
const extr1 = "Os danos (pontos de vida que o alvo perde) são calculados assim:\n\
danos = força * tipo * ataque1 * ataque2 / (defesa * fator)\n\
Dependendo do tipo de ataque, os danos podem aumentar ou diminuir\n\
Quando não há opções modificando ataque e defesa, é 1\n\
O tipo também define se será usado ataque e defesa normal ou especial\n\
Nota: quando o personagem está com fome e/ou com sede, o ataque é mais fraco"
#
const nome2 = "Tipo"
const tipo2 = "opc_opcao"
const clas2 = var.classe1
const vari2 = "tipoatk"
const extr2 = config:atknomes
#
const nome3 = "Força iniciante"
const tipo3 = "opc_numero"
const clas3 = var.classe1
const vari3 = "ataquemin"
#
const nome4 = "Força perito"
const tipo4 = "opc_numero"
const clas4 = var.classe1
const vari4 = "ataquemax"
#
const nome5 = "Fator mínimo"
const info5 = "Fator é um número aleatório de fator mínimo a fator máximo"
const tipo5 = "opc_numero"
const clas5 = var.classe1
const vari5 = "fatormin"
#
const nome6 = "Fator máximo"
const info6 = "Fator é um número aleatório de fator mínimo a fator máximo"
const tipo6 = "opc_numero"
const clas6 = var.classe1
const vari6 = "fatormax"
#
const nome7 = "Ataque1"
const tipo7 = "opc_numero"
const clas7 = var.classe1
const vari7 = "ataque1"
#
const nome8 = "Somar perso"
const info8 = "Se deve somar o ataque do personagem em ataque1"
const tipo8 = "opc_simnao"
const clas8 = var.classe1
const vari8 = "ataque1perso"
#
const nome9 = "Somar arma"
const info9 = "Se deve somar o ataque da arma em ataque1"
const tipo9 = "opc_simnao"
const clas9 = var.classe1
const vari9 = "ataque1arma"
#
const nome10 = "Somar munição"
const info10 = "Se deve somar o ataque da munição em ataque1"
const tipo10 = "opc_simnao"
const clas10 = var.classe1
const vari10 = "ataque1municao"
#
const nome11 = "Ataque2"
const info11 = "Se 0, ataque2 é o ataque do personagem"
const tipo11 = "opc_numero"
const clas11 = var.classe1
const vari11 = "ataque2"
#
const nome12 = "Bônus"
const info12 = "Quais raças e classes atacam com mais força"
const tipo12 = "opc_tipos"
const clas12 = var.classe1
const vari12 = "tipobonus"
#
const nome13 = "Danos dif. nível"
const info13 = "Se a diferença de nível entre os personagens influencia nos danos"
const tipo13 = "opc_opcao"
const clas13 = var.classe1
const vari13 = "atknivel"
const extr13 = "Não\nSomente sem arma\nSim"
#
const nome14 = "Velocidade"
const info14 = "Em porcentagem"
const tipo14 = "opc_numero"
const clas14 = var.classe1
const vari14 = "vel"
#
const nome15 = "Precisão perso"
const info15 = "Quanta precisão o personagem ganha durante o ataque"
const tipo15 = "opc_numero"
const clas15 = var.classe1
const vari15 = "addprecisao"
#
const nome16 = "Alcance sem arma"
const info16 = "Alcance em pontos de movimento, para percorrer salas em uma direção"
const tipo16 = "opc_numero"
const clas16 = var.classe1
const vari16 = "armamove"


classe menu_hab_msg0
herda comando_menu
const titulo = "Mensagens 1"
const menufim = var.menufim
const colunas = 25
const n_variavel = "atk1"
#
const titu1 = " * Ao usar, exceto poções comidas etc"
#
const nome1 = "Perso"
const tipo1 = "opc_linha"
const clas1 = var.classe1
const vari1 = n_variavel + "_usar1"
#
const nome2 = "Alvo"
const info2 = "Se vazio, pega mensagem da opção 1"
const tipo2 = "opc_linha"
const clas2 = var.classe1
const vari2 = n_variavel + "_usar2"
#
const nome3 = "Outros"
const info3 = "Se vazio, pega mensagem da opção 1"
const tipo3 = "opc_linha"
const clas3 = var.classe1
const vari3 = n_variavel + "_usar3"
#
const titu4 = " * Acertou em si mesmo"
#
const nome4 = "Perso"
const tipo4 = "opc_linha"
const clas4 = var.classe1
const vari4 = n_variavel + "_auto1"
#
const nome5 = "Outros"
const info5 = "Se vazio, pega mensagem da opção 4"
const tipo5 = "opc_linha"
const clas5 = var.classe1
const vari5 = n_variavel + "_auto2"
#
const titu6 = " * Acertou outro personagem"
#
const nome6 = "Perso"
const tipo6 = "opc_linha"
const clas6 = var.classe1
const vari6 = n_variavel + "_perso1"
#
const nome7 = "Alvo"
const info7 = "Se vazio, pega mensagem da opção 6"
const tipo7 = "opc_linha"
const clas7 = var.classe1
const vari7 = n_variavel + "_perso2"
#
const nome8 = "Outros"
const info8 = "Se vazio, pega mensagem da opção 6"
const tipo8 = "opc_linha"
const clas8 = var.classe1
const vari8 = n_variavel + "_perso3"
#
const titu9 = " * Matou o alvo"
#
const nome9 = "Perso"
const tipo9 = "opc_linha"
const clas9 = var.classe1
const vari9 = n_variavel + "_matou1"
#
const nome10 = "Alvo"
const info10 = "Se vazio, pega mensagem da opção 9"
const tipo10 = "opc_linha"
const clas10 = var.classe1
const vari10 = n_variavel + "_matou2"
#
const nome11 = "Outros"
const info11 = "Se vazio, pega mensagem da opção 9"
const tipo11 = "opc_linha"
const clas11 = var.classe1
const vari11 = n_variavel + "_matou3"
#
const titu12 = " * Errou o alvo"
#
const nome12 = "Perso"
const tipo12 = "opc_linha"
const clas12 = var.classe1
const vari12 = n_variavel + "_errou1"
#
const nome13 = "Alvo"
const info13 = "Se vazio, pega mensagem da opção 12"
const tipo13 = "opc_linha"
const clas13 = var.classe1
const vari13 = n_variavel + "_errou2"
#
const nome14 = "Outros"
const info14 = "Se vazio, pega mensagem da opção 12"
const tipo14 = "opc_linha"
const clas14 = var.classe1
const vari14 = n_variavel + "_errou3"
#
const titu15 = " * Não teve efeito"
#
const nome15 = "Perso"
const tipo15 = "opc_linha"
const clas15 = var.classe1
const vari15 = n_variavel + "_nulo1"
#
const nome16 = "Alvo"
const info16 = "Se vazio, pega mensagem da opção 15"
const tipo16 = "opc_linha"
const clas16 = var.classe1
const vari16 = n_variavel + "_nulo2"
#
const nome17 = "Outros"
const info17 = "Se vazio, pega mensagem da opção 15"
const tipo17 = "opc_linha"
const clas17 = var.classe1
const vari17 = n_variavel + "_nulo3"
#
const titu18 = " * O alvo esquivou (opcional)"
#
const nome18 = "Perso"
const info18 = "Quando não há mensagens de esquiva, pega de Errou o alvo"
const tipo18 = "opc_linha"
const clas18 = var.classe1
const vari18 = n_variavel + "_esquivou1"
#
const nome19 = "Alvo"
const info19 = "Se vazio, pega mensagem da opção 18"
const tipo19 = "opc_linha"
const clas19 = var.classe1
const vari19 = n_variavel + "_esquivou2"
#
const nome20 = "Outros"
const info20 = "Se vazio, pega mensagem da opção 18"
const tipo20 = "opc_linha"
const clas20 = var.classe1
const vari20 = n_variavel + "_esquivou3"


classe menu_hab_msg1
herda menu_hab_msg0
const titulo = "Mensagens 2"
const n_variavel = "atk2"


classe menu_hab_msg2
herda menu_hab_msg0
const titulo = "Mensagens 3"
const n_variavel = "atk3"


classe menu_hab_msg3
herda menu_hab_msg0
const titulo = "Mensagens 4"
const n_variavel = "atk4"


classe menu_hab_msg4
herda menu_hab_msg0
const titulo = "Mensagens 5"
const n_variavel = "atk5"


classe menu_hab_comum
herda comando_menu
const nome3 = "Taxa de falha"
const info3 = "Porcentagem de falha para habilidades e magias que por natureza\n\
podem falhar. De 0 a 100 (100 sempre falha) ou -1 se nunca falha."
const tipo3 = "opc_numero"
const clas3 = var.classe1
const vari3 = "falha"
const extr3 = "-1 100"
#
const nome4 = "Requer arma"
const info4 = "Se vazio, a arma é ignorada: ataca sem arma mesmo se estiver empunhando uma"
const tipo4 = "opc_vetor"
const clas4 = var.classe1
const vari4 = "tipoarma"
const extr4 = config:arma_txt
#
const nome5 = "Consome"
const tipo5 = "menu_hab_custo--"
#
const nome6 = "Preço da aula"
const info6 = "Quantas moedas os personagens cobram para ensinar"
const tipo6 = "opc_numero"
const clas6 = var.classe1
const vari6 = "valor"
#
const nome7 = "Pontos de aula"
const info7 = "Quantos pontos de aula consome por aula"
const tipo7 = "opc_numero"
const clas7 = var.classe1
const vari7 = "aulapontos"
#
const nome8 = "Depende de"
const info8 = "Habilidades, magias e idiomas necessários para aprender esse ataque"
const tipo8 = "opc_tipodep"
const clas8 = var.classe1
const vari8 = "depende"
#
const nome9 = "Iniciante nível"
const info9 = "A partir de que nível pode ter a primeira aula"
const tipo9 = "opc_numero"
const clas9 = var.classe1
const vari9 = "aulaini"
#
const nome10 = "Perito nível"
const info10 = "A partir de que nível pode ter todas as aulas"
const tipo10 = "opc_numero"
const clas10 = var.classe1
const vari10 = "aulafim"
#
const nome11 = "Somente tipos"
const info11 = "Se estiver vazio, todas as classes e raças podem aprender"
const tipo11 = "opc_tipos"
const clas11 = var.classe1
const vari11 = "tipoperso"


classe menu_hab_ataque
herda menu_hab_comum
const titulo = "Hab " + txt(var.classe1, 4)
const colunas = 25
#
const nome1 = "Alvo"
const tipo1 = "opc_opcao"
const clas1 = var.classe1
const vari1 = "tipoalvo"
const extr1 = "Qualquer um; ataque\n\
Exceto o personagem; ataque\n\
Todos dividindo o ataque\n\
Outros dividindo o ataque\n\
Todos sem dividir o ataque\n\
Outros sem dividir o ataque\n\
Qualquer um; não é ataque\n\
O personagem; não é ataque\n\
Ataque automático\n\
Nenhum (não ataca)"
#
#
const nome2 = "Condição"
const tipo2 = "opc_opcao"
const clas2 = var.classe1
const vari2 = "a_batalha"
const extr2 = "Nenhuma\nDurante a batalha\nFora da batalha"
#
const nome12 = "Afeta o alvo"
const tipo12 = "menu_hab_alvo--"
#
const nome13 = "Munição"
const tipo13 = "opc_opcao"
const clas13 = var.classe1
const vari13 = "tipomunicao"
const extr13 = "Não usa\nUsa mas não perde\nUsa e perde uma munição"
#
const nome14 = "Munição sem arma"
const info14 = "Que munição do inventário deve usar quando não usar uma arma"
const tipo14 = "opc_listaitem"
const clas14 = var.classe1
const vari14 = "itemmunicao"
#
const nome15 = "Ataque"
const tipo15 = "menu_hab_opcataque--"
#
const titu16 = " * Textos"
#
const nome16 = "Informações"
const info16 = "Informações extras"
const tipo16 = "opc_texto"
const clas16 = var.classe1
const vari16 = "msgextra"
#
const nome17 = "Mensagens 1"
const tipo17 = "menu_hab_msg0--"
#
const nome18 = "Mensagens 2"
const tipo18 = "menu_hab_msg1--"
#
const nome19 = "Mensagens 3"
const tipo19 = "menu_hab_msg2--"
#
const nome20 = "Mensagens 4"
const tipo20 = "menu_hab_msg3--"
#
const nome21 = "Mensagens 5"
const tipo21 = "menu_hab_msg4--"
#
const nome22 = "Menus de mensagens"
const tipo22 = "opc_ajuda--"
const extr22 = menu_hab_ataque:msg1 + "\n" + menu_hab_ataque:msg2
#
const msg1 = "\c3Menus de mensagens:\b\n\
Em princípio, pode-se colocar todas as mensagens no menu Mensagens 1.\n\
Se você colocar mensagens em mais de um desses menus, as mensagens\n\
de batalha serão pegas de um dos menus, escolhido aleatoriamente.\n\
O efeito do golpe será o mesmo, apenas a mensagem muda.\n\
\c3Perso, alvo e outros:\b\n\
Se quiser mensagens diferentes para o personagem, para o alvo e para\n\
quem estiver assistindo, coloque as mensagens nas opções perso, alvo\n\
e outros. Se alvo e outros estiverem vazios, será usada a do perso."
const msg2 = "\c3Ao usar, exceto poções comidas etc:\b\n\
É a mensagem enviada assim que o personagem usa a habilidade ou magia,\n\
independente dela falhar ou não. No entanto, essa mensagem não é enviada\n\
quando a magia for disparada por um item (quando o personagem comeu ou\n\
bebeu o item, usou pergaminho, etc)."


classe menu_magia_ataque
herda menu_hab_ataque
const titulo = "Magia " + txt(var.classe1, 6)
const colunas = 25
