classe h_cmd_carregar
herda comando_comum
const classemenu = "m_cmd_carregar"
const posic = 5
#
const m_ajuda = "-t\n\
-s\n\
-s <objeto>\n\
Carrega a arma que você estiver empunhando com um objeto que você está\n\
carregando. Sem argumentos você verifica se a arma está carregada\n\
e com o quê."
const m_naover = "Você não vê $m."
const m_sem_arma = "Você não está empunhando nenhuma arma."
const m_sem_municao = "$O não usa munição"
const m_descarregado = "$O está descarregad$u."
const m_carregado = "$O está carregad$u com:"
const m_nao_pode1 = "$O não pode ser carregad$u."
const m_conhecimento = "Não possui conhecimento suficiente em $m."
const m_carrega_um1 = "Você carrega $a com $o."
const m_carrega_um2 = "$P carrega $a com $o."
const m_carrega_mais1 = "Você carrega $a com $mx $o."
const m_carrega_mais2 = "$P carrega $a com $mx $o."
#
const m_nao_pode2 = "Não é possível carregar $o com $n."
const m_cheio = "Não consegue carregar $o com mais nada."
const m_espaco = "Não há espaço suficiente para carregar $o com $n."
const m_nivel = "Você precisa chegar no nível $m para carregar $o com $n."
const m_renascer1 = "Você precisa renascer uma vez para carregar com $n."
const m_renascer2 = "Você precisa renascer duas vezes para carregar com $n."
const m_renascer3 = "Você precisa renascer $m vezes para carregar com $n."
#
const admordem = "m_ajuda m_naover m_sem_arma m_sem_municao m_descarregado\n\
m_carregado m_nao_pode1 m_conhecimento m_carrega_um1 m_carrega_um2\n\
m_carrega_mais1 m_carrega_mais2\n\
m_nao_pode2 m_cheio m_espaco m_nivel m_renascer1 m_renascer2 m_renascer3"

func escr
  refvar perm_msg = $perm_[arg0.jogperm].item
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  refvar arma = ref(arg0.iempu.obj)
  se !arma
    $mens.mp(m_sem_arma, arg0)
  senao !arma.armatipo || int(arma.armamunicao) != 2
    $mens.mp(m_sem_municao, arg0, nulo, arma)
  senao !arg1
    textotxt t
    listaitem item
    epara item = arma.dentro1.ini, item, item.depois
      continuar !item.obj.visivel(arg0)
      t.addfim(txt(item.obj.objtot) + " " + txtcopiamai(item.obj.descnome, "A"))
    efim
    se !t.linhas
      $mens.mp(m_descarregado, arg0, nulo, arma)
    senao
      $mens.p(arg0, nulo, arma)
      t.juntalin("(", "x)")
      t.addini($mens.txt(m_carregado, arg0))
      arg0.msg2(t.remove(1000))
    fimse
  senao !arma.armacarregar
    $mens.p(arg0, nulo, arma)
    $mens.msg(m_naopode, arg0)
  senao
    refvar tipo = txtsublin(config:arma_txt, arma.armatipo, 1)
    se arg0.var.h_[tipo]_ < arma.armacarregar
      $mens.mens = tipo
      $mens.mp(m_conhecimento, arg0, nulo, arma)
    senao
      nomeobj n # Para reconhecer os itens
      listaobj l
      txt100 erro # Mensagem de erro, no caso de não ter carregado
      int8 achou # Se carregou com algum item
      n.ini(arg1, misc:objmax, misc:tudo)
      epara l.addfim(arg0.dentro1), l, l.ini.remove
        ref ll = l.objini
        continuar !ll.visivel(arg0) || !n.nome(ll.var.z_ident ?? ll.ident, ll.objtot)
        refvar e = carregar(arg0, arma, ll, int(n))
        e ? (erro = e) : achou++
        sair achou >= 10
      efim
      se achou
      senao erro
        arg0.msg(erro)
      senao
        $mens.mens = arg1
        $mens.mp(m_naover, arg0)
      fimse
    fimse
  fimse

func carregar # Carrega arma com um item
# arg0=personagem, arg1=arma, arg2=item, arg3=quantidade de itens
# Retorna mensagem de erro ou nulo se carregou
  se txtproclin(arg1.i_municao, txt(arg2, 2)) < 0
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_nao_pode2, arg0)
  senao arg1.dentro1.total + (arg2.item == 2 ? 1 : arg3) > arg1.objmax
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_cheio, arg0)
  senao arg1.volden + arg2.voltot * arg3 / arg2.objtot > arg1.volmax
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_espaco, arg0)
  senao int(arg2.pnivel) > arg0.pnivel
    $mens.mens = int(arg2.pnivel)
    $mens.p(arg0, nulo, arg1, arg2)
    ret $mens.txt(m_nivel, arg0)
  senao int(arg2.renascer) > arg0.prenascer
    $mens.mens = int(arg2.renascer)
    $mens.p(arg0, nulo, arg1, arg2)
    se arg2.renascer == 1
      ret $mens.txt(m_renascer1, arg0)
    senao arg2.renascer == 2
      ret $mens.txt(m_renascer2, arg0)
    fimse
    ret $mens.txt(m_renascer3, arg0)
  fimse
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_carregarobj(arg0, arg1, arg2, arg3, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_carregarobj(arg0, arg1, arg2, arg3, arg1), nulo
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    ret e.objini.cmd_carregarobj(arg0, arg1, arg2, arg3, arg2), nulo
  efim
  $mens.p(arg0, arg1, arg2)
  $mens.mens = arg3
  se arg3 <= 1
    $mens.mvis2(m_carrega_um1, m_carrega_um2)
  senao
    $mens.mvis2(m_carrega_mais1, m_carrega_mais2)
  fimse
  ref r = arg2.mudadono(arg1, arg3)
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_carregouobj(arg0, arg1, r, total, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_carregouobj(arg0, arg1, r, total, arg1)
  efim
  epara e.addfim(r.evento), e, e.ini.remove
    e.objini.cmd_carregouobj(arg0, arg1, r, total, r)
  efim
  arg0.p_espera += 2


classe m_cmd_carregar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem alvo"
const tipo2 = "opc_linha"
const vari2 = "m_naover"
#
const nome3 = "Sem arma"
const tipo3 = "opc_linha"
const vari3 = "m_sem_arma"
#
const nome4 = "Sem munição"
const tipo4 = "opc_linha"
const vari4 = "m_sem_municao"
#
const nome5 = "Sem descarregado"
const tipo5 = "opc_linha"
const vari5 = "m_descarregado"
#
const nome6 = "Sem carregado"
const tipo6 = "opc_linha"
const vari6 = "m_carregado"
#
const nome7 = "Não pode carregar"
const tipo7 = "opc_linha"
const vari7 = "m_nao_pode1"
#
const nome8 = "Sem conhecimento"
const tipo8 = "opc_linha"
const vari8 = "m_conhecimento"
#
const nome9 = "Carrega 1 perso"
const tipo9 = "opc_linha"
const vari9 = "m_carrega_um1"
#
const nome10 = "Carrega 1 outros"
const tipo10 = "opc_linha"
const vari10 = "m_carrega_um2"
#
const nome11 = "Carrega vários perso"
const tipo11 = "opc_linha"
const vari11 = "m_carrega_mais1"
#
const nome12 = "Carrega vários outros"
const tipo12 = "opc_linha"
const vari12 = "m_carrega_mais2"
#
const nome13 = "Munição errada"
const tipo13 = "opc_linha"
const vari13 = "m_nao_pode2"
#
const nome14 = "Está cheio"
const tipo14 = "opc_linha"
const vari14 = "m_cheio"
#
const nome15 = "Sem espaço"
const tipo15 = "opc_linha"
const vari15 = "m_espaco"
#
const nome16 = "Nível insuficiente"
const tipo16 = "opc_linha"
const vari16 = "m_nivel"
#
const nome17 = "Renascer 1 vez"
const tipo17 = "opc_linha"
const vari17 = "m_renascer1"
#
const nome18 = "Renascer 2 vezes"
const tipo18 = "opc_linha"
const vari18 = "m_renascer2"
#
const nome19 = "Renascer mais vezes"
const tipo19 = "opc_linha"
const vari19 = "m_renascer3"
