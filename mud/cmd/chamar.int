classe h_cmd_chamar
herda comando_comum
const objmenu = $m_cmd_chamar
const objtipo = 2 # Pode renomear
const objcmd = config:animal1 ? este
const txtajuda = config:animal1 ? txtajuda1(m_ajuda)
const posic = 5
#
const m_ajuda = "-t\n\
-s $arg-animal\n\
-s -\n\
Chama de volta para o comando $Cmd-animal $um-animal que está solt$genero-animal.\n\
Com um hífen, chama todos os animais que for possível chamar.\n\
Sem argumentos, mostra $seus-animais solt$genero-animals."
const m_sem_animal = "Não há $nenhum-animal para chamar."
const m_titulo = "Chamar quem?"
const m_linha = "$num  $nome  N$nivel"
const m_numero_invalido = "$m não é número de $animal."
const m_nao_tem = "Você não tem $m."
const m_nao_solto = "$A não está solt$t."
const m_auto = "Chamar a si própri$s?"
const m_montado = "$O está montad$u em $a."
const m_nao_chamar = "Não é possível chamar $a."
const m_nenhum_chamar = "Não é possível chamar nenhum animal."
const m_entra1 = "Você traz $a de volta."
const m_entra2 = "$P traz $a de volta."
const admordem = "m_ajuda m_sem_animal m_titulo m_linha m_numero_invalido\n\
m_nao_tem m_nao_solto m_auto m_montado m_nao_chamar m_nenhum_chamar\n\
m_entra1 m_entra2"
#
ref animal # Objeto do animal
const t1_num = misc:persotxt(animal.pnumero)
const t1_nivel = animal.pnivel

func t1_nome
  refvar m = animal.descnome2
  ret txtcopiamai(txtproclin("o\na\num\numa", txt1(m + "x")) < 0 ? m : txt2(m), "A")

func escr
  se !arg1 # Sem argumentos
    textotxt t
    listaitem i
    epara i = arg0.lseguir.ini, i, i.depois
      continuar i.obj.tseguir != 3 || i.obj.p_animal == 1
      animal = i.obj
      refvar indice = txt(i.obj.pnumero + 100) + " "
      t.addfim(indice + txtremove(vartroca(m_linha, "$", "t1_"), "d"))
    efim
    ret !t.linhas, $mens.mp(m_sem_animal, arg0)
    t.ordena
    textopos pos
    epara pos = t.ini, pos, pos.depois
      pos.mudar("", 0, 4)
    efim
    ret arg0.msg2("\b\c6" + $mens.tp(m_titulo, arg0) + "\b\n" + t.remove(1000))
  senao arg1 == "-"
    listaobj animais
    listaitem i
    epara i = arg0.lseguir.ini, i, i.depois
      ref r = i.obj
      r.perso && r.tseguir == 3 && !r.persolugar && r.p_animal != 1 && animais.addfim(r)
    efim
    ret !animais, $mens.mp(m_nenhum_chamar, arg0)
    epara i = animais.ini, i, i.depois
      chamar(arg0, i.obj)
    efim
    ret
  fimse
  $mens.mens = arg1
  ret misc:persoindice(arg1) < 0, $mens.mp(m_numero_invalido, arg0)
  ref r = misc:personum(arg0, arg1)
  ret !r, $mens.mp(m_nao_tem, arg0)
  ret r.dono.perso, $mens.mp(m_nao_solto, arg0, r)
  ret r == arg0, $mens.mp(m_auto, arg0, r)
  ret r.persolugar, $mens.mp(m_montado, arg0, r, r.persolugar.objini)
  ret r.p_animal == 1, $mens.mp(m_nao_chamar, arg0, r)
  chamar(arg0, r)

func chamar
# arg0 = quem está chamando
# arg1 = animal
  refvar sala = arg0.dono
  ret cmd_chamaranim(arg0, sala, arg1), nulo
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_chamaranim(arg0, sala, arg1, arg0), nulo
  efim
  epara e.addfim(sala.evento), e, e.ini.remove
    ret e.objini.cmd_chamaranim(arg0, sala, arg1, sala), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_chamaranim(arg0, sala, arg1, arg1), nulo
  efim
  arg1.atkenv && arg1.batalhafim # Encerra batalha se este personagem está lutando
  e.addfim1(sala.dono.dentro2, arg1.dono.dentro2)
  arg1.mudadono(arg0)
  $mens.p(arg0, arg1)
  epara e.remove(arg0, arg1), e, e.ini.remove
    $mens.msgvis(m_entra2, e.objini)
  efim
  $mens.msgvis(m_entra1, arg0)
  cmd_chamouanim(arg0, sala, arg1)
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_chamouanim(arg0, sala, arg1, arg0)
  efim
  epara e.addfim(sala.evento), e, e.ini.remove
    e.objini.cmd_chamouanim(arg0, sala, arg1, sala)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_chamouanim(arg0, sala, arg1, arg1)
  efim


classe m_cmd_chamar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Nenhum animal solto"
const tipo2 = "opc_linha"
const vari2 = "m_sem_animal"
#
const nome3 = "Título"
const tipo3 = "opc_linha"
const vari3 = "m_sem_animal"
#
const nome4 = "Um animal solto"
const tipo4 = "opc_linha"
const vari4 = "m_sem_animal"
#
const nome5 = "Um animal solto"
const tipo5 = "opc_linha"
const vari5 = "m_sem_animal"
#
const nome6 = "Animal inválido"
const tipo6 = "opc_linha"
const vari6 = "m_numero_invalido"
#
const nome7 = "Não tem animal"
const tipo7 = "opc_linha"
const vari7 = "m_nao_tem"
#
const nome8 = "Animal não está solto"
const tipo8 = "opc_linha"
const vari8 = "m_nao_solto"
#
const nome9 = "Charar a si próprio"
const tipo9 = "opc_linha"
const vari9 = "m_auto"
#
const nome10 = "Montado no animal"
const tipo10 = "opc_linha"
const vari10 = "m_montado"
#
const nome11 = "Não chama animal"
const tipo11 = "opc_linha"
const vari11 = "m_nao_chamar"
#
const nome12 = "Não chama ninguém"
const tipo12 = "opc_linha"
const vari12 = "m_nenhum_chamar"
#
const nome13 = "Pegou perso"
const tipo13 = "opc_linha"
const vari13 = "m_entra1"
#
const nome14 = "Pegou outros"
const tipo14 = "opc_linha"
const vari14 = "m_entra2"
