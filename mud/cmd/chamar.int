classe h_cmd_chamar
herda comando_comum
const objmenu = $m_cmd_chamar
const objtipo = 2 # Pode renomear
const objcmd = config:animal1 ? este
const txtajuda = config:animal1 ? txtajuda1(m_ajuda)
const posic = 5
#
const m_ajuda = "-t\n-s\nChama de volta para o comando $Cmd-animal $um-animal que está solt$genero-animal."
const m_sem_animal = "Não possui $nenhum-animal solt$genero-animal."
const m_montado = "$O está montad$u em $a."
const m_nao_tem = "Você não tem $m."
const m_chama_solta = "Não é possível chamar $a."
const m_entra1 = "Você traz $a de volta."
const m_entra2 = "$P traz $a de volta."
const admordem = "m_ajuda m_sem_animal m_montado m_nao_tem\n\
m_chama_solta m_entra1 m_entra2"

func escr
  listaitem i
  epara i = arg0.lseguir.ini, i && i.obj.tseguir != 3, i.depois
  efim
  ret !i, $mens.mp(m_sem_animal, arg0)
  ref r = i.obj
  ret r.persolugar, $mens.mp(m_montado, arg0, r, r.persolugar.objini)
  ret r.p_animal < 2, $mens.mp(m_chama_solta, arg0, r)
  chamar(arg0, r)

func chamar
# arg0 = quem está chamando
# arg1 = animal
  refvar sala = arg0.dono
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_chamaranim(arg0, sala, arg1, arg0), nulo
  efim
  epara e.addfim(sala.evento), e, e.ini.remove
    ret e.objini.cmd_chamaranim(arg0, sala, arg1, sala), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_chamaranim(arg0, sala, arg1, arg1), nulo
  efim
  arg1.batalhafim
  e.addfim1(sala.dono.dentro2, arg1.dono.dentro2)
  arg1.mudadono(arg0)
  $mens.p(arg0, arg1)
  epara e.remove(arg0, arg1), e, e.ini.remove
    $mens.msgvis(m_entra2, e.objini)
  efim
  $mens.msgvis(m_entra1, arg0)
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_chamouanim(arg0, sala, arg1, arg0)
  efim
  epara e.addfim(sala.evento), e, e.ini.remove
    e.objini.cmd_chamouanim(arg0, sala, arg1, sala)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_chamouanim(arg0, sala, arg1, arg1)
  efim


classe m_cmd_chamar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Nenhum animal solto"
const tipo2 = "opc_linha"
const vari2 = "m_sem_animal"
#
const nome3 = "Montado no animal"
const tipo3 = "opc_linha"
const vari3 = "m_montado"
#
const nome4 = "Não tem animal"
const tipo4 = "opc_linha"
const vari4 = "m_nao_tem"
#
const nome5 = "Não chama"
const tipo5 = "opc_linha"
const vari5 = "m_chama_solta"
#
const nome6 = "Pegou perso"
const tipo6 = "opc_linha"
const vari6 = "m_entra1"
#
const nome7 = "Pegou outros"
const tipo7 = "opc_linha"
const vari7 = "m_entra2"
