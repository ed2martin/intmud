classe cmd_empunhar
herda comando_comum
const txtajuda = "\b\c3Empunhar\b\n\
Sintaxe: EMPUNHAR <objeto>\n\
Seu personagem empunha uma arma que está carregando."
const posic = 5

func escr
  ret !arg1, arg0.msg("Empunhar o que?")
  refvar perm_msg = $perm_[arg0.jogperm].vestir
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  refvar alvo = ref(arg0.persoesc)
  ret alvo.iempu, $mens.p(alvo) + $mens.mperso("$P já está empunhando uma arma.")
  refvar tipo1 = alvo.tipo1 ? "\n" + alvo.tipo1 + "\n" : ""
  refvar tipo2 = alvo.tipo2 ? "\n" + alvo.tipo2 + "\n" : ""
  refvar tipo3 = alvo.tipo3 ? "\n" + alvo.tipo3 + "\n" : ""
# Empunha
  listaobj l
  ref r
  nomeobj n # Para reconhecer os itens
  n.ini(arg1, misc:objmax)
  epara l.addfim(alvo.dentro1), l, l.ini.remove
    r = l.objini
    continuar !r.visivel(arg0) || !n.nome(r.ident, r.objtot)
    $mens.p(alvo, nulo, r)
    se !r.armatipo
      ret $mens.mperso("$O não é arma.")
    senao r.vestirsim == (alvo == arg0 ? 2 : 1)
      ret $mens.mperso("$P não consegue vestir $o.")
    senao int(r.pnivel) > alvo.pnivel
      ret $mens.mperso("$P precisa estar no nível " + r.pnivel + " para empunhar $o.")
    senao int(r.renascer) > alvo.prenascer
      se r.renascer == 1
        ret $mens.mperso("$P precisa renascer uma vez para empunhar $o.")
      senao r.renascer == 2
        ret $mens.mperso("$P precisa renascer duas vezes para empunhar $o.")
      fimse
      ret $mens.mperso("$P precisa renascer " + r.renascer + " vezes para empunhar $o.")
    senao !alvo.var.h_[txtsublin(config:arma_txt, r.armatipo, 1)]
      ret $mens.mperso("$P não conhece " + txtsublin(config:arma_txt, r.armatipo, 1) + ".")
    senao r.vestirnao
      se tipo1 && txtproc("\n" + txte(r.vestirnao) + "\n", tipo1) >= 0
        ret $mens.mperso("$P não consegue empunhar $o.")
      senao tipo2 && txtproc("\n" + txte(r.vestirnao) + "\n", tipo2) >= 0
        ret $mens.mperso("$P não consegue empunhar $o.")
      senao tipo3 && txtproc("\n" + txte(r.vestirnao) + "\n", tipo3) >= 0
        ret $mens.mperso("$P não consegue empunhar $o.")
      fimse
    fimse
    listaobj e
    epara e.addfim(alvo.evento, r.evento), e, e.ini.remove
      ret e.objini.cmd_vestir(alvo, r, 50), nulo
    efim
    r.vestir(50)
    $mens.mvis1som("$P empunha $o.", r.i_som_emp1)
    epara e.addfim(alvo.evento, r.evento), e, e.ini.remove
      e.objini.cmd_vestiu(alvo, r, 50)
    efim
    ret
  efim
  arg0.msg("Você não vê isso.")
