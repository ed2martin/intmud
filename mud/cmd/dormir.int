classe h_cmd_dormir
herda comando_comum
const objmenu = $m_cmd_dormir
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n\
-s [objeto]\n\
Dorme no chão. Com o nome de um objeto, tenta dormir no objeto."
const m_naover = "Você não vê $m"
const m_levantar = "Talvez você deva se levantar primeiro."
const m_nao_consegue = "Você não consegue dormir."
const m_nao_item = "Não é possível dormir $e $o."
const m_cheio = "Não cabe mais ninguém $e $o."
const m_dormir_chao1 = "Você deita e dorme."
const m_dormir_chao2 = "$P deita e dorme."
const m_dormir_item1 = "Você deita $e $o e dorme."
const m_dormir_item2 = "$P deita $e $o e dorme."
const admordem = "m_ajuda m_naover m_levantar m_nao_consegue m_nao_item\n\
m_cheio m_dormir_chao1 m_dormir_chao2 m_dormir_item1 m_dormir_item2"

func escr
  ref r
# Checa posição
  se !arg0.podeposicao(4)
    refvar m = arg0.podeposicao4msg ?? $t_[arg0.tipo1].podeposicao4msg
    ret $mens.mp(m ?? m_nao_consegue, arg0)
  fimse
  casovar arg0.posicao
  casose "5" # Descansando
  casose "6" # Sentado
    r = arg0.poslugar.objlista
    sair
  casose "8" # Em pé
    sair
  casose # Outras posições
    ret arg0.msgposicao
  casofim
# Obtém aonde quer dormir
  se arg1
    refvar obj = misc:itemdono(arg0, arg1)
    ret r && r != obj, $mens.mp(m_levantar, arg0)
    r = obj
  fimse
  listaobj l
# Dormir em algum objeto
  se r
    casovar r.i_movel(arg0)
    casose
      ret $mens.mp(m_nao_item, arg0, nulo, r)
    casose "2"
    casose "4"
    casofim
    se r != arg0.poslugar.objlista && r.i_lugar <= r.persolugar.total
      ret $mens.mp(m_cheio, arg0, nulo, r)
    fimse
    ret cmd_dormirobj(arg0, r), nulo
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_dormirobj(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_dormirobj(arg0, r, r), nulo
    efim
    $mens.p(arg0, nulo, r)
    $mens.mtodos2(m_dormir_item1, m_dormir_item2)
    arg0.poslugar.remove
    arg0.poslugar = r.persolugar.addfim(arg0)
    arg0.posicao = 4
    cmd_dormiuobj(arg0, r)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_dormiuobj(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_dormiuobj(arg0, r, r)
    efim
# Dormir num objeto que não existe
  senao arg1
    $mens.mens = arg1
    $mens.mp(m_naover, arg0)
# Dormir no chão
  senao
    ret cmd_dormirchao(arg0), nulo
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_dormirchao(arg0), nulo
    efim
    $mens.p(arg0)
    $mens.mtodos2(m_dormir_chao1, m_dormir_chao2)
    arg0.poslugar.remove, arg0.posicao = 4
    cmd_dormiuchao(arg0)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_dormiuchao(arg0)
    efim
  fimse


classe h_cmd_acordar
herda comando_comum
const objmenu = $m_cmd_acordar
const objtipo = 2 # Pode renomear
const posic = 4
#
const m_ajuda = "-t\n\
-s [personagem]\n\
Acorda. Com o nome de um personagem, tenta acordar o personagem."
const m_naover = "Você não vê $m"
const m_perso_acordado = "Mas você já está acordad$s."
const m_alvo_acordado = "$A não está dormindo."
const m_nao_consegue1 = "Você tenta acordar $a."
const m_nao_consegue2 = "$P tenta acordar $a."
const m_nao_perso = "Você não consegue acordar."
const m_acorda_perso1 = "Você acorda."
const m_acorda_perso2 = "$P acorda."
const m_acorda_alvo1 = "Você acorda $a."
const m_acorda_alvo2 = "$P acorda você."
const m_acorda_alvo3 = "$P acorda $a."
const admordem = "m_ajuda m_naover m_perso_acordado m_alvo_acordado\n\
m_nao_consegue1 m_nao_consegue2 m_nao_perso m_acorda_perso1 m_acorda_perso2\n\
m_acorda_alvo1 m_acorda_alvo2 m_acorda_alvo3"

func escr
# Acordar outro personagem
  se arg1
    ret arg0.posicao == 4, arg0.msgposicao
    ref r = misc:persodono(arg0, arg1)
    se !r
      $mens.mens = arg1
      $mens.mp(m_naover, arg0)
      ret
    senao r.posicao != 4
      ret $mens.mp(m_alvo_acordado, arg0, r)
    senao r.bitver & 0xC00 == 0x400
      $mens.p(arg0, r)
      $mens.mtodos2(m_nao_consegue1, m_nao_consegue1)
      ret
    fimse
    ret cmd_acordar(r, arg0), nulo
    listaobj l
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_acordar(r, arg0, r), nulo
    efim
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_acordar(r, arg0, arg0), nulo
    efim
    r.posicao = r.podeposicao(5) ? 5 : r.podeposicao(6) ? 6 : 8
    $mens.p(arg0, r)
    $mens.mtodos3(m_acorda_alvo1, m_acorda_alvo2, m_acorda_alvo3)
    cmd_acordou(r, arg0)
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_acordou(r, arg0, r)
    efim
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_acordou(r, arg0, arg0)
    efim
# Acordar o próprio personagem
  senao arg0.posicao > 4
    ret $mens.mp(m_perso_acordado, arg0)
  senao arg0.bitver & 0xC00 == 0x400
    ret $mens.mp(m_nao_perso, arg0, r)
  senao
    ret cmd_acordar(arg0, nulo), nulo
    listaobj l
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_acordar(arg0, nulo, arg0), nulo
    efim
    arg0.posicao = arg0.podeposicao(5) ? 5 : arg0.podeposicao(6) ? 6 : 8
    $mens.p(arg0)
    $mens.mtodos2(m_acorda_perso1, m_acorda_perso2)
    cmd_acordou(arg0, nulo)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_acordou(arg0, nulo, arg0)
    efim
  fimse


classe m_cmd_dormir
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Alvo não existe"
const tipo2 = "opc_linha"
const vari2 = "m_naover"
#
const nome3 = "Levantar"
const tipo3 = "opc_linha"
const vari3 = "m_levantar"
#
const nome4 = "Não consegue"
const tipo4 = "opc_linha"
const vari4 = "m_nao_consegue"
#
const nome5 = "Item impróprio"
const tipo5 = "opc_linha"
const vari5 = "m_nao_item"
#
const nome6 = "Sem lugar"
const tipo6 = "opc_linha"
const vari6 = "m_cheio"
#
const nome7 = "Dorme no chão perso"
const tipo7 = "opc_linha"
const vari7 = "m_dormir_chao1"
#
const nome8 = "Dorme no chão outros"
const tipo8 = "opc_linha"
const vari8 = "m_dormir_chao2"
#
const nome9 = "Dorme em item perso"
const tipo9 = "opc_linha"
const vari9 = "m_dormir_item1"
#
const nome10 = "Dorme em item outros"
const tipo10 = "opc_linha"
const vari10 = "m_dormir_item2"


classe m_cmd_acordar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Alvo não existe"
const tipo2 = "opc_linha"
const vari2 = "m_naover"
#
const nome3 = "Perso acordado"
const tipo3 = "opc_linha"
const vari3 = "m_perso_acordado"
#
const nome4 = "Alvo acordado"
const tipo4 = "opc_linha"
const vari4 = "m_alvo_acordado"
#
const nome5 = "Não consegue perso"
const tipo5 = "opc_linha"
const vari5 = "m_nao_consegue1"
#
const nome6 = "Não consegue outros"
const tipo6 = "opc_linha"
const vari6 = "m_nao_consegue2"
#
const nome7 = "Você não consegue"
const tipo7 = "opc_linha"
const vari7 = "m_nao_perso"
#
const nome8 = "Você acorda perso"
const tipo8 = "opc_linha"
const vari8 = "m_acorda_perso1"
#
const nome9 = "Você acorda outros"
const tipo9 = "opc_linha"
const vari9 = "m_acorda_perso2"
#
const nome10 = "Acordou perso"
const tipo10 = "opc_linha"
const vari10 = "m_acorda_alvo1"
#
const nome11 = "Acordou alvo"
const tipo11 = "opc_linha"
const vari11 = "m_acorda_alvo2"
#
const nome12 = "Acordou outros"
const tipo12 = "opc_linha"
const vari12 = "m_acorda_alvo3"
