classe h_cmd_autoapagar
herda comando_comum
const objmenu = $m_cmd_autoapagar
const objtipo = 2 # Pode renomear
const objnivel = 4
const posic = 0
#
const m_ajuda = "-t\n\
-s\n\
Apaga definitivamente o seu personagem, retirando também você do jogo.\n\
Após digitar esse comando, é necessário confirmar e digitar a senha atual\n\
do seu personagem"
const m_confirma = "Deseja realmente apagar o personagem $r? Tecle S ou N"
const m_senha = "Digite a senha ou tecle Z para cancelar"
const m_senha_erro = "Senha incorreta; autoapagar cancelado"
const m_cancelar = "Autoapagar cancelado"
const m_erro = "Não foi possível apagar o personagem."
const admordem = "m_ajuda m_confirma m_senha m_senha_erro m_cancelar m_erro"

func escr
  ret arg0.senha, passo(arg0.sock, "\b")
  arg0.msg("Seu personagem não está salvo. Basta sair do MUD teclando SAIR.")

func passo
  arg0.passo = este
  arg1 == "\b" && (arg0.tpasso = "1")
  refvar p = arg0.perso
# Pergunta se quer apagar o personagem
  se arg0.tpasso == "1"
    se !arg1 || arg1 == "\b"
      $mens.mp(m_confirma, p)
    senao arg1 == "s"
      $mens.mp(m_senha, p)
      arg0.tpasso = "2"
    senao
      $mens.mp(m_cancelar, p)
      arg0.passo = ""
    fimse
    ret 1
  fimse
# Pede a senha para apagar o personagem
  arqsav sav1
  se !arg1
    $mens.mp(m_senha, p)
  senao arg1 == "z"
    $mens.mp(m_cancelar, p)
    arg0.passo = ""
  senao p.senhacod ? !sav1.senhacod(arg1, p.senha) : (arg1 !== p.senha)
    $mens.mp(m_senha_erro, p)
    arg0.passo = ""
  senao sav1.apagar("sav1/" + txt2(arg0.cnome) + ".sav")
    $misclog.autoapagar(arg0.nome + " N" + arg0.perso.pnivel)
    p.senha = ""
    apagar(p)
    $miscsav.apagousav(txt2(arg0.cnome))
  senao
    $mens.mp(m_erro, p)
    arg0.passo = ""
  fimse
  ret 1


classe m_cmd_autoapagar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Confirma"
const tipo2 = "opc_linha"
const vari2 = "m_confirma"
#
const nome3 = "Senha"
const tipo3 = "opc_linha"
const vari3 = "m_senha"
#
const nome4 = "Senha incorreta"
const tipo4 = "opc_linha"
const vari4 = "m_senha_erro"
#
const nome5 = "Cancelar"
const tipo5 = "opc_linha"
const vari5 = "m_cancelar"
#
const nome6 = "Erro ao apagar"
const tipo6 = "opc_linha"
const vari6 = "m_erro"
