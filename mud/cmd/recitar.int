classe h_cmd_recitar
herda comando_comebebe
const objmenu = $m_cmd_recitar
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n\
-s <objeto>\n\
-s <objeto> <alvo>\n\
Tenta recitar alguma coisa, geralmente um pergaminho.\n\
Conforme as magias contidas no pergaminho, é possível direcioná-las\n\
para um personagem ou mesmo um item."
const m_sem_args = "Recitar o quê?"
const m_naover = "Você não vê $m."
const m_nao_recita = "Não há nada para recitar em $o."
const m_embriaguez = "Você não consegue ler o que está escrito $e $o."
const m_nivel = "$O é complicad$u demais."
const m_recitou1 = "Você recita $o."
const m_recitou2 = "$P recita $o."
const admordem = "m_ajuda m_sem_args m_naover m_nao_recita m_embriaguez\n\
m_nivel m_recitou1 m_recitou2"

func escr
  listaobj l
# Obtém o objeto
  misc:separa(arg1) # Separa nome do objeto do nome do alvo
  txt100 nome = misc:sep2
  ret !arg1, $mensjog.msg(m_sem_args, arg0)
  ref r = misc:itemdentro(arg0, misc:sep1)
# Checa se consegue recitar
  se !r
    $mens.mens = misc:sep1
    ret $mens.mp(m_naover, arg0)
  senao r.f_tipo != 4 # Checa se pode recitar
    ret $mens.mp(m_nao_recita, arg0, nulo, r)
  senao arg0.p_bebida >= $miscfome.cheio / 2
    ret $mens.mp(m_embriaguez, arg0, nulo, r)
  senao int(r.pnivel) > arg0.pnivel
    ret $mens.mp(m_nivel, arg0, nulo, r)
  senao
    refvar f = txt(arg0.dono.s_falar(arg0))
    ret f, arg0.msg(f)
  fimse
# Eventos
  ret cmd_recitar(arg0, r, nulo, nome), nulo
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_recitar(arg0, r, arg0, nome), nulo
  efim
  epara l.addfim(r.evento), l, l.ini.remove
    ret l.objini.cmd_recitar(arg0, r, r, nome), nulo
  efim
# Recita o objeto
  $mens.p(arg0, nulo, r)
  $mens.mvis2(r.f_msgperso ?? m_recitou1, r.f_msgoutros ?? r.f_msgperso ?? m_recitou2)
  r.apagar(1)
  efeito(arg0, nome, r)
# Eventos
  cmd_recitou(arg0, r, nulo, nome)
  epara l.addfim(arg0.evento), l, l.ini.remove
    l.objini.cmd_recitou(arg0, r, arg0, nome)
  efim
  epara l.addfim(r.evento), l, l.ini.remove
    l.objini.cmd_recitou(arg0, r, r, nome)
  efim


classe m_cmd_recitar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem argumentos"
const tipo2 = "opc_linha"
const vari2 = "m_sem_args"
#
const nome3 = "Sem alvo"
const tipo3 = "opc_linha"
const vari3 = "m_naover"
#
const nome4 = "Não recita"
const tipo4 = "opc_linha"
const vari4 = "m_nao_recita"
#
const nome5 = "Embriagado"
const tipo5 = "opc_linha"
const vari5 = "m_embriaguez"
#
const nome6 = "Nível insuficiente"
const tipo6 = "opc_linha"
const vari6 = "m_nivel"
#
const nome7 = "Recitou perso"
const info7 = "Quando não houver mensagem no menu \"Fome e sede\" do item"
const tipo7 = "opc_linha"
const vari7 = "m_recitou1"
#
const nome8 = "Recitou alvo"
const info8 = "Quando não houver mensagem no menu \"Fome e sede\" do item"
const tipo8 = "opc_linha"
const vari8 = "m_recitou2"
