classe h_cmd_levantar
herda comando_comum
const objmenu = $m_cmd_levantar
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n-s\nFica em pé."
const m_em_pe = "\c9Você já está em pé.\b"
const m_montado = "\c9Você está montad$s $e $a.\b"
const m_levantou1 = "Você levantou-se."
const m_levantou2 = "$P levantou-se."
const admordem = "m_ajuda m_em_pe m_montado m_levantou1 m_levantou2"

func escr
# Checa posição
  casovar arg0.posicao
  casose "5" # Descansando
  casose "6" # Sentado
    ref r = arg0.poslugar.objlista
    sair !r.perso
    ret $mens.mp(m_montado, arg0, r)
  casose "8" # Em pé
    ret $mens.mp(m_em_pe, arg0)
  casose # Outras posições
    ret arg0.msgposicao
  casofim
# Ficar em pé
  ret cmd_levantar(arg0), nulo
  listaobj l
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_levantar(arg0), nulo
  efim
  arg0.poslugar.remove
  arg0.posicao = 8
  $mens.p(arg0)
  $mens.mtodos2(m_levantou1, m_levantou2)
  cmd_levantou(arg0)
  epara l.addfim(arg0.evento), l, l.ini.remove
    l.objini.cmd_levantou(arg0)
  efim


classe h_cmd_sentar
herda comando_comum
const objmenu = $m_cmd_sentar
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n\
-s [objeto]\n\
Senta-se no chão. Com o nome de um objeto, tenta sentar no objeto."
const m_sentado = "\c9Você já está sentad$s.\b"
const m_nao_consegue = "Você não consegue sentar."
const m_levantar = "\c9Talvez você deva se levantar primeiro.\b"
const m_item_nao = "Não é possível sentar $e $o."
const m_item_cheio = "Não cabe mais ninguém $e $o."
const m_naover = "Você não vê $m."
const m_carregando = "Você está carregando alguém."
const m_sentou_item1 = "Você sentou-se $e $o."
const m_sentou_item2 = "$P sentou-se $e $o."
const m_sentou_chao1 = "Você sentou-se."
const m_sentou_chao2 = "$P sentou-se."
const admordem = "m_ajuda m_sentado m_nao_consegue m_levantar m_item_nao\n\
m_item_cheio m_naover m_carregando m_sentou_item1 m_sentou_item2\n\
m_sentou_chao1 m_sentou_chao2"

func escr
  ref r
# Checa posição
  se !arg0.podeposicao(6)
    refvar m = arg0.podeposicao6msg ?? $t_[arg0.tipo1].podeposicao6msg
    ret $mens.mp(m ?? m_nao_consegue, arg0)
  fimse
  casovar arg0.posicao
  casose "5" # Descansando
    r = arg0.poslugar.objlista
    sair
  casose "6" # Sentado
    ret $mens.mp(m_sentado, arg0)
  casose "8" # Em pé
    sair
  casose # Outras posições
    ret arg0.msgposicao
  casofim
# Obtém aonde quer se sentar
  se arg1
    refvar obj = misc:itemdono(arg0, arg1)
    ret r && r != obj, $mens.mp(m_levantar, arg0)
    r = obj
  fimse
  listaobj l
# Sentar em algum objeto
  se r
    se arg0.persolugar.total
      ret $mens.mp(m_carregando, arg0)
    senao !r.i_movel(arg0)
      ret $mens.mp(m_item_nao, arg0, nulo, r)
    senao r != arg0.poslugar.objlista && int(r.i_lugar) <= r.persolugar.total
      ret $mens.mp(m_item_cheio, arg0, nulo, r)
    fimse
    ret cmd_sentarobj(arg0, r), nulo
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_sentarobj(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_sentarobj(arg0, r, r), nulo
    efim
    arg0.poslugar.remove
    arg0.poslugar = r.persolugar.addfim(arg0)
    arg0.posicao = 6
    $mens.p(arg0, nulo, r)
    $mens.mtodos2(m_sentou_item1, m_sentou_item2)
    cmd_sentouobj(arg0, r)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_sentouobj(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_sentouobj(arg0, r, r)
    efim
# Sentar-se num objeto que não existe
  senao arg1
    $mens.mens = arg1
    $mens.mp(m_naover, arg0)
# Sentar-se no chão
  senao
    ret cmd_sentarchao(arg0), nulo
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_sentarchao(arg0), nulo
    efim
    arg0.poslugar.remove
    arg0.posicao = 6
    $mens.p(arg0)
    $mens.mtodos2(m_sentou_chao1, m_sentou_chao2)
    cmd_sentouchao(arg0)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_sentouchao(arg0)
    efim
  fimse


classe h_cmd_descansar
herda comando_comum
const objmenu = $m_cmd_descansar
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n\
-s [objeto]\n\
Descansa no chão. Com o nome de um objeto, tenta descansar no objeto."
const m_descansando = "\c9Você já está descansando.\b"
const m_nao_consegue = "Você não consegue descansar."
const m_levantar = "\c9Talvez você deva se levantar primeiro.\b"
const m_item_nao = "Não é possível descansar $e $o."
const m_item_cheio = "Não cabe mais ninguém $e $o."
const m_naover = "Você não vê $m."
const m_carregando = "Você está carregando alguém."
const m_descansou_item1 = "Você descansa $e $o."
const m_descansou_item2 = "$P descansa $e $o."
const m_descansou_chao1 = "Você descansa."
const m_descansou_chao2 = "$P descansa."
const admordem = "m_ajuda m_descansando m_nao_consegue m_levantar\n\
m_item_nao m_item_cheio m_naover m_carregando\n\
m_descansou_item1 m_descansou_item2 m_descansou_chao1 m_descansou_chao2"

func escr
  ref r
# Checa posição
  se !arg0.podeposicao(5)
    refvar m = arg0.podeposicao5msg ?? $t_[arg0.tipo1].podeposicao5msg
    ret $mens.mp(m ?? m_nao_consegue, arg0)
  fimse
  casovar arg0.posicao
  casose "5" # Descansando
    ret $mens.mp(m_descansando, arg0)
  casose "6" # Sentado
    r = arg0.poslugar.objlista
    sair
  casose "8" # Em pé
    sair
  casose # Outras posições
    ret arg0.msgposicao
  casofim
# Obtém aonde quer descansar
  se arg1
    ref i = misc:itemdono(arg0, arg1)
    ret r && r != i, $mens.mp(m_levantar, arg0)
    r = i
  fimse
  listaobj l
# Descansar em algum objeto
  se r
    se arg0.persolugar.total
      ret $mens.mp(m_carregando, arg0)
    senao !r.i_movel(arg0)
      ret $mens.mp(m_item_nao, arg0, nulo, r)
    senao r != arg0.poslugar.objlista && int(r.i_lugar) <= r.persolugar.total
      ret $mens.mp(m_item_cheio, arg0, nulo, r)
    fimse
    ret cmd_descansarobj(arg0, r), nulo
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_descansarobj(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_descansarobj(arg0, r, r), nulo
    efim
    arg0.poslugar.remove
    arg0.poslugar = r.persolugar.addfim(arg0)
    arg0.posicao = 5
    $mens.p(arg0, nulo, r)
    $mens.mtodos2(m_descansou_item1, m_descansou_item2)
    cmd_descansouobj(arg0, r)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_descansouobj(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_descansouobj(arg0, r, r)
    efim
# Descansar num objeto que não existe
  senao arg1
    $mens.mens = arg1
    $mens.mp(m_naover, arg0)
# Descansar no chão
  senao
    ret cmd_descansarchao(arg0), nulo
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_descansarchao(arg0), nulo
    efim
    arg0.poslugar.remove
    arg0.posicao = 5
    $mens.p(arg0)
    $mens.mtodos2(m_descansou_chao1, m_descansou_chao2)
    cmd_descansouchao(arg0)
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_descansouchao(arg0)
    efim
  fimse


classe m_cmd_levantar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Está em pé"
const tipo2 = "opc_linha"
const vari2 = "m_em_pe"
#
const nome3 = "Montado"
const tipo3 = "opc_linha"
const vari3 = "m_montado"
#
const nome4 = "Levantou perso"
const tipo4 = "opc_linha"
const vari4 = "m_levantou1"
#
const nome5 = "Levantou outros"
const tipo5 = "opc_linha"
const vari5 = "m_levantou2"


classe m_cmd_sentar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem alvo"
const tipo2 = "opc_linha"
const vari2 = "m_naover"
#
const nome3 = "Você carregando"
const tipo3 = "opc_linha"
const vari3 = "m_carregando"
#
const nome4 = "Sentado"
const tipo4 = "opc_linha"
const vari4 = "m_sentado"
#
const nome5 = "Não consegue"
const tipo5 = "opc_linha"
const vari5 = "m_nao_consegue"
#
const nome6 = "Levantar"
const tipo6 = "opc_linha"
const vari6 = "m_levantar"
#
const nome7 = "Não é possível"
const tipo7 = "opc_linha"
const vari7 = "m_item_nao"
#
const nome8 = "Item cheio"
const tipo8 = "opc_linha"
const vari8 = "m_item_cheio"
#
const nome9 = "Sentou chão perso"
const tipo9 = "opc_linha"
const vari9 = "m_sentou_chao1"
#
const nome10 = "Sentou chão outros"
const tipo10 = "opc_linha"
const vari10 = "m_sentou_chao2"
#
const nome11 = "Sentou item perso"
const tipo11 = "opc_linha"
const vari11 = "m_sentou_item1"
#
const nome12 = "Sentou item outros"
const tipo12 = "opc_linha"
const vari12 = "m_sentou_item2"


classe m_cmd_descansar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem alvo"
const tipo2 = "opc_linha"
const vari2 = "m_naover"
#
const nome3 = "Você carregando"
const tipo3 = "opc_linha"
const vari3 = "m_carregando"
#
const nome4 = "Descansando"
const tipo4 = "opc_linha"
const vari4 = "m_descansando"
#
const nome5 = "Não consegue"
const tipo5 = "opc_linha"
const vari5 = "m_nao_consegue"
#
const nome6 = "Levantar"
const tipo6 = "opc_linha"
const vari6 = "m_levantar"
#
const nome7 = "Não é possível"
const tipo7 = "opc_linha"
const vari7 = "m_item_nao"
#
const nome8 = "Item cheio"
const tipo8 = "opc_linha"
const vari8 = "m_item_cheio"
#
const nome9 = "Descansou chão perso"
const tipo9 = "opc_linha"
const vari9 = "m_descansou_chao1"
#
const nome10 = "Descansou chão outros"
const tipo10 = "opc_linha"
const vari10 = "m_descansou_chao2"
#
const nome11 = "Descansou item perso"
const tipo11 = "opc_linha"
const vari11 = "m_descansou_item1"
#
const nome12 = "Descansou item outros"
const tipo12 = "opc_linha"
const vari12 = "m_descansou_item2"
