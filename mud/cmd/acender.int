classe cmd_acender
herda comando_comum
const txtajuda = "\b\c3Acender\b\n\
Sintaxe: ACENDER <objeto>\n\
Acende um objeto (um fósforo, uma lamparina, etc)."
const posic = 5
#
const m_sem_args = "Acender o quê?"

func escr
  listaobj l
  nomeobj n # Para reconhecer os itens
  textotxt nomes # Nomes dos itens pegos
  ret !arg1, $mensjog.msg(m_sem_args, arg0)
  n.ini(arg1, 1)
  epara l.addfim(arg0.dentro1, arg0.dono.dentro1), l, l.ini.remove
    continuar !l.objini.visivel(arg0) || !n.nome(l.objini.ident, l.objini.objtot)
    ref r
    r = l.objini
# Checa se pode acender
    se !r.luzitem || r.luzitem == 3
      ret $mens.mp("Você não consegue acender $o.", arg0, nulo, r)
    senao !r.luztempo
      ret $mens.mp("$O não acende mais.", arg0, nulo, r)
    senao r.luztempo > 0
      ret $mens.mp("$O já está aces$u.", arg0, nulo, r)
    senao r.luzitem == 6 # Se requer fogo para acender
      l.limpar
      epara l.addfim(arg0.dentro1, arg0.dono.dentro1), l, l.ini.remove
        sair int(l.objini.luzitem) >= 3 && int(l.objini.luztempo) > 0
      efim
      ret !l.objini, $mens.mp("Você precisa de fogo para acender $o.", arg0, nulo, r)
    senao r.luzitem == 7 # Se acende ao vestir
      ret $mens.mp("Você acenderá $o quando usar.", arg0, nulo, r)
    fimse
# Eventos
    l.limpar
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_acender(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_acender(arg0, r, r), nulo
    efim
# Acende
    r.luztempo.pos, r.recalc = 1, r.ajustapeso
    $mens.p(arg0, nulo, r)
    $mens.mvis1(r.luzliga ? r.luzliga : "$P acende $o.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_acendeu(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_acendeu(arg0, r, r)
    efim
    ret
  efim
  $mens.mens = arg1
  $mens.mp("Você não vê $m.", arg0)


classe cmd_apagar
herda comando_comum
const txtajuda = "\b\c3Apagar\b\n\
Sintaxe: APAGAR <objeto>\n\
Apaga um objeto (um fósforo, uma lamparina, etc)."
const posic = 5

func escr
  listaobj l
  nomeobj n # Para reconhecer os itens
  textotxt nomes # Nomes dos itens pegos
  ret !arg1, arg0.msg("Apagar o quê?")
  n.ini(arg1, 1)
  epara l.addfim(arg0.dentro1, arg0.dono.dentro1), l, l.ini.remove
    continuar !l.objini.visivel(arg0) || !n.nome(l.objini.ident, l.objini.objtot)
    ref r
    r = l.objini
# Checa se pode apagar
    se !r.luzitem || r.luzitem == 3
      ret $mens.mp("Você não consegue apagar $o.", arg0, nulo, r)
    senao !r.luztempo || r.luztempo < 0
      ret $mens.mp("$O já está apagad$u.", arg0, nulo, r)
    fimse
# Eventos
    l.limpar
    epara l.addfim(arg0.evento), l, l.ini.remove
      ret l.objini.cmd_apagar(arg0, r, arg0), nulo
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      ret l.objini.cmd_apagar(arg0, r, r), nulo
    efim
# Apaga
    r.luztipo == 5 ? (r.luztempo = 0) : r.luztempo.neg, r.recalc = 1, r.ajustapeso
    $mens.p(arg0, nulo, r)
    $mens.mvis1("$P apaga $o.")
    epara l.addfim(arg0.evento), l, l.ini.remove
      l.objini.cmd_apagou(arg0, r, arg0)
    efim
    epara l.addfim(r.evento), l, l.ini.remove
      l.objini.cmd_apagou(arg0, r, r)
    efim
    ret
  efim
  $mens.mens = arg1
  $mens.mp("Você não vê $m.", arg0)
