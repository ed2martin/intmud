classe h_cmd_acender
herda comando_comum
const objmenu = $m_cmd_acender
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n\-s <objeto>\nAcende um objeto (um fósforo, uma lamparina, etc)."
const m_sem_args = "Acender o quê?"
const m_naover = "Você não vê $m."
const m_naoconsegue = "Você não consegue acender $o."
const m_naoacende = "$O não acende mais."
const m_estaaceso = "$O já está aces$u."
const m_acendefogo = "Você precisa de fogo para acender $o."
const m_acendeusar = "Você acenderá $o quando usar."
const m_acende1 = "Você acende $o."
const m_acende2 = "$P acende $o."
const admordem = "m_ajuda m_sem_args m_naover m_naoconsegue m_naoacende m_estaaceso\n\
m_acendefogo m_acendeusar m_acende1 m_acende2"

func escr
  listaobj l
  textotxt nomes # Nomes dos itens pegos
  ret !arg1, $mensjog.msg(m_sem_args, arg0)
  $mens.mens = arg1
  ref r = misc:itemtudo(arg0, arg1)
  ret !r, $mens.mp(m_naover, arg0)
# Checa se pode acender
  se !r.luzitem || r.luzitem == 3
    ret $mens.mp(m_naoconsegue, arg0, nulo, r)
  senao !r.luztempo
    ret $mens.mp(m_naoacende, arg0, nulo, r)
  senao r.luztempo > 0
    ret $mens.mp(m_estaaceso, arg0, nulo, r)
  senao r.luzitem == 6 # Se requer fogo para acender
    epara l.addfim(arg0.dentro1, arg0.dono.dentro1), l, l.ini.remove
      sair int(l.objini.luzitem) >= 3 && int(l.objini.luztempo) > 0
    efim
    ret !l.objini, $mens.mp(m_acendefogo, arg0, nulo, r)
  senao r.luzitem == 7 # Se acende ao vestir
    ret $mens.mp(m_acendeusar, arg0, nulo, r)
  fimse
# Eventos
  ret cmd_acender(arg0, r), nulo
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_acender(arg0, r, arg0), nulo
  efim
  epara l.addfim(r.evento), l, l.ini.remove
    ret l.objini.cmd_acender(arg0, r, r), nulo
  efim
# Acende
  r.luztempo.pos, r.recalc = 1, r.ajustapeso
  $mens.p(arg0, nulo, r)
  $mens.mvis2(r.luzliga1 ?? m_acende1, r.luzliga2 ?? m_acende2)
  cmd_acendeu(arg0, r)
  epara l.addfim(arg0.evento), l, l.ini.remove
    l.objini.cmd_acendeu(arg0, r, arg0)
  efim
  epara l.addfim(r.evento), l, l.ini.remove
    l.objini.cmd_acendeu(arg0, r, r)
  efim


classe h_cmd_apagar
herda comando_comum
const objmenu = $m_cmd_apagar
const objtipo = 2 # Pode renomear
const posic = 5
#
const m_ajuda = "-t\n\-s <objeto>\nApaga um objeto (um fósforo, uma lamparina, etc)."
const m_sem_args = "Apagar o quê?"
const m_naover = "Você não vê $m."
const m_naoconsegue = "Você não consegue apagar $o."
const m_apagado = "$O já está apagad$u."
const m_apaga1 = "Você apaga $o."
const m_apaga2 = "$P apaga $o."
const admordem = "m_ajuda m_sem_args m_naover m_naoconsegue m_apagado m_apaga1 m_apaga2"

func escr
  listaobj l
  textotxt nomes # Nomes dos itens pegos
  ret !arg1, $mensjog.msg(m_sem_args, arg0)
  $mens.mens = arg1
  ref r = misc:itemtudo(arg0, arg1)
  ret !r, $mens.mp(m_naover, arg0)
# Checa se pode apagar
  se !r.luzitem || r.luzitem == 3
    ret $mens.mp(m_naoconsegue, arg0, nulo, r)
  senao !r.luztempo || r.luztempo < 0
    ret $mens.mp(m_apagado, arg0, nulo, r)
  fimse
# Eventos
  ret cmd_apagar(arg0, r), nulo
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_apagar(arg0, r, arg0), nulo
  efim
  epara l.addfim(r.evento), l, l.ini.remove
    ret l.objini.cmd_apagar(arg0, r, r), nulo
  efim
# Apaga
  r.luztipo == 5 ? (r.luztempo = 0) : r.luztempo.neg, r.recalc = 1, r.ajustapeso
  $mens.p(arg0, nulo, r)
  $mens.mvis2(m_apaga1, m_apaga2)
  cmd_apagou(arg0, r)
  epara l.addfim(arg0.evento), l, l.ini.remove
    l.objini.cmd_apagou(arg0, r, arg0)
  efim
  epara l.addfim(r.evento), l, l.ini.remove
    l.objini.cmd_apagou(arg0, r, r)
  efim



classe m_cmd_acender
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem argumentos"
const tipo2 = "opc_linha"
const vari2 = "m_sem_args"
#
const nome3 = "Sem alvo"
const tipo3 = "opc_linha"
const vari3 = "m_naover"
#
const nome4 = "Não consegue"
const tipo4 = "opc_linha"
const vari4 = "m_naoconsegue"
#
const nome5 = "Não acende mais"
const tipo5 = "opc_linha"
const vari5 = "m_naoacende"
#
const nome6 = "Já está aceso"
const tipo6 = "opc_linha"
const vari6 = "m_estaaceso"
#
const nome7 = "Acende com fogo"
const tipo7 = "opc_linha"
const vari7 = "m_acendefogo"
#
const nome8 = "Acende ao usar"
const tipo8 = "opc_linha"
const vari8 = "m_acendeusar"
#
const nome9 = "Acende perso"
const tipo9 = "opc_linha"
const vari9 = "m_acende1"
#
const nome10 = "Acende outros"
const tipo10 = "opc_linha"
const vari10 = "m_acende2"


classe m_cmd_apagar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem argumentos"
const tipo2 = "opc_linha"
const vari2 = "m_sem_args"
#
const nome3 = "Sem alvo"
const tipo3 = "opc_linha"
const vari3 = "m_naover"
#
const nome4 = "Não consegue"
const tipo4 = "opc_linha"
const vari4 = "m_naoconsegue"
#
const nome5 = "Está apagado"
const tipo5 = "opc_linha"
const vari5 = "m_apagado"
#
const nome6 = "Apaga perso"
const tipo6 = "opc_linha"
const vari6 = "m_apaga1"
#
const nome7 = "Apaga outros"
const tipo7 = "opc_linha"
const vari7 = "m_apaga2"
