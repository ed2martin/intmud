classe h_cmd_descarregar
herda comando_comum
const classemenu = "m_cmd_descarregar"
const posic = 5
const m_ajuda = "-t\n-s\nDescarrega a arma que você estiver empunhando."
#
const m_sem_arma = "Você não está empunhando nenhuma arma."
const m_sem_municao = "$O não usa munição"
const m_muita_coisa = "Você não consegue carregar tanta coisa."
const m_descarregado = "$O não está carregad$u."
const m_conhecimento = "Não possui conhecimento suficiente em $m."
const m_nao_pode = "$O não pode ser descarregad$u."
const m_descarrega1 = "Você descarrega $o."
const m_descarrega2 = "$P descarrega $o."
const admordem = "m_ajuda m_sem_arma m_sem_municao m_muita_coisa\n\
m_descarregado m_conhecimento m_nao_pode m_descarrega1 m_descarrega2"

func escr
  refvar perm_msg = $perm_[arg0.jogperm].item
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  refvar arma = ref(arg0.iempu.obj)
  se !arma
    $mens.mp(m_sem_arma, arg0)
  senao !arma.armatipo || int(arma.armamunicao) != 2
    $mens.mp(m_sem_municao, arg0, nulo, arma)
  senao !arma.armacarregar
    $mens.mp(m_nao_pode, arg0, nulo, arma)
  senao
    refvar tipo = txtsublin(config:arma_txt, arma.armatipo, 1)
    se arg0.var.h_[tipo]_ < arma.armacarregar
      $mens.mens = tipo
      $mens.mp(m_conhecimento, arg0, nulo, arma)
    senao arma.dentro1.total == 0
      $mens.mp(m_descarregado, arg0, nulo, arma)
    senao
      listaobj l
      epara l.addfim(arma.dentro1), l, l.ini.remove
        continuar descarregar(arg0, arma, l.objini, l.objini.objtot)
        ret $mens.mp(m_muita_coisa, arg0)
      efim
      $mens.p(arg0, nulo, arma)
      $mens.mvis2(m_descarrega1, m_descarrega2)
    fimse
  fimse

func descarregar # Descarrega arma, tirando um item
# arg0=personagem, arg1=arma, arg2=item, arg3=quantidade de itens
# Retorna 1 se descarregou ou 0 se o personagem não consegue carregar tanta coisa
  se arg0.dentro1.total + (arg2.item == 2 ? 1 : arg3) > arg0.objmax
    ret 0
  senao arg0.volden + arg2.voltot * arg3 / arg2.objtot > arg0.volmax
    ret 0
  fimse
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_descarregarobj(arg0, arg1, arg2, arg3, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_descarregarobj(arg0, arg1, arg2, arg3, arg1), nulo
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    ret e.objini.cmd_descarregarobj(arg0, arg1, arg2, arg3, arg2), nulo
  efim
  ref r = arg2.mudadono(arg0, arg3)
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_descarregouobj(arg0, arg1, r, total, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_descarregouobj(arg0, arg1, r, total, arg1)
  efim
  epara e.addfim(r.evento), e, e.ini.remove
    e.objini.cmd_descarregouobj(arg0, arg1, r, total, r)
  efim
  arg0.p_espera += 2
  ret 1


classe m_cmd_descarregar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem arma"
const tipo2 = "opc_linha"
const vari2 = "m_sem_arma"
#
const nome3 = "Sem munição"
const tipo3 = "opc_linha"
const vari3 = "m_sem_municao"
#
const nome4 = "Muita coisa"
const tipo4 = "opc_linha"
const vari4 = "m_muita_coisa"
#
const nome5 = "Está descarregado"
const tipo5 = "opc_linha"
const vari5 = "m_descarregado"
#
const nome6 = "Sem conhecimento"
const tipo6 = "opc_linha"
const vari6 = "m_conhecimento"
#
const nome7 = "Não pode descarregar"
const tipo7 = "opc_linha"
const vari7 = "m_nao_pode"
#
const nome8 = "Descarrega perso"
const tipo8 = "opc_linha"
const vari8 = "m_descarrega1"
#
const nome9 = "Descarrega outros"
const tipo9 = "opc_linha"
const vari9 = "m_descarrega1"
