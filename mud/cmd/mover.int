classe h_comando_mover_cfg
herda comando_comum
const m_impossivel = "Impossível seguir nessa direção."
const m_portafechada = "$P est$o fechad$a."
const m_itemfechado = "$O está fechad$u."
const m_espaco = "Não tem espaço para seguir nessa direção."
const m_flutuando = "Você não consegue porque não está flutuando."
const m_cansado = "Você está cansad$s demais."
const m_animcansado = "$A está cansad$t demais."
const m_seguir = "Você segue $a."
const m_ignorar = "$A não quer obedecer."
const m_escuro = "Está escuro..."
const m_cego = "Você está ceg$s."
const m_dormindo = "Você vê estranhas imagens em seus sonhos..."
const admordem = "m_impossivel m_portafechada m_itemfechado m_espaco m_flutuando\n\
m_cansado m_animcansado m_seguir m_ignorar m_escuro m_cego m_dormindo"


classe comando_mover
herda comando_comum
const objtipo = 1 # Pode criar atalho
const posic = 6 # Posição mínima para usar o comando
const m_entra1 = "$P chegou."
const m_entra2 = m_entra1
const m_entra3 = m_entra1
const m_sai1 = "$P foi embora."
const m_sai2 = m_sai1
const m_sai3 = m_sai1
const m_fugir1 = "Você fugiu."
const m_fugir2 = "$P fugiu."
const dir = ""
listaobj lseg # Para não seguir ciclicamente
int8 andou # Se conseguiu andar (deve fechar a porta)
const admordem = "m_porta m_sai1 m_sai2 m_sai3 m_sai4 m_fugir1 m_fugir2\n\
m_entra1 m_entra2 m_entra3 m_entra4"

func escr # Executa o comando
# arg0 = personagem
# arg1 = o que digitou após a direção
  $comando_mover.lseg.limpar
  $comando_mover.andou = 0
  ref sala = arg0.dono
# Anda
  txt100 dir1
  ref obj = objmover2(arg0) # Personagem ou item que vai mover em uma direção
  ret !obj, nulo
  se !arg0.jog || rand($miscfome.bebedir, $miscfome.cheio) > arg0.p_bebida
    dir1 = dir
    refvar saladest = obj.dono.dirsala(dir1, obj)
    ret !mover(obj, saladest, nulo, obj.dono.diritem(dir1, obj), arg0), nulo
  senao # Bêbado andando em uma direção aleatória
    dir1 = txt(fugirdir(obj)) ?? dir
    refvar saladest = obj.dono.dirsala(dir1, obj)
    ret !$cmd_[dir1].mover(obj, saladest, nulo, nulo, arg0), nulo
  fimse
# Fecha a porta
  se sala.fecha_[dir1] && sala.porta_[dir1] < 2 && $comando_mover.andou
    sala.mudaporta2(dir1, sala.tranc_[dir1](obj) == 2 ? 3 : 2)
  fimse
# Próximo comando
  arg1 && arg0.sock.comandos.addini(arg1 + "\n" + arg0.sock.perm)

func objmover1 # Retorna o objeto que deve se movimentar ou nulo se deve ficar parado
# Não envia mensagens
# arg0 = personagem que digitou o comando
  ret arg0.item, arg0
  ret !arg0.poslugar, arg0.posicao >= 8 ? arg0
  ref obj = arg0.poslugar.objlista # Personagem ou item que vai mover em uma direção
  se !obj.perso # Não é personagem
    ret arg0.posicao != 7 && obj.i_mover(arg0) ? obj
  senao obj.tseguir == 3 # Animal tem dono
    ret obj.iseguir.objlista != arg0, nulo
  senao int(obj.p_montar_tipo) < 3 # Animal não tem dono e não obedece
    ret nulo
  fimse
  ret arg0.posicao != 7 && obj.posicao >= 8, obj

func objmover2 # Retorna o objeto que deve se movimentar ou nulo se deve ficar parado
# Se não puder se movimentar, envia mensagens
# arg0 = personagem que digitou o comando
  ret arg0.item, arg0
  ret !arg0.poslugar, arg0.posicao >= 8 ? arg0 : (arg0.msgposicao : nulo)
  ref obj = arg0.poslugar.objlista # Personagem ou item que vai mover em uma direção
  se !obj.perso # Não é personagem
    ret arg0.posicao != 7 && obj.i_mover(arg0), obj
    ret arg0.msgposicao : nulo
  senao obj.tseguir == 3 # Animal tem dono
    ref is = obj.iseguir.objlista
    ret is != arg0, $mens.mp(comando_mover_cfg:m_ignorar, arg0, obj) : nulo
  senao int(obj.p_montar_tipo) < 3 # Animal não tem dono e não obedece
    ret $mens.mp(comando_mover_cfg:m_ignorar, arg0, obj) : nulo
  fimse
  ret arg0.posicao != 7 && obj.posicao >= 8, obj
  arg0.posicao == 7 ? arg0.msgposicao : obj.msgposicao(arg0)

func mover # Personagem anda em uma direção
# arg0 = personagem ou item que quer se mover
# arg1 = sala destino (se não for sala, não deixa prosseguir nessa direção)
# arg2 = item aonde está entrando (na sala origem) ou nulo
# arg3 = item de onde está saindo (na sala destino) ou nulo
# arg4 = personagem no controle e para quem enviar mensagens; se nulo assume arg0
# Retorna 1 se conseguiu mover ou 0 se não conseguiu
  refvar p1 = arg4 ?? arg0
  refvar perm_msg = $perm_[p1.jogperm].andar
  se perm_msg
    $mensjog.msg(perm_msg, p1)
    ret 0
  fimse
  listaobj l
  ref s1 = arg0.dono # Sala aonde está
  debug dbg
  dbg.ini
  $comando_mover.lseg.addfim(arg0)
# Verifica se pode mover e acerta textos de abriu1 e abriu2
  txt300 abriu1 # Mensagem, se abriu a porta da casa
  txt300 abriu2
  se !arg1.s_sala
    $mens.mp(comando_mover_cfg:m_impossivel, p1)
    ret 0
  senao !arg0.j_imortal && (arg2 ? arg2.i_aberto >= 3 : s1.porta_[dir] & 3 >= 2)
    ret !s1.socktot, 0
    refvar casa = $c_[arg2 ? arg2.i_fechadura(p1) : s1.fechadura_[dir](p1)]
    refvar cod = casa.chave
    listaitem i
    se cod
      epara i = arg0.dentro1.ini, i, i.depois
        sair cod == i.obj.i_chave && i.obj.visivel(arg0)
      efim
    fimse
    se !i
      se arg2
        $mens.mp(comando_mover_cfg:m_itemfechado, p1, nulo, arg2)
      senao
        p1.msg($mens_porta.mens(comando_mover_cfg:m_portafechada, s1, dir))
      fimse
      ret 0
    fimse
    casovar casa.infotaxa
    casose "2" # Casa bloqueada por falta de pagamento
      $mens.mp(config:casamsgbloq, p1)
      ret 0
    casose "1" # Atrasou pagamento da taxa
      $mens.mp(config:casamsgaviso, p1)
    casofim
    abriu1 = config:casaabriu1
    abriu2 = config:casaabriu2
  senao !arg0.cabedentro(arg1)
    $mens.mp(comando_mover_cfg:m_espaco, p1)
    ret 0
  senao !arg2 && s1.voar_[dir](arg0) && !(arg0.bitver & 512 || arg0.bitinv & 48)
    $mens.mp(comando_mover_cfg:m_flutuando, p1)
    ret 0
  fimse
# Verifica pontos de movimento
  uint8 total = s1.totalmove + arg1.totalmove
  !s1.visivel(arg0) && (total *= 2) # No escuro gasta mais movimentos
  total *= (2 + $miscfome.f.[arg0.p_sede] + $miscfome.f.[arg0.p_fome]) / 2
  (arg0.bitver & 512 || arg0.bitinv & 48) && (total /= 2) # Flutuando gasta menos movimentos
  se arg0.pmove < intdiv((total + 1) / 2)
    refvar m = p1 == arg0 ? comando_mover_cfg:m_cansado : comando_mover_cfg:m_animcansado
    $mens.mp(m, p1, arg0)
    ret 0
  fimse
# Eventos antes
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_mover(arg0, s1, dir, arg0, arg1, arg2, arg3, p1), 0
  efim
  epara l.addfim(s1.evento), l, l.ini.remove
    ret l.objini.cmd_mover(arg0, s1, dir, s1, arg1, arg2, arg3, p1), 0
  efim
  epara l.addfim(arg3.evento), l, l.ini.remove
    ret l.objini.cmd_mover(arg0, s1, dir, arg3, arg1, arg2, arg3, p1), 0
  efim
  epara l.addfim(arg1.evento), l, l.ini.remove
    ret l.objini.cmd_moverdest(arg0, s1, dir, arg1, arg1, arg2, arg3, p1), 0
  efim
  epara l.addfim(arg2.evento), l, l.ini.remove
    ret l.objini.cmd_moverdest(arg0, s1, dir, arg2, arg1, arg2, arg3, p1), 0
  efim
# Muda de sala
  $comando_mover.andou = 1
  !arg0.j_imortal && (arg0.pmove -= intdiv((total + 1) / 2))
  arg0.sock.cmd_espera = arg0.p_espera = intmin(30, total ?? 1) * 2
  se !(s1.socktot || arg1.socktot) # Não tem jogador por perto
    arg0.mudadono(arg1)
  senao arg0.p_bebida >= $miscfome.bebedir - rand(20) # Cambaleando não anda em silêncio
    refvar m1 = (!arg2 ? s1.msg_sai0_[dir])
    (abriu1 || m1) && $mens.mp(abriu1 + (abriu1 && m1 ? "\n") + m1, p1, nulo, arg2)
    refvar m2 = ((arg2 ? arg2.i_entraitem4 : s1.msg_sai4_[dir]) ?? m_sai4)
    msgsair(arg0, arg2, abriu2 + (abriu2 ? "\n") + m2, 1)
    arg2.i_entraitem0 && $mens.msg(arg2.i_entraitem0, arg0)
    arg0.mudadono(arg1)
    msgentrar(arg0, arg3, (arg3 ? arg3.i_saiitem4 : s1.msg_entra4_[dir]) ?? m_entra4)
  senao arg0.bitinv & 48 # Voando ou voando alto
    refvar m1 = (!arg2 ? s1.msg_sai0_[dir])
    (abriu1 || m1) && $mens.mp(abriu1 + (abriu1 && m1 ? "\n") + m1, p1, nulo, arg2)
    refvar m2 = ((arg2 ? arg2.i_entraitem3 : s1.msg_sai3_[dir]) ?? m_sai3)
    msgsair(arg0, arg2, abriu2 + (abriu2 ? "\n") + m2, 1)
    arg2.i_entraitem0 && $mens.msg(arg2.i_entraitem0, arg0)
    arg0.mudadono(arg1)
    msgentrar(arg0, arg3, (arg3 ? arg3.i_saiitem3 : s1.msg_entra3_[dir]) ?? m_entra3)
  senao arg0.bitver & 0x200 # Flutuando
    refvar m1 = (!arg2 ? s1.msg_sai0_[dir])
    (abriu1 || m1) && $mens.mp(abriu1 + (abriu1 && m1 ? "\n") + m1, p1, nulo, arg2)
    refvar m2 = ((arg2 ? arg2.i_entraitem2 : s1.msg_sai2_[dir]) ?? m_sai2)
    msgsair(arg0, arg2, abriu2 + (abriu2 ? "\n") + m2, 1)
    arg2.i_entraitem0 && $mens.msg(arg2.i_entraitem0, arg0)
    arg0.mudadono(arg1)
    msgentrar(arg0, arg3, (arg3 ? arg3.i_saiitem2 : arg0.s1.msg_entra2_[dir]) ?? m_entra2)
  senao # Não está voando nem flutuando
    refvar m1 = (!arg2 ? s1.msg_sai0_[dir])
    (abriu1 || m1) && $mens.mp(abriu1 + (abriu1 && m1 ? "\n") + m1, p1, nulo, arg2)
    refvar m2 = ((arg2 ? arg2.i_entraitem1 : s1.msg_sai1_[dir]) ?? m_sai1)
    msgsair(arg0, arg2, abriu2 + (abriu2 ? "\n") + m2, 1)
    arg2.i_entraitem0 && $mens.msg(arg2.i_entraitem0, arg0)
    arg0.mudadono(arg1)
    msgentrar(arg0, arg3, (arg3 ? arg3.i_saiitem1 : s1.msg_entra1_[dir]) ?? m_entra1)
  fimse
# Eventos depois
  epara l.addfim(arg0.evento), l, l.ini.remove
    l.objini.cmd_moveu(arg0, s1, dir, arg0, arg1, arg2, arg3, p1)
  efim
  epara l.addfim(s1.evento), l, l.ini.remove
    l.objini.cmd_moveu(arg0, s1, dir, s1, arg1, arg2, arg3, p1)
  efim
  epara l.addfim(arg3.evento), l, l.ini.remove
    l.objini.cmd_moveu(arg0, s1, dir, arg3, arg1, arg2, arg3, p1)
  efim
  epara l.addfim(arg1.evento), l, l.ini.remove
    l.objini.cmd_moveudest(arg0, s1, dir, arg1, arg1, arg2, arg3, p1)
  efim
  epara l.addfim(arg2.evento), l, l.ini.remove
    l.objini.cmd_moveudest(arg0, s1, dir, arg2, arg1, arg2, arg3, p1)
  efim
# Checa se morre ao entrar
  casovar arg1.s_morre(arg0)
  casose "1" # Morre
    sair arg0.j_imortal
    arg0.morreu
    ret 0
  casose "2" # Perde itens e dinheiro e morre
    sair arg0.j_imortal
    arg0.var.z_moedas = ""
  casose "3" # Perde itens e morre
    sair arg0.j_imortal
    epara l.addfim(arg0.dentro1, arg0.dentro2), l, l.ini.remove
      l.objini.item && !l.objini.manterperso && apagar(l.objini)
    efim
    arg0.morreu
    ret 0
  casose "4" # Perde dinheiro e morre
    sair arg0.j_imortal
    arg0.var.z_moedas = ""
    arg0.morreu
    ret 0
  casofim
# Seguir
# Nota: não verifica personagens sentados em um item; esses já andam com o item
  listaobj l2
  epara l2.addfim(arg0, arg0.persolugar), l2, l2.ini.remove
    epara l.addfim(l2.objini.lseguir), l, l.ini.remove
      refvar obj = objmover1(l.objini)
      se !obj || obj.dono != s1 || $comando_mover.lseg.possui(obj)
      senao obj.tseguir == 3 # Animal sempre segue o dono
        mover(obj, arg1, arg2, arg3, obj)
      senao l2.objini.visivel(obj) && s1.visivel(obj)
        se dir != "entrar"
          refvar saladest = s1.dirsala(dir, obj)
          se saladest # Nota: a sala destino pode mudar conforme o personagem
            $mens.mp(comando_mover_cfg:m_seguir, obj, l2.objini)
            mover(obj, saladest, arg2, arg3, obj)
          fimse
          continuar
        fimse
        $mens.mp(comando_mover_cfg:m_seguir, obj, l2.objini)
        mover(obj, arg1, arg2, arg3, obj)
      fimse
    efim
  efim
  ret 1

func msgsair # Envia mensagens do personagem saindo da sala
# arg0 = personagem
# arg1 = item aonde está entrando (na sala origem) ou nulo
# arg2 = mensagem saiu da sala origem
# arg3 = verdadeiro se não deve andar em silêncio
  listaobj l
  $mens.p(arg0, nulo, arg1)
  refvar ssai = arg0.bitinv & 0x30 ? arg0.p_som_voasai : arg0.p_som_sai
  txt100 carga = arg0.desccarga
  carga && (carga = arg0.perso ? " carregando " + carga : " com " + carga)
  l.addfim(arg0.dono.dentro2)
  l.remove(arg0, arg0.persolugar)
  se !arg3 && arg0.bitver & 256 # Anda em silêncio
    $mens.mens = " em silêncio" + carga
    epara nulo, l, l.ini.remove
      l.objini.bitver & 0x1000 && $mens.msgsomvis(arg2, ssai, l.objini)
    efim
  senao
    $mens.mens = carga
    epara nulo, l, l.ini.remove
      $mens.msgsomvis(arg2, ssai, l.objini)
    efim
  fimse

func msgentrar # Envia mensagens do personagem entrando na sala
# arg0 = personagem
# arg1 = item de onde está saindo (na sala destino) ou nulo
# arg2 = mensagem entrou na sala destino
# arg3 = verdadeiro se não deve andar em silêncio
  $mens.p(arg0, nulo, arg1)
  listaobj l
  refvar sent = arg0.bitinv & 0x30 ? arg0.p_som_voaentra : arg0.p_som_entra
  txt100 carga = arg0.desccarga
  l.addfim(arg0.dono.dentro2)
  l.remove(arg0, arg0.persolugar)
  se !arg3 && arg0.bitver & 256 # Anda em silêncio
    $mens.mens = " em silêncio" + (carga ? " carregando " + carga)
    epara nulo, l, l.ini.remove
      l.objini.bitver & 0x1000 && $mens.msgsomvis(arg2, sent, l.objini)
    efim
  senao
    $mens.mens = carga ? " carregando " + carga
    epara nulo, l, l.ini.remove
      $mens.msgsomvis(arg2, sent, l.objini)
    efim
  fimse

func fugirdir # Obtém direção para onde pode fugir
# arg0 = personagem
# Retorna: direção ou "" se não há para onde fugir
  refvar dono = ref(arg0.dono)
  txt100 t
  prog p
  epara p.inifunctudo(dono, "dir_"), p.lin, p.depois
    continuar !arg0.cabedentro(dono.dirsala(txt(p.texto, 4), arg0))
    refvar y = txt(p.texto, 4)
    !dono.fugir_[y](arg0) && int(dono.porta_[y]) < 2 && (t += y + " ")
  efim
  ret t ? txtsub(t, rand(intsub(t)), 1) : ""

func fugir # Foge nessa direção
# arg0 = personagem
# arg1 = sala destino ou nulo se deve obter a sala a partir do personagem
  ref r = arg1 ?? arg0.dono.dirsala(dir, arg0)
  ret !r, nulo
  ref perso = arg0.poslugar.objlista ?? arg0
  arg0.pmove -= 5
  arg0.batalhafim
  perso.batalhafim
  $mens.mens = ""
  $mens.mp(m_fugir1, perso)
  refvar mentra = arg0.dono.msg_entra1_[dir] ?? m_entra1
  msgsair(perso, nulo, m_fugir2, 1)
  perso.mudadono(r)
  msgentrar(perso, nulo, mentra, 1)


classe h_cmd_n
herda comando_mover
const m_sai1 = "$P foi para o norte$m."
const m_sai2 = "$P flutuou para o norte$m."
const m_sai3 = "$P voou para o norte$m."
const m_sai4 = "$P foi para o norte cambaleando."
const m_fugir1 = "Você fugiu para o norte."
const m_fugir2 = "$P fugiu para o norte$m."
const m_entra1 = "$P chegou do sul$m."
const m_entra2 = "$P flutuou do sul$m."
const m_entra3 = "$P voou do sul$m."
const m_entra4 = "$P chegou do sul cambaleando."
const m_porta = "a porta norte"
const dir = "n"


classe h_cmd_s
herda comando_mover
const m_sai1 = "$P foi para o sul$m."
const m_sai2 = "$P flutuou para o sul$m."
const m_sai3 = "$P voou para o sul$m."
const m_sai4 = "$P foi para o sul cambaleando."
const m_fugir1 = "Você fugiu para o sul."
const m_fugir2 = "$P fugiu para o sul$m."
const m_entra1 = "$P chegou do norte$m."
const m_entra2 = "$P flutuou do norte$m."
const m_entra3 = "$P voou do norte$m."
const m_entra4 = "$P chegou do norte cambaleando."
const m_porta = "a porta sul"
const dir = "s"


classe h_cmd_l
herda comando_mover
const m_sai1 = "$P foi para leste$m."
const m_sai2 = "$P flutuou para leste$m."
const m_sai3 = "$P voou para leste$m."
const m_sai4 = "$P foi para leste cambaleando."
const m_fugir1 = "Você fugiu para leste."
const m_fugir2 = "$P fugiu para leste$m."
const m_entra1 = "$P chegou do oeste$m."
const m_entra2 = "$P flutuou do oeste$m."
const m_entra3 = "$P voou do oeste$m."
const m_entra4 = "$P chegou do oeste cambaleando."
const m_porta = "a porta leste"
const dir = "l"


classe h_cmd_o
herda comando_mover
const m_sai1 = "$P foi para oeste$m."
const m_sai2 = "$P flutuou para oeste$m."
const m_sai3 = "$P voou para oeste$m."
const m_sai4 = "$P foi para oeste cambaleando."
const m_fugir1 = "Você fugiu para oeste."
const m_fugir2 = "$P fugiu para oeste$m."
const m_entra1 = "$P chegou do leste$m."
const m_entra2 = "$P flutuou do leste$m."
const m_entra3 = "$P voou do leste$m."
const m_entra4 = "$P chegou do leste cambaleando."
const m_porta = "a porta oeste"
const dir = "o"


classe h_cmd_c
herda comando_mover
const m_sai1 = "$P subiu$m."
const m_sai2 = "$P flutuou para cima$m."
const m_sai3 = "$P voou para cima$m."
const m_sai4 = "$P subiu cambaleando."
const m_fugir1 = "Você fugiu para cima."
const m_fugir2 = "$P fugiu para cima$m."
const m_entra1 = "$P chegou de baixo$m."
const m_entra2 = "$P flutuou de baixo$m."
const m_entra3 = "$P voou de baixo$m."
const m_entra4 = "$P chegou de baixo cambaleando."
const m_porta = "a passagem para cima"
const dir = "c"


classe h_cmd_b
herda comando_mover
const m_sai1 = "$P desceu$m."
const m_sai2 = "$P flutuou para baixo$m."
const m_sai3 = "$P voou para baixo$m."
const m_sai4 = "$P desceu cambaleando."
const m_fugir1 = "Você fugiu para baixo."
const m_fugir2 = "$P fugiu para baixo$m."
const m_entra1 = "$P chegou de cima$m."
const m_entra2 = "$P flutuou de cima$m."
const m_entra3 = "$P voou de cima$m."
const m_entra4 = "$P chegou de cima cambaleando."
const m_porta = "a passagem para baixo"
const dir = "b"


classe h_cmd_ne
herda comando_mover
const m_sai1 = "$P foi para o nordeste$m."
const m_sai2 = "$P flutuou para o nordeste$m."
const m_sai3 = "$P voou para o nordeste$m."
const m_sai4 = "$P foi para o nordeste cambaleando."
const m_fugir1 = "Você fugiu para o nordeste."
const m_fugir2 = "$P fugiu para o nordeste$m."
const m_entra1 = "$P chegou do sudoeste$m."
const m_entra2 = "$P flutuou do sudoeste$m."
const m_entra3 = "$P voou do sudoeste$m."
const m_entra4 = "$P chegou do sudoeste cambaleando."
const m_porta = "a porta nordeste"
const dir = "ne"


classe h_cmd_no
herda comando_mover
const m_sai1 = "$P foi para o noroeste$m."
const m_sai2 = "$P flutuou para o noroeste$m."
const m_sai3 = "$P voou para o noroeste$m."
const m_sai4 = "$P foi para o noroeste cambaleando."
const m_fugir1 = "Você fugiu para o noroeste."
const m_fugir2 = "$P fugiu para o noroeste$m."
const m_entra1 = "$P chegou do sudeste$m."
const m_entra2 = "$P flutuou do sudeste$m."
const m_entra3 = "$P voou do sudeste$m."
const m_entra4 = "$P chegou do sudeste cambaleando."
const m_porta = "a porta noroeste"
const dir = "no"


classe h_cmd_se
herda comando_mover
const m_sai1 = "$P foi para o sudeste$m."
const m_sai2 = "$P flutuou para o sudeste$m."
const m_sai3 = "$P voou para o sudeste$m."
const m_sai4 = "$P foi para o sudeste cambaleando."
const m_fugir1 = "Você fugiu para o sudeste."
const m_fugir2 = "$P fugiu para o sudeste$m."
const m_entra1 = "$P chegou do noroeste$m."
const m_entra2 = "$P flutuou do noroeste$m."
const m_entra3 = "$P voou do noroeste$m."
const m_entra4 = "$P chegou do noroeste cambaleando."
const m_porta = "a porta sudeste"
const dir = "se"


classe h_cmd_so
herda comando_mover
const m_sai1 = "$P foi para o sudoeste$m."
const m_sai2 = "$P flutuou para o sudoeste$m."
const m_sai3 = "$P voou para o sudoeste$m."
const m_sai4 = "$P foi para o sudoeste cambaleando."
const m_fugir1 = "Você fugiu para o sudoeste."
const m_fugir2 = "$P fugiu para o sudoeste$m."
const m_entra1 = "$P chegou do nordeste$m."
const m_entra2 = "$P flutuou do nordeste$m."
const m_entra3 = "$P voou do nordeste$m."
const m_entra4 = "$P chegou do nordeste cambaleando."
const m_porta = "a porta sudoeste"
const dir = "so"


classe h_cmd_sair
herda comando_mover
const m_sai1 = "$P saiu$m."
const m_sai2 = "$P flutuou para fora$m."
const m_sai3 = "$P voou para fora$m."
const m_sai4 = "$P saiu cambaleando."
const m_fugir1 = "Você fugiu para fora."
const m_fugir2 = "$P fugiu para fora$m."
const m_entra1 = "$P chegou de dentro$m."
const m_entra2 = "$P flutuou de dentro$m."
const m_entra3 = "$P voou de dentro$m."
const m_entra4 = "$P chegou de dentro cambaleando."
const m_porta = "fora"
const dir = "sair"


classe h_cmd_entrar
herda comando_mover
const m_sai1 = "$P entrou$m."
const m_sai2 = "$P entrou flutuando$m."
const m_sai3 = "$P entrou voando$m."
const m_sai4 = "$P entrou cambaleando."
const m_fugir1 = "Você fugiu entrando."
const m_fugir2 = "$P fugiu entrando$m."
const m_entra1 = "$P entrou aqui$m."
const m_entra2 = "$P flutuou de fora$m."
const m_entra3 = "$P voou de fora$m."
const m_entra4 = "$P entrou aqui cambaleando."
const m_porta = "dentro"
const dir = "entrar"
#
const m_saiitem1 = "$P saiu $d $o$m."
const m_saiitem2 = "$P flutuou $d $o$m."
const m_saiitem3 = "$P voou $d $o$m."
const m_saiitem4 = "$P saiu $d $o cambaleando."
const m_entraitem1 = "$P entrou $e $o$m."
const m_entraitem2 = "$P flutuou para $o fora$m."
const m_entraitem3 = "$P voou para $o$m."
const m_entraitem4 = "$P entrou $e $o cambaleando."
const m_naover = "Você não vê $m."
const m_impossivelitem = "Não é possível entrar $e $o."
const m_impossivelporta = "Não é possível entrar $e $o por $M."
const m_fechadoporta = "A entrada $M $d $o está fechada."
const m_entradas = "$O possui as seguintes entradas:"
const m_embriaguez = "Você não consegue achar a entrada $d $o."

func escr # Executa o comando
# arg0 = personagem
# arg1 = o que digitou após a direção
  ret !arg1, comando_mover:escr(arg0, arg1)
# Obtém o item e a sala
  ref item = misc:itemdono(arg0, txt1(arg1))
  ref dest = item.i_salaentrar
  $mens.mens = arg1
  ret !item, $mens.mp(m_naover, arg0)
# Obtém a porta
  se !dest || txt2(arg1)
    listaitem i # Para verificar as salas
    int8 portas # Número de portas
    epara i = item.dentro2.ini, i, i.depois
      portas += i.obj.s_sala && i.obj.s_entrada
    efim
    se portas == 1
      epara i = item.dentro2.ini, i, i.depois
        sair i.obj.s_sala && i.obj.s_entrada
      efim
      dest = i.obj
    senao portas > 1 && txt2(arg1)
      nomeobj n
      n.ini(txt2(arg1), 1)
      epara i = item.dentro2.ini, i, i.depois
        sair i.obj.s_sala && i.obj.s_entrada && n.nome(i.obj.s_entrada, 1)
      efim
      dest = i.obj
      se !dest
        $mens.mens = txt2(arg1)
        ret $mens.mp(m_impossivelporta, arg0, nulo, item)
      senao int(dest.porta_sair) >= 2
        $mens.mens = txt2(arg1)
        ret $mens.mp(m_fechadoporta, arg0, nulo, item)
      fimse
    senao portas > 1
      $mens.mp(m_entradas, arg0, nulo, item)
      epara i = item.dentro2.ini, i, i.depois
        i.obj.s_sala && i.obj.s_entrada && arg0.msg(i.obj.s_entrada)
      efim
      ret
    fimse
  fimse
# Checa se pode entrar
  se !(dest.s_sala || dest.item && dest.dono.s_sala)
    ret $mens.mp(m_impossivelitem, arg0, nulo, item)
  fimse
# Checa se está bêbado
  se arg0.jog && rand($miscfome.bebedir, $miscfome.cheio) <= arg0.p_bebida
    ret $mens.mp(m_embriaguez, arg0, nulo, item)
  fimse
# Entra
  $comando_mover.lseg.limpar
  $comando_mover.andou = 0
  ref obj = objmover2(arg0)
  ret !obj, nulo
  se dest.item
    ret !mover(obj, dest.dono, item, dest, arg0)
  senao
    ret !mover(obj, dest, item, nulo, arg0), nulo
  fimse


classe menu_mover
herda comando_menu_cmd
const titulo = "Andar pelo jogo e saídas"
const menufim = "menu_econfig_msg"
#
const nome1 = "Todas as direções"
const tipo1 = "mmenu_mover_cfg--"
#
const nome2 = "Norte"
const tipo2 = "menu_mover_n--"
#
const nome3 = "Sul"
const tipo3 = "menu_mover_s--"
#
const nome4 = "Leste"
const tipo4 = "menu_mover_l--"
#
const nome5 = "Oeste"
const tipo5 = "menu_mover_o--"
#
const nome6 = "Cima"
const tipo6 = "menu_mover_c--"
#
const nome7 = "Baixo"
const tipo7 = "menu_mover_b--"
#
const nome8 = "Nordeste"
const tipo8 = "menu_mover_ne--"
#
const nome9 = "Noroeste"
const tipo9 = "menu_mover_no--"
#
const nome10 = "Sudeste"
const tipo10 = "menu_mover_se--"
#
const nome11 = "Sudoeste"
const tipo11 = "menu_mover_so--"
#
const nome12 = "Sair"
const tipo12 = "menu_mover_sair--"
#
const nome13 = "Entrar"
const tipo13 = "menu_mover_entrar--"
#
const nome14 = "Entrar/sair item"
const tipo14 = "menu_mover_item--"


classe mmenu_mover_cfg
herda comando_menu_cmd
const titulo = "Todas as direções"
const menufim = "menu_mover"
const classe = "comando_mover_cfg"
#
const nome1 = "Impossível"
const tipo1 = "opc_linha"
const vari1 = "m_impossivel"
#
const nome2 = "Porta fechada"
const tipo2 = "opc_linha"
const vari2 = "m_portafechada"
#
const nome3 = "Item fechado"
const tipo3 = "opc_linha"
const vari3 = "m_itemfechado"
#
const nome4 = "Sem espaço"
const tipo4 = "opc_linha"
const vari4 = "m_espaco"
#
const nome5 = "Precisa flutuar"
const tipo5 = "opc_linha"
const vari5 = "m_flutuando"
#
const nome6 = "Você cansado"
const tipo6 = "opc_linha"
const vari6 = "m_cansado"
#
const nome7 = "Animal cansado"
const tipo7 = "opc_linha"
const vari7 = "m_animcansado"
#
const nome8 = "Seguir"
const tipo8 = "opc_linha"
const vari8 = "m_seguir"
#
const nome9 = "Animal não obede"
const info9 = "Quando está montado em um animal"
const tipo9 = "opc_linha"
const vari9 = "m_ignorar"
#
const nome10 = "Está escuro"
const tipo10 = "opc_linha"
const vari10 = "m_escuro"
#
const nome11 = "Está cego"
const tipo11 = "opc_linha"
const vari11 = "m_cego"
#
const nome12 = "Não está acordado"
const tipo12 = "opc_linha"
const vari12 = "m_dormindo"


classe menu_mover_comum
herda comando_menu_cmd
const titulo = "Mover"
const menufim = "menu_mover"
#
const nome1 = "Nome da porta"
const tipo1 = "opc_linha"
const vari1 = "m_porta"
#
const titu2 = " * Na sala origem"
#
const nome2 = "Saiu andando"
const tipo2 = "opc_linha"
const vari2 = "m_sai1"
#
const nome3 = "Saiu flutuando"
const tipo3 = "opc_linha"
const vari3 = "m_sai2"
#
const nome4 = "Saiu voando"
const tipo4 = "opc_linha"
const vari4 = "m_sai3"
#
const nome5 = "Saiu cambaleando"
const tipo5 = "opc_linha"
const vari5 = "m_sai4"
#
const nome6 = "Fugir perso"
const tipo6 = "opc_linha"
const vari6 = "m_fugir1"
#
const nome7 = "Fugir outros"
const tipo7 = "opc_linha"
const vari7 = "m_fugir2"
#
const titu8 = " * Na sala destino"
#
const nome8 = "Chegou andando"
const tipo8 = "opc_linha"
const vari8 = "m_entra1"
#
const nome9 = "Chegou flutuando"
const tipo9 = "opc_linha"
const vari9 = "m_entra2"
#
const nome10 = "Chegou voando"
const tipo10 = "opc_linha"
const vari10 = "m_entra3"
#
const nome11 = "Chegou cambaleando"
const tipo11 = "opc_linha"
const vari11 = "m_entra4"


classe menu_mover_n
herda menu_mover_comum
const titulo = "Norte"
const classe = "cmd_n"


classe menu_mover_s
herda menu_mover_comum
const titulo = "Sul"
const classe = "cmd_s"


classe menu_mover_l
herda menu_mover_comum
const titulo = "Leste"
const classe = "cmd_l"


classe menu_mover_o
herda menu_mover_comum
const titulo = "Oeste"
const classe = "cmd_o"


classe menu_mover_c
herda menu_mover_comum
const titulo = "Cima"
const classe = "cmd_c"


classe menu_mover_b
herda menu_mover_comum
const titulo = "Baixo"
const classe = "cmd_b"


classe menu_mover_ne
herda menu_mover_comum
const titulo = "Nordeste"
const classe = "cmd_ne"


classe menu_mover_no
herda menu_mover_comum
const titulo = "Noroeste"
const classe = "cmd_no"


classe menu_mover_se
herda menu_mover_comum
const titulo = "Sudeste"
const classe = "cmd_se"


classe menu_mover_so
herda menu_mover_comum
const titulo = "Sudoeste"
const classe = "cmd_so"


classe menu_mover_sair
herda menu_mover_comum
const titulo = "Sair"
const classe = "cmd_sair"


classe menu_mover_entrar
herda menu_mover_comum
const titulo = "Entrar"
const classe = "cmd_entrar"


classe menu_mover_item
herda comando_menu_cmd
const titulo = "Entrar/sair item"
const menufim = "menu_mover"
const classe = "cmd_entrar"
#
const nome1 = "Entrou andando"
const tipo1 = "opc_linha"
const vari1 = "m_entraitem1"
#
const nome2 = "Entrou flutuando"
const tipo2 = "opc_linha"
const vari2 = "m_entraitem2"
#
const nome3 = "Entrou voando"
const tipo3 = "opc_linha"
const vari3 = "m_entraitem3"
#
const nome4 = "Entrou cambaleando"
const tipo4 = "opc_linha"
const vari4 = "m_entraitem4"
#
const nome5 = "Saiu andando"
const tipo5 = "opc_linha"
const vari5 = "m_saiitem1"
#
const nome6 = "Saiu flutuando"
const tipo6 = "opc_linha"
const vari6 = "m_saiitem2"
#
const nome7 = "Saiu voando"
const tipo7 = "opc_linha"
const vari7 = "m_saiitem3"
#
const nome8 = "Saiu cambaleando"
const tipo8 = "opc_linha"
const vari8 = "m_saiitem4"
#
const nome9 = "Sem alvo"
const tipo9 = "opc_linha"
const vari9 = "m_naover"
#
const nome10 = "Impossível item"
const tipo10 = "opc_linha"
const vari10 = "m_impossivelitem"
#
const nome11 = "Impossível porta"
const tipo11 = "opc_linha"
const vari11 = "m_impossivelporta"
#
const nome12 = "Porta fechada"
const tipo12 = "opc_linha"
const vari12 = "m_fechadoporta"
#
const nome13 = "Entradas"
const tipo13 = "opc_linha"
const vari13 = "m_entradas"
#
const nome14 = "Embriagado"
const tipo14 = "opc_linha"
const vari14 = "m_embriaguez"
