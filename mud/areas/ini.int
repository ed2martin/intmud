classe a_ini
herda area
const a_nome = "Ilha (início)"


classe b_ini
herda sala
const s_area = "ini"
const s_terreno = 1
const s_luta = 1
const s_item = 1


classe i_ini001
herda comum_itemgrupo
const nome = "diamante azul"
const descnome = "um diamante azul"
const msexo = 1
const descpos = "Aqui tem um diamante azul."
const pesoobj = 200

func cmd_mudadono # Objeto mudou de dono
  se donoantes.perso
    apagar(este)
    $mens.p(donoantes)
    $mens.mvis1("Diamante azul vira pó.")
  fimse


classe i_ini002
herda comum_itemgrupo
const nome = "diamante amarelo"
const descnome = "um diamante amarelo"
const msexo = 1
const descpos = "Aqui tem um diamante amarelo."
const pesoobj = 200

func cmd_mudadono # Objeto mudou de dono
  se donoantes.perso
    apagar(esteantes)
    $mens.p(dono)
    $mens.mvis1("Diamante amarelo vira pó.")
  fimse


classe i_ini003
herda comum_itemgrupo
const nome = "diamante vermelho"
const descnome = "um diamante vermelho"
const msexo = 1
const descpos = "Aqui tem um diamante vermelho."
const pesoobj = 200

func cmd_mudadono # Objeto mudou de dono
  se donoantes.perso
    apagar(este)
    $mens.p(donoantes)
    $mens.mvis1("Diamante vermelho vira pó.")
  fimse


classe i_ini004
herda comum_itemgrupo
const nome = "diamante verde"
const descnome = "um diamante verde"
const msexo = 1
const descpos = "Aqui tem um diamante verde."
const pesoobj = 200

func cmd_mudadono # Objeto mudou de dono
  se donoantes.perso
    apagar(este)
    $mens.p(donoantes)
    $mens.mvis1("Diamante verde vira pó.")
  fimse


classe i_ini005
herda comum_item
const nome = "manto escuro"
const descnome = "um manto escuro"
const descpos = "Um manto escuro relativamente pesado está aqui."
const descver = "Embora extremamente frágil, possui um pequeno foguete preso nele. Há uma\n\
pequena corda para acionar o foguete, mas antes você deve vestir o manto.\n\
O comando para puxar a corda é PUXAR."
const msexo = 1
const pesoobj = 15000
const vestir1 = "40"
const evperso = 1 # Eventos em perso: Sim

func cmd_mudadono # Objeto mudou de dono
  se !dono.perso # Se o novo dono não for um personagem
    apagar(este)
    $mens.p(donoantes)
    $mens.mvis1("O manto escuro cai no chão e se desintegra.")
  fimse

func cmd_escr # Escreveu algo
  se arg1 == "puxar"
    se !vestpos
      arg0.msg("Antes de acionar o foguete, você deve vestir o manto.")
    senao dono.dono != $s_ini004
      arg0.msg("Melhor não, é perigoso acionar o foguete aqui.")
    senao
      $mens.p(arg0)
      $mens.mtodos1("$P aciona o foguete e sobe rapidamente.")
      arg0.mudadono($s_ini002)
      $mens.mtodos2("", "$P pousou aqui.")
    fimse
    ret 1
  fimse


classe i_ini006
herda comum_itemperso
const nome = "tronco"
const descnome = "um tronco de árvore"
const msexo = 1
const descpos = "Um pedaço grande de tronco está aqui no centro."
const pesoobj = 1000000
const i_movel = 1 # 1=pode sentar, 2=pode sentar e deitar
const i_lugar = 100 # Quantos personagens podem sentar


classe p_ini001
herda comum_perso
const nome = "anão"
const descnome = "um pequeno anão"
const sexoini = 1
const p_agressivo = 1 # Para poder receber ataques (não participará de batalha)
const pesoobj = 70000
const persobat = este # Personagem usado na batalha
const eveste = 1 # Receber eventos do próprio objeto
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_atacar
  se "p_ini001" == arg1
    arg2.result = 0
    $mens.p(arg0, arg1)
    $mens.mtodos1(arg2.mensgolpe)
    arg0.msg("Anão: Tudo bem, você venceu, pode passar.")
    arg0.var.j_atacou_anao = 1
    ret 1
  fimse

func cmd_mover
  se arg2 == "s" && arg0.jog && !arg0.var.j_atacou_anao
    arg0.msg("Anão: Se quiser passar por mim terá de me pagar 10 galões de ouro.")
    arg0.msg("Aí eu vou tirar na sorte se deixo ou não você passar.")
    arg0.msg("Você não me mete medo. Eu sei brigar muito bem.")
    ret 1
  fimse


classe p_ini002
herda comum_perso
const nome = "Silva"
const sexoini = 1
const p_agressivo = 1 # Para poder receber ataques (não participará de batalha)
const pvida = 1000 # Para não dizer que o alvo está desmaiado
const pesoobj = 80000
const eveste = 1 # Receber eventos do próprio objeto
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_chegou # Alguém chegou na mesma sala
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou10
  arg0.msg("Silva: Oi! Sou o Sr. Silva. Moro nessa ilha.")

func entrou30
  se !arg0.var.h_chutar
    arg0.msg("Silva: Se vai sair da ilha, você precisa aprender alguns golpes.")
  senao
    arg0.msg("Silva: Já te ensinei o que eu sei sobre combates.")
  fimse

func entrou50
  se !arg0.var.h_chutar
    arg0.msg("Silva: Precisa saber se defender.")
    $mens.p(arg0)
    $mens.mvis1("Silva ensina $p a socar.")
    arg0.var.h_socar = 3
  fimse

func entrou70
  se !arg0.var.h_chutar
    arg0.msg("Silva: Agora tente me socar. Tecle socar Silva")
  fimse

func cmd_atacar
  se ![arg2](arg0, arg1) # Chama a função que trata do ataque específico
    arg0.msg("Silva bloqueia seu golpe.")
  fimse
  ret 1

func cmd_socar
  se arg1 != este
    arg0.msg("Silva: Tecle socar silva")
    ret 1
  senao
    $mens.p(arg0)
    $mens.mvis1("$P soca Silva, -2")
    msg("Silva: Nada mal para quem está começando!")
    msg("Silva: Agora vou te ensinar um golpe um pouco mais forte.")
    $mens.mvis1("Silva ensina $P a chutar.")
    arg0.var.h_chutar = 3
    arg0.msg("Silva: Tecle chutar silva ou, se estiver com preguiça, tecle ch silva.")
    ret 1
  fimse

func cmd_chutar
  se arg1 != este
    arg0.msg("Silva: Tecle chutar silva")
    ret 1
  senao
    $mens.p(arg0)
    $mens.mvis1("$P chuta Silva, -3")
    arg0.msg("Silva: Bom chute. Eu só tive 3 pontos de danos porque sou mais forte.")
    arg0.msg("Silva: Durante uma briga, você pode teclar apenas chutar ou socar.")
    arg0.msg("Silva: Te dou dois conselhos: Cheque suas habilidades teclando hab")
    arg0.msg("Silva: Cheque também sua vida teclando vida. Se chegar a 0 você morre.")
    ret 1
  fimse


classe p_ini003
herda comum_perso
const nome = "Joaquim"
const sexoini = 1
const pesoobj = 80000
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_chegou # Chegou na sala
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:evento(arg0) # Receber eventos do jogador
  se arg0.dentro1.objini("i_ini002") # Se tem diamante amarelo
    arg0.var.d_resposta_ = 20 # Já fez todas as perguntas
  senao
    misc:tempo("entrou", arg0) # Se não tem, dispara funções 'entrou'
  fimse

func entrou10
  arg0.msg("Joaquim: Olá. Não quer se sentar um pouco para conversarmos?")
  arg0.msg("O comando para sentar-se é SENTAR")

func cmd_sentouchao
  arg0.msg("Joaquim: No chão não! Sente-se nesse tronco de árvore.")
  arg0.msg("Primeiro tecle LEVANTAR para ficar em pé.")
  arg0.var.d_sentouchao_ = 1

func cmd_levantou
  se arg0.var.d_sentouchao_ == 1
    arg0.msg("Joaquim: Isso. Agora sente-se no tronco. Tecle SENTAR TRONCO")
    arg0.var.d_sentouchao_ = 0
  fimse

func cmd_descansouchao
  arg0.msg("Joaquim: Descansar no tronco é muito melhor.")

func cmd_descansouobj
  arg0.msg("Joaquim: Vejo que prefere descansar. Então não vou incomodar.")

func cmd_sentouobj
  se arg0.var.d_resposta_ == 0
    arg0.var.d_resposta_ = 1
    misc:tempo("pergunta", arg0) # Dispara funções 'pergunta'
  fimse

func pergunta10
  arg0.msg("Joaquim: Esse arquipélago já foi maior. Agora pouca gente vem aqui.")

func pergunta30
  arg0.msg("Joaquim: Sou um estudioso e curioso. Você também gosta de estudar?")

func pergunta55
  arg0.msg("Joaquim: Você parece saber das coisas. Então vou fazer umas perguntas.")

func pergunta80
  arg0.var.d_resposta_ = 2
  arg0.msg("Joaquim: Tem um lugar nessas ilhas que tem muitos diamantes vermelhos.")
  arg0.msg("Quantos tem nesse lugar? Tecle falar seguido de um número de 1 a 100.")

func cmd_falou
  casovar arg0.var.d_resposta_
  casose "0" # 0 = ainda não está fazendo perguntas
  casose "1" # 1 = está instruindo o jogador
    ret
  casose "2"
    arg1 != "78" && (arg0.var.d_resposta_ = 12)
  casose "12"
    arg0.var.d_resposta_++
    arg0.msg("Joaquim: Qual o nível do meu amigo Silva? É outro número de 1 a 100.")
    ret
  casose "3"
    arg1 != "27" && (arg0.var.d_resposta_ = 13)
  casose "13"
    arg0.var.d_resposta_++
    arg0.msg("Joaquim: Quando você chegou na ilha, que habilidade você já conhecia?")
    ret
  casose "4"
    se arg1 == "atirar" # Acertou todas as perguntas
      arg0.msg("Joaquim: Você acertou as perguntas. Como recompensa fique com este diamante.")
      arg0.msg("Joaquim: Quando tiver os quatro diamantes vá para o centro da ilha maior.")
      $mens.p(este, arg0)
      $mens.mvis1("$P dá diamante amarelo para $A.")
      criar("i_ini002", arg0)
      sair
    fimse
  casose "14"
    arg0.msg("Joaquim: Você errou pelo menos uma das perguntas que eu fiz.")
    arg0.msg("Volte aqui quando souber todas as respostas.")
    misc:tempo("errou", arg0) # Dispara funções 'errou'
  casofim
  arg0.var.d_resposta_ = 20 # Não está mais fazendo perguntas

func errou80
  arg0.msg("Joaquim: O que está esperando? Vá atrás das respostas.")


classe s_ini001
herda b_ini
const s_titulo = "Beco"
const s_desc = "A única saída visível aqui é um barranco à leste. Nas outras direções\n\
você só vê paredes. Para caminhar à leste, tecle L\n\
No entanto, se você for veterano, pode tentar caminhar a oeste, mas\n\
esteja ciente que veteranos começam no nível 0 ao invés do 1."
const s_luta = 0
const eveste = 1 # Receber eventos do próprio objeto
const dir_l = $s_ini002
const dir_o = $s_casa001
const inv_o = 1 # Esconde saída para oeste

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  arg0.var.h_atirar = 1 # Jogador deve saber atirar

func cmd_mover
  se arg2 == "l"
    arg0.msg("\c3Sua missão é adquirir 4 diamantes e ir até uma palmeira.")
    arg0.msg("Para ir de um lugar a outro tecle n, s, l, o, c, b\b")
    ref r
    r = criar("i_ini005", arg0)
    r.vestir(7)
    arg0.recalc = 1
  senao arg2 == "o"
    arg0.msg("Milagre! Você acaba de atravessar uma parede que parecia bastante sólida.")
    arg0.var.h_socar = 3
    arg0.var.h_chutar = 3
    arg0.recalc = 1
  fimse


classe s_ini002
herda b_ini
const s_luta = 0
const s_titulo = "Centro da ilha"
const s_desc = "Há uma palmeira um pouco seca aqui. Ao norte há um pequeno arbusto,\n\
e ao sul, uma antiga cabana de palha. A ilha continua nas outras direções,\n\
mas você vê apenas o chão."
const dir_l = $s_ini005
const dir_o = $s_ini007
const dir_s = $s_ini008
const dir_n = $s_ini010

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou20
  se !arg0.dentro1.objini("i_ini001")
  senao !arg0.dentro1.objini("i_ini002")
  senao !arg0.dentro1.objini("i_ini003")
  senao !arg0.dentro1.objini("i_ini004")
  senao
    arg0.msg("Os diamantes estão se fundindo")
    $mens.p(arg0)
    $mens.mvis2("", "$P sumiu da sua vista.")
    arg0.mudadono($s_ini003)
  fimse


classe s_ini003
herda b_ini
const descsala = "Nas nuvens"
const s_ocultar = 1

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou20
  apagar(arg0.dentro1.objini("i_ini001"))
  apagar(arg0.dentro1.objini("i_ini002"))
  apagar(arg0.dentro1.objini("i_ini003"))
  apagar(arg0.dentro1.objini("i_ini004"))
  arg0.msg("Os diamantes se fundiram e você está saindo da ilha.")

func entrou50
  arg0.msg("A ilha desaparece completamente da sua vista.")

func entrou80
  arg0.msg("De repente, você acorda. Foi tudo um sonho?")

func entrou110
  arg0.pnivel = 1
  arg0.recalc = 1
  arg0.mudadono($s_casa001)
  $mens.p(arg0)
  $mens.mvis2("", "$P chegou")


classe s_ini004
herda b_ini
# Altera a descrição da sala
const s_titulo = "Na névoa branca"
const descsala = "Você está caindo!"
const s_ocultar = 1

func cmd_salaperso # Personagem chegou
  se !arg0.jog # Apaga se não for jogador
    apagar(arg0)
  senao
    arg0.msg("Você está um pouco acima da névoa branca.")
    misc:tempo("entrou", arg0) # Dispara funções 'entrou'
  fimse

func entrou30
  arg0.msg("Tenta andar mas não consegue sair do lugar.")

func entrou60
  arg0.msg("De repente você começa a descer, cada vez mais rápido.")

func entrou90
  arg0.msg("Não consegue ver mais nada por causa da névoa branca.")

func entrou120
  arg0.msg("A névoa está se dissipando, mas você continua acelerando para baixo.")

func entrou150
  arg0.msg("Você olha para cima e só vê a névoa.\nEm baixo, casas e outras coisas muito pequenas.\
")

func entrou190
  arg0.msg("As casas estão ficando cada vez maiores.")

func entrou220
  arg0.msg("Olha para cima e vê pequenas nuvens no céu.")

func entrou250
  arg0.msg("Você se espatifou no chão, está morto.")

func entrou280
  se arg0.dentro2.objini("i_ini005")
    arg0.msg("Da próxima vez que estiver caindo, tecle PUXAR para puxar a corda")
    arg0.msg("que aciona o foguete.")
  fimse

func entrou300
  arg0.mudadono($s_ini002)
  $mens.p(arg0)
  $mens.mtodos2("", "$P chegou.")
  ret 1


classe s_ini005
herda b_ini
const s_titulo = "Região leste da ilha"
const s_desc = "Não há nada aqui, somente o chão de terra que se estende à oeste.\n\
Nas outras direções o chão termina e começa uma neblina branca.\n\
Acima da neblina só se vê o céu azul. O chão nessa região também é azul,\n\
mas há um [desenho] apagado (você pode ver teclando olhar desenho ou\n\
teclando ver desenho)."
const dir_o = $s_ini002
const dir_n = $s_ini004
const dir_s = $s_ini004
const dir_l = $s_ini004
const dir_b = $s_ini006
const inv_b = 1
const ver_desenho = "O desenho é o mapa de uma pequena ilha, com uma palmeira seca no meio,\n\
um arbusto e uma escada para baixo no norte, uma cabana ao sul e uma\n\
placa à oeste. Curiosamenente, há uma escada para baixo colada na borda\n\
leste da ilha, praticamente na altura do chão."


classe s_ini006
herda b_ini
const s_titulo = "Escada"
const s_desc = "A escada está bem firme, presa na rocha à leste. Mas infelizmente,\n\
é menor que parecia à primeira vista. Depois de 3 metros, a escada\n\
simplesmente acaba no nada. O vento aqui é um pouco forte, e não seria\n\
surpresa se alguma coisa caísse de cima."
const dir_c = $s_ini005
const eveste = 1 # Receber eventos do próprio objeto

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou100
  $mens.p(arg0)
  $mens.mtodos1("Você vê um diamante azul caindo.")
  criar("i_ini001", este)

func entrou130
  se dentro1.objini("i_ini001")
    $mens.p(arg0)
    $mens.mtodos1("Diamante azul desaparece ao longe enquanto cai.")
    apagar(dentro1.objini("i_ini001"))
  fimse

func entrou0 # Fim dos eventos 'entrou'
  apagar(dentro1.objini("i_ini001"))

func cmd_pegarobj
  se "i_ini001" == arg2 && arg0.dentro1.objini("i_ini001")
    arg0.msg("Você tenta pegar, mas não consegue. O diamante está um pouco longe.")
    ret 1
  fimse


classe s_ini007
herda b_ini
const s_titulo = "Região oeste da ilha"
const s_desc = "Há várias pedras vermelhas no chão. Há também uma [placa] que pode ser lida\n\
teclando olhar placa. À leste fica o centro da ilha. Nas outras direções só\n\
se vê o céu azul e uma névoa branca no lugar do chão."
const dir_l = $s_ini002
const dir_n = $s_ini004
const dir_s = $s_ini004
const dir_o = $s_ini004
const eveste = 1 # Receber eventos do próprio objeto
const ver_placa = "A placa diz: Depósito de diamantes vermelhos\n\
Para pegar tecle pegar diamante_vermelho\n\
Para soltar tecle soltar diamante_vermelho\n\
Muitas vezes pode-se abreviar os comandos.\n\
Exemplo, \"pe dia\" ao invés de \"pegar diamante_vermelho\"."
const s_reset = "S 100 ini003 78 s"

func cmd_pegarobj
  se arg2 == "i_ini003"
    se arg0.dentro1.objini("i_ini003")
      arg0.msg("Só pode carregar um diamante vermelho nessa ilha.")
    senao
      criar("i_ini003", arg0)
      $mens.p(arg0)
      $mens.mtodos1("$P pega Diamante Vermelho.")
    fimse
    ret 1
  fimse

func cmd_soltarobj
  se arg2 == "i_ini003"
    apagar(arg2)
    $mens.p(arg0)
    $mens.mtodos1("$P solta Diamante Vermelho.")
    ret 1
  fimse

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_ini003") && criar("i_ini003", este, 78)


classe s_ini008
herda b_ini
const s_titulo = "Região sul da ilha"
const s_desc = "Afastando-se do centro da ilha, em direção ao sul, há uma velha cabana.\n\
Ao norte a ilha continua, mas tanto no leste quanto no oeste, vê-se apenas\n\
uma névoa. Nota-se uma pequena [placa] em cima da porta (para ler o que\n\
está escrito na placa tecle olhar placa)."
const eveste = 1 # Receber eventos do próprio objeto
const dir_n = $s_ini002
const dir_s = $s_ini009
const portaini_s = 2 # Estado inicial: 2 Porta fechada
const portanome_s = "a porta da cabana"
const dir_l = $s_ini004
const dir_o = $s_ini004
inttempo fechaporta # Para fazer o vento bater a porta
const ver_placa = "A placa diz:\nPara abrir a porta ao sul, tecle abrir s"

func cmd_abriudir
  se arg2 == "s"
    fechaporta = 50
  fimse

func fechaporta_exec
  se porta_s == 1
    mudaporta("s", 2)
    msg("O vento bateu a porta da cabana.")
    $s_ini009.msg("O vento bateu a porta da cabana.")
  fimse


classe s_ini009
herda b_ini
const s_titulo = "Dentro da cabana"
const s_desc = "Há uma janela. A vista é do céu, e no lugar do chão, há uma\n\
névoa branca como neve. Há uma pessoa esquisita aqui."
const dir_n = $s_ini008
const portaini_n = 2 # Inicialmente fechado
const portanome_n = "a porta da cabana"
const eveste = 1 # Receber eventos do próprio objeto
const s_reset = "P 100 ini002 27 27 1"

func cmd_abriudir
  se arg2 == "n"
    $s_ini008.fechaporta = 50
  fimse

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se p_ini002:objsolto.total < 1
    criar("p_ini002", este, 27)
  fimse


classe s_ini010
herda b_ini
const s_titulo = "Região norte da ilha"
const s_desc = "Há um pequeno arbusto aqui. Olhando mais atentamente, você nota uma\n\
escada para baixo escondida pelo arbusto. É uma escada comprida, de modo\n\
que ela desaparece na névoa. Nas outras direções você só vê uma névoa\n\
branca, exceto ao sul, aonde a ilha continua."
const dir_n = $s_ini004
const dir_s = $s_ini002
const dir_l = $s_ini004
const dir_o = $s_ini004
const dir_b = $s_ini011


classe s_ini011
herda b_ini
const s_titulo = "Escada"
const s_desc = "Logo acima, uma ilha. Em baixo não se vê aonde termina."
const dir_c = $s_ini010
const dir_b = $s_ini012


classe s_ini012
herda b_ini
const s_titulo = "Escada"
const s_desc = "Daqui só se vê a parte da escada onde você está segurando. Não se vê\n\
aonde ela começa ou termina. Essa sensação dá um mal estar."
const dir_c = $s_ini011
const dir_b = $s_ini013


classe s_ini013
herda b_ini
const s_titulo = "Escada"
const s_desc = "Logo abaixo, uma ilha. Em cima não se vê aonde termina."
const dir_c = $s_ini012
const dir_b = $s_ini014


classe s_ini014
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Um pouco menor que a ilha grande, mas a névoa continua a mesma.\n\
Há uma escada para cima, mas não se vê aonde ela vai dar."
const dir_c = $s_ini013
const dir_s = $s_ini015
const dir_l = $s_ini017
const eveste = 1 # Receber eventos do próprio objeto

func cmd_mover
  se arg2 == "c"
    se arg0.dentro2.objini("i_ini005")
      arg0.msg("Você está vestindo algo muito pesado, e por isso não consegue subir.")
      ret 1
    senao arg0.dentro1.objini("i_ini005")
      arg0.msg("Você está segurando algo muito pesado, e por isso não consegue subir.")
      ret 1
    fimse
  fimse


classe s_ini015
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Você está bem em baixo da ilha maior, mas ela não está visível daqui.\n\
A neblina está forte e a distância é razoável. Tudo que você vê é\n\
a continuação da ilha ao norte e ao sul."
const dir_n = $s_ini014
const dir_s = $s_ini016
const s_reset = "P 100 ini001 1 1 1"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se p_ini001:objsolto.total < 1
    criar("p_ini001", este, 1)
  fimse


classe s_ini016
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "A neblina aqui é forte, mas não o suficiente para ocultar uma placa.\n\
Pode-se ler o que está escrito na [placa]."
const dir_n = $s_ini015
const eveste = 1 # Receber eventos do próprio objeto
const ver_placa = "A placa diz: você pode saber o que está carregando e vestindo teclando\n\
inv e equip, respectivamente. Talvez você queira remover ou vestir\n\
alguma coisa (comandos são remover e vestir seguido do nome do item)."
const s_reset = "S 100 ini004 45 s"

func cmd_pegarobj
  se arg2 == "i_ini004"
    se arg0.dentro1.objini("i_ini004")
      arg0.msg("Só pode carregar um diamante verde nessa ilha.")
    senao
      criar("i_ini004", arg0)
      $mens.p(arg0)
      $mens.mtodos1("$P pega Diamante Verde.")
    fimse
    ret 1
  fimse

func cmd_soltarobj
  se arg2 == "i_ini004"
    apagar(arg2)
    $mens.p(arg0)
    $mens.mtodos1("$P solta Diamante Verde.")
    ret 1
  fimse

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_ini004") && criar("i_ini004", este, 45)


classe s_ini017
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Ao norte há uma ponte de cordas, que não parece muito segura. A madeira\n\
que compõe o chão da ponte está bastante gasta, e as cordas não estão\n\
em muito melhor estado."
const dir_o = $s_ini014
const dir_n = $s_ini018


classe s_ini018
herda b_ini
const s_terreno = 5
const s_titulo = "Ponte de cordas"
const s_desc = "Você está em uma extremidade da ponte. Conforme o vento bate,\n\
a ponte balança um pouco. Felizmente há terra firme ao sul."
const dir_s = $s_ini017
const dir_n = $s_ini019


classe s_ini019
herda b_ini
const s_terreno = 5
const s_titulo = "Ponte de cordas"
const s_desc = "Há muita neblina, de modo que não se vê aonde a ponte começa ou termina.\n\
Conforme o vento bate, a ponte balança e você se segura para não cair.\n\
Algumas táboas estão quebradas aqui."
const dir_s = $s_ini018
const dir_n = $s_ini020


classe s_ini020
herda b_ini
const s_terreno = 5
const s_titulo = "Ponte de cordas"
const s_desc = "Você está em uma extremidade da ponte. Conforme o vento bate,\n\
a ponte balança um pouco. Há terra firme ao norte."
const dir_s = $s_ini019
const dir_n = $s_ini021


classe s_ini021
herda b_ini
const s_titulo = "Pequena ilha"
const s_desc = "Aqui se vê uma ponte de cordas bem gasta ao sul."
const dir_s = $s_ini020
const s_reset = "S 100 ini006 1 s\nP 100 ini003 31 31 1"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_ini006") && criar("i_ini006", este, 1)
  se p_ini003:objsolto.total < 1
    criar("p_ini003", este, 31)
  fimse
