# Quantas instruções uma função chamada pelo programa pode
# executar antes do controle retornar ao programa
exec = 5000

# Se deve abrir uma janela de texto - variável telatxt
telatxt = 1

# Aonde apresentar mensagens de erro no programa
log = 0

# Erros em blocos de instruções:
# 0=ignorar, 1=permitir apenas FimSe sem Se, 2=checar tudo
err = 1

classe edit_mud
# Conectar via telnet ou outro programa similar na porta especificada.
#
# Configuração:
const senhaadm = "modo"
const enderadm = "localhost" # Ou "" para permitir conexões externas
const portaadm = 2000
const espind = 2 # Quantos espaços usados para indentação
const funclin = 1 # Se deve colocar linha vazia entre funções: 0=não, 1=sim

comum serv servidor
socket conec
inttempo fechar # Para fechar socket após um tempo
textotxt texto # Texto que está enviando ou recebendo
int8 numcom # Comando: 0=esperando, 1=enviando, 2=recebendo

func iniclasse
  edit_mud:servidor.abrir(edit_mud:enderadm, edit_mud:portaadm)

func servidor_socket
  ref novo
  novo = criar("edit_mud")
  novo.conec = arg0
  novo.fechar = 100

func fechar_exec
  apagar(este)

func conec_msg
# telatxt t
# t.msg(arg0 + "\n")
  conec_msg[numcom](arg0)

func conec_msg0
  debug d
  txt100 t.3
  d.exec = 20000
  t.palavras(arg0)
  se t.0 != senhaadm
    apagar(este)
    ret
  senao !exec_[t.1](t.2)
    apagar(este)
  fimse

func conec_msg2
  prog p
  textopos pos
  int1 existe
  se arg0 != "---"
    se texto.bytes < 60000
      texto.addfim(arg0)
    fimse
  senao texto.bytes >= 60000
    conec.msg("-Classe muito grande\n")
    apagar(este)
  senao texto.linhas < 2
    conec.msg("-Faltou nome do arquivo da classe\n")
    apagar(este)
  senao
    pos = texto.ini
    existe = p.existe(pos.texto)
    pos.depois
    pos.mudar("", 0, 4)
    texto.addfim(p.criar(texto.remove(texto.linhas)))
    se texto.bytes <= 1
      se existe
        conec.msg("+OK Classe alterada\n")
      senao
        conec.msg("+OK Classe criada\n")
      fimse
    senao
      enquanto pos.lin
        conec.msg("-" + pos.texto + "\n")
        pos.depois
      efim
    fimse
    apagar(este)
  fimse

func conec_env
  textopos pos
  se numcom == 1
    se texto.linhas == 0
      apagar(este)
    senao
      pos = texto.ini
      conec.msg(pos.textolin(10) + "\n") && pos.remove(10)
    fimse
  fimse

func exec_salvar
  prog p
  p.salvar("L78N1I2C1F1V0")
  conec.msg("+OK arquivo salvo\n")

func exec_apagar
  prog p
  se p.apagar(arg0)
    conec.msg("+OK Classe apagada\n")
  senao
    conec.msg("-Classe não existe: " + arg0 + "\n")
  fimse

func exec_lista
  prog p
  p.iniclasse
  enquanto p.lin
    texto.addfim(p.texto)
    p.depois
  efim
  texto.addfim("---")
  numcom = 1
  conec_env
  ret 1

func exec_ler
  prog p
  se !p.inilinha(arg0)
    conec.msg("-Classe não existe: " + arg0 + "\n")
    ret
  fimse
  texto.addfim("ARQ:" + p.arquivo(arg0))
  enquanto p.lin
    se p.nivel == 0 && funclin
      casovar txt1(p.texto)
      casose "func"
      casose "varfunc"
        texto.addfim("")
      casofim
    fimse
    texto.addfim(txtesp(p.nivel * espind) + p.texto)
    p.depois
  efim
  texto.addfim("---")
  numcom = 1
  conec_env
  ret 1

func exec_mudar
  texto.addfim(arg0)
  numcom = 2
  ret 1
