classe a_ini
herda area
const a_nome = "Área inicial"


classe b_ini
herda sala
const s_terreno = 1
const s_luta = 1


classe p_anao
herda comum_persobat
const nome = "anão"
const ident = "anão"
const sexo = 1
const descpos = "Um anão está aqui."
const descver = "Você não vê nada de especial."


classe i_diamante_azul
herda comum_itemgrupo
const nome = "diamante azul"
const sexo = 0
const descpos = "Aqui tem um diamante azul."
const pesoobj = 200


classe i_diamante_amarelo
herda comum_itemgrupo
const nome = "diamante amarelo"
const sexo = 0
const descpos = "Aqui tem um diamante amarelo."
const pesoobj = 200


classe i_diamante_vermelho
herda comum_itemgrupo
const nome = "diamante vermelho"
const sexo = 0
const descpos = "Aqui tem um diamante vermelho."
const pesoobj = 200


classe i_diamante_verde
herda comum_itemgrupo
const nome = "diamante verde"
const sexo = 0
const descpos = "Aqui tem um diamante verde."
const pesoobj = 200


classe i_manto_ini
herda comum_item
const nome = "manto escuro"
const sexo = 0
const descpos = "Um manto escuro relativamente pesado está aqui."
const pesoobj = 30000
const vestir1 = 8

func cmd_mudadono # Objeto mudou de dono
  se !dono.perso # Se o novo dono não for um personagem
    apagar(este)
    $mens.p(este)
    $mens.mvis1("O manto escuro se desintegra.")


classe p_ini_silva
herda comum_perso
const nome = "Silva"
const sexo = 1
const perso = 2 # Para poder receber ataques (não participará de batalha)
const pesoobj = 80000

func cmd_jog # Jogador chegou na mesma sala
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'
  arg0.evento.addfim(este) # Receber eventos do jogador

func entrou10
  arg0.msg("Silva: Oi! Sou o Sr. Silva. Moro nessa ilha.")

func entrou30
  se !arg0.var.h_chutar
    arg0.msg("Silva: Se vai sair da ilha, você precisa aprender alguns golpes.")
  senao
    arg0.msg("Silva: Já te ensinei o que eu sei sobre combates.")

func entrou50
  se !arg0.var.h_chutar
    arg0.msg("Silva: Precisa saber se defender.")
    $mens.p(arg0)
    $mens.mvis1("Silva ensina $p a socar.")
    arg0.var.h_socar = 1

func entrou70
  se !arg0.var.h_chutar
    arg0.msg("Silva: Agora tente me socar. Tecle socar Silva")

func cmd_atacar
  ret [arg2](arg0, arg1) # Chama a função que trata do ataque específico

func cmd_socar
  se arg1 != este
    arg0.msg("Silva: Tecle socar silva")
    ret 1
  senao
    $mens.p(arg0)
    $mens.mvis1("$P soca Silva, 250 de 250")
    msg("Silva: Nada mal para quem está começando!")
    msg("Silva: Agora vou te ensinar um golpe um pouco mais forte.")
    $mens.mvis1("Silva ensina $P a chutar.")
    arg0.var.h_chutar = 1
    arg0.msg("Tecle chutar silva ou, se estiver com preguiça, tecle ch silva.")
    ret 1

func cmd_chutar
  se arg1 != este
    arg0.msg("Silva: Tecle chutar silva")
    ret 1
  senao
    $mens.p(arg0)
    $mens.mvis1("$P chuta Silva, 250 de 250")
    arg0.msg("Silva: Bom chute. Eu só continuo com 250 de vida porque sou mais forte.")
    arg0.msg("Silva: Durante uma briga, você pode teclar apenas chutar.")
    arg0.msg("Silva: Te dou dois conselhos: Cheque suas habilidades teclando hab")
    arg0.msg("Silva: Cheque também sua vida teclando vida. Se chegar a 0 você morre.")
    ret 1


classe s_ini001
herda b_ini
const s_titulo = "Salão de espera"
const s_desc = "O chão está coberto por uma nuvem branca, de modo que você não consegue\n\
ver onde pisa. Para todos os outros lugares que você olha, só vê o azul\n\
celeste do céu. Há claridade, mas não é possível saber de onde vem."
inttempo espera

func cmd_jog # Jogador chegou
  se espera == 0
    espera = 10

func espera_exec # Restaura a vida do jogador
  listaitem item
  ref r
  epara item = dentro2.ini, item, item.depois
    r = item.obj
    se r.vida < r.vidamax / 2
      r.vida += 1, espera = 10
    senao
      r.mudadono(config:salaini)
    fimse
  efim


classe s_ini002
herda sala
const limpar = 20
const s_luta = 0
const s_titulo = "Beco"
const s_desc = "A única saída visível aqui é um barranco à leste. Nas outras direções\n\
você só vê paredes. Para caminhar à leste, tecle L\n\
No entanto, se você for veterano, pode tentar caminhar a oeste, mas\n\
esteja ciente que veteranos começam no nível 0 ao invés do 1.\n\
A qualquer momento, tecle ajuda para saber os comandos do jogo."
const dir_l = $s_ini003
const dir_o = $s_teste002
const inv_o = 1 # Esconde saída para oeste

func cmd_jog
  arg0.evento.addfim(este) # Receber eventos dos jogadores

func cmd_l
  arg0.msg("\c3Sua missão é adquirir 4 diamantes e ir até uma palmeira.")
  arg0.msg("Para ir de um lugar a outro tecle n, s, l, o, c, b e para ajuda tecle ajuda\b")
  arg0.pnivel = 1
  arg0.recalc = 1
  ref r
  r = criar("i_manto_ini", arg0)
  r.vestir(8)
# Para teste: já começa com golpes socar e chutar
# arg0.var.h_socar = 1
# arg0.var.h_chutar = 1

func cmd_o
  arg0.msg("Milagre! Você acaba de atravessar uma parede que parecia bastante sólida.")
  arg0.var.h_socar = 1
  arg0.var.h_chutar = 1
  arg0.pnivel = 0
  arg0.recalc = 1


classe s_ini003
herda sala
const limpar = 20
const s_luta = 0
const s_titulo = "Centro da ilha"
const s_desc = "Há uma palmeira um pouco seca aqui. Ao norte há um pequeno arbusto,\n\
e ao sul, uma antiga cabana de palha. Nas outras direções, o chão\n\
se confunde com uma névoa."
const dir_l = $s_ini006
const dir_o = $s_ini007
const dir_s = $s_ini008
const dir_n = $s_ini010

func entrou1
  se !arg0.dentro1.objini("i_diamante_azul")
  senao !arg0.dentro1.objini("i_diamante_amarelo")
  senao !arg0.dentro1.objini("i_diamante_vermelho")
  senao !arg0.dentro1.objini("i_diamante_verde")
  senao
    arg0.msg("Os diamantes estão se fundindo")
    $mens.p(arg0)
    $mens.mvis2("", "$P sumiu da sua vista.")
    arg0.mudadono($s_ini004)


classe s_ini004
herda sala
const limpar = 20

func cmd_jog # Jogador chegou
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou20
  arg0.msg("A ilha desaparece completamente da sua vista")
  apagar(arg0.dentro1.objini("i_diamante_azul"))
  apagar(arg0.dentro1.objini("i_diamante_amarelo"))
  apagar(arg0.dentro1.objini("i_diamante_vermelho"))
  apagar(arg0.dentro1.objini("i_diamante_verde"))
  arg0.msg("Os diamantes se fundiram e você está saindo da ilha")

func entrou50
  arg0.msg("Uma imagem está se tornando nítida agora")

func entrou70
  arg0.mudadono($s_teste002)
  $mens.p(arg0)
  $mens.mvis2("", "$P chegou")


classe s_ini005
herda sala
const limpar = 20
const s_desc = "Na névoa branca"

func cmd_jog
  arg0.msg("Você está um pouco acima da névoa branca.")
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou30
  arg0.msg("Tenta andar mas não consegue sair do lugar")

func entrou60
  arg0.msg("De repente você começa a descer, cada vez mais rápido")

func entrou90
  arg0.msg("Não consegue ver mais nada por causa da névoa branca")

func entrou120
  arg0.msg("A névoa está se dissipando, mas você continua acelerando para baixo")

func entrou150
  arg0.msg("Você olha para cima e só vê a névoa.\nEm baixo, casas e outras coisas muito pequenas.\
")

func entrou190
  arg0.msg("As casas estão ficando cada vez maiores.")

func entrou220
  arg0.msg("Olha para cima e vê pequenas nuvens no céu.")

func entrou250
  arg0.msg("Você se espatifou no chão, está morto.")

func entrou280
  arg0.msg("Para jogar novamente na ilha tecle fim")

func entrou290
  arg0.msg("Da próxima vez que perceber que está caindo da ilha, já pode teclar fim antecipadamen\
te.")


classe s_ini006
herda sala
const limpar = 20
const s_titulo = "Região leste da ilha"
const s_desc = "Não há nada aqui, somente o chão de terra que se estende à oeste.\n\
Nas outras direções o chão termina e começa uma neblina branca.\n\
Acima da neblina só se vê o céu azul. O chão nessa região também é azul,\n\
mas há um [desenho] apagado. Para ver tecle ver desenho."
const dir_o = $s_ini003
const dir_n = $s_ini005
const dir_s = $s_ini005
const dir_l = $s_ini005

func entrou
  ver(arg0)

func cmd_jog
  arg0.evento.addfim(este) # Receber eventos dos jogadores
  !dentro2.objini("p_anao") && criar("p_anao", este, 1)

func cmd_ver
  se arg1 == "desenho"
    arg0.msg("Um diamante azul está enterrado. Para desenterrar tecle desenterrar")
    arg0.msg("Nos lugares em que se pode enterrar você pode enterrar teclando enterrar")
    arg0.msg("seguido do nome do objeto.")
    ret 1

func cmd_desenterrar
  se arg1 == "diamante azul"
    se arg0.var.i_Diamante_Azul
      arg0.msg("Só pode carregar um diamante azul nesta ilha")
    senao
      arg0.var.i_Diamante_Azul = 1
      $mens.p(arg0)
      $mens.mtodos1("$P desenterra Diamante Azul")
    fimse
    ret 1


classe s_ini007
herda sala
const limpar = 20
const s_titulo = "Região oeste da ilha"
const s_desc = "Há várias pedras vermelhas no chão. Há também uma [placa] que pode ser lida\n\
teclando ver placa. À leste fica o centro da linha. Nas outras direções só\n\
se vê o céu azul e uma névoa branca no lugar do chão."
const dir_l = $s_ini003
const dir_n = $s_ini005
const dir_s = $s_ini005
const dir_o = $s_ini005

func cmd_jog
  arg0.evento.addfim(este) # Receber eventos dos jogadores
  se !dentro1.objini("i_diamante_vermelho") # Se não tem diamante vermelho
    criar("i_diamante_vermelho", este, 78) # Cria diamante vermelho
  fimse

func ver_placa
  arg0.msg("A placa diz: Depósito de diamantes vermelhos")
  arg0.msg("Para pegar tecle pegar diamante_vermelho")
  arg0.msg("Para soltar tecle soltar diamante_vermelho")
  arg0.msg("Muitas vezes pode-se abreviar os comandos.")
  arg0.msg("Exemplo, \"pe dia\" ao invés de \"pegar diamante_vermelho\".")
  ret 1

func cmd_pegarobj
  se arg2 == "i_diamante_vermelho"
    se arg0.dentro1.objini("i_diamante_vermelho")
      arg0.msg("Só pode carregar um diamante vermelho nessa ilha.")
    senao
      criar("i_diamante_vermelho", arg0)
      $mens.p(arg0)
      $mens.mtodos1("$P pega Diamante Vermelho.")
    fimse
    ret 1

func cmd_soltarobj
  se arg2 == "i_diamante_vermelho"
    apagar(arg2)
    $mens.p(arg0)
    $mens.mtodos1("$P solta Diamante Vermelho.")
    ret 1


classe s_ini008
herda sala
const limpar = 20
const s_titulo = "Região sul da ilha"
const s_desc = "Afastando-se do centro da ilha, em direção ao sul, há uma velha cabana.\n\
Nota-se uma pequena [placa] em cima da porta (para ver o que está escrido\n\
na placa tecle ver placa)."
const dir_n = $s_ini003
const dir_s = $s_ini009
const dir_l = $s_ini005
const dir_o = $s_ini005
const portaini_s = 2 # Inicialmente fechado
inttempo fechaporta # Para fazer o vento bater a porta

func ver_placa
  arg0.msg("A placa diz:")
  arg0.msg("Para abrir a porta, tecle abrir porta")
  ret 1

func ini
  sala:ini
  evento.addfim(este) # Receber eventos da sala

func cmd_abriudir
  se arg2 == "s"
    fechaporta = 50

func fechaporta_exec
  se porta_s == 1
    mudaporta("s", 2)
    msg("O vento bateu a porta.")
    $s_ini009.msg("O vento bateu a porta.")


classe s_ini009
herda sala
const limpar = 20
const s_titulo = "Dentro da cabana"
const s_desc = "Há uma janela. A vista é do céu, e no lugar do chão, há uma\n\
névoa branca como neve. Há uma pessoa esquisita aqui."
const dir_n = $s_ini008
const portaini_n = 2 # Inicialmente fechado

func ini
  sala:ini, evento.addfim(este) # Receber eventos da sala

func cmd_abriudir
  se arg2 == "n"
    $s_ini008.fechaporta = 50

func cmd_jog
  !dentro2.objini("p_ini_silva") && criar("p_ini_silva", este)
