classe misc
# Miscelânea: tabelas (variáveis) e funções
# Deve ser usado como misc:variável ao invés de $misc.variável
#
# Objeto usado para acessar as saídas das salas
comum ref obj
#
# Objetos usados em misc:separa, misc:sepitem e misc:sepperso
comum txt100 sep1 # Objeto procurado
comum txt100 sep2 # Objeto ou personagem destino
#
# Mensagem conforme o sexo
const sletra0 = "a" # sletra[sexo] = "o" ou "a"
const sletra1 = "o"
const sum0 = "uma" # sum[sexo] = "um" ou "uma"
const sum1 = "um"
#
# Direções
const d1n = "n"
const d1s = "s"
const d1l = "l"
const d1o = "o"
const d1c = "c"
const d1b = "b"
const d1e = "l"
const d1w = "o"
const d1u = "c"
const d1d = "b"
const d1ne = "ne"
const d1no = "no"
const d1se = "se"
const d1so = "so"
const d1nw = "no"
const d1sw = "so"
#
# Direções opostas
const d2n = "s"
const d2s = "n"
const d2l = "o"
const d2o = "l"
const d2c = "b"
const d2b = "c"
const d2e = "o"
const d2w = "l"
const d2u = "b"
const d2d = "c"
const d2ne = "so"
const d2no = "se"
const d2se = "no"
const d2so = "ne"
const d2nw = "se"
const d2sw = "ne"
#
# Tipos de golpes e personagens
# arg0 = número do golpe
# retorna: 0=normal, 1=especial
# Nota 1: a quantidade de tipos está um pouco exagerada
# Nota 2: não necessariamente todos os tipos se aplicam a golpes e PNJs

func atktipo
  casovar arg0
  casose "0" # contusão/normal (um golpe de pancada)
  casose "1" # luta (um golpe de alguma arte marcial)
  casose "2" # cortante (ex. faca)
  casose "3" # perfurante (ex. flecha)
    ret 0
  casose "4" # fogo
  casose "5" # água
  casose "6" # gelo
    ret 1
  casose "7" # terra (ex. terremoto)
  casose "8" # pedra (ex. atirar pedras)
  casose "9" # ar/vento/aéreo (ex. furacão)
    ret 0
  casose "10" # luz
  casose "11" # trevas
  casose "12" # elétrico (ex. raio)
  casose "13" # vegetal (ex. planta carnívora)
  casose "14" # psíquico (ex. confusão)
    ret 1
  casose "15" # inseto
  casose "16" # fantasma
  casose "17" # dragão
    ret 0
  casofim
  ret 0 # Tipo desconhecido: golpe normal
#
# Ajusta defesa conforme o tipo de personagem
# somar 2 = dobra defesa
# subtrair 2 = reduz defesa pela metade
# 9 ou mais = imune ao tipo de golpe

func def0 # Contusão/Normal
  def.1 -= 2 # luta
  def.2 -= 2 # cortante
  def.3 -= 2 # perfurante
  def.16 += 100 # fantasma

func def1 # Luta
  def.9 -= 2 # ar
  def.8 += 2 # pedra
  def.11 += 2 # trevas
  def.15 += 2 # inseto

func def2 # Cortante
  def.4 -= 2 # fogo
  def.5 -= 2 # água
  def.12 -= 2 # eletricidade
  def.6 += 2 # gelo
  def.8 += 2 # pedra
  def.11 += 2 # trevas

func def3 # Perfurante
  def.4 -= 2 # fogo
  def.5 -= 2 # água
  def.12 -= 2 # eletricidade
  def.6 += 2 # gelo
  def.8 += 2 # pedra
  def.11 += 2 # trevas

func def4 # Fogo
  def.5 -= 2 # água
  def.7 -= 2 # terra
  def.8 -= 2 # pedra
  def.4 += 2 # fogo
  def.6 += 2 # gelo
  def.13 += 2 # vegetal
  def.15 += 2 # inseto

func def5 # Água
  def.12 -= 2 # elétrico
  def.13 -= 2 # vegetal
  def.4 += 2 # fogo
  def.5 += 2 # água
  def.6 += 2 # gelo

func def6 # Gelo
  def.1 -= 2 # luta
  def.4 -= 2 # fogo
  def.8 -= 2 # pedra
  def.2 += 2 # cortante
  def.3 += 2 # perfurante
  def.6 += 2 # gelo

func def7 # Terra
  def.4 -= 2 # fogo
  def.5 -= 2 # água
  def.6 -= 2 # gelo
  def.13 -= 2 # vegetal
  def.8 += 2 # pedra
  def.12 += 100 # elétrico

func def8 # Pedra
  def.1 -= 2 # luta
  def.5 -= 2 # água
  def.7 -= 2 # terra
  def.13 -= 2 # vegetal
  def.0 += 2 # contusão/normal
  def.2 += 2 # cortante
  def.3 += 2 # perfurante
  def.4 += 2 # fogo
  def.9 += 2 # ar

func def9 # Ar
  def.6 -= 2 # gelo
  def.8 -= 2 # pedra
  def.12 -= 2 # elétrico
  def.1 += 2 # luta
  def.13 += 2 # vegetal
  def.15 += 2 # inseto
  def.7 += 100 # terra

func def10 # Luz
  def.13 -= 2 # vegetal
  def.16 -= 2 # fantasma
  def.10 += 2 # luz
  def.11 += 2 # trevas
  def.14 += 100 # psíquico

func def11 # Trevas
  def.10 -= 2 # luz
  def.11 -= 2 # trevas
  def.16 -= 2 # fantasma
  def.1 += 2 # luta
  def.15 += 2 # inseto
  def.14 += 100 # psíquico

func def12 # Elétrico
  def.7 -= 2 # terra
  def.9 += 2 # ar
  def.12 += 2 # elétrico

func def13 # Vegetal
  def.2 -= 2 # cortante
  def.4 -= 2 # fogo
  def.6 -= 2 # gelo
  def.9 -= 2 # ar
  def.15 -= 2 # inseto
  def.5 += 2 # água
  def.7 += 2 # terra
  def.10 += 2 # luz
  def.12 += 2 # elétrico
  def.13 += 2 # vegetal

func def14 # Psíquico
  def.1 -= 2 # luta
  def.11 -= 2 # trevas
  def.14 -= 2 # psíquico
  def.11 += 2 # trevas
  def.15 += 2 # inseto
  def.16 += 2 # fantasma

func def15 # Inseto
  def.4 -= 2 # fogo
  def.9 -= 2 # ar
  def.8 -= 2 # pedra
  def.1 += 2 # luta
  def.7 += 2 # terra
  def.13 += 2 # vegetal

func def16 # Fantasma
  def.16 -= 2 # fantasma
  def.10 -= 2 # luz
  def.11 -= 2 # trevas
  def.15 += 2 # inseto
  def.0 += 100 # contusão/normal
  def.1 += 100 # luta
  def.2 += 100 # cortante
  def.3 += 100 # perfurante

func def17 # Dragão
  def.6 -= 2 # gelo
  def.17 -= 2 # dragão
  def.3 += 2 # perfurante
  def.4 += 2 # fogo
  def.5 += 2 # água
  def.12 += 2 # elétrico
  def.13 += 2 # vegetal

func tempo # Eventos de tempo (cria objeto), vide classe evtempo
  se txt(arg0) && ref(arg1)
    ref r
    r = criar("evtempo")
    r.tempo1 = r.tempo2 = 1
    r.obj = este
    r.func = arg0
    r.tfunc = inttotal(arg0)
    r.perso1 = arg1, r.perso1.evento.addfim(r)
    r.perso2 = arg2, r.perso2.evento.addfim(r)
    r.perso3 = arg3, r.perso3.evento.addfim(r)

func separa # Separa o nome do objeto do nome do destino
# arg0 = texto completo
# Faz: misc:sep1 = objeto procurado, misc:sep2 = alvo
  casovar intsub(arg0)
  casose "0"
  casose "1"
    misc:sep1 = arg0
    misc:sep2 = ""
    ret
  casose "2"
    se txt1(arg0) != int(txt1(arg0))
      misc:sep1 = txt1(arg0)
      misc:sep2 = txt2(arg0)
    senao
      misc:sep1 = arg0
      misc:sep2 = ""
    fimse
    ret
  casose "3"
    misc:sep1 = txtsub(arg0, 0, 2)
    misc:sep2 = txtsub(arg0, 2)
    ret
  casose
    uint8 tam
    tam = intsub(arg0) - 1
    misc:sep1 = txtsub(arg0, 0, tam)
    misc:sep2 = txtsub(arg0, tam)
  casofim

func sepitem # Procura o item destino
# arg0 = personagem, misc:sep2 = nome do alvo
# Retorna: o alvo ou nulo se não encontrar
  listaitem i
  nomeobj n
  n.ini(misc:sep2, 1)
  epara i = arg0.dentro1.ini, i, i.depois
    ret arg0.visivel(i.obj) && n.nome(i.obj.ident), i.obj
  efim
  epara i = arg0.dono.dentro1.ini, i, i.depois
    ret arg0.visivel(i.obj) && n.nome(i.obj.ident), i.obj
  efim

func sepperso # Procura o personagem destino
# arg0 = personagem, misc:sep2 = nome do alvo
# Retorna: o alvo ou nulo se não encontrar
  listaitem i
  nomeobj n
  n.ini(misc:sep2, 1)
  epara i = arg0.dentro2.ini, i, i.depois
    ret arg0.visivel(i.obj) && n.nome(i.obj.ident), i.obj
  efim
  epara i = arg0.dono.dentro2.ini, i, i.depois
    ret arg0.visivel(i.obj) && n.nome(i.obj.ident), i.obj
  efim


classe evtempo
# Processa eventos de tempo
#
# Deve ser criado com:
# misc:tempo("nome da função", personagens)
# É permitido até 3 personagens.
# Se um dos personagens mudar de sala, o evento é encerrado.
#
# Resultado: são executadas as funções cujo nome é o nome
# especificado seguido do tempo decorrido, em décimos de segundo
# A função com o nome especificado seguido de "0" é executada
# quando o objeto for apagado.
#
# Na função:
# arg0 a arg2 = personagens
# arg3 = objeto evtempo
#
inttempo tempo1 # Para gerar eventos de tempo
uint32 tempo2 # tempo2-tempo1 = Tempo decorrido
txt100 func # Nome da função que deve chamar
uint8 tfunc # Tamanho de func em caracteres
ref obj # Objeto que contém a função
ref perso1 # Primeiro personagem
ref perso2 # Segundo personagem
ref perso3 # Terceito personagem

func fim # Este objeto foi apagado
  obj.[func]0(perso1, perso2, perso3, este)

func cmd_saiu # Personagem saiu da sala
  apagar(este)

func cmd_fim # Personagem saiu do jogo
  apagar(este)

func tempo1_exec # Executa eventos
  se !obj
    apagar(este)
    ret
  fimse
  prog p
  uint32 x
  uint32 v
  x = 1000000
  epara p.inifunc(obj, func), p.lin, p.depois
    v = txt(p.texto, tfunc)
    v > tempo2 && v < x && (x = v)
  efim
  x == 1000000 && apagar(este)
  v = tempo2
  tempo1 = x - tempo2
  tempo2 = x
  obj.[func + v](perso1, perso2, perso3, este)
