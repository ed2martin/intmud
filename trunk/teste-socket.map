[teste]
comum serv servidor
socket conec
socket remoto
const msg = conec.msg(arg0)
func iniclasse
  se ![arg0]:servidor.abrir("localhost", 2000)
    terminar
func servidor_socket
  criar("teste", arg0)
func ini
  conec = arg0
  conec.colmin = 20
  conec.colmax = 30
  msg("Comandos:\n")
  msg("c=conectar\n")
  msg("f=fechar conexão\n")
  msg("i=informações\n")
  msg("m seguido ou não de um texto = enviar mensagem\n")
#  conec.proto = 2
func conec_msg
  conec.msg("Você escreveu " + arg0 + "\n")
  escr_[txt1(arg0)](txt2(arg0))
func escr_c
  se remoto.abrir("localhost", 1956)
    msg("Conectando\n")
  senão
    msg("Erro ao conectar\n")
func escr_f
  remoto.fechar
  msg("Conexão fechada\n")
func escr_m
  remoto.msg(arg0 + "\n")
  msg("> " + arg0 + "\n")
#remoto.proto = 4 # Papovox
func escr_i
  msg(escr_i[remoto.proto] + "\n")
  msg("Colunas remoto: " + remoto.colmin + " e " + remoto.colmax + "\n")
  msg("Colunas local: " + conec.colmin + " e " + conec.colmax + "\n")
const escr_i0 = "Desconectado"
const escr_i1 = "Conectando"
const escr_i2 = "Conectado Telnet"
const escr_i3 = "Conectado IRC"
const escr_i4 = "Conectado Papovox"
const escr_i5 = "Conectado Tela"
func remoto_con
  msg("Conectado\n")
func remoto_err
  msg("Erro ao conectar: " + arg0 + "\n")
func remoto_msg
  msg("< " + arg0 + "\n")
func remoto_fechou
  msg("Conexão fechada remotamente\n")
