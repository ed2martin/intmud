classe comum_item
herda comum_obj
# Itens
const item = 1 # Para identificar que é item
const bitinv = 3 # Grau de invisibilidade do personagem/item
const sexo = 0 # Sexo: 0=feminino, 1=masculino
const nome = "sem nome"
const ident = nome
const descpos = txtmaimin(nome) + " está aqui."
const descver = "Você não vê nada de especial."
const jogsav = 1 # Se deve salvar o item ao salvar personagem do jogador
sav uint16 pnivel # Nível atual (apenas para mostrar ao jogador)
sav uint8 vestpos # Em que posição está vestindo, 0=nenhuma

func ini # Objeto foi criado (arg0=dono, arg1=nível)
  se arg2 || inivar
    textotxt t
    epara t.addfim(txttroca(txt(arg2) + "\n" + inivar, ",", "\n")), t.linhas, var.mudar(t.remove)
    efim
  fimse
  pnivel = arg1 ? arg1 : nivel, comum_obj:ini, i_aberto = i_abertoini
  cmd_ini(arg0, arg1, arg2, arg3, arg4)
  arg0 && mudadono(arg0)
  epara nulo, var.ini("e_"), var.mudar(var.ini("e_"))
    criar(var.ini("e_"), este, var.valor(var.ini("e_")))
  efim

func fim # Objeto foi apagado
  ref r
  r = dono, comum_obj:fim, cmd_fim(r)

func mudadono # Item muda de dono (arg0 = novo dono, arg1 = quantidade)
  cmd_mudadono(arg0), dono = arg0

func vestir # Veste o item, arg0=posição
  dono.recalc = 1 # Para recalcular atributos
  ref r
  r = dono, idono.remove, vestpos = arg0
  se vestpos
    idono = r.dentro2.addfim(este)
  senao
    idono = r.dentro1.addfim(este)

const apagar = apagar(este) # Apaga o item (arg0 = quantidade)


classe comum_itemgrupo
herda comum_item
# Itens mais simples:
# Quando houver mais de um em um mesmo lugar, eles são agrupados em um
# único objeto. A única exceção é quando forem vestidos (não são agrupados).
const item = 2 # Para identificar que é item
const pnivel = 0
sav uint8 objtot # Quantidade de itens

func objproc # Procura um objeto do mesmo tipo em uma listaobj
# Objetivo: saber se pode agrupar objetos
# arg0 = variável listaobj
# Retorna: o objeto correspondente ou nulo se não houver
  ret arg0.objini(este)

func ini # Objeto foi criado (arg0=dono, arg1=quantidade)
  objtot = arg1
  !objtot && (objtot = 1) # Garante pelo menos um item
  comum_obj:ini, cmd_ini(arg0, arg1, arg2, arg3, arg4)
  arg0 && mudadono(arg0, objtot)

func mudadono # Item muda de dono (arg0 = novo dono, arg1 = quantidade)
  ret !ref(arg0) || 0 >= arg1, nulo
  ref r
  r = objproc(arg0.dentro1) # r = objeto no novo dono
  se objtot > arg1 # Se não vai mover tudo
    se r
      r.objtot += arg1, r.ajustapeso
    senao
      criar(este, arg0, arg1)
    fimse
    objtot -= arg1, ajustapeso
  senao r # Se já existe objeto no destino
    r.objtot += arg1, r.ajustapeso
    apagar(este)
  senao # Senão, apenas muda o dono
    cmd_mudadono(arg0), dono = arg0
  fimse

func vestir # Veste o item, arg0=posição
  uint8 pos
  pos = arg0
  se !pos # Checa se deve remover
    ret !vestpos, nulo # Retorna se não está vestindo
    dono.recalc = 1 # Para recalcular atributos
    ref r
    r = objproc(dono.dentro1) # Procura objeto que não está vestindo
    se r # Encontrou: aumenta a quantidade
      r.objtot += 1, r.ajustapeso, apagar(este)
    senao # Não encontrou: move
      vestpos = 0, r = dono, idono.remove, idono = r.dentro1.addfim(este)
    fimse
  senao vestpos # Se já está vestindo, muda a posição
    dono.recalc = 1 # Para recalcular atributos
    vestpos = pos
  senao objtot <= 1 # Se tem só um objeto, veste
    dono.recalc = 1 # Para recalcular atributos
    vestpos = pos
    ref r
    r = dono, idono.remove, idono = r.dentro2.addfim(este)
  senao # Tem mais de um objeto, cria objeto e veste
    dono.recalc = 1 # Para recalcular atributos
    objtot -= 1, ajustapeso
    ref r
    r = criar(este)
    r.dono = dono, r.vestpos = pos
    r.idono.remove, r.idono = dono.dentro2.addfim(r)
  fimse

func apagar # Apaga o item (arg0 = quantidade)
  objtot -= arg0, !objtot && apagar(este)


classe comum_itemperso
herda comum_item
# Itens completos
const item = 3 # Para identificar que é item
intexec recalc # Se deve acertar variáveis do item
listaobj persolugar # Quem está sentado ou deitado
# const i_abertoini = 0 # Valor inicial de aberto
sav uint8 i_aberto # Situação atual do container, ou 0 se não for container

func fim # Objeto foi apagado
  ref r
  persolugar.limpar, r = dono, comum_obj:fim, cmd_fim(r)

func mudadono # Item muda de dono (arg0 = novo dono, arg1 = quantidade)
  persolugar.limpar, cmd_mudadono(arg0), dono = arg0

func recalc_exec # Acerta variáveis do item
  se int(i_abertoini) < 2 || i_aberto < 2
    i_aberto = i_abertoini
