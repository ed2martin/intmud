classe a_casa
herda area
const a_nome = "Dentro da casa"
#
# Tarefas:
# 1. upar na sala ampla
# 2. levar banquinho no quarto para subir no sótão
# 3. desafiar um PNJ para poder salvar o jogo
# 4. pegar comida na sala de jantar (tem duas com a mesma descrição)
# 5. soltar comida para passar pelo cão de guarda
# 6. pegar a espingarda
# 7. descer pela chaminé, até a sala ampla
# 8. pode explorar algumas salas com um MOB que são frágeis a tiros
# 9. pegar um animal
# 10. passar pelas múmias, são frágeis apenas à água
# 11. ir no porão e explorar algumas galerias subterrâneas
# 12. encontrar um eremita nas galerias, dar um livro e água para pegar o gancho
# 13. conseguir a corda nos dutos de ar da casa
#
# Mapa da casa:
#
# .          sótão------no telhado
# .                        |
# .        no telhado---no telhado---no telhado
# .
# .
# .         sala de jogos
# .              |
# .           corredor----quarto---banheiro
# .              |
# .  quarto----sala----corredor---madeira podre
# .                       |
# .         banheiro---corredor---quarto -> sótão
# .                       |
# .                     baixo


classe b_casa
herda sala
const s_terreno = 1
const s_luta = 1
const s_item = 1


classe p_boneco_de_papel
herda comum_persobat
const nome = "Boneco de papel"
const sexo = 1
const descpos = "Um boneco de papel está se mexendo."
const descver = "Parece ser muito frágil, pois é feito de papel."
const p_agressivo = 1


classe p_boneco_menina
herda comum_persobat
const nome = "Boneco de papel"
const sexo = 1
const descpos = "Um boneco de papel está se mexendo."
const descver = "Parece ser muito frágil, pois é feito de papel."
const p_agressivo = 1
intinc tempo # Para não sair do jogo logo de cara

func cmd_recalc1 # Aumenta pontos de vida e precisão
  pvidamax = pvidamax * 2 + 50
  precisao += 10

func cmd_pnj_proc
  se tempo && !atkenv # Se passou um tempo e não está batalhando
    apagar(este)
    dono.msg("Boneco de papel caiu do telhado.")
    ret 1


classe p_boneco_salvar
herda comum_persobat
const nome = "Boneco de papelão"
const sexo = 1
const descpos = "Um boneco de papelão está se mexendo."
const descver = "Parece frágil, pois é feito de papelão."
const p_agressivo = 1
intinc tempo # Para não sair do jogo logo de cara

func cmd_recalc1 # Aumenta pontos de vida
  pvidamax = pvidamax * 2 + 50

func cmd_pnj_proc
  se tempo && !atkenv # Se passou um tempo e não está batalhando
    apagar(este)
    dono.msg("Boneco de papel caiu do telhado.")
    ret 1


classe p_casa_menina
herda comum_persobat
const nome = "Menina"
const sexo = 0
const descpos = "Uma menina está sentada aqui, dobrando papéis."
const descver = "Debaixo do braço carrega diversas artes feitas dobrando-se papéis."
const eveste = 1 # Receber eventos do próprio objeto
const evsala = 1 # Receber eventos do dono se o dono for sala
inttempo tempo

func cmd_mudadono
  tempo = 70

func cmd_chegou # Chegou na sala
  dono.dentro2.objini("jogador") ? tempo.pos : tempo.neg
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func cmd_saiu # Chegou da sala
  dono.dentro2.objini("jogador") ? tempo.pos : tempo.neg

func entrou20
  arg0.msg("Menina: olá!")

func entrou40
  arg0.msg("Menina: Ao leste tem uns bonecos fortes. Se perceber que vai perder,")
  arg0.msg("Menina: tecle fugir. Depois vá ao quarto e tecle DEITAR.")

func tempo_exec
  tempo = rand(80, 200)
  dono.msg("Menina joga um boneco de papel pelo telhado.")

func cmd_atacar
  se arg1 == este # Atacar a menina
# Obtém o personagem de nível mais alto
    uint16 nivel
    listaitem i
    epara i = dono.dentro2.ini, i, i.depois
      nivel < i.obj.pnivel && (nivel = i.obj.pnivel)
    efim
# Cria bonecos de papel de nível um pouco abaixo
    nivel -= 2
    dono.msg("Menina acabou derrubando dois bonecos de papel.")
    ref r
    r = criar("p_boneco_menina", dono, nivel, "h_socar=1,h_chutar=1")
    r.batalhaini(arg0)
    r = criar("p_boneco_menina", dono, nivel, "h_socar=1,h_chutar=1")
    r.batalhaini(arg0)


classe s_casa001
herda b_casa
const s_luta = 0 # Sem luta
const s_titulo = "Quarto"
const s_desc = "Enquanto os outros cômodos são de um tamanho desproporcinal, esse\n\
quarto se destaca por ser pequeno. Dois beliches ocupam quase todo\n\
o espaço. O espaço de um metro entre eles segue à leste.\n\
Você pode descansar mais um pouco teclando DEITAR"
const dir_l = $s_casa002

func cmd_salaperso # Personagem chegou
  misc:evento(arg0) # Receber eventos do personagem
# Para teste: muda o nível do personagem
# arg0.pnivel = 2, arg0.recalc = 1

func cmd_escr # Descansar
  se arg1 == "deitar"
    uint8 total
    epara nulo, total < config:animal1, total += 1
      arg0.animal.[total].restaurar
    efim
    arg0.restaurar
    $mens.p(arg0)
    $mens.mtodos1("$P descansa e se recupera.")
    ret 1


classe s_casa002
herda b_casa
const s_titulo = "Sala ampla"
const s_desc = "Esta é uma sala bastante ampla, tanto em largura quanto em comprimento.\n\
Há corredores ao norte e ao leste. Há também janelas ao sul, e está\n\
aberta, mas é alto demais para pular. Um boneco de papel balança\n\
lá fora, conforme o vento."
const dir_o = $s_casa001
const dir_n = $s_casa003
const dir_l = $s_casa008
inttempo tempomax

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  tempomax = rand(10, 50)
  misc:evento(arg0) # Receber eventos do jogador

func tempomax_exec
  ret !dentro2.objini("jogador"), nulo
  tempomax = rand(50, 80)
  se misc:salacriar("p_boneco_de_papel", 1, "h_socar=1,h_chutar=1")
    msg("Um boneco de papel entrou pela janela.")

func cmd_fugir
  arg0.pmove -= 5
  arg0.batalhafim
  $mens.p(arg0)
  $mens.mtodos1("$P fugiu para oeste.")
  arg0.mudadono($s_casa001)
  $mens.mvis2("", "$P chegou do leste.")
  ret 1


classe s_casa003
herda b_casa
const s_titulo = "Passagem"
const s_desc = "O chão é todo de madeira, mas o que mais se destaca são as paredes e\n\
o teto. Foram esculpidos de forma que parecesse uma caverna.\n\
Há um antigo [quadro] preso na parede."
const dir_s = $s_casa002
const dir_n = $s_casa004
const dir_l = $s_casa006

func cmd_salaperso # Personagem chegou
  se arg0.jog && config:escolhenivel
    misc:evento(arg0) # Receber eventos do jogador
    misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou2
  arg0.msg("Nesta versão de teste do jogo tecle aqui:")
  arg0.msg("A ou AA para aumentar 1 ou 10 níveis")
  arg0.msg("D ou DD para diminuir 1 ou 10 níveis")

func ver_quadro
  arg0.msg("Quadro")
  arg0.msg("Você vê o desenho de uma antiga mansão, num belo dia de sol.")
  arg0.msg("A assinatura você não consegue ler, está muito apagada.")
  arg0.msg("Mas há uma data aqui, 19/8/2012.")
  ret 1

func cmd_escr
  casovar txtmin(arg1)
  casose "aa"
    arg0.pnivel += 9
    arg0.recalc = 1
  casose "a"
    arg0.pnivel += 1
    arg0.recalc = 1
    se arg0.pnivel > 80
      arg0.msg("Nível máximo é 80")
      arg0.pnivel = 80
    senao
      arg0.msg(arg0.nome + " é nível " + arg0.pnivel)
    fimse
    ret 1
  casose "dd"
    arg0.pnivel -= 9
    arg0.recalc = 1
  casose "d"
    arg0.pnivel -= 1
    arg0.recalc = 1
    arg0.msg(arg0.nome + " é nível " + arg0.pnivel)
    ret 1
  casofim


classe s_casa004
herda b_casa
const s_titulo = "Sala de jogos"
const s_desc = "Aqui há várias máquinas de jogos de azar, embora estejam desligadas.\n\
Você se pergunta se o dono da casa pratica atividades ilegais com essas\n\
máquinas. Entretanto, há um jogo diferente, que não requer eletricidade.\n\
Chama-se [batalha], são pequenos bonecos que lutam entre si."
const dir_s = $s_casa003

func ver_batalha
  listaitem i
  textotxt t
  epara i = $s_casa005.dentro2.ini, i, i.depois
    i.obj.jog && t.addfim(i.obj.nome)
  efim
  arg0.msg("Jogo Batalha")
  casovar t.linhas
  casose "0"
    arg0.msg("Você não vê nenhum boneco nesse jogo. Que esquisito!")
    sair
  casose "1"
    arg0.msg("Você vê um boneco chamado " + t.remove)
    sair
  casose
    arg0.msg("Você vê os seguintes bonecos: " + txttroca(t.remove(1000), "\n", ", "))
  casofim
  arg0.msg("Tem um botão esquisito, para apertá-lo tecle APERTAR")
  ret 1

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:evento(arg0) # Receber eventos do jogador

func cmd_escr
  se arg1 == "apertar"
    $mens.p(arg0)
    $mens.mtodos1("$P apertou o botão e foi transformado em um boneco.")
    arg0.mudadono($s_casa005)
    $mens.mtodos2("", "Boneco $p acaba de cair aqui.")
    ret 1


classe s_casa005
herda b_casa
const s_titulo = "Arena"
const s_desc = "Você está dentro de um jogo esquisito. Você é uma simples marionete,\n\
comandada por uma pessoa que mais parece um gigante daqui de dentro.\n\
Há uma saída para cima, porém fica lacrada quando há mais de um boneco."
const s_luta = 2 # Permite luta entre jogadores
const dir_c = $s_casa004
const eveste = 1 # Receber eventos do próprio objeto

func cmd_c
  se dentro2.total > 1
    arg0.msg("A saída para cima está lacrada.")
  senao
    arg0.mudadono($s_casa004)
    $mens.p(arg0)
    $mens.mtodos2("", "$P acaba de sair de dentro do jogo.")
  fimse
  ret 1


classe s_casa006
herda b_casa
const s_titulo = "Quarto"
const s_desc = "Um pequeno quarto de empregada, agora usado como quarto de despejo.\n\
No entanto, ainda há o que restou de uma cama de casal."
const dir_o = $s_casa003
const dir_l = $s_casa007


classe s_casa007
herda b_casa
const s_titulo = "Banheiro"
const s_desc = "Um pequeno banheiro, ou pelo menos o que restou dele. Ainda tem um\n\
chuveiro, mas aonde tinha a privada agora só resta o [cano]."
const dir_o = $s_casa006

func ver_cano
  arg0.msg("Um papel está preso na parte de dentro do cano.")
  arg0.msg("Basicamente diz \"Para repetir a descrição da sala tecle VER\".")
  ret 1


classe s_casa008
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Com um grande tapete vermelho no chão, esse corredor vai de leste a oeste.\n\
Um grande lustre ilumina o local. Nota-se uma pequena saída ao sul."
const dir_l = este
const dir_o = $s_casa002
const dir_s = $s_casa009
const eveste = 1 # Receber eventos do próprio objeto

func cmd_l
  arg0.msg("A madeira está podre e você caiu, mas não resistiu à queda.")
  $mens.p(arg0)
  $mens.mtodos2("", "$P foi para leste e caiu na madeira podre.")
  arg0.morreu
  ret 1


classe s_casa009
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Um pouco menor que o corredor ao norte, mas nem por isso menos luxuoso.\n\
Uma escada dá acesso ao andar de baixo. Apenas um grande lustre ilumina\n\
o local."
const dir_n = $s_casa008
const dir_o = $s_casa010
const dir_l = $s_casa011


classe s_casa010
herda b_casa
const s_titulo = "Banheiro"
const s_desc = "Contém o que se espera de um banheiro. Uma pia, um vaso e um lugar para\n\
tomar banho. Não é grande, nem muito luxuoso, apenas um banheiro para os\n\
empregados."
const dir_l = $s_casa009


classe s_casa011
herda b_casa
const s_titulo = "Quarto"
const dir_o = $s_casa009
const dir_c = misc:obj.var.d_escada_ ? $s_casa012
const s_desc0 = "Há um tapete persa no chão bastante gasto e sujo. Alguém deixou a cama\n\
de baixo do beliche desarrumada. Há um alçapão no teto, sabe-se lá o que\n\
tem dentro."
const s_desc1 = " Mas mesmo subindo em cima do beliche, ainda fica um pouco alto."
const s_desc2 = "\n\
Tem também alguma coisa em baixo do beliche, mas em pé não dá para ver\n\
o que é."

func s_desc
  se arg0.var.d_escada_ # Escada aberta
    ret s_desc0
  senao arg0.posicao < 7 # Escada fechada, jogador sentado ou deitado
    ret s_desc0 + s_desc1
  senao # Escada fechada, jogador em pé
    ret s_desc0 + s_desc1 + s_desc2

func cmd_salaperso # Personagem chegou
  misc:salacriar("i_casa_escada")


classe i_casa_escada
herda comum_item
const nome = "escada"
const sexo = 0
const pesoobj = 10200
const descver = "Pode-se abrí-la teclando ABRIR ESCADA e fechar com FECHAR ESCADA."
const visivel = arg0.posicao < 7 || arg0.var.d_escada_
const eveste = 1 # Receber eventos do próprio objeto

func descpos
  se misc:obj.var.d_escada_
    ret "Uma grande escada está aberta aqui."
  senao
    ret "Uma escada está em baixo do beliche."

func cmd_pegarobj
  se arg2 == este
    arg0.msg("A escada é grande demais para você pegar.")
    ret 1

func cmd_abrirobj
  se arg1 == este
    se arg0.var.d_escada_
      arg0.msg("A escada já está aberta.")
    senao
      arg0.msg("Você abre a escada.")
      arg0.var.d_escada_ = 1
    fimse
    ret 1

func cmd_fecharobj
  se arg1 == este
    se arg0.var.d_escada_
      arg0.msg("Você fecha a escada.")
      arg0.var.d_escada_ = 0
    senao
      arg0.msg("A escada já está fechada.")
    fimse
    ret 1


classe s_casa012
herda b_casa
const s_titulo = "Sótão"
const s_desc = "Um lugar bastante apertado, entre o teto e o telhado. Como pode-se\n\
imaginar, está bastante sujo, pois a casa é antiga. Há aqui uma pequena\n\
janela para leste, cuja vista dá para o telhado."
const dir_b = $s_casa011
const dir_l = $s_casa013


classe s_casa013
herda b_casa
const s_titulo = "No telhado"
const s_desc = "Você anda com cuidado, pois as telhas são de cerâmica, e já estão bastante\n\
gastas. Aqui é bastante alto, e o vento bate forte."
const dir_o = $s_casa012
const dir_s = $s_casa014


classe s_casa014
herda b_casa
const s_titulo = "Laje"
const s_desc = "Aqui o chão é firme, e você consegue andar com segurança. Toda essa\n\
área foi impermeabilizada. Nota-se um revestimento preto sobre o chão.\n\
Talvez quisessem fazer um jardim aqui. Ao norte, começa o telhado."
const dir_n = $s_casa013
const dir_o = $s_casa015
const dir_l = $s_casa016

func cmd_salaperso # Personagem chegou
  misc:salacriar("p_casa_menina", 10, "h_chutar=1")


classe s_casa015
herda b_casa
const s_titulo = "Telhado"
const s_desc = "À leste começa uma laje. À oeste, o telhado continua, mas você não sente.\n\
segurança para caminhar."
const dir_l = $s_casa014


classe s_casa016
herda b_casa
const s_titulo = "Laje"
const s_desc = "Essa parte da laje bem que poderia ser um jardim. Há um pequeno muro à leste,\n\
que se estende ao sul. Não é possível caminhar no telhado ao norte,\n\
devido ao forte vento."
const dir_o = $s_casa014

func cmd_salaperso # Personagem chegou
  se arg0.jog && !arg0.senha # Jogador que ainda não salvou o jogo
    ref r
    r = criar("p_boneco_salvar", este, 10, "h_socar=1,h_chutar=1")
    r.batalhaini(arg0)
    misc:evento(arg0) # Receber eventos do jogador

func cmd_fugir
  arg0.pmove -= 5
  arg0.batalhafim
  $mens.p(arg0)
  $mens.mtodos1("$P fugiu para oeste.")
  arg0.mudadono($s_casa001)
  $mens.mvis2("", "$P chegou do leste.")
  ret 1
