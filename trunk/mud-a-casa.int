classe a_casa
herda area
const a_nome = "Dentro da casa"
#
# Tarefas:
# 1. upar na sala ampla
# 2. abrir a escada no quarto para subir no sótão
# 3. desafiar um PNJ para poder salvar o jogo
# 4. pegar comida na sala de jantar (tem duas com a mesma descrição)
# 5. soltar comida para passar pelo cão de guarda
# 6. pegar a espingarda
# 7. descer pela chaminé, até a sala ampla
# 8. pode explorar algumas salas com um MOB que são frágeis a tiros
# 9. pegar um animal
# 10. passar pelas múmias, são frágeis apenas à água
# 11. ir no porão e explorar algumas galerias subterrâneas
# 12. encontrar um eremita nas galerias, dar um livro e água para pegar o gancho
# 13. conseguir a corda nos dutos de ar da casa
#
# Mapa da casa:
#
# .          sótão------no telhado
# .                        |
# .        no telhado---no telhado---no telhado
# .
# .
# .         sala de jogos
# .              |
# .           corredor----quarto---banheiro
# .              |
# .  quarto----sala----corredor---madeira podre
# .                       |
# .         banheiro---corredor---quarto -> sótão
# .                       |
# .                     baixo
const a_reset = 2
const a_tempo = 1


classe b_casa
herda sala
const s_area = "casa"
const s_terreno = 1
const s_luta = 1
const s_item = 1


classe p_casa001
herda comum_persobat
const nome = "Boneco de papel"
const sexoini = 1
const descpos = "Um boneco de papel está se mexendo."
const descver = "Parece ser muito frágil, pois é feito de papel."
const p_agressivo = 2


classe p_casa002
herda comum_persobat
const nome = "Boneco de papel"
const sexoini = 1
const descpos = "Um boneco de papel está se mexendo."
const descver = "Parece ser muito frágil, pois é feito de papel."
ref alvo # Quem esse personagem deve atacar

func cmd_recalc1 # Aumenta pontos de vida e precisão
  pvidamax = pvidamax * 2 + 50
  precisao += 10

func cmd_ini # Personagem criado: anota o alvo
  alvo = arg3

func cmd_pnj_proc
  se alvo.dono == dono # Se alvo está na mesma sala: ataca
    batalhaini(alvo)
  senao # Alvo em outra sala: boneco vai embora
    apagar(este)
    dono.msg("Boneco de papel caiu do telhado.")
    ret 1


classe p_casa003
herda comum_persobat
const nome = "Boneco de papelão"
const sexoini = 1
const descpos = "Um boneco de papelão está se mexendo."
const descver = "Parece frágil, pois é feito de papelão."
const eveste = 1 # Receber eventos do próprio objeto
ref alvo # Quem esse personagem deve atacar

func cmd_ini # Personagem foi criado: anota o alvo
# Nota: o alvo foi passado como arg3 pela função criar()
  alvo = arg3

func cmd_recalc1 # Aumenta pontos de vida
  pvidamax = pvidamax * 2 + 50

func cmd_pnj_proc # Processamento do PNJ
# Deve retornar 1 se não deve prosseguir com as ações do PNJ
  se alvo.dono == dono # Se alvo está na mesma sala: ataca
    batalhaini(alvo)
  senao # Alvo em outra sala: boneco vai embora
    apagar(este)
    dono.msg("Boneco de papelão caiu do telhado.")
    ret 1

func cmd_fim # Personagem foi apagado
  se pvida == 0 && alvo # Se morreu e o alvo ainda existe
    alvo.var.j_salvar = 1 # Altera uma variável do alvo

func cmd_atkrec # Somente o jogador alvo pode atacar o boneco
  se arg0 != alvo
    $mens.p(arg0, este)
    $mens.mtodos2("$A impede o seu ataque.", "$A impede o ataque de $P.")
    ret 1


classe p_casa004
herda comum_persobat
const nome = "Menina"
const sexoini = 0
const descpos = "Uma menina está sentada aqui, dobrando papéis."
const descver = "Debaixo do braço carrega diversas artes feitas dobrando-se papéis."
const eveste = 1 # Receber eventos do próprio objeto
const evsala = 1 # Receber eventos do dono se o dono for sala
inttempo tempo

func cmd_mudadono
  tempo = 70

func cmd_chegou # Chegou na sala
  dono.dentro2.objini("jogador") ? tempo.pos : tempo.neg
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func cmd_saiu # Saiu da sala
  dono.dentro2.objini("jogador") ? tempo.pos : tempo.neg

func entrou20
  se arg0.var.j_salvar
    arg0.msg("Menina: Você destruiu meu boneco de papelão!!!")
  senao !arg0.senha
    arg0.msg("Menina: Olá!")

func entrou40
  se arg0.var.j_salvar
    arg0.msg("Menina: Agora vá até o quarto. Poderá salvar o jogo lá.")
  senao !arg0.senha
    arg0.msg("Menina: Ao leste tem uns bonecos fortes. Se perceber que vai perder,")
    arg0.msg("Menina: tecle fugir. Depois vá ao quarto e tecle DEITAR.")

func tempo_exec
  tempo = rand(80, 200)
  dono.msg("Menina joga um boneco de papel pelo telhado.")

func cmd_recalc1 # Faz a menina ser praticamente imune a ataques
  uint8 x
  enquanto x < 30
    deftipo.[x] = 200, x += 1
  efim
  evasao = 0

func desaparece2
  dono.msg("Menina ficou transparente por um instante")
  batalhafim

func cmd_atacar
  se arg1 == este # Atacar a menina
    misc:tempo("desaparece", este) # Dispara funções 'desaparece'
    ret
# Para teste:
# Obtém o personagem de nível mais alto e cria bonecos 2 níveis abaixo
    uint16 nivel
    listaitem i
    epara i = dono.dentro2.ini, i, i.depois
      nivel < i.obj.pnivel && (nivel = i.obj.pnivel)
    efim
    dono.msg("Menina acabou derrubando dois bonecos de papel.")
    criar("p_casa002", dono, nivel - 2, "h_socar=1", arg0)
    criar("p_casa002", dono, nivel - 2, "h_socar=1", arg0)


classe s_casa001
herda b_casa
const s_luta = 0 # Sem luta
const s_titulo = "Quarto"
const s_desc = misc:obj.var.j_salvar ? s_desc1 + s_desc2 : s_desc1
const s_desc1 = "Enquanto os outros cômodos são de um tamanho desproporcinal, esse\n\
quarto se destaca por ser pequeno. Dois beliches ocupam quase todo\n\
o espaço. O espaço de um metro entre eles segue à leste.\n\
Você pode descansar mais um pouco teclando DEITAR"
const s_desc2 = "\nPode também salvar o jogo ou mudar a senha teclando SALVAR"
const dir_l = $s_casa002

func cmd_salaperso # Personagem chegou
  misc:evento(arg0) # Receber eventos do personagem
# Para teste: muda o nível do personagem
# arg0.pnivel = 18, arg0.recalc = 1

func cmd_escr # Descansar
  se arg1 == "deitar"
    uint8 total
    epara nulo, total < config:animal1, total += 1
      arg0.animal.[total].restaurar
    efim
    arg0.restaurar
    $mens.p(arg0)
    $mens.mtodos1("$P descansa e se recupera.")
    ret 1
  senao arg1 == "salvar" && (arg0.var.j_salvar || arg0.senha)
    arg0.mudasenha
    ret 1

const s_pnj = 0


classe s_casa002
herda b_casa
const s_titulo = "Sala ampla"
const s_desc = "Esta é uma sala bastante ampla, tanto em largura quanto em comprimento.\n\
Há corredores ao norte e ao leste. Há também janelas ao sul, e está\n\
aberta, mas é alto demais para pular. Um boneco de papel balança\n\
lá fora, conforme o vento."
const dir_o = $s_casa001
const dir_n = $s_casa003
const dir_l = $s_casa008
inttempo tempomax

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  tempomax = rand(10, 50)
  misc:evento(arg0) # Receber eventos do jogador

func tempomax_exec
  ret !dentro2.objini("jogador"), nulo
  tempomax = rand(50, 80)
  se misc:salacriar("p_casa001", 1, "h_socar=1,h_chutar=1,e_apagar=100")
    msg("Um boneco de papel entrou pela janela.")

func cmd_fugir
  arg0.pmove -= 5
  arg0.batalhafim
  $mens.p(arg0)
  $mens.mtodos1("$P fugiu para oeste.")
  arg0.mudadono($s_casa001)
  $mens.mvis2("", "$P chegou do leste.")
  ret 1


classe s_casa003
herda b_casa
const s_titulo = "Passagem"
const s_desc = "O chão é todo de madeira, mas o que mais se destaca são as paredes e\n\
o teto. Foram esculpidos de forma que parecesse uma caverna.\n\
Há um antigo [quadro] preso na parede."
const dir_s = $s_casa002
const dir_n = $s_casa004
const dir_l = $s_casa006
const ver_quadro = "Quadro\n\
Você vê o desenho de uma antiga mansão, num belo dia de sol.\n\
A assinatura você não consegue ler, está muito apagada.\n\
Mas há uma data aqui, 8/9/2012."

func cmd_salaperso # Personagem chegou
  se arg0.jog && config:escolhenivel
    misc:evento(arg0) # Receber eventos do jogador
    misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou2
  arg0.msg("Nesta versão de teste do jogo tecle aqui:")
  arg0.msg("A ou AA para aumentar 1 ou 10 níveis")
  arg0.msg("D ou DD para diminuir 1 ou 10 níveis")

func cmd_escr
  casovar txtmin(arg1)
  casose "aa"
    arg0.pnivel += 9
    arg0.recalc = 1
  casose "a"
    arg0.pnivel += 1
    arg0.recalc = 1
    se arg0.pnivel > 80
      arg0.msg("Nível máximo é 80")
      arg0.pnivel = 80
    senao
      arg0.msg(arg0.nome + " é nível " + arg0.pnivel)
    fimse
    ret 1
  casose "dd"
    arg0.pnivel -= 9
    arg0.recalc = 1
  casose "d"
    arg0.pnivel -= 1
    arg0.recalc = 1
    arg0.msg(arg0.nome + " é nível " + arg0.pnivel)
    ret 1
  casofim


classe s_casa004
herda b_casa
const s_titulo = "Sala de jogos"
const s_desc = "Aqui há várias máquinas de jogos de azar, embora estejam desligadas.\n\
Você se pergunta se o dono da casa pratica atividades ilegais com essas\n\
máquinas. Entretanto, há um jogo diferente, que não requer eletricidade.\n\
Chama-se [batalha], são pequenos bonecos que lutam entre si."
const dir_s = $s_casa003
const botao = "\nTem um botão esquisito, para apertá-lo tecle APERTAR"

func ver_batalha
  ret !arg1, 1 # Indica que existe descrição extra
  listaitem i
  textotxt t
  epara i = $s_casa005.dentro2.ini, i, i.depois
    i.obj.jog && t.addfim(i.obj.nome)
  efim
  arg0.msg("Jogo Batalha")
  casovar t.linhas
  casose "0"
    ret "Você não vê nenhum boneco nesse jogo." + botao
  casose "1"
    ret "Você vê um boneco chamado " + t.remove + botao
  casofim
  ret "Você vê os seguintes bonecos: " + txttroca(t.remove(1000), "\n", ", ") + botao

func cmd_salaperso # Personagem chegou
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:evento(arg0) # Receber eventos do jogador

func cmd_escr
  se arg1 == "apertar"
    $mens.p(arg0)
    $mens.mtodos1("$P apertou o botão e foi transformado em um boneco.")
    arg0.mudadono($s_casa005)
    $mens.mtodos2("", "Boneco $p acaba de cair aqui.")
    ret 1


classe s_casa005
herda b_casa
const s_titulo = "Arena"
const s_desc = "Você está dentro de um jogo esquisito. Você é uma simples marionete,\n\
comandada por uma pessoa que mais parece um gigante daqui de dentro.\n\
Há uma saída para cima, porém fica lacrada quando há mais de um boneco."
const s_luta = 2 # Permite luta entre jogadores
const dir_c = $s_casa004
const eveste = 1 # Receber eventos do próprio objeto

func cmd_c
  se dentro2.total > 1
    arg0.msg("A saída para cima está lacrada.")
  senao
    arg0.mudadono($s_casa004)
    $mens.p(arg0)
    $mens.mtodos2("", "$P acaba de sair de dentro do jogo.")
  fimse
  ret 1


classe s_casa006
herda b_casa
const s_titulo = "Quarto"
const s_desc = "Um pequeno quarto de empregada, agora usado como quarto de despejo.\n\
No entanto, ainda há o que restou de uma cama de casal."
const dir_o = $s_casa003
const dir_l = $s_casa007


classe s_casa007
herda b_casa
const s_titulo = "Banheiro"
const s_desc = "Um pequeno banheiro, ou pelo menos o que restou dele. Ainda tem um\n\
chuveiro, mas aonde tinha a privada agora só resta o [cano]."
const dir_o = $s_casa006
const ver_cano = "Um papel está preso na parte de dentro do cano.\n\
Basicamente diz \"Para repetir a descrição da sala tecle VER\"."


classe s_casa008
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Com um grande tapete vermelho no chão, esse corredor vai de leste a oeste.\n\
Um grande lustre ilumina o local. Nota-se uma pequena saída ao sul."
const dir_l = este
const dir_o = $s_casa002
const dir_s = $s_casa009
const eveste = 1 # Receber eventos do próprio objeto

func cmd_l
  arg0.msg("A madeira está podre e você caiu, mas não resistiu à queda.")
  $mens.p(arg0)
  $mens.mtodos2("", "$P foi para leste e caiu na madeira podre.")
  arg0.morreu
  ret 1


classe s_casa009
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Um pouco menor que o corredor ao norte, mas nem por isso menos luxuoso.\n\
Um simples mosaico decora o teto. Abaixo dele, um grande lustre de cinco\n\
velas ilumina o lugar. É impressionante como simples velas produzem tanta\n\
luz. As paredes, no entanto, são brancas, praticamente sem decoração.\n\
Ao sul, há um arco que dá acesso a um patamar."
const dir_n = $s_casa008
const dir_o = $s_casa010
const dir_l = $s_casa011
const dir_s = $s_casa017


classe s_casa010
herda b_casa
const s_titulo = "Banheiro"
const s_desc = "Contém o que se espera de um banheiro. Uma pia, um vaso e um lugar para\n\
tomar banho. Não é grande, nem muito luxuoso, apenas um banheiro para os\n\
empregados."
const dir_l = $s_casa009


classe s_casa011
herda b_casa
const s_titulo = "Quarto"
const dir_o = $s_casa009
const dir_c = misc:obj.var.d_escada_ ? $s_casa012
const s_desc0 = "Há um tapete persa no chão bastante gasto e sujo. Alguém deixou a cama\n\
de baixo do beliche desarrumada. Há um alçapão no teto, sabe-se lá o que\n\
tem dentro."
const s_desc1 = " Mas mesmo subindo em cima do beliche, ainda fica um pouco alto."
const s_desc2 = "\n\
Tem também alguma coisa em baixo do beliche, mas em pé não dá para ver\n\
o que é."

func s_desc
  se arg0.var.d_escada_ # Escada aberta
    ret s_desc0
  senao arg0.posicao < 7 # Escada fechada, jogador sentado ou deitado
    ret s_desc0 + s_desc1
  senao # Escada fechada, jogador em pé
    ret s_desc0 + s_desc1 + s_desc2

const s_reset = "S 100 casa001 0 s"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  !dentro1.objini("i_casa001") && criar("i_casa001", este, 0)


classe i_casa001
herda comum_item
const nome = "escada"
const sexo = 0
const pesoobj = 10200
const descver = "Pode-se abrí-la teclando ABRIR ESCADA e fechar com FECHAR ESCADA."
const visivel = arg0.posicao < 7 || arg0.var.d_escada_
const eveste = 1 # Receber eventos do próprio objeto

func descpos
  se misc:obj.var.d_escada_
    ret "Uma grande escada está aberta aqui."
  senao
    ret "Uma escada está em baixo do beliche."

func cmd_pegarobj
  se arg2 == este
    arg0.msg("A escada é grande demais para você pegar.")
    ret 1

func cmd_abrirobj
  se arg1 == este
    se arg0.var.d_escada_
      arg0.msg("A escada já está aberta.")
    senao
      arg0.msg("Você abre a escada.")
      arg0.var.d_escada_ = 1
    fimse
    ret 1

func cmd_fecharobj
  se arg1 == este
    se arg0.var.d_escada_
      arg0.msg("Você fecha a escada.")
      arg0.var.d_escada_ = 0
    senao
      arg0.msg("A escada já está fechada.")
    fimse
    ret 1


classe s_casa012
herda b_casa
const s_titulo = "Sótão"
const s_desc = "Um lugar bastante apertado, entre o teto e o telhado. Como pode-se\n\
imaginar, está bastante sujo, pois a casa é antiga. Há aqui uma pequena\n\
janela para leste, cuja vista dá para o telhado."
const dir_b = $s_casa011
const dir_l = $s_casa013


classe s_casa013
herda b_casa
const s_titulo = "No telhado"
const s_desc = "Você anda com cuidado, pois as telhas são de cerâmica, e já estão bastante\n\
gastas. Aqui é bastante alto, e o vento bate forte."
const dir_o = $s_casa012
const dir_s = $s_casa014


classe s_casa014
herda b_casa
const s_titulo = "Laje"
const s_desc = "Aqui o chão é firme, e você consegue andar com segurança. Toda essa\n\
área foi impermeabilizada. Nota-se um revestimento preto sobre o chão.\n\
Talvez quisessem fazer um jardim aqui. Ao norte, começa o telhado."
const dir_n = $s_casa013
const dir_o = $s_casa015
const dir_l = $s_casa016
const s_reset = "P 100 casa004 10 10 1"
const s_pnj = 0

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se inttotal($p_casa004) < 1
    criar("p_casa004", este, 10)
  fimse


classe s_casa015
herda b_casa
const s_titulo = "Telhado"
const s_desc = "À leste começa uma laje. À oeste, o telhado continua, mas você não sente.\n\
segurança para caminhar."
const dir_l = $s_casa014


classe s_casa016
herda b_casa
const s_titulo = "Laje"
const s_desc = "Essa parte da laje bem que poderia ser um jardim. Há um pequeno muro à leste,\n\
que se estende ao sul. Não é possível caminhar no telhado ao norte,\n\
devido ao forte vento."
const dir_o = $s_casa014

func cmd_salaperso # Personagem chegou
  se arg0.jog && !arg0.senha && !arg0.var.j_salvar # Jogador que ainda não salvou o jogo
    criar("p_casa003", este, 10, "h_socar=1,h_chutar=1", arg0)
    misc:evento(arg0) # Receber eventos do jogador

func cmd_fugir
  arg0.pmove -= 5
  arg0.batalhafim
  $mens.p(arg0)
  $mens.mtodos1("$P fugiu para oeste.")
  arg0.mudadono($s_casa014)
  $mens.mvis2("", "$P chegou do leste.")
  ret 1


classe s_casa017
herda b_casa
const s_titulo = "Patamar superior"
const s_desc = "Este espaço faz parte de um grande saguão, que continua ao sul. Um\n\
balaústre de madeira nessa direção separa você de uma queda de seis\n\
metros. À leste e oeste nota-se duas escadas. Um tapete comprido cobre\n\
as escadas e este patamar. Ao norte passando por um arco, há um corredor."
const dir_n = $s_casa009
const dir_o = $s_casa031
const dir_l = $s_casa032
const s_pnj = 0


classe s_casa018
herda b_casa
const dir_l = $s_casa031
const s_titulo = "Patamar intermediário"
const dir_n = $s_casa020
const s_desc = "Você está exatamente na metade da escada, que começa ao sul e termina\n\
à leste. Há um tapete decorando toda a escada. Balaustres de madeira\n\
impede que se caia para o andar de baixo. Este patamar faz parte de um\n\
grande saguão, que continua ao sul e à leste."


classe s_casa019
herda b_casa
const s_titulo = "Patamar intermediário"
const s_desc = "Você está exatamente na metade da escada, que começa ao sul e termina\n\
à oeste. Há um tapete decorando toda a escada. Balaustres de madeira\n\
impede que se caia para o andar de baixo. Este patamar faz parte de um\n\
grande saguão, que continua ao sul e à oeste."
const dir_n = $s_casa027
const dir_o = $s_casa032


classe s_casa020
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Um pequeno corredor que continua ao norte. Há um arco no meio, separando\n\
o corredor. As paredes possuem painéis que lembram tapetes persas.\n\
O chão é todo quadriculado. Uma luz amarelada do lustre ilumina o lugar."
const dir_n = $s_casa021
const dir_s = $s_casa018
const dir_l = $s_casa022


classe s_casa021
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Um pequeno corredor que continua ao sul. Há um arco no meio, separando\n\
o corredor. As paredes possuem painéis que lembram tapetes persas.\n\
O chão é todo quadriculado. Uma luz amarelada do lustre ilumina o lugar."
const dir_s = $s_casa020
const dir_l = $s_casa023
const dir_o = $s_casa030


classe s_casa022
herda b_casa
const s_titulo = "Toalete"
const s_desc = "Aqui há apenas o básico que se espera de um toalete, uma pia de granito\n\
e uma privada. No entanto, o brilho dourado da torneira é marcante.\n\
Olhando atentamente para o vaso sanitário, nota-se um pequeno fio de\n\
de água que não pára de correr."
const dir_o = $s_casa020
const dir_l = !misc:obj.jog ? $s_casa026
const dir_n = !misc:obj.jog ? $s_casa023
const dir_s = !misc:obj.jog ? $s_casa031


classe s_casa023
herda b_casa
const s_titulo = "Sala de jantar"
const s_desc = "Esta é a principal sala de jantar da casa. Nota-se pelo seu tamanho.\n\
No centro há uma mesa redonda, com lugares para pelo menos doze pessoas.\n\
No teto, mais ou menos sobre a mesa, um grande lustre de cristal\n\
ilumina o ambiente."
const dir_o = $s_casa021
const dir_l = $s_casa024
const dir_s = !misc:obj.jog ? $s_casa022
const dir_n = !misc:obj.jog ? $s_casa030
const a_reset = "P 50 casa010 4 6 1\nP 50 casa011 4 6 1"

func a_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de a_reset
  se inttotal($p_casa010) < 1 && rand(100) < 50
    criar("p_casa010", este, rand(4, 6))
  fimse
  se inttotal($p_casa011) < 1 && rand(100) < 50
    criar("p_casa011", este, rand(4, 6))
  fimse


classe s_casa024
herda b_casa
const s_titulo = "Sala de estar"
const s_desc = "Poltronas e sofás, repletos de adornos, se destacam neste lugar.\n\
Uma grande lareira, na parede à leste, fornece o calor necessário\n\
durante os dias de inverno. Ao norte, é possível aprecisar a vista\n\
através de uma janela."
const dir_o = $s_casa023
const dir_s = $s_casa025
const s_luta = 0
const s_pnj = 0
const s_reset = "P 100 casa006 0 0 1"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se inttotal($p_casa006) < 1
    criar("p_casa006", este, 0)
  fimse


classe s_casa025
herda b_casa
const s_titulo = "Recreação"
const s_desc = "Esta sala é dedicada principalmente às crianças. Há uma estante com\n\
diversos brinquedos. A maioria deles é bem antiga, como peões e corda\n\
de pular. Uma placa bem visível na entrada diz claramente para manter\n\
tudo em ordem."
const dir_n = $s_casa024
const dir_l = $s_casa026
const s_pnj = 0
const s_luta = 0
const s_reset = "P 100 casa005 0 0 1"

func s_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de s_reset
  se inttotal($p_casa005) < 1
    criar("p_casa005", este, 0)
  fimse


classe s_casa026
herda b_casa
const s_titulo = "Sala de jantar"
const s_desc = "Quando há pouca gente na casa, esta sala de estar costuma ser usada.\n\
No centro há uma mesa com espaço para quatro pessoas, com relativo\n\
conforto. Na parede fica uma prateleira, onde ficam pratos e talheres."
const dir_o = $s_casa025
const dir_l = $s_casa027
const dir_s = !misc:obj.jog ? $s_casa032
const dir_n = !misc:obj.jog ? $s_casa029
const a_reset = "P 50 casa010 4 6 1\nP 50 casa011 4 6 1"

func a_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de a_reset
  se inttotal($p_casa010) < 1 && rand(100) < 50
    criar("p_casa010", este, rand(4, 6))
  fimse
  se inttotal($p_casa011) < 1 && rand(100) < 50
    criar("p_casa011", este, rand(4, 6))
  fimse


classe s_casa027
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Um grande corredor que continua ao norte. No meio há um arco de madeira,\n\
separando o corredor. Nas paredes foram feitas diversas pinturas.\n\
O chão é todo encarpetado, porém carece de uma limpeza."
const dir_n = $s_casa028
const dir_s = $s_casa019
const dir_o = $s_casa026
const dir_l = $s_casa030


classe s_casa028
herda b_casa
const s_titulo = "Corredor"
const s_desc = "Um grande corredor que continua ao sul. No meio há um arco de madeira,\n\
separando o corredor. Nas paredes foram feitas diversas pinturas.\n\
O chão é todo encarpetado, porém carece de uma limpeza."
const dir_s = $s_casa027
const dir_o = $s_casa029


classe s_casa029
herda b_casa
const s_titulo = "Despensa"
const s_desc = "Você está em uma sala escura de tamanho razoável. O chão e as paredes\n\
são um pouco rústicos, compostos apenas de ladrilhos e azulejos.\n\
Há diversas caixas empilhadas. A única saída é à leste."
const dir_l = $s_casa028
const dir_o = !misc:obj.jog ? $s_casa023
const dir_n = !misc:obj.jog ? $s_casa028
const dir_s = !misc:obj.jog ? $s_casa026
const a_reset = "P 50 casa009 3 5 1\nF 100\nP 100 casa008 3 5 1"

func a_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de a_reset
  ref p # Último personagem criado
  ref u # Último personagem ou animal criado
  se inttotal($p_casa009) < 1 && rand(100) < 50
    p = u = criar("p_casa009", este, rand(3, 5))
  fimse
  ret u, nulo
  se inttotal($p_casa008) < 1
    criar("p_casa008", este, rand(3, 5))
  fimse


classe s_casa030
herda b_casa
const s_titulo = "Cozinha"
const s_desc = "O espaço aqui é relativamente grande. Aparentemente tudo está no seu\n\
devido lugar. Há um fogão de nove bocas, um pouco antigo, e em um canto,\n\
meio escondido, um pequeno forno microondas. No entanto, a geladeira\n\
e os armários estão vazios."
const dir_l = $s_casa021
const dir_o = $s_casa027
const dir_c = !misc:obj.jog ? $s_casa022
const dir_b = !misc:obj.jog ? $s_casa026
const a_reset = "P 50 casa007 3 5 1\nF 100\nP 100 casa008 3 5 1"

func a_reset1 # Realiza o reset
# Não alterar; função criada automaticamente a partir de a_reset
  ref p # Último personagem criado
  ref u # Último personagem ou animal criado
  se inttotal($p_casa007) < 1 && rand(100) < 50
    p = u = criar("p_casa007", este, rand(3, 5))
  fimse
  ret u, nulo
  se inttotal($p_casa008) < 1
    criar("p_casa008", este, rand(3, 5))
  fimse


classe s_casa031
herda b_casa
const s_titulo = "Escadaria"
const s_desc = "Um tapete cobre o chão. Na parede, ao norte, há um quadro de um antigo\n\
barão, que parece estar olhando para você. Ao sul, um balaústre de\n\
madeira. A escada continua à leste e oeste."
const dir_l = $s_casa017
const dir_o = $s_casa018
const dir_n = !misc:obj.jog ? $s_casa022


classe s_casa032
herda b_casa
const s_titulo = "Escadaria"
const s_desc = "Um tapete cobre o chão. Ao norte, preso fixado na parede, há uma espada\n\
e um escudo antigos. Ao sul, um balaústre de madeira. A escada continua\n\
à leste e oeste."
const dir_o = $s_casa017
const dir_l = $s_casa019
const dir_n = !misc:obj.jog ? $s_casa026


classe p_casa005
herda comum_perso
const nome = "Menino"
const sexoini = 1
const nivel = 4
const descpos = "Um menino está aqui brincando."
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_chegou # Chegou na sala
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou50
  arg0.msg("Menino: Tem qualquer coisa estranha em baixo de um beliche, em um dos")
  arg0.msg("quartos, mas não se consegue ver estando em pé.")


classe p_casa006
herda comum_perso
const nome = "Senhora"
const sexoini = 0
const descpos = "Uma senhora brinca com cartas, sentada em uma poltrona."
const nivel = 8
const evsala = 1 # Receber eventos do dono se o dono for sala

func cmd_chegou # Chegou na sala
  ret !arg0.jog, nulo # Retorna se não for jogador
  misc:tempo("entrou", arg0) # Dispara funções 'entrou'

func entrou50
  arg0.msg("Senhora: Minhas cartas dizem...")

func entrou100
  arg0.msg("Senhora: Para repetir a descrição da sala, tecle a palavra ver.")

func entrou160
  arg0.msg("Senhora: Você pode também examinar um item ou personagem teclando ver")
  arg0.msg("e o nome do que quer ver.")


classe p_casa007
herda comum_persobat
const nome = "Bife"
const sexoini = 1
const descpos = "Um bife está flutuando aqui."
const p_mover = 1
const p_agressivo = 2
const inivar = "h_arremessar=1"


classe p_casa008
herda comum_persobat
const nome = "Alface"
const sexoini = 1
const descpos = "Um alface está flutuando aqui."
const p_mover = 1
const p_agressivo = 2
const inivar = "h_arremessar=1"


classe p_casa009
herda comum_persobat
const nome = "Banana"
const sexoini = 0
const descpos = "Uma banana está flutuando aqui."
const p_mover = 1
const p_agressivo = 2
const inivar = "h_arremessar=1"


classe p_casa010
herda comum_persobat
const nome = "Peão"
const sexoini = 1
const descpos = "Um peão está girando aqui."
const p_mover = 1
const p_agressivo = 2
const inivar = "h_arremessar=1"


classe p_casa011
herda comum_persobat
const nome = "Boneca"
const sexoini = 0
const descpos = "Uma boneca está parada aqui."
const p_mover = 1
const p_agressivo = 2
const inivar = "h_arremessar=1"
