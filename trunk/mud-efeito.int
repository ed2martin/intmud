classe adm_efeito
herda comando_adm
# Comandos de efeitos
#
const txtajuda = "\b\c3Efeitos\b\n\
Magias e habilidades podem lançar efeitos sobre um personagem.\n\
Esses comandos permitem criar e editar os efeitos.\n\
\c2LEF\b [nome]\n\
  Lista efeitos que começam com o nome especificado.\n\
\c2CEF\b <nome>\n\
  Cria um efeito. Nome deve ser o nome do efeito, e deve ser composto\n\
  de letras, mas pode ter espaços no meio.\n\
\c2EEF\b <nome>\n\
  Edita um efeito.\n\
\c2AEF\b <nome>\n\
  Apaga um efeito."


classe adm_lef
herda comando_adm, comando_comum

func escr # Listar efeitos
  prog p
  textotxt t
  epara p.iniclasse("e_"), p.lin && t.linhas < 499, p.depois
    t.addfim(txtcopiamai(txt2(txte(p.texto)), "A") + ", " + [p.texto]:desc)
  efim
  se t.linhas
    arg0.msg2("\b\c3Efeitos:\b\n" + t.remove(500))
  senao
    arg0.msg("\bNenhum efeito encontrado.")
  fimse
  ret 1


classe adm_cef
herda comando_adm, comando_comum

func escr # Criar efeito
  prog p
  txt80 nome
  uint16 num
# Checa se digitou nome do ataque
  se arg1 == ""
    arg0.msg("Tecle o nome da magia após CEF")
    ret 1
  fimse
# Checa se nome válido
  nome = txte(txtremove(arg1, EMDSA))
  se txtsepara(nome, "DO+DOL,DOL+DO") != nome
    arg0.msg("Nome inválido para efeito: " + nome)
    ret 1
  senao p.existe("e_" + nome)
    arg0.msg("Já existe o efeito: " + nome)
    ret 1
  fimse
# Cria efeito
  txt200 lin
  lin = "e_" + txts(txtmin(nome)) + "\nb-efeito\nherda comum_efeito"
  lin += "\nconst e_nome = \"" + txtcopiamai(nome, "A") + "\""
  prog p
  p.criar(lin)
  criar("admordena", "b-efeito")
# config:salvar
  arg0.msg("\bCriou efeito: " + nome)
  admlog(arg0, "criou efeito " + nome)
  ret 1


classe adm_aef
herda comando_adm, comando_comum

func escr # Apagar efeito
  prog p
  se arg1 == ""
    arg0.msg("Tecle um ou mais nomes de efeitos após AEF")
    ret 1
  fimse
  uint16 x
  enquanto txtsub(arg1, x, 1)
    se !p.existe("e_" + txtsub(arg1, x, 1))
      arg0.msg("Efeito não existe: " + txtsub(arg1, x, 1))
      ret 1
    senao e_[txtsub(arg1, x, 1)]:interno
      arg0.msg("Efeito não pode ser apagado: " + txtsub(arg1, x, 1))
      ret 1
    fimse
    x += 1
  efim
  tpasso = arg1, p_aef
  ret 1

func passo # Confirmar que quer apagar efeito
  passo = ""
  se arg1 == "n"
    arg0.msg("Cancelado")
  senao arg1 == "s"
    prog p
    uint16 x
    enquanto txtsub(tpasso, x, 1)
      p.apagar("e_" + txtsub(tpasso, x, 1))
      x += 1
    efim
    config:salvar
    se x < 2
      arg0.msg("Efeito apagado: " + tpasso)
      admlog(arg0, "apagou efeito " + tpasso)
    senao
      arg0.msg("Efeitos apagados: " + tpasso)
      admlog(arg0, "apagou efeitoss " + tpasso)
    fimse
  senao
    passo = "aef"
    refvar m1 = txt2(tpasso) ? "efeitos" : "efeito"
    arg0.msg("Apagar " + m1 + " " + tpasso + "? Tecle S ou N")
  fimse
  ret 1


classe adm_eef
herda comando_adm, comando_comum

func escr # Editar efeito
  prog p
  se arg1 == ""
    arg0.msg("Tecle o nome do efeito após EEF")
  senao !p.existe("e_" + arg1)
    arg0.msg("Efeito não existe: " + arg1)
  senao e_[txtsub(arg1, x, 1)]:interno
    arg0.msg("Efeito não pode ser editado: " + txtsub(arg1, x, 1))
  senao
    arg0.sock.var.classe1 = "e_" + txts(txtmin(arg1))
    arg0.sock.var.menufim = "menu_efeito"
    arg0.sock.m_menu(var.menufim)
  fimse
  ret 1


classe menu_efeito
herda comando_menu
const titulo = "Efeito " + txt(var.classe1, 2)
const colunas = 25
#
const nome1 = "Nome"
const info1 = "Nome que aparece no comando efeitos"
const tipo1 = "opc_linha"
const clas1 = var.classe1
const vari1 = "e_nome"
#
const nome2 = "Descrição"
const info2 = "Descrição do efeito, para os jogadores"
const tipo2 = "opc_texto"
const clas2 = var.classe1
const vari2 = "e_desc"
#
const nome3 = "Descrição interna"
const info3 = "Uma breve descrição do que faz; aparece apenas no comando lef"
const tipo3 = "opc_linha"
const clas3 = var.classe1
const vari3 = "desc"
#
const nome4 = "Quantidade máx."
const info4 = "Quantos efeitos desse tipo um personagem pode ter"
const tipo4 = "opc_numero"
const clas4 = var.classe1
const vari4 = "maximo"
const extr4 = "1 10"
#
const nome5 = "Condição"
const tipo5 = "opc_opcao"
const clas5 = var.classe1
const vari5 = "e_condicao"
const extr5 = "Nenhuma; permanente\n\
Tem pontos de vida\n\
Durante a batalha e tem pontos de vida\n\
Fora da batalha e tem pontos de vida"
const exec5 = "ajustaobj"
#
const nome6 = "Duração"
const info6 = "Quantos décimos de segundo o efeito dura ou 0 se não expira"
const tipo6 = "opc_numero"
const clas6 = var.classe1
const vari6 = "tempo1"
const extr6 = "0 1000000"
#
const nome7 = "Duração por nível"
const info7 = "Quantos décimos de segundo dura para cada nível de quem lançou"
const tipo7 = "opc_numero"
const clas7 = var.classe1
const vari7 = "tempo2"
const extr7 = "0 100000"
#
const nome8 = "Atrib números"
const tipo8 = "opc_atribnum"
const clas8 = var.classe1
const vari8 = "recalc_auto"
const exec8 = "recalc_ajusta"
#
const nome9 = "Atrib sim/não"
const tipo9 = "opc_atribbit"
const clas9 = var.classe1
const vari9 = "recalc_auto"
const exec9 = "recalc_ajusta"
#
const nome10 = "Atrib ataque"
const info10 = "Afetam os danos quando o personagem ataca alguém"
const tipo10 = "opc_atribatk"
const clas10 = var.classe1
const vari10 = "recalc_auto"
const exec10 = "recalc_ajusta"
#
const nome11 = "Atrib defesa"
const info11 = "Afetam os danos quando alguém ataca o personagem"
const tipo11 = "opc_atribdef"
const clas11 = var.classe1
const vari11 = "recalc_auto"
const exec11 = "recalc_ajusta"
#
const nome12 = "Tempo reduzir"
const info12 = "De quantos em quantos décimos de segundo reduz algo"
const tipo12 = "opc_numero"
const clas12 = var.classe1
const vari12 = "r_tempo"
const extr12 = "0 10000"
#
const nome13 = "Reduz vida"
const tipo13 = "opc_numero"
const clas13 = var.classe1
const vari13 = "r_vida"
#
const nome14 = "Reduz mana"
const tipo14 = "opc_numero"
const clas14 = var.classe1
const vari14 = "r_mana"
#
const nome15 = "Reduz vigor"
const tipo15 = "opc_numero"
const clas15 = var.classe1
const vari15 = "r_move"
#
const nome16 = "Mensagens"
const tipo16 = "menu_efeito_msg--"
#
const nome17 = "Eventos hora"
const info17 = "Se a função cmd_hora é chamada quando a hora MUD mudar (5 minutos)"
const tipo17 = "opc_simnao"
const clas17 = var.classe1
const vari17 = "evhora"
const exec17 = "ajustaobj"
#
const nome18 = "Eventos perso"
const info18 = "Se recebe eventos do personagem que está com o efeito"
const tipo18 = "opc_simnao"
const clas18 = var.classe1
const vari18 = "evperso"
const exec18 = "ajustaobj"


classe menu_efeito_msg
herda comando_menu
const titulo = "Mensagens"
const menufim = var.menufim
const colunas = 25
#
const titu1 = " * Criou"
#
const nome1 = "Perso"
const tipo1 = "opc_linha"
const clas1 = var.classe1
const vari1 = "msgini1"
#
const nome2 = "Outros"
const tipo2 = "opc_linha"
const clas2 = var.classe1
const vari2 = "msgini2"
#
const titu3 = " * Falhou"
#
const nome3 = "Perso"
const tipo3 = "opc_linha"
const clas3 = var.classe1
const vari3 = "msgfalhou1"
#
const nome4 = "Outros"
const tipo4 = "opc_linha"
const clas4 = var.classe1
const vari4 = "msgfalhou2"
#
const titu5 = " * Não teve efeito"
#
const nome5 = "Perso"
const tipo5 = "opc_linha"
const clas5 = var.classe1
const vari5 = "msgnulo1"
#
const nome6 = "Outros"
const tipo6 = "opc_linha"
const clas6 = var.classe1
const vari6 = "msgnulo2"
#
const titu7 = " * Apagou"
#
const nome7 = "Perso"
const tipo7 = "opc_linha"
const clas7 = var.classe1
const vari7 = "msgfim1"
#
const nome8 = "Outros"
const tipo8 = "opc_linha"
const clas8 = var.classe1
const vari8 = "msgfim2"
#
const titu9 = " * Ao reduzir vida/mana/vigor"
#
const nome9 = "Perso"
const tipo9 = "opc_linha"
const clas9 = var.classe1
const vari9 = "msgreduz1"
#
const nome10 = "Outros"
const tipo10 = "opc_linha"
const clas10 = var.classe1
const vari10 = "msgreduz2"


classe comum_efeito
# Efeitos que afetam os personagens
sav listaitem idono # Uso interno: aonde o objeto está
listaitem evrec.3 # Usado internamente, para receber eventos
ref dono # Personagem que contém esse efeito; mesmo que: idono.objlista
const objsav = 2 != e_condicao # Se deve salvar o efeito ao salvar personagem do jogador
sav inttempo t_reduzir # Tempo para a próxima redução
sav inttempo t_duracao # Duração do efeito
sav inttempo t_tempo1 # Para gerar eventos de tempo
sav uint32 t_tempo2 # t_tempo2-t_tempo1 = Tempo decorrido
# const evperso = 1 # Receber eventos do personagem
const e_condicao = 1
const maximo = 1
const msgini1 = msgini2
const msgfim1 = msgfim2
const msgfalhou1 = msgfalhou2
const msgnulo1 = msgnulo2
const msgreduz1 = msgreduz2
intexec evini # Para inicializar o efeito
#
# Ordem que deve organizar as constantes:
const admordem = "e_nome\n\
e_condicao\n\
e_desc\n\
desc\n\
maximo\n\
r_mana r_move r_tempo r_vida\n\
tempo1 tempo2\n\
msgini1\n\
msgini2\n\
msgfalhou1\n\
msgfalhou2\n\
msgnulo1\n\
msgnulo2\n\
msgfim1\n\
msgfim2\n\
msgreduz1\n\
msgreduz2\n\
evhora\nevperso"

func ini # Cria efeito
# arg0 = personagem que receberá o efeito
# arg1 = nível de quem usou o efeito
  evini = 1
  ret !(dono = arg0), apagar(este) # Acerta dono e apaga efeito se for nulo
  idono = dono.dentro2.addini(este) # Adiciona como personagem
  ret 0 > arg1, t_duracao = -1
  ret dono.dentro2.total(este) > maximo || e_condicao == (dono.contr.atkenv ? 3 : 2), t_duracao \
= -2
  t_reduzir = r_tempo, t_duracao = intpos(tempo1 + tempo2 * arg1)

func evini_exec # Inicializa efeito
  se t_duracao >= 0
    evhora && (evrec.0 = $miscsav.evhora.addfim(este))
    evperso && (evrec.1 = dono.evento.addfim(este))
    e_condicao >= 2 && (evrec.2 = dono.contr.atkbat.addfim(este))
    dono.recalc = 1
    $mens.p(dono) + $mens.mvis2(msgini1, msgini2)
    cmd_ini(dono)
  senao t_duracao == -1
    apagar(este), idono.remove
    $mens.p(dono) + $mens.mvis2(msgfalhou1, msgfalhou2)
  senao
    apagar(este), idono.remove
    $mens.p(dono) + $mens.mvis2(msgnulo1, msgnulo2)
  fimse

func fim # Apaga efeito
  ret evini || !idono, nulo
  idono.remove
  dono.recalc = 0
  dono.recalc_exec
  dono.pvida > 0 && $mens.p(dono) + $mens.mvis2(msgfim1, msgfim2)
  cmd_fim(dono)

func ajustaobj # Acerta dono e eventos do objeto
  dono = idono.objlista
  evrec.0.remove, evhora && (evrec.0 = $miscsav.evhora.addfim(este))
  evrec.1.remove, evperso && (evrec.1 = dono.evento.addfim(este))
  evrec.2.remove, e_condicao >= 2 && (evrec.2 = dono.contr.atkbat.addfim(este))

func t_duracao_exec # Efeito expirou: apaga efeito
  apagar(este)

func t_reduzir_exec # Executa redução
  t_reduzir = r_tempo
  $mens.p(dono) + $mens.mvis2(msgreduz1, msgreduz2)
  dono.pmana -= r_mana
  dono.pmove -= r_move
  se dono.pvida
    dono.pvida -= r_vida
    dono.acertapos(1)
  fimse
  cmd_reduz(dono)

func tempoini # Inicia contagem de tempo
  t_tempo1 = t_tempo2 = 1

func t_tempo1_exec # Executa eventos
  prog p
  uint32 x
  uint32 v
  x = 1000000
  epara p.inifunc(este, "cmd_tempo"), p.lin, p.depois
    (v = txt(p.texto, 9)) > t_tempo2 && v < x && (x = v)
  efim
  v = t_tempo2
  se x != 1000000
    t_tempo1 = x - t_tempo2
    t_tempo2 = x
  fimse
  cmd_tempo[v](dono)


classe e_apagar
# Efeito usado internamente:
# Apaga o personagem depois de um tempo sem jogador por perto
# Se o personagem for capturado, esse efeito desaparece
#
sav listaitem idono # Uso interno: aonde o objeto está
listaitem evrec.2 # Usado internamente, para receber eventos
ref dono # Personagem que contém esse efeito; mesmo que: idono.objlista
const interno = 1 # Para não ser editável e apagável
const desc = "Usado internamente: apaga PNJ após um tempo sem jogador na sala"
const objsav = 0 # Não salvar o efeito ao salvar o personagem
inttempo tatual # Contagem de tempo para apagar o personagem
uint16 tapaga # Tempo para apagar o personagem
intexec recalc # Se deve acertar os eventos que recebe
# func fim
# telatxt t
# t.msg("Efeito apagado\n")

func ini # Cria efeito
# arg0 = personagem que receberá o efeito
# arg1 = quanto tempo para apagar o personagem, em décimos de segundo
  ret !(dono = arg0), apagar(este) # Acerta dono e apaga efeito se for nulo
  idono = dono.dentro2.addini(este) # Adiciona como personagem
  !dono && apagar(este) # Apaga se não tiver dono
  evrec.0 = dono.evento.addfim(este)
  evrec.1 = dono.dono.evento.addfim(este)
  tapaga = arg1, !dono.dono.socktot && (tatual = tapaga) # Acerta tempo

const cmd_saiu = recalc = 1 # Alguém saiu da sala
const ajustaobj = recalc_exec # Acerta dono e eventos do objeto

func recalc_exec # Acerta eventos
  dono = idono.objlista
  dono.dono.socktot ? (tatual = 0) : !tatual && (tatual = tapaga)

func cmd_chegou # Alguém mudou de sala: acerta eventos do dono do personagem
  ret dono.dono.perso, apagar(este) # Apaga se pertence a jogador
  evrec.1.remove, evrec.1 = dono.dono.evento.addfim(este) # Acerta eventos
  dono.dono.socktot ? (tatual = 0) : !tatual && (tatual = tapaga)

func tatual_exec # Apaga personagem
  apagar(este), apagar(dono)
