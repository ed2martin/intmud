classe adm_efeito
# Comandos de efeitos
#
const adm_efeito = "\b\c3Efeitos\b\n\
Magias e habilidades podem lançar efeitos sobre um personagem.\n\
Esses comandos permitem criar e editar os efeitos.\n\
\c2LEF\b [nome]\n\
  Lista efeitos que começam com o nome especificado.\n\
\c2CEF\b <nome>\n\
  Cria um efeito. Nome deve ser o nome do efeito, e deve ser composto\n\
  de letras, mas pode ter espaços no meio.\n\
\c2EEF\b <nome>\n\
  Edita um efeito.\n\
\c2AEF\b <nome>\n\
  Apaga um efeito."

func cmd_lef # Listar efeitos
  prog p
  textotxt t
  epara p.iniclasse("e_"), p.lin && t.linhas < 499, p.depois
    t.addfim(txtcopiamai(txt2(txte(p.texto)), "A") + ", " + [p.texto]:desc)
  efim
  se t.linhas
    msg2("\b\c3Efeitos:\b\n" + t.remove(500))
  senao
    msg("\bNenhum efeito encontrado.")
  fimse
  ret 1

func cmd_cef # Criar efeito
  prog p
  txt80 nome
  uint16 num
# Checa se digitou nome do ataque
  se arg0 == ""
    msg("Tecle o nome da magia após CEF")
    ret 1
  fimse
# Checa se nome válido
  nome = txte(txtremove(arg0, EMDSA))
  se txtsepara(nome, "DO+DOL,DOL+DO") != nome
    msg("Nome inválido para efeito: " + nome)
    ret 1
  senao p.existe("e_" + nome)
    msg("Já existe o efeito: " + nome)
    ret 1
  fimse
# Cria efeito
  txt200 lin
  lin = "e_" + txts(txtmin(nome)) + "\nb-efeito\nherda comum_efeito"
  lin += "\nconst e_nome = \"" + txtcopiamai(nome, "A") + "\""
  prog p
  p.criar(lin)
  criar("admordena", "b-efeito")
# config:salvar
  msg("\bCriou efeito: " + nome)
  admlog("criou efeito " + nome)
  ret 1

func cmd_aef # Apagar efeito
  prog p
  se arg0 == ""
    msg("Tecle um ou mais nomes de efeitos após AEF")
    ret 1
  fimse
  uint16 x
  enquanto txtsub(arg0, x, 1)
    se !p.existe("e_" + txtsub(arg0, x, 1))
      msg("Efeito não existe: " + txtsub(arg0, x, 1))
      ret 1
    senao e_[txtsub(arg0, x, 1)]:interno
      msg("Efeito não pode ser apagado: " + txtsub(arg0, x, 1))
      ret 1
    fimse
    x += 1
  efim
  tpasso = arg0, p_aef
  ret 1

func p_aef # Confirmar que quer apagar efeito
  passo = ""
  se arg0 == "n"
    msg("Cancelado")
  senao arg0 == "s"
    prog p
    uint16 x
    enquanto txtsub(tpasso, x, 1)
      p.apagar("e_" + txtsub(tpasso, x, 1))
      x += 1
    efim
    config:salvar
    se x < 2
      msg("Efeito apagado: " + tpasso)
      admlog("apagou efeito " + tpasso)
    senao
      msg("Efeitos apagados: " + tpasso)
      admlog("apagou efeitoss " + tpasso)
    fimse
  senao
    passo = "aef"
    refvar m1 = txt2(tpasso) ? "efeitos" : "efeito"
    msg("Apagar " + m1 + " " + tpasso + "? Tecle S ou N")
  fimse
  ret 1

func cmd_eef # Editar efeito
  prog p
  se arg0 == ""
    msg("Tecle o nome do efeito após EEF")
  senao !p.existe("e_" + arg0)
    msg("Efeito não existe: " + arg0)
  senao e_[txtsub(arg0, x, 1)]:interno
    msg("Efeito não pode ser editado: " + txtsub(arg0, x, 1))
  senao
    var.classe1 = "e_" + txts(txtmin(arg0))
    var.menufim = "menu_efeito"
    m_menu(var.menufim)
  fimse
  ret 1


classe menu_efeito
const titulo = "Efeito " + txt(var.classe1, 2)
const colunas = 25
#
const nome1 = "Nome"
const info1 = "Nome que aparece no comando efeitos"
const tipo1 = "linha"
const clas1 = var.classe1
const vari1 = "e_nome"
#
const nome2 = "Descrição"
const info2 = "Uma breve descrição do que faz; aparece no comando lef"
const tipo2 = "linha"
const clas2 = var.classe1
const vari2 = "desc"
#
const nome3 = "Quantidade máx."
const info3 = "Quantos efeitos desse tipo um personagem pode ter"
const tipo3 = "numero"
const clas3 = var.classe1
const vari3 = "maximo"
const extr3 = "1 10"
#
const nome4 = "Condição"
const tipo4 = "opcao"
const clas4 = var.classe1
const vari4 = "e_condicao"
const extr4 = "Nenhuma; permanente\n\
Tem pontos de vida\n\
Durante a batalha e tem pontos de vida\n\
Fora da batalha e tem pontos de vida"
const exec4 = "ajustaobj"
#
const nome5 = "Duração"
const info5 = "Quantos décimos de segundo o efeito dura ou 0 se não expira"
const tipo5 = "numero"
const clas5 = var.classe1
const vari5 = "tempo1"
const extr5 = "0 10000"
#
const nome6 = "Duração por nível"
const info6 = "Quantos décimos de segundo dura para cada nível de quem lançou"
const tipo6 = "numero"
const clas6 = var.classe1
const vari6 = "tempo2"
const extr6 = "0 10000"
#
const nome7 = "Atrib números"
const tipo7 = "atribnum"
const clas7 = var.classe1
const vari7 = "recalc_auto"
const exec7 = "recalc_ajusta"
#
const nome8 = "Atrib sim/não"
const tipo8 = "atribbit"
const clas8 = var.classe1
const vari8 = "recalc_auto"
const exec8 = "recalc_ajusta"
#
const nome9 = "Atrib ataque"
const info9 = "Afetam os danos quando o personagem ataca alguém"
const tipo9 = "atribatk"
const clas9 = var.classe1
const vari9 = "recalc_auto"
const exec9 = "recalc_ajusta"
#
const nome10 = "Atrib defesa"
const info10 = "Afetam os danos quando alguém ataca o personagem"
const tipo10 = "atribdef"
const clas10 = var.classe1
const vari10 = "recalc_auto"
const exec10 = "recalc_ajusta"
#
const nome11 = "Tempo reduzir"
const info11 = "De quantos em quantos décimos de segundo reduz algo"
const tipo11 = "numero"
const clas11 = var.classe1
const vari11 = "r_tempo"
const extr11 = "0 10000"
#
const nome12 = "Reduz vida"
const tipo12 = "numero"
const clas12 = var.classe1
const vari12 = "r_vida"
#
const nome13 = "Reduz mana"
const tipo13 = "numero"
const clas13 = var.classe1
const vari13 = "r_mana"
#
const nome14 = "Reduz vigor"
const tipo14 = "numero"
const clas14 = var.classe1
const vari14 = "r_move"
#
const nome15 = "Mensagens"
const tipo15 = "menu--"
const clas15 = "menu_efeito_msg"
#
const nome16 = "Função de tempo"
const info16 = "O nome da função que será executada de forma semelhante a misc:tempo"
const tipo16 = "linha"
const clas16 = var.classe1
const vari16 = "r_func"
#
const nome17 = "Eventos perso"
const info17 = "Se recebe eventos do personagem que está com o efeito"
const tipo17 = "simnao"
const clas17 = var.classe1
const vari17 = "evperso"
const exec17 = "ajustaobj"


classe menu_efeito_msg
const titulo = "Mensagens"
const menufim = var.menufim
const colunas = 25
#
const titu1 = " * Criou"
#
const nome1 = "Perso"
const tipo1 = "linha"
const clas1 = var.classe1
const vari1 = "msgini1"
#
const nome2 = "Outros"
const tipo2 = "linha"
const clas2 = var.classe1
const vari2 = "msgini2"
#
const titu3 = " * Falhou"
#
const nome3 = "Perso"
const tipo3 = "linha"
const clas3 = var.classe1
const vari3 = "msgfalhou1"
#
const nome4 = "Outros"
const tipo4 = "linha"
const clas4 = var.classe1
const vari4 = "msgfalhou2"
#
const titu5 = " * Não teve efeito"
#
const nome5 = "Perso"
const tipo5 = "linha"
const clas5 = var.classe1
const vari5 = "msgnulo1"
#
const nome6 = "Outros"
const tipo6 = "linha"
const clas6 = var.classe1
const vari6 = "msgnulo2"
#
const titu7 = " * Apagou"
#
const nome7 = "Perso"
const tipo7 = "linha"
const clas7 = var.classe1
const vari7 = "msgfim1"
#
const nome8 = "Outros"
const tipo8 = "linha"
const clas8 = var.classe1
const vari8 = "msgfim2"
#
const titu9 = " * Ao reduzir vida/mana/vigor"
#
const nome9 = "Perso"
const tipo9 = "linha"
const clas9 = var.classe1
const vari9 = "msgreduz1"
#
const nome10 = "Outros"
const tipo10 = "linha"
const clas10 = var.classe1
const vari10 = "msgreduz2"


classe comum_efeito
# Efeitos que afetam os personagens
sav listaitem idono # Uso interno: aonde o objeto está
listaitem evrec # Usado internamente, para receber eventos
listaitem evbat # Para apagar o objeto ao iniciar/terminar batalha
ref dono # Personagem que contém esse efeito; mesmo que: idono.objlista
const jogsav = 2 != e_condicao # Se deve salvar o efeito ao salvar personagem do jogador
sav inttempo t_reduzir # Tempo para a próxima redução
sav inttempo t_duracao # Duração do efeito
sav inttempo t_tempo1 # Para gerar eventos de tempo
sav uint32 t_tempo2 # t_tempo2-t_tempo1 = Tempo decorrido
# const evperso = 1 # Receber eventos do personagem
const e_condicao = 1
const maximo = 1
const msgini1 = msgini2
const msgfim1 = msgfim2
const msgfalhou1 = msgfalhou2
const msgnulo1 = msgnulo2
const msgreduz1 = msgreduz2
intexec evini # Para inicializar o efeito
#
# Ordem que deve organizar as constantes:
const admordem = "e_nome\n\
e_condicao\n\
desc\n\
maximo\n\
r_func r_mana r_move r_tempo r_vida\n\
tempo1 tempo2\n\
msgini1\n\
msgini2\n\
msgfalhou1\n\
msgfalhou2\n\
msgnulo1\n\
msgnulo2\n\
msgfim1\n\
msgfim2\n\
msgreduz1\n\
msgreduz2\nevperso"

func ini # Cria efeito
# arg0 = personagem que receberá o efeito
# arg1 = nível de quem usou o efeito
  evini = 1
  ret !(dono = arg0), apagar(este) # Acerta dono e apaga efeito se for nulo
  idono = dono.dentro2.addini(este) # Adiciona como personagem
  ret 0 > arg1, t_duracao = -1
  ret dono.dentro2.total(este) > maximo || e_condicao == (dono.contr.atkenv ? 3 : 2), t_duracao \
= -2
  t_reduzir = r_tempo, t_duracao = intpos(tempo1 + tempo2 * arg1)

func evini_exec # Inicializa efeito
  se t_duracao >= 0
    evperso && (evrec = dono.evento.addfim(este))
    e_condicao >= 2 && (evbat = dono.contr.atkbat.addfim(este))
    dono.recalc = 1
    r_func && (t_tempo1 = t_tempo2 = 1)
    $mens.p(dono) + $mens.mvis2(msgini1, msgini2)
    cmd_ini
  senao t_duracao == -1
    apagar(este), idono.remove
    $mens.p(dono) + $mens.mvis2(msgfalhou1, msgfalhou2)
  senao
    apagar(este), idono.remove
    $mens.p(dono) + $mens.mvis2(msgnulo1, msgnulo2)
  fimse

func fim # Apaga efeito
  ret evini || !idono, nulo
  idono.remove
  dono.recalc = 0
  dono.recalc_exec
  dono.pvida > 0 && $mens.p(dono) + $mens.mvis2(msgfim1, msgfim2)
  cmd_fim

func ajustaobj # Acerta dono e eventos do objeto
  dono = idono.objlista
  evrec.remove, evperso && (evrec = dono.evento.addfim(este))
  evbat.remove, e_condicao >= 2 && (evbat = dono.contr.atkbat.addfim(este))

func t_duracao_exec # Efeito expirou: apaga efeito
  apagar(este)

func t_reduzir_exec # Executa redução
  t_reduzir = r_tempo
  $mens.p(dono) + $mens.mvis2(msgreduz1, msgreduz2)
  dono.pmana -= r_mana
  dono.pmove -= r_move
  se dono.pvida
    dono.pvida -= r_vida
    dono.acertapos(1)

func t_tempo1_exec # Executa eventos
  ret !r_func, nulo
  prog p
  uint32 x
  uint32 v
  x = 1000000
  refvar tam = inttotal(r_func)
  epara p.inifunc(este, r_func), p.lin, p.depois
    (v = txt(p.texto, tam)) > t_tempo2 && v < x && (x = v)
  efim
  v = t_tempo2
  se x != 1000000
    t_tempo1 = x - t_tempo2
    t_tempo2 = x
  fimse
  [r_func + v](dono)


classe e_apagar
# Efeito usado internamente:
# Apaga o personagem depois de um tempo sem jogador por perto
# Se o personagem for capturado, esse efeito desaparece
#
sav listaitem idono # Uso interno: aonde o objeto está
listaitem evrec.2 # Usado internamente, para receber eventos
ref dono # Personagem que contém esse efeito; mesmo que: idono.objlista
const interno = 1 # Para não ser editável e apagável
const desc = "Usado internamente: apaga PNJ após um tempo sem jogador na sala"
const jogsav = 0 # Não salvar o efeito ao salvar o personagem
inttempo tatual # Contagem de tempo para apagar o personagem
uint16 tapaga # Tempo para apagar o personagem
intexec recalc # Se deve acertar os eventos que recebe
# func fim
# telatxt t
# t.msg("Efeito apagado\n")

func ini # Cria efeito
# arg0 = personagem que receberá o efeito
# arg1 = quanto tempo para apagar o personagem, em décimos de segundo
  ret !(dono = arg0), apagar(este) # Acerta dono e apaga efeito se for nulo
  idono = dono.dentro2.addini(este) # Adiciona como personagem
  !dono && apagar(este) # Apaga se não tiver dono
  evrec.0 = dono.evento.addfim(este)
  evrec.1 = dono.dono.evento.addfim(este)
  tapaga = arg1, !dono.dono.socktot && (tatual = tapaga) # Acerta tempo

const cmd_saiu = recalc = 1 # Alguém saiu da sala
const ajustaobj = recalc_exec # Acerta dono e eventos do objeto

func recalc_exec # Acerta eventos
  dono = idono.objlista
  dono.dono.socktot ? (tatual = 0) : !tatual && (tatual = tapaga)

func cmd_chegou # Alguém mudou de sala: acerta eventos do dono do personagem
  ret dono.dono.perso, apagar(este) # Apaga se pertence a jogador
  evrec.1.remove, evrec.1 = dono.dono.evento.addfim(este) # Acerta eventos
  dono.dono.socktot ? (tatual = 0) : !tatual && (tatual = tapaga)

func tatual_exec # Apaga personagem
  apagar(este), apagar(dono)
