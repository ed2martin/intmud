classe cmd_vender
herda cmd_avaliar
const txtajuda = "\b\c3Vender\b\n\
Sintaxe: VENDER <nome do objeto>\n\
         VENDER <quantidade> <nome do objeto>\n\
         VENDER CASA\n\
Vende objetos que você está carregando para um vendedor.\n\
Somente em alguns casos é possível vender mais de um objeto de uma vez.\n\
Vende também uma casa que você possui."
const vender = 1


classe cmd_avaliar
herda comando_comum
const txtajuda = "\b\c3Avaliar\b\n\
Sintaxe: AVALIAR <nome do objeto>\n\
         AVALIAR CASA\n\
Pergunta a um vendedor quanto ele paga por um item que você está carregando\n\
ou por uma casa que você possui."
txt200 lin
int8 abre # Usado internamente: horário de abertura
int8 fecha # Usado internamente: horário de fechamento
int1 mudou # Para saber se já enviou uma mensagem no comando avaliar
ref p # Vendedor que está verificando
txt30 tipo1 # arg0.tipo1
txt30 tipo2 # arg0.tipo2
txt30 tipo3 # arg0.tipo3
const vender = 0

func escr
  ret !arg1, arg0.msg(vender ? "Vender o que?" : "Avaliar o que?")
  tipo1 = arg0.tipo1 ? "\n" + txte(arg0.tipo1) + "\n" : ""
  tipo2 = arg0.tipo2 ? "\n" + txte(arg0.tipo2) + "\n" : ""
  tipo3 = arg0.tipo3 ? "\n" + txte(arg0.tipo3) + "\n" : ""
  listaobj l
# Avaliar/comprar casas
  se arg1 == "casa"
    l.addfim(arg0.dono.dentro2)
    epara l.remove(arg0), l, l.ini.remove
      p = l.objini # Personagem que está sendo verificado
      continuar !$a_[p.lojacasa] || p.atkenv || p.cmd_loja(arg0)
      continuar tipo1 && txtproc("\n" + txte(p.lojanao) + "\n", tipo1) >= 0
      continuar tipo2 && txtproc("\n" + txte(p.lojanao) + "\n", tipo2) >= 0
      continuar tipo3 && txtproc("\n" + txte(p.lojanao) + "\n", tipo3) >= 0
      se !p.lojasim # negocia com todos
      senao tipo1 && txtproc("\n" + txte(p.lojasim) + "\n", tipo1) >= 0
      senao tipo2 && txtproc("\n" + txte(p.lojasim) + "\n", tipo2) >= 0
      senao tipo3 && txtproc("\n" + txte(p.lojasim) + "\n", tipo3) >= 0
      senao
        continuar
      fimse
      abre = p.lojaini - misc:hora, fecha = p.lojafim - misc:hora
      se abre < fecha ? abre > 0 || fecha <= 0 : abre > 0 && fecha <= 0
        arg0.msg(txtcopiamai(p.descnome, "A") + ": Volte outra hora")
        continuar
      senao
        listaobj e
        epara e.addfim(arg0.evento, p.evento), e, e.ini.remove
          sair e.objini.cmd_negociar(arg0, p, "vender")
        efim
        continuar e
      fimse
      indiceitem item
      refvar r = item.obj("cj " + txt2(arg0.sock.cnome) + " " + p.lojacasa)
      refvar valor = intpos(r.valor * (100 - p.taxacompra) / 100)
      se !r
        ret arg0.msg("Você não tem uma casa aqui.")
      senao vender
        arg0.var.z_moedas_ += valor
        r.nomejog = ""
        r.mudachave # Já faz r.objmudou=1
        $mens.p(arg0, p)
        $mens.mens = r.nome ? r.nome : "uma casa"
        lin = "$P vende $m para $a"
        se valor == 1
          $mens.mvis2(lin + " por uma moeda.", lin + ".")
        senao valor == 2
          $mens.mvis2(lin + " por duas moedas.", lin + ".")
        senao
          $mens.mvis2(lin + " por " + valor + " moedas.", lin + ".")
        fimse
        ret
      senao
        se valor == 0
          lin = "Não pagaria nenhuma moeda pela sua casa."
        senao valor == 1
          lin = "Eu pagaria uma única moeda pela sua casa."
        senao
          lin = "Eu pagaria " + valor + " moedas pela sua casa."
        fimse
        ret arg0.msg(txtcopiamai(p.descnome, "A") + ": " + lin)
      fimse
    efim
  fimse
# Avaliar/comprar itens
  nomeobj n # Para reconhecer os itens
  n.ini(arg1, 1000000)
  epara l.addfim(arg0.dentro1), l, l.ini.remove
    continuar !l.objini.visivel(arg0) || !n.nome(l.objini.ident, l.objini.objtot)
    ret exec(arg0, l.objini, int(n))
  efim
# Não encontrou o item
  arg0.msg("Você não vê isso.")

func exec # Avalia ou vende um item
# arg0 = personagem
# arg1 = item
# arg2 = quantidade de itens
  listaobj l
  mudou = 0, l.addfim(arg0.dono.dentro2)
  epara l.remove(arg0), l, l.ini.remove
    p = l.objini # Personagem que está sendo verificado
    continuar p.atkenv
    continuar arg1.tipoitem & p.tipoitem == 0 # Checa se tem interesse
    continuar tipo1 && txtproc("\n" + txte(p.lojanao) + "\n", tipo1) >= 0
    continuar tipo2 && txtproc("\n" + txte(p.lojanao) + "\n", tipo2) >= 0
    continuar tipo3 && txtproc("\n" + txte(p.lojanao) + "\n", tipo3) >= 0
    abre = p.lojaini - misc:hora, fecha = p.lojafim - misc:hora
    se abre < fecha ? abre > 0 || fecha <= 0 : abre > 0 && fecha <= 0
      arg0.msg(txtcopiamai(p.descnome, "A") + ": Volte outra hora")
      mudou = 1
      continuar
    senao
      listaobj e
      epara e.addfim(arg0.evento, p.evento), e, e.ini.remove
        sair e.objini.cmd_negociar(arg0, p, "vender")
      efim
      continuar e
    fimse
    int32 valor # Valor do item
    valor = intpos(arg1.valor * (100 - p.taxacompra) / 100)
    se vender
      valor *= arg2
      arg1.apagar(arg2)
      arg0.var.z_moedas_ += valor
      $mens.p(arg0, p, arg1)
      se arg2 == 1
        lin = "$P vende $o para $a"
      senao
        lin = "$P vende " + arg2 + "x $o para $a"
      fimse
      se valor == 1
        $mens.mvis2(lin + " por uma moeda.", lin + ".")
      senao valor == 2
        $mens.mvis2(lin + " por duas moedas.", lin + ".")
      senao
        $mens.mvis2(lin + " por " + valor + " moedas.", lin + ".")
      fimse
      ret
    senao
      se valor == 0
        lin = "Não pagaria nenhuma moeda por isso."
      senao valor == 1
        lin = "Eu pagaria uma única moeda por isso."
      senao
        lin = "Eu pagaria " + valor + " moedas por isso."
      fimse
      arg0.msg(txtcopiamai(p.descnome, "A") + ": " + lin)
      mudou = 1
    fimse
  efim
# Ninguém quer comprar
  !mudou && arg0.msg("Ninguém quer comprar " + arg1.descnome + ".")
