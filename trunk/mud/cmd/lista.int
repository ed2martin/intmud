classe cmd_lista
herda comando_comum
const txtajuda = "\b\c3Lista\b\n\
Sintaxe: LISTA [item]\n\
Sem argumentos mostra a lista de produtos sendo vendidos.\n\
Seguido de um nome ou número mostra informações sobre um produto."
const posic = 5
txt200 lin
uint32 valor # Usado internamente: valor do item
uint32 nivel # Usado internamente: nível do item
int8 abre # Usado internamente: horário de abertura
int8 fecha # Usado internamente: horário de fechamento
real2 lucro # Para calcular o lucro do vendedor
int32 linha
ref p # Vendedor que está verificando
txt30 tipo1 # arg0.tipo1
txt30 tipo2 # arg0.tipo2
txt30 tipo3 # arg0.tipo3

func escr
  tipo1 = arg0.tipo1 ? "\n" + txte(arg0.tipo1) + "\n" : ""
  tipo2 = arg0.tipo2 ? "\n" + txte(arg0.tipo2) + "\n" : ""
  tipo3 = arg0.tipo3 ? "\n" + txte(arg0.tipo3) + "\n" : ""
  arg1 ? escr2(arg0, arg1) : escr1(arg0)

func escr1 # Consultar a lista de itens
  textotxt t
  listaobj l
  linha = 1, l.addfim(arg0.dono.dentro2)
  epara l.remove(arg0), l, l.ini.remove
    p = l.objini # Personagem que está sendo verificado
    continuar p.atkenv || !(p.lojaitem || p.lojainv || $s_[p.lojasala] || $a_[p.lojacasa] || \
$a_[p.lojachave])
# Checa se pode vender alguma coisa
    lin = p.cmd_loja(arg0)
    se lin
      t.addfim(txtcopiamai(p.descnome, "A") + ": " + lin)
      continuar
    senao tipo1 && txtproc("\n" + txte(p.lojanao) + "\n", tipo1) >= 0
      t.addfim(txtcopiamai(p.descnome, "A") + ": Não negocio com " + arg0.tipo1)
      continuar
    senao tipo2 && txtproc("\n" + txte(p.lojanao) + "\n", tipo2) >= 0
      t.addfim(txtcopiamai(p.descnome, "A") + ": Não negocio com " + arg0.tipo2)
      continuar
    senao tipo3 && txtproc("\n" + txte(p.lojanao) + "\n", tipo3) >= 0
      t.addfim(txtcopiamai(p.descnome, "A") + ": Não negocio com " + arg0.tipo3)
      continuar
    senao !p.lojasim # negocia com todos
    senao tipo1 && txtproc("\n" + txte(p.lojasim) + "\n", tipo1) >= 0
    senao tipo2 && txtproc("\n" + txte(p.lojasim) + "\n", tipo2) >= 0
    senao tipo3 && txtproc("\n" + txte(p.lojasim) + "\n", tipo3) >= 0
    senao
      t.addfim(txtcopiamai(p.descnome, "A") + ": Não negocio com você")
      continuar
    fimse
    abre = p.lojaini - misc:hora, fecha = p.lojafim - misc:hora
    se abre < fecha ? abre > 0 || fecha <= 0 : abre > 0 && fecha <= 0
      t.addfim(txtcopiamai(p.descnome, "A") + ": Volte outra hora")
      continuar
    senao
      listaobj e
      epara e.addfim(arg0.evento, p.evento), e, e.ini.remove
        lin = e.objini.cmd_negociar(arg0, p, "comprar")
        sair lin
      efim
      se lin
        t.addfim(txtcopiamai(p.descnome, "A") + ": " + lin)
        continuar
      fimse
    fimse
    t.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " informa:\b")
    lucro = (100 + p.taxavenda) / 100
# Itens fixos - lojaitem
    se 1
      textotxt t2
      t2.addfim(p.lojaitem)
      enquanto t2.linhas
        continuar !(lin = t2.remove)
        nivel = txt2(lin), lin = "i_" + txt1(lin)
        nivel == 0 && (nivel = [lin]:nivel)
        valor = lucro * [lin]:valor
        lin = txtcopiamai([lin]:descnome, "A")
        lin = "\b\c2" + linha + (linha < 10 ? "\b. " : "\b.") + lin
        lin += txtesp(40 - inttotal(lin + valor)) + "$" + valor
        nivel && (lin += "   N" + nivel)
        t.addfim(lin), linha += 1
      efim
    fimse
# Itens que possui e itens em uma sala
    se 1
      listaobj e
      p.lojainv && e.addfim(p.dentro1)
      epara e.addfim($s_[p.lojasala].dentro1), e, e.ini.remove
        nivel = e.objini.pnivel
        valor = lucro * e.objini.valor
        lin = "\b\c2" + linha + (linha < 10 ? "\b. " : "\b.")
        lin += txtcopiamai(e.objini.descnome, "A")
        lin += txtesp(40 - inttotal(lin + valor)) + "$" + valor
        nivel && (lin += "   N" + nivel)
        lin += "   x" + e.objini.objtot
        t.addfim(lin), linha += 1
      efim
    fimse
# Casas
    se $a_[p.lojacasa]
      lin = ""
      prog prg
      epara prg.iniclasse("c_" + p.lojacasa), prg.lin, prg.depois
        sair $[prg.texto].area != p.lojacasa
        continuar $[prg.texto].nomejog
        valor = lucro * $[prg.texto].valor
        lin = "\b\c2" + linha + (linha < 10 ? "\b. " : "\b.")
        lin += txtcopiamai($[prg.texto].nome, "A")
        lin += txtesp(40 - inttotal(lin + valor)) + "$" + valor
        t.addfim(lin), linha += 1
      efim
      !lin && t.addfim("Não há nenhuma casa à venda.")
    fimse
# Chaves
    se $a_[p.lojachave]
      indiceitem item
      se item.obj("cj " + txt2(arg0.sock.cnome) + " " + p.lojachave)
        valor = lucro * config:casachave
        lin = "\b\c2" + linha + (linha < 10 ? "\b. " : "\b.") + "Chave da casa"
        lin += txtesp(40 - inttotal(lin + valor)) + "$" + valor
        t.addfim(lin), linha += 1
        valor = lucro * config:casafechadura
        lin = "\b\c2" + linha + (linha < 10 ? "\b. " : "\b.") + "Fechadura da casa"
        lin += txtesp(40 - inttotal(lin + valor)) + "$" + valor
        t.addfim(lin), linha += 1
      senao
        t.addfim("Você não possui nenhuma casa nessa cidade.")
      fimse
    fimse
  efim
  se t.linhas
    arg0.msg2(t.remove(1000))
  senao
    arg0.msg("Ninguém vende nada aqui.")

func escr2 # Consultar um item
  listaobj l
  nomeobj n
  linha = 0, l.addfim(arg0.dono.dentro2), n.ini(arg1, 1)
  epara l.remove(arg0), l, l.ini.remove
    p = l.objini # Personagem que está sendo verificado
    continuar p.atkenv || p.cmd_loja(arg0)
    continuar !(p.lojaitem || p.lojainv || $s_[p.lojasala] || $a_[p.lojacasa] || $a_[p.lojachave]\
)
# Checa se pode vender alguma coisa
    continuar tipo1 && txtproc("\n" + txte(p.lojanao) + "\n", tipo1) >= 0
    continuar tipo2 && txtproc("\n" + txte(p.lojanao) + "\n", tipo2) >= 0
    continuar tipo3 && txtproc("\n" + txte(p.lojanao) + "\n", tipo3) >= 0
    abre = p.lojaini - misc:hora, fecha = p.lojafim - misc:hora
    continuar abre < fecha ? abre > 0 || fecha <= 0 : abre > 0 && fecha <= 0
    listaobj e
    epara e.addfim(arg0.evento, p.evento), e, e.ini.remove
      sair e.objini.cmd_negociar(arg0, p, "comprar")
    efim
    continuar e
    lucro = (100 + p.taxavenda) / 100
# Itens fixos - lojaitem
    textotxt t2
    epara t2.addfim(p.lojaitem), t2.linhas, t2.remove
      continuar !t2.ini.texto
      lin = "i_" + txt1(t2.ini.texto)
      continuar txt(linha += 1) != arg1 && !n.nome([lin]:nome, 1)
      ref r
      r = criar(lin), apagar(r)
      valor = lucro * r.valor
      t2.limpar
      t2.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " mostra:\b")
      lin = txtcopiamai(r.descnome, "A") + (r.pnivel ? ", nível " + r.pnivel)
      t2.addfim(lin + ", custa $" + valor + ".")
      $comando_identificar.ident(r, t2)
      ret arg0.msg2(t2.remove(1000))
    efim
# Itens que possui e itens em uma sala
    se 1
      listaobj e
      p.lojainv && e.addfim(p.dentro1)
      epara e.addfim($s_[p.lojasala].dentro1), e, e.ini.remove
        continuar txt(linha += 1) != arg1 && !n.nome(e.objini.ident, 1)
        nivel = e.objini.pnivel
        valor = lucro * e.objini.valor
        t2.limpar
        t2.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " mostra:\b")
        lin = txtcopiamai(e.objini.descnome, "A")
        e.objini.pnivel && (lin += ", nível " + e.objini.pnivel)
        t2.addfim(lin + ", custa $" + valor + ".")
        $comando_identificar.ident(e.objini, t2)
        ret arg0.msg2(t2.remove(1000))
      efim
    fimse
# Casas
    se $a_[p.lojacasa]
      lin = ""
      prog prg
      ref r
      epara prg.iniclasse("c_" + p.lojacasa), prg.lin, prg.depois
        r = $[prg.texto]
        sair r.area != p.lojacasa
        continuar r.nomejog || txt(linha += 1) != arg1 && !n.nome(r.ident, 1)
        valor = lucro * r.valor
        t2.limpar
        t2.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " mostra:\b")
        t2.addfim(r.descloja + ", custa $" + valor + ".")
        r.desc && t2.addfim(r.desc)
        ret arg0.msg2(t2.remove(1000))
      efim
    fimse
# Chave e fechadura da casa
    se $a_[p.lojachave]
      indiceitem item
      se !item.obj("cj " + txt2(arg0.sock.cnome) + " " + p.lojachave)
      senao txt(linha += 1) == arg1 || n.nome("chave da casa", 1)
        valor = (100 + p.taxavenda) / 100 * config:casachave
        t2.limpar
        t2.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " explica:\b")
        t2.addfim("Faço uma cópia da chave da sua casa por $" + valor + ".")
        ret arg0.msg2(t2.remove(1000))
      senao txt(linha += 1) == arg1 || n.nome("fechadura da casa", 1)
        valor = (100 + p.taxavenda) / 100 * config:casafechadura
        t2.limpar
        t2.addfim("\b\c6" + txtcopiamai(p.descnome, "A") + " explica:\b")
        t2.addfim("Troco a fechadura da sua casa por $" + valor + ".")
        ret arg0.msg2(t2.remove(1000))
      fimse
    fimse
  efim
  arg0.msg("Ninguém vende isso aqui.")
