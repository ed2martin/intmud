classe mens
ref perso # Personagem que está enviando a mensagem
ref alvo # Personagem ou objeto alvo da ação
ref obj # Objeto secundário
ref r # Objeto que vai receber a mensagem

func iniclasse
  criar(arg0)

func p # Define os personagens
  perso=arg0, alvo=arg1, obj=arg2

func mtodos1 # Mensagem só para quem estiver acordado
# arg0 = mensagem para todos
  se perso.dono.jogtot && arg0
    listaobj l0
    l0.addfim(perso.dono.dentro)
    enquanto l0
      l0.ini.obj.msginv(arg0), l0.ini.remove
    efim

func mtodos2 # Mensagem só para quem estiver acordado
# arg0 = mensagem para personagem, arg1 = mensagem para outros
  se perso.dono.jogtot
    arg0 && perso.msginv(arg0)
    se arg1
      listaobj l0
      l0.addfim(perso.dono.dentro)
      l0.remove(perso)
      enquanto l0
        l0.ini.obj.msginv(arg1), l0.ini.remove
      efim

func mtodos3 # Mensagem só para quem estiver acordado
# arg0,arg1,arg2 = mensagens para personagem, alvo e outros
  se perso.dono.jogtot
    arg0 && perso.msginv(arg0)
    arg1 && alvo.msginv(arg1)
    se arg2
      listaobj l0
      l0.addfim(perso.dono.dentro)
      l0.remove(perso, alvo)
      enquanto l0
        l0.ini.obj.msginv(arg2), l0.ini.remove
      efim

func mvis1 # Mensagem só para quem pode ver o personagem e estiver acordado
# arg0 = mensagem para todos
  se perso.dono.jogtot && arg0
    perso.msginv(arg0)
    listaobj l0
    l0.addfim(perso.dono.dentro)
    l0.remove(perso)
    enquanto l0
      l0.ini.obj.msgvis(arg0), l0.ini.remove
    efim

func mvis2 # Mensagem só para quem pode ver o personagem e estiver acordado
# arg0 = mensagem para personagem, arg1 = mensagem para outros
  se perso.dono.jogtot
    arg0 && perso.msginv(arg0)
    se arg1
      listaobj l0
      l0.addfim(perso.dono.dentro)
      l0.remove(perso)
      enquanto l0
        l0.ini.obj.msgvis(arg1), l0.ini.remove
      efim

func mvis3 # Mensagem só para quem pode ver o personagem e estiver acordado
# arg0,arg1,arg2 = mensagens para personagem, alvo e outros
  se perso.dono.jogtot
    arg0 && perso.msginv(arg0)
    arg1 && alvo.msgvis(arg1)
    se arg2
      listaobj l0
      l0.addfim(perso.dono.dentro)
      l0.remove(perso, alvo)
      enquanto l0
        l0.ini.obj.msgvis(arg2), l0.ini.remove
      efim

func proc # Prepara e envia mensagem
# arg0=mensagem, arg1=objeto
  textotxt t0
  textopos p0
  int16 i # Posição em p0
  txt1 opc # Opção
  txt100 lin
  t0.addfim(arg0)
  p0 = t0.ini, i = 0, r = arg1
  enquanto (i = p0.txtproc("$", i)) >= 0
    opc = p0.texto(i+1, 1)
    se opc === "$"
      p0.mudar("", i, 1)
      i += 1
    senao opc === txtmai(opc)
      lin = p0.texto(i+2)
      p0.mudar(opcmai_[opc](lin), i, inttotal(lin)+2)
    senao opc === txtmin(opc)
      lin = p0.texto(i+2)
      p0.mudar(opcmin_[opc](lin), i, inttotal(lin)+2)
    senao
      p0.mudar("", i, 2)
    fimse
  efim
  arg1.msg(t0.remove(1000))

func opcmai_p # Nome do personagem
  se r == perso
    ret "Você" + arg0
  senao r.visivel(perso)
    ret txtmaimin(perso.nome) + arg0
  senao perso.perso
    ret "Alguém" + arg0
  senao
    ret "Alguma coisa" + arg0

func opcmin_p # Nome do personagem
  se r == perso
    ret "você" + arg0
  senao r.visivel(perso)
    ret txtmin(perso.nome) + arg0
  senao perso.perso
    ret "alguém" + arg0
  senao
    ret "alguma coisa" + arg0

func opcmai_a # Nome do alvo
  se r == alvo
    ret "Você" + arg0
  senao r.visivel(alvo)
    ret txtmaimin(alvo.nome) + arg0
  senao alvo.perso
    ret "Alguém" + arg0
  senao
    ret "Alguma coisa" + arg0

func opcmin_a # Nome do alvo
  se r == alvo
    ret "você" + arg0
  senao r.visivel(alvo)
    ret txtmin(alvo.nome) + arg0
  senao alvo.perso
    ret "Alguém" + arg0
  senao
    ret "Alguma coisa" + arg0

func opcmai_o # Nome do objeto
  se r == obj
    ret "Você" + arg0
  senao r.visivel(obj)
    ret txtmaimin(obj.nome) + arg0
  senao obj.perso
    ret "Alguém" + arg0
  senao
    ret "Alguma coisa" + arg0

func opcmin_o # Nome do objeto
  se r == obj
    ret "você" + arg0
  senao r.visivel(obj)
    ret txtmin(obj.nome) + arg0
  senao obj.perso
    ret "alguém" + arg0
  senao
    ret "alguma coisa" + arg0

func opcmai_q # Gênero do personagem
  se perso.perso
    ret sexoB[perso.sexo] + arg0
  senao
    ret sexoD[perso.sexo] + arg0

func opcmin_q # Gênero do personagem
  se perso.perso
    ret sexoA[perso.sexo] + arg0
  senao
    ret sexoC[perso.sexo] + arg0

func opcmai_r # Gênero do alvo
  se perso.perso
    ret sexoB[alvo.sexo] + arg0
  senao
    ret sexoD[alvo.sexo] + arg0

func opcmin_r # Gênero do alvo
  se perso.perso
    ret sexoA[alvo.sexo] + arg0
  senao
    ret sexoC[alvo.sexo] + arg0

func opcmai_s # Gênero do objeto
  se perso.perso
    ret sexoB[obj.sexo] + arg0
  senao
    ret sexoD[obj.sexo] + arg0

func opcmin_s # Gênero do objeto
  se perso.perso
    ret sexoA[obj.sexo] + arg0
  senao
    ret sexoC[obj.sexo] + arg0

#func opcmai_a
## arg0 é o que vem depois de "$A", até 100 caracteres e sem "\n"
#ret "teste" + arg0 # Substitui "$a" por "teste"

#func opcmin_a
#ret "R$$" + arg0 # Um $ no texto deve ser retornado com o$$
## Ou assim: ret txttroca("R$", "$", "$$") + arg0

const sexoA0 = "a"
const sexoA1 = "o"
const sexoB0 = "A"
const sexoB1 = "O"
const sexoC0 = "uma"
const sexoC1 = "um"
const sexoD0 = "Uma"
const sexoD1 = "Um"
