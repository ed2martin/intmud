classe adm_item
# Comandos de itens
#
const adm_item = "\b\c3Comandos de itens\b\n\
\c2LITEM\b [nome]\n\
  Lista itens que começam com o nome especificado.\n\
\c2CITEM\b <nome>\n\
  Cria item. Nome deve ser o nome de uma área ou do item\n\
  (nome da área seguido de um número de três dígitos de 001 a 999).\n\
\c2EITEM\b <nome>\n\
  Edita item.\n\
\c2AITEM\b <nome>\n\
  Apaga item."

func cmd_litem # Listar itens
  prog p
  se !p.iniclasse("i_" + arg0)
    msg("\bNenhum item encontrado.")
  senao
    textotxt t
    txt100 lin
    t.addfim("\b\c6Item Tipo Quantidade Nome\b")
    enquanto p.lin && t.linhas < 500
      lin = txt(p.texto, 2) + "  " + txt(" SGC", [p.texto]:item, 1)
      t.addfim(lin + " " + inttotal($[p.texto]) + "  " + [p.texto]:nome)
      p.depois
    efim
    msg2(t.remove(500))
  fimse
  ret 1

func cmd_citem # Criar item
  prog p
  txt40 nome
  uint16 num
# Checa se digitou nome da sala
  se arg0 == ""
    msg("Tecle o nome do item ou da área após CITEM")
    ret 1
  fimse
# Checa nome completo
  nome = txtmin(txtremove(arg0, "EMDCT7"))
  num = int(txtfim(nome, 3)) + 1000
  se num > 1000 && txtfim(num, 3) == txtfim(arg0, 3)
    se !p.existe("a_" + txt(nome, 0, inttotal(nome) - 3))
      msg("Área não existe: " + txt(nome, 0, inttotal(nome) - 3))
      ret 1
    senao p.existe("i_" + nome)
      msg("Item já existe: " + nome)
      ret 1
    fimse
# Checa nome de área
  senao !p.existe("a_" + nome)
    msg("Área não existe: " + nome)
    ret 1
  senao
    epara num = 1001, p.existe("i_" + nome + txtfim(num, 3)), num += 1
      continuar num < 2000
      msg("Atingido limite de itens na área " + nome)
      ret 1
    efim
    nome += txtfim(num, 3)
  fimse
# Pergunta que tipo de item criar
  var.opcclasse = nome
  ret p_citem("\b")

func p_citem # Escolher o tipo de item ao criar
  txt40 deriv
  casovar txtmin(arg0)
  casose
    msg("Opção inválida")
  casose "\b"
  casose ""
    passo = "citem"
    msg("Entre com o tipo de item, O mostra as opções, Z cancela")
    ret 1
  casose "z"
    msg("Retornando ao jogo")
    passo = "", var.opcclasse = ""
    ret 1
  casose "o"
    txtmais.limpar
    txtmais.addfim("S Simples")
    txtmais.addfim("G Agrupa vários em um objeto para economizar memória")
    txtmais.addfim("C Completo (luz, mobília, container)")
    txtmostra
    ret 1
  casose "s"
    deriv = "comum_item"
    sair
  casose "g"
    deriv = "comum_itemgrupo"
    sair
  casose "c"
    deriv = "comum_itemperso"
    sair
  casofim
# Cria item
  prog p
  txt100 area
  area = txt(var.opcclasse, 0, inttotal(var.opcclasse) - 3)
  p.criar("i_" + var.opcclasse + "\na-" + area + "\nherda " + deriv)
  criar("admordena", "a-" + area)
# config:salvar
  msg("\bItem criado: " + var.opcclasse)
  admlog("criou item " + var.opcclasse)
  passo = "", var.opcclasse = ""
  ret 1

func cmd_aitem # Apagar item
  prog p
  refvar nome = txtmin(txtremove(arg0, "EMDCT7"))
  se nome == ""
    msg("Tecle um ou mais nomes de itens após AITEM")
    ret 1
  fimse
  uint16 x
  enquanto txtsub(nome, x, 1)
    se !p.existe("i_" + txtsub(nome, x, 1))
      msg("Item não existe: " + txtsub(nome, x, 1))
      ret 1
    fimse
    x += 1
  efim
  tpasso = nome, p_aitem
  ret 1

func p_aitem # Confirmar que quer apagar item
  passo = ""
  se arg0 == "n"
    msg("Cancelado")
  senao arg0 == "s"
    prog p
    uint16 x
    enquanto txtsub(tpasso, x, 1)
      p.apagar("i_" + txtsub(tpasso, x, 1))
      x += 1
    efim
    config:salvar
    se x < 2
      msg("Item apagado: " + tpasso)
      admlog("apagou item " + tpasso)
    senao
      msg("Itens apagados: " + tpasso)
      admlog("apagou itens " + tpasso)
    fimse
  senao
    txt20 m1
    passo = "aitem"
    m1 = txt2(tpasso) ? "itens" : "item"
    msg("Apagar " + m1 + " " + tpasso + "? Tecle S ou N")
  fimse
  ret 1

func cmd_eitem # Editar item
  prog p
  uint8 num
  refvar nome = txtmin(txtremove(arg0, "EMDCT7"))
  se nome == ""
    msg("Tecle o nome do item após EITEM")
  senao !p.existe("i_" + nome)
    msg("\bItem não existe: " + nome)
  senao !p.existe("menu_item" + (num = i_[nome]:item))
    msg("\bItem não pode ser editado: " + nome)
  senao
    var.classe1 = "i_" + txtmin(nome)
    var.menufim = "menu_item" + num
    m_menu("menu_item" + num)
  fimse
  ret 1


classe menu_iarma
const titulo = "Tipo de arma"
const menufim = var.menufim
const colunas = 23
#
const nome1 = "Arma tipo"
const tipo1 = "opcao"
const clas1 = var.classe1
const vari1 = "armatipo"
const extr1 = "Não é arma\n" + txtsublin(config:arma_txt, 1)
#
const nome2 = "Arma precisão"
const info2 = "Em porcentagem, 100=mesma precisão de atacar sem arma"
const tipo2 = "numero"
const clas2 = var.classe1
const vari2 = "armaprecisao"
#
const nome3 = "Ataque mín"
const tipo3 = "numero"
const clas3 = var.classe1
const vari3 = "armamin"
#
const nome4 = "Ataque máx"
const tipo4 = "numero"
const clas4 = var.classe1
const vari4 = "armamax"
#
const nome5 = "Só ataca manual"
const info5 = "Se o personagem não usa essa arma em ataques automáticos"
const tipo5 = "simnao"
const clas5 = var.classe1
const vari5 = "armamanual"


classe menu_icomum
const nome1 = "Nome"
const tipo1 = "linha"
const clas1 = var.classe1
const vari1 = "nome"
#
const nome2 = "Palavras chave"
const info2 = "Palavras que os jogadores digitam para se referir ao item"
const tipo2 = "linha"
const clas2 = var.classe1
const vari2 = "ident"
#
const nome3 = "Descrição curta"
const info3 = "Descrição usada na maioria dos comandos"
const tipo3 = "linha"
const clas3 = var.classe1
const vari3 = "descnome"
#
const nome4 = "Descrição sala"
const info4 = "Descrição quando está na sala"
const tipo4 = "linha"
const clas4 = var.classe1
const vari4 = "descpos"
#
const nome5 = "Descrição ao ver"
const tipo5 = "texto"
const clas5 = var.classe1
const vari5 = "descver"
#
const nome6 = "Descrição ident"
const info6 = "Texto adicionado após identificar (com magia de identificação)"
const tipo6 = "texto"
const clas6 = var.classe1
const vari6 = "descident"
#
const nome7 = "Sexo Msg"
const info7 = "Usado nas mensagens com o objeto"
const tipo7 = "opcao"
const clas7 = var.classe1
const vari7 = "msexo"
const extr7 = "Feminino\nMasculino"
#
const nome8 = "Peso"
const info8 = "Em gramas"
const tipo8 = "numero"
const clas8 = var.classe1
const vari8 = "pesoobj"
const extr8 = "1 1000000000"
const exec8 = "ajustapeso"
#
const nome9 = "Volume"
const tipo9 = "numero"
const clas9 = var.classe1
const vari9 = "volobj"
const extr9 = "1 1000000000"
const exec9 = "ajustapeso"
#
const nome13 = "Nível"
const info13 = "Nível mínimo para vestir/empunhar o item"
const tipo13 = "numero"
const clas13 = var.classe1
const vari13 = "nivel"
#
const nome14 = "Mantém na sala"
const info14 = "Como a opção \"Apagar itens\" da sala ou área afeta o item"
const tipo14 = "simnao"
const clas14 = var.classe1
const vari14 = "mantersala"
#
const nome15 = "Quanto vale"
const tipo15 = "numero"
const clas15 = var.classe1
const vari15 = "valor"
#
const nome16 = "Tipo de produto"
const info16 = "Para saber se um vendedor pode comprar esse item"
const tipo16 = "vetor"
const clas16 = var.classe1
const vari16 = "tipoitem"
const extr16 = config:produtos
#
const nome17 = "Aonde vestir"
const tipo17 = "vetor"
const clas17 = var.classe1
const vari17 = "vestir1"
const extr17 = config:equip_txt
#
const nome18 = "Podem vestir"
const tipo18 = "opcao"
const clas18 = var.classe1
const vari18 = "vestirsim"
const extr18 = "Todos\nJogadores e PNJs\nAnimais capturados"
#
const nome19 = "Não podem vestir"
const info19 = "Raças e classes que não podem usar esse item"
const tipo19 = "tipos"
const clas19 = var.classe1
const vari19 = "vestirnao"
#
const nome20 = "Atrib núm vestir"
const tipo20 = "atribnum"
const clas20 = var.classe1
const vari20 = "recalc_auto"
const exec20 = "recalc_ajusta"
#
const nome21 = "Atrib s/n vestir"
const tipo21 = "atribbit"
const clas21 = var.classe1
const vari21 = "recalc_auto"
const exec21 = "recalc_ajusta"
#
const nome22 = "Atrib atk vestir"
const info22 = "Afetam os danos quando o personagem ataca alguém"
const tipo22 = "atribatk"
const clas22 = var.classe1
const vari22 = "recalc_auto"
const exec22 = "recalc_ajusta"
#
const nome23 = "Atrib def vestir"
const info23 = "Afetam os danos quando alguém ataca o personagem"
const tipo23 = "atribdef"
const clas23 = var.classe1
const vari23 = "recalc_auto"
const exec23 = "recalc_ajusta"
#
const nome24 = "Tipo de arma"
const tipo24 = "menu--"
const clas24 = "menu_iarma"
#
const nome25 = "Pega animal nível"
const info25 = "Nível médio dos animais que podem ser capturados com esse item,\n\
ou 0 se não puder capturar"
const tipo25 = "numero"
const clas25 = var.classe1
const vari25 = "i_captura"
#
const nome26 = "Chave"
const info26 = "Código da chave; abre fechaduras com o mesmo código"
const tipo26 = "linha"
const clas26 = var.classe1
const vari26 = "i_chave"
#
const nome50 = "Variáveis"
const tipo50 = "linha"
const clas50 = var.classe1
const vari50 = "inivar"
#
const nome51 = "Eventos item"
const info51 = "Receber eventos do próprio item"
const tipo51 = "simnao"
const clas51 = var.classe1
const vari51 = "eveste"
const exec51 = "ajustaobj"
#
const nome52 = "Eventos em perso"
const info52 = "Se recebe eventos do dono se for personagem"
const tipo52 = "simnao"
const clas52 = var.classe1
const vari52 = "evperso"
const exec52 = "ajustaobj"
#
const nome53 = "Eventos em item"
const info53 = "Se recebe eventos do dono se for item"
const tipo53 = "simnao"
const clas53 = var.classe1
const vari53 = "evitem"
const exec53 = "ajustaobj"
#
const nome54 = "Eventos em sala"
const info54 = "Se recebe eventos da sala se estiver em uma sala"
const tipo54 = "simnao"
const clas54 = var.classe1
const vari54 = "evsala"
const exec54 = "ajustaobj"


classe menu_item1
herda menu_icomum
const titulo = "Item simples " + txt(var.classe1, 2)
const colunas = 23
#
const nome10 = "Volume dentro"
const tipo10 = "numero"
const clas10 = var.classe1
const vari10 = "volmax"
#
const nome11 = "Expande"
const info11 = "Volume aumenta se tiver itens dentro"
const tipo11 = "simnao"
const clas11 = var.classe1
const vari11 = "volexp"
const exec11 = "ajustapeso"
#
const nome12 = "Objetos máximo"
const tipo12 = "numero"
const clas12 = var.classe1
const vari12 = "objmax"


classe menu_item2
herda menu_icomum
const titulo = "Item grupo " + txt(var.classe1, 2)
const colunas = 23
#
const nome13 = "Nível"
const info13 = "Todos os itens " + txt(var.classe1, 2) + " possuem o mesmo nível"
const tipo13 = "numero"
const clas13 = var.classe1
const vari13 = "nivel"


classe menu_item3
herda menu_item1
const titulo = "Item " + txt(var.classe1, 2)
const colunas = 23
#
const nome27 = "Mobília"
const info27 = "Sentar/dormir confortavelmente se recupera mais rápido"
const tipo27 = "opcao"
const clas27 = var.classe1
const vari27 = "i_movel"
const extr27 = "Não\n\
Sentar\n\
Sentar e dormir\n\
Sentar confortavelmente\n\
Sentar e dormir confortavelmente"
#
const nome28 = "Lugares"
const tipo28 = "numero"
const clas28 = var.classe1
const vari28 = "i_lugar"
#
const nome29 = "Container"
const tipo29 = "opcao"
const clas29 = var.classe1
const vari29 = "i_abertoini"
const exec29 = "recalc_exec"
const extr29 = "Não\n\
Sempre aberto\n\
Inicialmente aberto\n\
Inicialmente fechado\n\
Inicialmente trancado\n\
Indefinido; não checa se está aberto, fechado ou trancado"
#
const nome30 = "Trancar"
const tipo30 = "opcao"
const clas30 = var.classe1
const vari30 = "i_tranc"
const extr30 = "Não pode ser trancado\nPode ser trancado\nTranca ao fechar"
#
const nome31 = "Fechadura"
const info31 = "Código da chave que tranca e destranca o container"
const tipo31 = "linha"
const clas31 = var.classe1
const vari31 = "i_fechadura"
#
const nome32 = "Fonte de luz"
const tipo32 = "menu--"
const clas32 = "menu_iluz"


classe menu_iluz
const titulo = "Fonte de luz"
const menufim = var.menufim
const colunas = 23
#
const nome1 = "Tipo de luz"
const tipo1 = "opcao"
const clas1 = var.classe1
const vari1 = "luzitem"
const extr1 = "Não aceita acender/apagar, sem fogo\n\
Lanterna (acende/apaga, sem fogo)\n\
Lanterna 2 (acende/apaga, inclusive na água)\n\
Não aceita acender/apagar, produz fogo\n\
Lampião (acende/apaga, produz fogo)\n\
Fósforo (acende uma vez, produz fogo)\n\
Tocha (acende com fogo, produz fogo)"
#
const nome2 = "Duração da luz"
const info2 = "Em décimos de segundo, se menor que zero começa apagado"
const tipo2 = "numero"
const clas2 = var.classe1
const vari2 = "luzini"
#
const nome3 = "Acendeu mensagem"
const tipo3 = "linha"
const clas3 = var.classe1
const vari3 = "luzliga"
#
const nome4 = "Sem luz mensagem"
const info4 = "Mensagem quando a luz acaba, se estiver vazio não envia nenhuma mensagem"
const tipo4 = "linha"
const clas4 = var.classe1
const vari4 = "luztxt"
#
const nome5 = "Sem luz item some"
const info5 = "Se o item some (é apagado) quando a luz acaba"
const tipo5 = "simnao"
const clas5 = var.classe1
const vari5 = "luzfim"
