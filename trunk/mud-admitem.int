classe jog_admitem
# Comandos de itens
#
const adm_item = "\b\c3Comandos de itens\b\n\
\c2LITEM\b [nome]\n\
  Lista itens que começam com o nome especificado.\n\
\c2CITEM\b <tipo> <nome>\n\
  Cria item. Tipo é uma letra entre S, G ou C:\n\
  S simples, G agrupa em um objeto, C completo\n\
  Nome deve ser o nome de uma área ou do item\n\
  (nome da área seguido de um número de três dígitos de 001 a 999).\n\
\c2EITEM\b <nome>\n\
  Edita item.\n\
\c2AITEM\b <nome>\n\
  Apaga item."

func cmd_litem # Listar itens
  prog p
  se !p.iniclasse("i_" + arg0)
    msg("\bNenhum item encontrado.")
  senao
    textotxt t
    t.addfim("\bItens:")
    enquanto p.lin && t.linhas < 500
      t.addfim(txt(p.texto, 2) + "  " + [p.texto]:nome)
      p.depois
    efim
    msg2(t.remove(500))
  fimse
  ret 1

func cmd_citem # Criar item
  prog p
  uint16 num
  txt40 deriv
  txt40 nome
# Obtém a classe base conforme o tipo de item
  casovar txtmin(txt1(arg0))
  casose "s"
    deriv = "comum_item"
    sair
  casose "g"
    deriv = "comum_itemgrupo"
    sair
  casose "c"
    deriv = "comum_itemperso"
    sair
  casose
    msg("Tecle o tipo e o nome após CITEM; os tipos válidos são:")
    msg("S simples, G agrupa em um objeto, C completo")
    ret 1
  casofim
# Checa se digitou nome do item
  nome = txt2(arg0)
  se nome == ""
    msg("Tecle o nome do item após CITEM e o tipo")
    ret 1
  fimse
# Checa nome completo
  num = int(txtfim(nome, 3)) + 1000
  se num > 1000 && txtfim(num, 3) == txtfim(arg0, 3)
    se !p.existe("a_" + txt(nome, 0, inttotal(nome) - 3))
      msg("Área não existe: " + txt(nome, 0, inttotal(nome) - 3))
      ret 1
    senao p.existe("i_" + nome)
      msg("Item já existe: " + nome)
      ret 1
    fimse
# Checa nome de área
  senao !p.existe("a_" + nome)
    msg("Área não existe: " + nome)
    ret 1
  senao
    epara num = 1001, p.existe("i_" + nome + txtfim(num, 3)), num += 1
      continuar num < 2000
      msg("Atingido limite de itens na área " + nome)
      ret 1
    efim
    nome += txtfim(num, 3)
  fimse
# Cria item
  txt100 area
  area = txt(nome, 0, inttotal(nome) - 3)
  p.criar("i_" + nome + "\na-" + area + "\nherda " + deriv)
  config:salvar
  msg("\bItem criado: " + nome)
  ret 1

func cmd_aitem # Apagar item
  prog p
  se arg0 == ""
    msg("Tecle um ou mais nomes do item após AITEM")
    ret 1
  fimse
  uint16 x
  enquanto txtsub(arg0, x, 1)
    se !p.existe("i_" + txtsub(arg0, x, 1))
      msg("Item não existe: " + txtsub(arg0, x, 1))
      ret 1
    fimse
    x += 1
  efim
  tpasso = arg0, p_aitem
  ret 1

func p_aitem # Confirmar que quer apagar item
  passo = ""
  se arg0 == "n"
    msg("Cancelado")
  senao arg0 == "s"
    prog p
    uint16 x
    enquanto txtsub(tpasso, x, 1)
      p.apagar("i_" + txtsub(tpasso, x, 1))
      x += 1
    efim
    config:salvar
    se x < 2
      msg("Item apagado: " + tpasso)
    senao
      msg("Itens apagados: " + tpasso)
    fimse
  senao
    txt20 m1
    passo = "aitem"
    m1 = txt2(tpasso) ? "itens" : "item"
    msg("Apagar " + m1 + " " + tpasso + "? Tecle S ou N")
  fimse
  ret 1

func cmd_eitem # Editar item
  prog p
  uint8 num
  se !p.existe("i_" + arg0)
    msg("\bItem não existe: " + arg0)
  senao !p.existe("adm_item" + (num = i_[arg0]:item))
    msg("\bItem não pode ser editado: " + arg0)
  senao
    var.classe1 = "i_" + txtmin(arg0)
    m_menu("adm_item" + num)
  fimse
  ret 1


classe adm_icomum
const nome1 = "Nome"
const tipo1 = "linha"
const classe1 = var.classe1
const var1 = "nome"
#
const nome2 = "Palavras chave"
const info2 = "Palavras que os jogadores digitam para se referir ao item"
const tipo2 = "linha"
const classe2 = var.classe1
const var2 = "ident"
#
const nome3 = "Sexo"
const tipo3 = "opcao"
const classe3 = var.classe1
const var3 = "sexo"
const extra3 = "feminino\nmasculino"
#
const nome4 = "Descrição padrão"
const info4 = "Descrição quando está na sala"
const tipo4 = "linha"
const classe4 = var.classe1
const var4 = "descpos"
#
const nome5 = "Descrição ao ver"
const tipo5 = "texto"
const classe5 = var.classe1
const var5 = "descver"
#
const nome41 = "Variáveis"
const tipo41 = "linha"
const classe41 = var.classe1
const var41 = "inivar"
#
const nome42 = "Eventos item"
const info42 = "Receber eventos do próprio item"
const tipo42 = "simnao"
const classe42 = var.classe1
const var42 = "eveste"
#
const nome43 = "Eventos em perso"
const info43 = "Se recebe eventos do dono se for personagem"
const tipo43 = "simnao"
const classe43 = var.classe1
const var43 = "evperso"
#
const nome44 = "Eventos em item"
const info44 = "Se recebe eventos do dono se for item"
const tipo44 = "simnao"
const classe44 = var.classe1
const var44 = "evitem"
#
const nome45 = "Eventos em sala"
const info45 = "Se recebe eventos da sala se estiver em uma sala"
const tipo45 = "simnao"
const classe45 = var.classe1
const var45 = "evsala"


classe adm_item1
herda adm_icomum
const titulo = "Item simples " + txt(var.classe1, 2)
#
const nome6 = "Nível"
const tipo6 = "numero"
const classe6 = var.classe1
const var6 = "nivel"
#
const nome7 = "Quanto vale"
const tipo7 = "numero"
const classe7 = var.classe1
const var7 = "valor"
#
const nome8 = "Vestir posição 1"
const tipo8 = "opcao"
const classe8 = var.classe1
const var8 = "vestir1"
const extra8 = adm_item1:equip
#
const nome9 = "Vestir posição 2"
const tipo9 = "opcao"
const classe9 = var.classe1
const var9 = "vestir2"
const extra9 = adm_item1:equip
#
const nome10 = "Vestir posição 3"
const tipo10 = "opcao"
const classe10 = var.classe1
const var10 = "vestir3"
const extra10 = adm_item1:equip
#
const nome11 = "Peso"
const info11 = "Em gramas"
const tipo11 = "numero"
const classe11 = var.classe1
const var11 = "pesoobj"
const extra11 = "1 1000000000"
#
const nome12 = "Volume"
const tipo12 = "numero"
const extra12 = "1 1000000000"
const var12 = "volobj"
#
const nome13 = "Volume máximo"
const tipo13 = "numero"
const classe13 = var.classe1
const var13 = "volmax"
#
const nome14 = "Objetos máximo"
const tipo14 = "numero"
const classe14 = var.classe1
const var14 = "objmax"
#
const nome15 = "Expande"
const info15 = "Volume aumenta se tiver itens dentro"
const tipo15 = "simnao"
const classe15 = var.classe1
const var15 = "volexp"
#
comum textotxt t # Lista das posições aonde pode vestir
const equip = adm_item1:t.ini.textolin(100) # Texto da variável t

func iniclasse
  uint8 n
  [arg0]:t.addfim("Não")
  epara n = 1, misc:equip(n) != "desconhecido", n += 1
    [arg0]:t.addfim(txtmaimin(misc:equip(n)))
  efim


classe adm_item2
herda adm_icomum
const titulo = "Item grupo " + txt(var.classe1, 2)
#
const nome6 = "Nível"
const info6 = "Todos os itens " + txt(var.classe1, 2) + " possuem o mesmo nível"
const tipo6 = "numero"
const classe6 = var.classe1
const var6 = "pnivel"
#
const nome7 = "Quanto vale"
const tipo7 = "numero"
const classe7 = var.classe1
const var7 = "valor"
#
const nome8 = "Vestir posição 1"
const tipo8 = "opcao"
const classe8 = var.classe1
const var8 = "vestir1"
const extra8 = adm_item1:equip
#
const nome9 = "Vestir posição 2"
const tipo9 = "opcao"
const classe9 = var.classe1
const var9 = "vestir2"
const extra9 = adm_item1:equip
#
const nome10 = "Vestir posição 3"
const tipo10 = "opcao"
const classe10 = var.classe1
const var10 = "vestir3"
const extra10 = adm_item1:equip
#
const nome11 = "Peso"
const info11 = "Em gramas"
const tipo11 = "numero"
const classe11 = var.classe1
const var11 = "pesoobj"
const extra11 = "1 1000000000"
#
const nome12 = "Volume"
const tipo12 = "numero"
const extra12 = "1 1000000000"
const var12 = "volobj"


classe adm_item3
herda adm_item1
const titulo = "Item " + txt(var.classe1, 2)
#
const nome17 = "Mobília"
const tipo17 = "opcao"
const classe17 = var.classe1
const var17 = "i_movel"
const extra17 = "Não\nPodem sentar\nPodem sentar e deitar"
#
const nome18 = "Lugares"
const tipo18 = "numero"
const classe18 = var.classe1
const var18 = "i_lugar"
