MAPAGRANDE

[_config]
# Configuração
const porta = 1963 # Nota: O Papovox só se conecta na porta 1963
const salaini = $casa1_sala  # Quando entra no jogo, entra nessa sala

[comum_perso]
# Personagens
const perso = 1 # Indica que é personagem
const alias = "" # Nomes que o usuário digita para acessar o personagem
uint8 posicao  # Posição atual
const pospadrao = 8 # Posição padrão
# Posições válidas:
# 0=morto
# 1=mortalmente ferido
# 2=incapacitado
# 3=fraco
# 4=dormindo
# 5=descansando
# 6=sentado
# 7=lutando
# 8=em pé
const sala = sala_item.objx # Sala do personagem
listaitem sala_item # Referência à sala do personagem

func ini # Objeto foi criado: atualiza posição atual
  posicao = pospadrao

func mudasala # Muda de sala
# arg0=nova sala
  sala_item.remove
  arg0.dentro.add2(este)
  sala_item = arg0.dentro.fim

func descrsala # Mostra descrição de um lugar
  txt100 saidas
  msg("\cb" + sala.nome + "\b\n")
  msg(sala.descr + "\n")
  saidas = ds1("norte", "n") + ds1("sul", "s") + ds1("leste", "l")
  saidas += ds1("oeste", "o") + ds1("cima", "c") + ds1("baixo", "b")
  se saidas
    msg("\c6Saídas\b:" + saidas + "\n")
  senão
    msg("\c9Nenhuma saída\b\n")

func ds1 # Usado em descrsala
  se sala.[arg0] = nulo | sala.[arg0]_ini = 1
    ret ""
  senão sala.[arg0]_agora < 2
    ret " \c6" + arg1 + "\b"
  senao
    ret " \c9!" + arg1 + "\b"

func escr # Escreveu alguma coisa
# arg0=o que escreveu
  ref obj # Objeto que vai processar o comando
  indiceitem ind # Para procurar o objeto
### Procura objeto
  ind.ini("com_" + txt1(arg0))
  enquanto ind
    se ind.obj & nivel >= ind.obj.nivel
      sair
    fimse
    ind.depois
  efim
  obj = ind.obj
### Processa comando
  se obj = nulo
    msg("O quê?\n")
  senão posicao < obj.posic
    se posicao = 0 # Morto
      msg("\c9Você está morto!!!\b\n")
    senão posicao = 1 | posicao = 2 # Incapacitado
      msg("\c9Você está muito mal, incapaz de fazer qualquer coisa.\b\n")
    senão posicao = 3 # Fraco
      msg("\c9A única coisa que você pode fazer agora é pensar nas estrelas.\b\n")
    senão posicao = 4 # Dormindo
      msg("\c9Nos seus sonhos...\b\n")
    senão posicao = 5 # Descansando
      msg("\c9Você se sente relaxado demais para fazer isso.\b\n")
    senão posicao = 6 # Sentado
      msg("\c9Talvez você deva ficar em pé primeiro.\b\n")
    senão posicao = 7 # Lutando
      msg("\c9Sem chance! Você está lutando pela sua vida.\b\n")
    senão
      msg("\c9Você tenta, mas não consegue.\b\n")
    fimse
  senão
    obj.exec(este, arg0)

[comum_sala]
listaobj dentro # Itens e personagens dentro da sala
#
# *** Saídas
# ref norte
# ref sul
# ref leste
# ref oeste
# ref cima
# ref baixo
#
# *** Estado inicial das saídas:
# 0=nenhuma porta
# 1=nenhuma porta/invisível
# 2=aberta
# 3=fechada
# 4=trancada
# 5=trancada; se fechada é trancada
# uint8 norte_ini
# uint8 sul_ini
# uint8 leste_ini
# uint8 oeste_ini
# uint8 cima_ini
# uint8 baixo_ini
#
# *** Estado atual das saídas
# 0=nenhuma porta
# 1=aberta
# 2=fechada
# 3=trancada
uint8 norte_agora
uint8 sul_agora
uint8 leste_agora
uint8 oeste_agora
uint8 cima_agora
uint8 baixo_agora
#
# *** Número da chave para trancar e destrancar a porta
# uint8 norte_chave
# uint8 sul_chave
# uint8 leste_chave
# uint8 oeste_chave
# uint8 cima_chave
# uint8 baixo_chave

func iniclasse # Para criar um objeto quando a classe for criada
  criar(arg0)

func ini # Objeto criado: atualiza saídas
  ini_sai(norte_ini, norte_agora)
  ini_sai(sul_ini, sul_agora)
  ini_sai(leste_ini, leste_agora)
  ini_sai(oeste_ini, oeste_agora)
  ini_sai(cima_ini, cima_agora)
  ini_sai(baixo_ini, baixo_agora)

func ini_sai # Usado em ini; atualiza saídas
# arg0=valor inicial da saída
# arg1=valor atual da saída
  arg1 = arg0
  se arg1 >= 4
    arg1 = 3
  senão arg1 >= 1
    arg1 -= 1

[entra]
# Entrando no jogo
comum serv servidor # Para receber conexões
socket conec # Usuário conectado
inttempo fechar # Para encerrar conexão após um tempo
inttempo espera # Tempo para pedir o apelido
inttempo sair   # Para sair se esperou demais

func iniclasse
# Esperar conexões
  se ![arg0]:servidor.abrir("", _config:porta)
    terminar

func servidor_socket # Conectado: cria objeto
  criar("entra", arg0)

func ini # Objeto foi criado
  conec = arg0
  conec.proto = 2
  conec.aflooder = 1
  conec.msg("+OK Para Telnet pressione ENTER\n")
  fechar = 100 # 10 segundos depois desconecta

func fechar_exec
  conec.msg("-Tempo esgotado\n")
  apagar(este)

func espera_exec
  conec.msg("Digite o nome do seu personagem\n")
  sair=600 # Para esperar 60 segundos

func sair_exec
  conec.msg("Tempo esgotado\n")
  apagar(este)

func conec_fechou
  apagar(este)

func conec_msg # Recebeu mensagem do usuário
# arg0=mensagem
  txt30 nome
  indiceitem ind
  se espera # Se deve aguardar, ignora o que recebeu
    ret
  senão fechar # Se está no primeiro passo
    fechar=0
    espera=10
    se arg0 != "" # Papovox sempre fornece um nome
      conec.msg("+OK Entrando via Papovox\n")
      conec.proto = 4
    senão # Nome vazio: é Telnet
      conec.msg("-OK Entrando via telnet; para sair tecle quit\n")
      conec.cores = 2
    fimse
  senão arg0 = "quit" # Sai do MUD
    apagar(este)
  senão arg0 = "" # Se não digitou um apelido
    espera = 1
  senão ind.obj("usr_" + nome) # Se apelido está sendo usado
    conec.msg("Apelido já está sendo usado; digite outro\n")
  senão # Cria objeto do jogador
    apagar(este)
    criar("jog", conec, nome)
  fimse

[jog]
herda comum_perso
# Personagem do jogador
socket conec
indiceobj i_apelido
const alias = txt(i_apelido, 4)

func ini # Objeto criado
# arg0=conec
# arg1=apelido
  conec = arg0
  i_apelido = "usr_" + arg1
  comum_perso:ini
  mudasala(_config:salaini)
  msg("NOTA: Para encerrar o MUD tecle ENCERRAR\n")
  descrsala

func conec_fechou # Conexão encerrada
  apagar(este)

const msg = conec.msg(arg0) # Envia mensagem para o usuário

func conec_msg # Recebeu mensagem do usuário
  escr(arg0)
