classe cmd_quem
herda comando_comum, comando_ajuda
const txtajuda = "\b\c3Quem\b\n\
Sintaxe: QUEM\n\
Mostra quem está conectado no MUD e jogando."
const posic = 0

func escr
  textotxt t0
  txt100 t1
  int16 total.2
# Lista de personagens dos jogadores
  indiceitem item
  epara item.ini("pn "), txt1(item.txt) == "pn", item.depois
    continuar item.obj.jogconfig.17 && !arg0.jogconfig.23
    refvar obj = item.obj
    total.[obj.jogconfig.14] += 1
    t1 = (obj.jogconfig.14 ? "A" : "B") + txt(999000 - obj.pnivel) + " N"
    t1 += txt(txt(obj.pnivel) + "      ", 0, 6) + obj.nome
    obj.sock == nulo && (t1 += "  [Desconectado]")
    se arg0.jogconfig.23
      obj.jogconfig.23 && (t1 += "  [Admin]")
      obj.jogconfig.17 && (t1 += "  [InvQuem]")
      obj.sock.imonit && (t1 += "  [Monit " + obj.sock.imonit.objlista.perso.nome + "]")
    senao
      obj.jogconfig.23 && config:mostraadm && (t1 += "  [Admin]")
    fimse
    arg0.jogconfig.20 && (t1 += "  " + txt(obj.dono, 2))
    t0.addfim(t1)
  efim
# Lista de jogadores sem personagem
  epara item.ini("un "), txt1(item.txt) == "un", item.depois
    refvar obj = item.obj
    continuar obj.perso.jog # Continuar se personagem é jogador
    continuar obj.jogconfig.17 && !arg0.jogconfig.23
    total.[obj.jogconfig.14] += 1
    t1 = (obj.jogconfig.14 ? "A999001 N?     " : "B999001 N?     ") + item.obj.nome
    obj.jogconfig.17 && (t1 += "  [InvQuem]")
    t0.addfim(t1)
  efim
# Mostra a quantidade de personagens online
  total.0 && t0.addfim("B 00000 \b\c6Jogadores online: " + total.0 + "\b")
  total.1 && t0.addfim("A 00000 \b\c6Imortais online: " + total.1 + "\b")
# Ordena e mostra para o jogador
  t0.ordena
  textopos pos
  txt1 ch
  epara pos = t0.ini, pos, pos.depois
    pos.mudar("", 0, 8)
  efim
  arg0.msg2(t0.remove(1000))


classe cmd_hab
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = "\b\c3Hab\b\n\
Sintaxe: HAB  [nome da habilidade]\n\
Sem argumentos mostra as habilidades do seu personagem.\n\
Seguido de um nome, mostra informações sobre a habilidade.\n\
Geralmente pode-se usar uma habilidade teclando-se o nome dela."

func escr
  ref r
  r = arg0.persoesc
  se arg1
    ref obj
    prog p
    epara p.iniclasse("cmd_" + txt1(arg1)), p.lin, p.depois
      obj = $[p.texto].atkajuda(r)
      ret obj, arg0.msg2(obj.txtajuda(r))
    efim
    ret arg0.msg("Nenhuma informação sobre a habilidade: " + arg1)
  fimse
  txt100 n
  r.persoesc != arg0 && (n = " de " + r.nome)
  textotxt h
  txt100 t1
  txt100 t2
  epara t1 = r.var.ini("h_"), txt(t1, 0, 2) == "h_", t1 = r.var.depois(t1)
    t2 = txte(txt(t1, 2)) # , t2 += txtesp(30 - inttotal(t2))
    t2 += " / " + txtsublin(misc:aulas, r.var.[t1], 1)
    h.addfim(t2)
  efim
  ret !h.linhas, arg0.msg("Nenhuma habilidade.")
  h.addini("\b\c6Habilidades" + n + ":\b")
  arg0.msg2(h.remove(1000))


classe cmd_magia
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = "\b\c3Magia\b\n\
Sintaxe: MAGIA  [nome da magia]\n\
Sem argumentos mostra as magias do seu personagem.\n\
Seguido de um nome, mostra informações sobre a magia.\n\
Geralmente pode-se usar uma magia teclando-se lançar e o nome dela."

func escr
  ref r
  r = arg0.persoesc
  se arg1
    ref obj
    prog p
    epara p.iniclasse("magia_" + txt1(arg1)), p.lin, p.depois
      obj = $[p.texto].atkajuda(r)
      ret obj, arg0.msg2(obj.txtajuda(r))
    efim
    ret arg0.msg("Nenhuma informação sobre a magia: " + arg1)
  fimse
  txt100 n
  r.persoesc != arg0 && (n = " de " + r.nome)
  textotxt h
  txt100 t1
  txt100 t2
  epara t1 = r.var.ini("m_"), txt(t1, 0, 2) == "m_", t1 = r.var.depois(t1)
    t2 = txte(txt(t1, 2)) # , t2 += txtesp(30 - inttotal(t2))
    t2 += " / " + txtsublin(misc:aulas, r.var.[t1], 1)
    h.addfim(t2)
  efim
  ret !h.linhas, arg0.msg("Nenhuma magia.")
  h.addini("\b\c6Magias" + n + ":\b")
  arg0.msg2(h.remove(1000))


classe cmd_idioma
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = "\b\c3Idioma\b\n\
Sintaxe: IDIOMA\n\
Mostra os idiomas que o seu personagem conhece."

func escr
  txt100 n
  ref r
  r = arg0.persoesc
  r != arg0 && (n = " de " + r.nome)
  textotxt h
  txt100 t1
  txt100 t2
  epara t1 = r.var.ini("i_"), txt(t1, 0, 2) == "i_", t1 = r.var.depois(t1)
    t2 = txte(txt(t1, 2)) # , t2 += txtesp(30 - inttotal(t2))
    t2 += " / " + txtsublin(misc:aulas, r.var.[t1], 1)
    h.addfim(t2)
  efim
  ret !h.linhas, arg0.msg("Nenhum idioma.")
  h.addini("\b\c6Idiomas" + n + ":\b")
  arg0.msg(h.remove(1000))


classe cmd_outros
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = "\b\c3Outros\b\n\
Sintaxe: OUTROS\n\
Mostra itens e habilidades extras. Geralmente são permanentes (não pode-se\n\
pegar, soltar, dar e vestir)."

func escr
  txt100 n
  r = arg0.persoesc
  r != arg0 && (n = " de " + r.nome)
  textotxt h
  txt100 t1
  txt100 t2
  epara t1 = r.var.ini("o_"), txt(t1, 0, 2) == "o_", t1 = r.var.depois(t1)
    t2 = txte(txt(t1, 2)) # , t2 += txtesp(30 - inttotal(t2))
    se txt(int(r.var.[t1])) == t.var.[t1]
      t2 += " / " + txtsublin(misc:aulas, r.var.[t1], 1)
    senao
      t2 += " / " + r.var.[t1]
    fimse
    h.addfim(t2)
  efim
  ret !h.linhas, arg0.msg("Nenhum item.")
  h.addini("\b\c6Outros" + n + ":\b")
  arg0.msg(h.remove(1000))


classe cmd_inv
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = config:animal1 ? txttroca(ta2, "$", config:animal1) : ta1
const ta1 = "\b\c3Inv\b\n\
Sintaxe: INV\n\
Mostra os itens que o seu personagem está carregando."
const ta2 = "\b\c3Inv\b\n\
Sintaxe: INV\n\
         INV <número>\n\
Mostra os itens que o seu personagem está carregando.\n\
Seguido de um número de 0 a $ mostra os itens do animal correspondente."

func escr
  textotxt t
  listaitem item
  refvar r = misc:objperso(arg0, arg1)
  ret !r, nulo
  epara item = r.dentro1.fim, item, item.antes
    continuar !item.obj.visivel(arg0)
    refvar nn = item.obj.atribs.linhas ? " (" + txttroca(item.obj.atribs.ini.textolin(100), \
"\n", " ") + ") " : " "
    t.addfim(txt(item.obj.objtot) + nn + txtcopiamai(item.obj.descnome, "A"))
  efim
  t.juntalin("(", "x)")
  refvar nome = r == arg0 ? "Você" : txtcopiamai(r.nome, "A")
  se t
    arg0.msg2("\b\c6" + nome + " carrega:\b\n" + t.remove(1000))
  senao
    arg0.msg("\b" + nome + " não está carregando nada.")
  fimse


classe cmd_equip
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = config:animal1 ? txttroca(ta2, "$", config:animal1) : ta1
const ta1 = "\b\c3Equip\b\n\
Sintaxe: EQUIP\n\
         EQUIP TUDO\n\
Mostra os itens que o seu personagem está vestindo ou usando.\n\
Digitando EQUIP TUDO, são mostradas também as posições aonde o seu\n\
personagem pode vestir algo."
const ta2 = "\b\c3Equip\b\n\
Sintaxe: EQUIP\n\
         EQUIP TUDO\n\
         EQUIP <número>\n\
Mostra os itens que o seu personagem está vestindo ou usando.\n\
Digitando EQUIP TUDO, são mostradas também as posições aonde o seu\n\
personagem pode vestir algo.\n\
Seguido de um número de 0 a $ mostra os itens do animal correspondente."

func escr
  textotxt t
  listaitem item
  ref pos.55
  txt50 nomepos
# Obtém o personagem
  refvar r = arg1 == "tudo" ? arg0 : misc:objperso(arg0, arg1)
  ret !r, nulo
# Preenche pos
  epara item = r.dentro2.fim, item, item.antes
    continuar !item.obj.vestpos || !item.obj.visivel(arg0)
    pos.[item.obj.vestpos] = item.obj
  efim
# Mostra as posições
  txt512 lin
  refvar lugar = arg1 == "tudo" ? r.equippos : 0
  epara lin = config:equip_ordem, lin, lin = txt2(lin)
    refvar obj = pos.[1 + txt1(lin)]
    se obj.vestpos && obj.visivel(arg0)
      nomepos = "\c2(" + misc:equip(obj.vestpos) + ")\b "
      nomepos += txtesp(23 - inttotal(nomepos))
      obj.atribs.linhas && (nomepos += "(" + txttroca(obj.atribs.ini.textolin(100), "\n", " ") + \
") ")
      t.addfim(nomepos + txtcopiamai(obj.descnome, "A"))
    senao lugar & intbit(1 + txt1(lin))
      nomepos = "\c2(" + misc:equip(1 + txt1(lin)) + ")\b "
      se nomepos != "\c2(desconhecido)\b "
        t.addfim(nomepos + txtesp(23 - inttotal(nomepos)) + "nada")
      fimse
    fimse
  efim
# Envia mensagem
  refvar nome = r == arg0 ? "Você" : txtcopiamai(r.nome, "A")
  se t.linhas
    refvar eq = r.msexo ? " está equipado" : " está equipada"
    arg0.msg2("\b\c6" + nome + eq + " com:\b\n" + t.remove(1000))
  senao
    arg0.msg("\b" + nome + " não está vestindo/usando nada.")
  fimse


classe cmd_efeito
herda comando_comum, comando_ajuda
const posic = 0
const txtajuda = "\b\c3Efeito\b\n\
Sintaxe: EFEITO  [nome do efeito]\n\
Sem argumentos mostra os efeitos que estão afetando o seu personagem.\n\
Seguido de um nome, mostra informações sobre o efeito."

func escr
  listaitem i
  ref r
  r = arg0.persoesc
  se arg1
    ref obj
    nomeobj n
    n.ini(arg1, 1)
    epara i = r.dentro2.ini, i, i.depois
      continuar !i.obj.e_nome || !n.nome(i.obj.e_nome)
      txt100 lin
      lin = "Efeito " + i.obj.e_nome + ", duração "
      lin += i.obj.t_duracao ? txt(int(i.obj.t_duracao / 10)) : " indeterminada"
      ret arg0.msg2(i.obj.e_desc ? lin + "\n" + i.obj.e_desc : lin)
    efim
    ret arg0.msg("Nenhuma informação sobre o efeito: " + arg1)
  fimse
  txt100 n
  r.persoesc != arg0 && (n = " de " + r.nome)
  textotxt h
  epara i = r.dentro2.ini, i, i.depois
    continuar !i.obj.e_nome
    h.addfim("1 " + i.obj.e_nome + (i.obj.t_duracao ? " (" + int(i.obj.t_duracao / 10) + \
")"))
  efim
  refvar nome = r == arg0 ? "você" : r.nome
  ret !h.linhas, arg0.msg("\bNenhum efeito afetando " + nome + ".")
  h.ordenalin("", "x")
  arg0.msg2("\b\c6Efeitos afetando " + nome + ":\b\n" + h.remove(1000))


classe cmd_vida
herda comando_comum, comando_ajuda
const posic = 0
const txtajuda = config:animal1 ? txttroca(ta2, "$", config:animal1) : ta1
const ta1 = "\b\c3Vida\b\n\
Sintaxe: VIDA\n\
Mostra os pontos de vida, mana, vigor, nível e experiência do seu personagem."
const ta2 = "\b\c3Vida\b\n\
Sintaxe: VIDA\n\
         VIDA <número>\n\
Mostra os pontos de vida, mana, vigor, nível e experiência do seu personagem.\n\
Seguido de um número de 0 a $ mostra as informações do animal correspondente."

func escr
  refvar r = misc:objperso(arg0, arg1)
  ret !r, nulo
  se r == arg0
    arg0.msg(vidalin(arg0))
  senao
    arg0.msg(txtcopiamai(r.nome, "A") + ": " + vidalin(r))

func vidalin
  txt100 lin
  lin = "Vida " + arg0.pvida + "/" + arg0.pvidamax
  lin += "  Mana " + arg0.pmana + "/" + arg0.pmanamax
  lin += "  Vigor " + arg0.pmove + "/" + arg0.pmovemax
  lin += "  Nível " + arg0.pnivel
  lin += "  Experiência " + arg0.expatual + "/" + arg0.expmax
  ret lin


classe cmd_moedas
herda comando_comum, comando_ajuda
const txtajuda = "\b\c3Moedas\b\n\
Sintaxe: MOEDAS\n\
Mostra quantas moedas você está carregando."
const posic = 0

func escr
  txt100 lin
  txt100 lin2
  se !arg0.var.z_moedas_
    lin = "Nenhuma moeda"
  senao arg0.var.z_moedas_ == 1
    lin = "Uma moeda"
  senao arg0.var.z_moedas_ == 2
    lin = "Duas moedas"
  senao
    lin = txtnum(arg0.var.z_moedas_, ".") + " moedas"
  fimse
  se arg0.persoesc == arg0 || !arg0.persoesc.var.z_moedas_
  senao arg0.persoesc.var.z_moedas_ == 1
    lin2 = " uma moeda"
  senao arg0.var.z_moedas_ == 2
    lin2 = " duas moeda"
  senao
    lin2 = " " + txtnum(arg0.var.z_moedas_, ".") + " moedas"
  fimse
  lin2 && (lin += ", " + txtcopiamai(arg0.persoesc.nome, "A") + lin2)
  arg0.msg(lin + ".")


classe cmd_hora
herda comando_comum, comando_ajuda
const txtajuda = "\b\c3Hora\b\n\
Sintaxe: HORA\n\
Mostra a hora atual, no calendário do MUD e no servidor."
const posic = 0

func escr
  txt100 t1
  datahora d
  d.agora
  t1 = "  (" + semanamud(misc:dia) + ")"
  arg0.msg("Hora atual MUD:      " + misc:hora + ":" + misc:min + t1)
  arg0.msg("Hora atual servidor: " + d.hora + ":" + d.min)
  t1 = "Tempo total de jogo: "
  arg0.tempojogo2 == 1 && (t1 += "1 dia ")
  arg0.tempojogo2 > 1 && (t1 += txt(arg0.tempojogo2) + " dias ")
  t1 += intdiv((864000 - arg0.tempojogo1) / 36000)
  t1 += ":"
  t1 += intdiv((864000 - arg0.tempojogo1) / 600) % 60
  arg0.msg(t1 + " horas")
  se arg0.sock
    int32 x
    x = arg0.sock.tempojogo
    se x >= 24 * 36000
      t1 = "Mais de 24 horas"
    senao
      t1 = intdiv(x / 36000)
      t1 += ":" + intdiv(x / 600) % 60 + " horas"
    fimse
    arg0.msg("Tempo dessa sessão:  " + t1)

func semanamud
  casovar arg0
  casose "0"
    ret "Domingo"
  casose "1"
    ret "Segunda-feira"
  casose "2"
    ret "Terça-feira"
  casose "3"
    ret "Quarta-feira"
  casose "4"
    ret "Quinta-feira"
  casose "5"
    ret "Sexta-feira"
  casose "6"
    ret "Sábado"
  casofim


classe cmd_onde
herda comando_comum, comando_ajuda
const txtajuda = "\b\c3Onde\b\n\
Sintaxe: ONDE\n\
Mostra a área em que você está e os jogadores que estiverem por perto."
const posic = 0

func escr
  textotxt t0
  txt100 area
  ref r
  epara r = arg0.dono, r.dono, r = r.dono
  efim
  area = r.s_area
# Checa se pode ver a sala
  se !r.visivel(arg0)
    se 1 & arg0.bitver
      arg0.msg("Está escuro...")
    senao
      arg0.msg("Você está ceg" + (arg0.msexo ? "o" : "a") + ".")
    fimse
    ret
  fimse
# Dados da área
  t0.addfim("\b\c6Área " + $a_[area].a_nome + ":\b")
# Lista de personagens dos jogadores
  indiceitem item
  epara item.ini("pn "), txt1(item.txt) == "pn", item.depois
# continuar item.obj.jogconfig.18 && !arg0.jogconfig.18
    continuar !item.obj.visivel(arg0)
    epara r = item.obj.dono, r.dono, r = r.dono
    efim
    continuar area != r.s_area
    misc:obj = arg0
    t0.addfim(item.obj.nome + "    " + item.obj.dono.s_titulo)
  efim
  t0.txtmostra
  arg0.msg2(t0.remove(1000))


classe cmd_saidas
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = "\b\c3Saídas\b\n\
Sintaxe: SAÍDAS\n\
Mostra as saídas óbvias do lugar onde você está."

func escr
  se !arg0.dono.visivel(arg0)
    arg0.msg("Está escuro...")
    ret
  fimse
  textotxt t
  txt80 dir
  ref r
  misc:obj = arg0
  epara dir = misc:listadir, dir, dir = txt2(dir)
    refvar nome = txt1(dir)
    continuar !(r = arg0.dono.dir_[nome]) || arg0.dono.inv_[nome]
    se int(arg0.dono.porta_[nome]) >= 2
      t.addfim(txt(txtmai(nome) + "       ", 0, 8) + "Está fechado")
    senao r.visivel(arg0)
      t.addfim(txt(txtmai(nome) + "       ", 0, 8) + r.s_titulo)
    senao
      t.addfim(txt(txtmai(nome) + "       ", 0, 8) + "Está escuro")
    fimse
  efim
  se t.linhas
    arg0.msg2("\b\c6Saídas óbvias:\b\n" + t.remove(1000))
  senao
    arg0.msg("Não há nenhuma saída óbvia.")
  fimse


classe cmd_estat
herda comando_comum, comando_ajuda
const posic = 5
const txtajuda = config:animal1 ? txttroca(ta2, "$", config:animal1) : ta1
const ta1 = "\b\c3Estat\b\n\
Sintaxe: ESTAT\n\
Mostra as estatísticas e diversas informações sobre o seu personagem."
const ta2 = "\b\c3Estat\b\n\
Sintaxe: ESTAT\n\
         ESTAT <número>\n\
Mostra as estatísticas e diversas informações sobre o seu personagem ou\n\
o animal você escolheu.\n\
Seguido de um número de 0 a $ mostra as estatísticas do animal\n\
correspondente."

func escr
  textotxt t
  textotxt h
  txt100 t1
# Obtém o personagem
  refvar r = misc:objperso(arg0, arg1)
  ret !r, nulo
  t.addfim("\b\c6Estatísticas de \"" + r.nome + "\"\b")
# Vida, mana e vigor
  t1 = "Vida " + r.pvida + "/" + r.pvidamax
  t1 += "  Mana " + r.pmana + "/" + r.pmanamax
  t1 += "  Vigor " + r.pmove + "/" + r.pmovemax
  t.addfim(t1)
# Fome, sede e embriaguês, somente para administradores
  se arg0.jogconfig.23 && r.jog
    t1 = "Sede " + r.p_sede + " fome " + r.p_fome + " embriaguês " + r.p_bebida
    t1 += " digestão " + r.p_diges + "; máximo é " + $miscfome.cheio
    t.addfim(t1)
  fimse
# Tipo
  r.tipo1 && h.addfim(txtcopiamai(txte(r.tipo1), "A"))
  r.tipo2 && h.addfim(txtcopiamai(txte(r.tipo2), "A"))
  r.tipo3 && h.addfim(txtcopiamai(txte(r.tipo3), "A"))
  h.linhas && t.addfim("Tipo/raça/classe: " + txttroca(h.remove(10), "\n", ", "))
  t.addfim("Sexo:             " + (r.psexo ? "Masculino" : "Feminino"))
# Quem pegou
  se r != r.contr && r.var.z_dono
    t1 = "Pego por:         " + txte(r.var.z_dono)
    t.addfim(t1 + " (no nível " + int(r.var.z_pnivel) + ")")
  fimse
# Moedas e pontos de aulas
  r.var.z_moedas_ && t.addfim("Moedas:           " + txtnum(r.var.z_moedas_, "."))
  r.var.z_aulas_ && t.addfim("Aulas:            " + txtnum(r.var.z_aulas_, "."))
# Nível
  t.addfim("Nível:            " + r.pnivel)
  t.addfim("Experiência:      " + r.expatual + "/" + r.expmax)
# Batalha
  t1 = "Ataque/defesa:    " + int(r.atknorm * r.atktipo.0) + "/"
  se !r.deftipo.0
    t.addfim(t1 + "Imune")
  senao
    t.addfim(t1 + int(r.defnorm / r.deftipo.0))
  fimse
# t.addfim("Ataque/defesa:    " + r.atknorm + "/" + r.defnorm)
  t1 = "Atq/def especial: " + int(r.atkesp * r.atktipo.1) + "/"
  se !r.deftipo.1
    t.addfim(t1 + "Imune")
  senao
    t.addfim(t1 + int(r.defesp / r.deftipo.1))
  fimse
# t.addfim("Atq/def especial: " + r.atkesp + "/" + r.defesp)
  t.addfim("Precisão/evasão:  " + r.precisao + "/" + r.evasao)
  t.addfim("Velocidade:       " + r.pveloc)
# Mortes
  se r == r.contr
    t.addfim("Mortes:           " + r.mortes)
  senao
    t.addfim("Desmaios:         " + r.mortes)
  fimse
# Carregando
  t.addfim("Peso total:       " + txtnum((r.pesoden + r.pesoobj) / 1000, "0") + " Kg")
  t1 = "Carregando:       " + r.dentro1.total + "/" + r.objmax
  t1 += r.objmax == 1 ? " item" : " itens"
  t1 += ", " + txtnum((r.pesoden - r.pesoveste) / 1000, "0")
  t1 += "/" + txtnum(r.pesomax / 1000, "0") + " Kg"
  t.addfim(t1)
# Teeleentra e telesai
  r.var.z_teleentra && t.addfim("Teleentra:        " + r.var.z_teleentra)
  r.var.z_telesai && t.addfim("Telesai:          " + r.var.z_telesai)
# Mensagem para o usuário
  arg0.msg2(t.remove(1000))
