classe comando_ataque
herda comando_comum
const obj = arg0.persobat.var.h_[nome] ? este
const objajuda = obj(arg0) # Objeto que contém a ajuda desse comando
uint8 result # Resultado do ataque, usado em execgolpe e msgefeito
# 0=cancelar ataque (não gera nenhuma mensagem), 1=errou o alvo
# 2=não teve efeito, 3=pouco efetivo, 4=normal, 5=muito efetivo

func txtajuda
  txt100 lin
  lin = nome + ": ataque " + misc:nometipo(tipo) + " força " + ataque + " velocidade " + \
vel
  ret extra ? lin + "\n" + extra : lin

func escr # Atacar alguém
  ref r
  se (r = arg0.persoalvo) == nulo
    arg0.msg("Alvo não está por perto")
  senao arg0.persobat.ataqueini
    ataca(arg0, r)

func ataca # Chamado quando personagem (arg0) quer atacar alguém (arg1)
  ataca1(arg0, arg1)

func ataca1 # arg0 ataca arg1
  ref r
  r = arg1.persobat
  velgolpe(arg0.persobat, r) # Tempo do golpe
  execgolpe(arg0.persobat, r, ataque) # Ataca
  result && arg0.dono.msg(texto(arg0, arg1) + msgefeito(r))
  r.energia == 0 && r.morreu
  arg0.persobat.energia == 0 && arg0.persobat.morreu

func ataca2 # arg0 ataca todos dividindo a força do ataque
# arg1 é o primeiro alvo
  real atk
  listaobj l
# Obtém ataque e lista de personagens
  l.addfim(arg0.dono.dentro1)
  l.remove(arg0, arg1)
  l.addini(arg1)
  atk = ataque / l.total
# Ataca
  ref r
  enquanto l.total > 1
    r = l.fim.obj.persobat
    velgolpe(arg0.persobat, r) # Tempo do golpe
    execgolpe(arg0.persobat, r, atk) # Ataca
    result && l.fim.obj.msg(texto(arg0, l.fim.obj) + msgefeito(r))
    r.energia == 0 && r.morreu
    l.fim.remove
  efim
# Ataca o alvo
  r = l.fim.obj.persobat
  velgolpe(arg0, r) # Tempo do golpe
  execgolpe(arg0, r, atk) # Ataca
  result && arg0.dono.msg(texto(arg0, l.fim.obj) + msgefeito(r))
  r.energia == 0 && r.morreu
  arg0.persobat.energia == 0 && arg0.persobat.morreu

func ataca3 # arg0 ataca todos sem dividir a força do ataque
# arg1 é o primeiro alvo
  listaobj l
# Obtém ataque e lista de personagens
  l.addfim(arg0.dono.dentro1)
  l.remove(arg0, arg1)
  l.addini(arg1)
# Ataca
  ref r
  enquanto l.total > 1
    r = l.fim.obj.persobat
    velgolpe(arg0.persobat, r) # Tempo do golpe
    execgolpe(arg0.persobat, r, ataque) # Ataca
    result && l.fim.obj.msg(texto(arg0, l.fim.obj) + msgefeito(r))
    l.fim.obj.energia == 0 && l.fim.obj.morreu
    r.energia == 0 && r.morreu
    l.fim.remove
  efim
# Ataca o alvo
  r = l.fim.obj.persobat
  velgolpe(arg0, r) # Tempo do golpe
  execgolpe(arg0, r, atk) # Ataca
  result && arg0.dono.msg(texto(arg0, l.fim.obj) + msgefeito(r))
  r.energia == 0 && r.morreu
  arg0.persobat.energia == 0 && arg0.persobat.morreu

func msgefeito # Gera mensagem de como o golpe acertou
# arg0 = personagem alvo, se houver
# result = como acertou
  txt100 msg1
  casovar result
  casose "1"
    ret ", errou"
  casose "2"
    ret ", nada acontece"
  casose "3"
    arg0 && (msg1 = " " + arg0.energia + " de " + arg0.enermax)
    ret ", pouco efeito" + msg1
  casose "4"
    arg0 && (msg1 = ", " + arg0.energia + " de " + arg0.enermax)
    ret msg1
  casose "5"
    arg0 && (msg1 = " " + arg0.energia + " de " + arg0.enermax)
    ret ", muito efeito" + msg1
  casofim
  ret ""

func velgolpe # Acerta o tempo de espera conforme o golpe
# arg0 = personagem que está atacando
# arg1 = personagem alvo
# vel = velocidade do golpe
  uint8 tempo
# Se velocidades dos personagens iguais e velocidade do golpe=100: tempo=30
  tempo = 10 + arg1.veloc * 2000 / (arg0.veloc * vel + 1)
  tempo < 5 && (tempo = 5)
  tempo > 60 && (tempo = 60)
  arg0.t_aguarde < tempo && (arg0.t_aguarde = tempo)

func execgolpe # Realiza um ataque e coloca resultado em result
# arg0 = personagem que está atacando
# arg1 = personagem alvo
# arg2 = força do ataque, 0=não calcular danos
# tipo = tipo de ataque
  real dano
# Checa se pode realmente atacar
  result = 1
  se arg0.dono.cmd_ataque(arg0, arg1, este)
    ret
  senao arg0.cmd_ataque(arg0, arg1, este)
    ret
  senao arg1.cmd_ataquerec(arg0, arg1, este)
    ret
  fimse
# Indica que atacou, recebe experiência se vencer
  arg0.atkrec.remove
  arg0.atkrec = arg1.atkenv.addfim(arg0)
  se arg1.dono.perso
    arg1.dono.alvo2 = arg0.nome # Para comando alvo sem argumento
    arg0.podepegar && exgolpe_i(arg1.dono.var.a_[arg0.nome]) # Coloca na agenda
  senao
    arg1.alvo2 = arg0.nome # Para comando alvo sem argumento
    arg0.podepegar && exgolpe_i(arg1.var.a_[arg0.nome]) # Coloca na agenda
  fimse
# Checa se acertou o alvo
  dano = rand(1, 20)
  se dano == 20 # Acertou automaticamente
  senao dano == 1 # Errou o alvo automaticamente
    ret
  senao # Checa se acertou
# dano += precisao * arg0.precisao / 100 - arg1.evasao
# !arg0.visivel(arg1) && (dano -= 5) # Invisível é mais difícil de acertar
# se dano <= 0
# ret 0
# fimse
  fimse
# Calcula dano
  dano = arg1.deftipo.[tipo]
  se dano >= 80 # Defesa muito alta
    result = 2
    ret
  senao arg2 == 0 # Nenhum valor de ataque, apenas indica que atacou
    result = 4
    ret
  fimse
  result = dano <= -2 ? 5 : dano >= 2 ? 3 : 4
  dano = arg1.defesa * misc:defbonus.[128 + dano]
  dano = arg0.ataque / dano + arg0.nivel - arg1.nivel
  dano = intpos(dano) * arg2 / 65
# Atualiza pontos de vida conforme os danos
  dano < 1 && (dano = 1) # Pelo menos 1 de dano
  arg1.energia -= dano

func exgolpe_i # Chamado internamente por execgolpe para adicionar na agenda
  !arg0 && (arg0 = 1)


classe cmd_socar
herda comando_ataque
const nome = "Socar"
const texto = arg0.nomebat + " soca " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 80
const vel = 150
const extra = "Golpe físico de força"


classe cmd_chutar
herda comando_ataque
const nome = "Socar"
const texto = arg0.nomebat + " chuta " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 100
const vel = 100
const extra = "Golpe físico de força"


classe cmd_multissoco
herda comando_ataque
const nome = "Multissoco"
const texto = arg0.nomebat + " dá um multissoco em todos, " + arg1.nomebat
const tipo = 4 # Força
const ataque = 100
const vel = 100
const extra = "Golpe físico de força que acerta todos"
const ataca = ataca2(arg0, arg1)


classe cmd_bolha
herda comando_ataque
const nome = "Bolha"
const texto = arg0.nomebat + " lança bolhas em " + arg1.nomebat
const tipo = 0 # Água
const ataque = 90
const vel = 100
const extra = "Lança bolhas de água"


classe cmd_surf
herda comando_ataque
const nome = "Surf"
const texto = arg0.nomebat + " cria uma onda gigante, " + arg1.nomebat
const tipo = 0 # Água
const ataque = 90
const vel = 100
const extra = "Cria uma onda, ideal para surfistas"
const ataca = ataca2(arg0, arg1)


classe cmd_bico
herda comando_ataque
const nome = "Bico"
const texto = arg0.nomebat + " pica " + arg1.nomebat + " com o bico"
const tipo = 1 # Ar
const ataque = 90
const vel = 100
const extra = "Cria uma onda, ideal para surfistas"


classe cmd_ventania
herda comando_ataque
const nome = "Ventania"
const texto = arg0.nomebat + " produz um vento contra " + arg1.nomebat
const tipo = 1 # Ar
const ataque = 90
const vel = 100
const extra = "Produz um vento forte"


classe cmd_choque
herda comando_ataque
const nome = "Choque"
const texto = arg0.nomebat + " eletrocuta " + arg1.nomebat
const tipo = 2 # Elétrico
const ataque = 90
const vel = 100
const extra = "Dá uma descarga elétrica"


classe cmd_onda_trovao
herda comando_ataque
const nome = "Onda trovão"
const tipo = 2 # Elétrico
const ataque = 0
const vel = 100
const extra = "Paralisa o adversário com eletricidade"

func ataca
  execgolpe(arg0, arg1, 0)
  se result == 0
  senao result < 3
    arg0.dono.msg(arg0.nomebat + " tenta paralisar " + arg1.nomebat + msgefeito(arg1))
  senao
    arg1.novoefeito("e_paralisado")
    arg0.dono.msg(arg0.nomebat + " paralisa " + arg1.nomebat + " com choque")


classe cmd_chama
herda comando_ataque
const nome = "Chama"
const texto = arg0.nomebat + " lança chama em " + arg1.nomebat
const tipo = 3 # Fogo
const ataque = 90
const vel = 100
const extra = "Pode causar queimaduras"

func ataca
  ataca1(arg0, arg1)
  ref r
  r = arg1.objefeito("e_congelado")
  se r
    apagar(r)
    arg1.dono.msg(arg1.nomebat + " descongelou")
  senao rand(100) < 20 && arg1.defvar.3 < 80
    arg1.novoefeito("e_queimando")
    arg1.dono.msg(arg1.nomebat + " está queimando")


classe cmd_brasa
herda comando_ataque
const nome = "Brasa"
const texto = arg0.nomebat + " lança brasa em " + arg1.nomebat
const tipo = 3 # Fogo
const ataque = 90
const vel = 100
const extra = "Pode causar queimaduras"

func ataca
  ataca1(arg0, arg1)
  ref r
  r = arg1.objefeito("e_congelado")
  se r
    apagar(r)
    arg1.dono.msg(arg1.nomebat + " descongelou")
  senao rand(100) < 20 && arg1.defvar.3 < 80
    arg1.novoefeito("e_queimando")
    arg1.dono.msg(arg1.nomebat + " está queimando")


classe cmd_lamber
herda comando_ataque
const nome = "Lamber"
const texto = arg0.nomebat + " lambe " + arg1.nomebat
const tipo = 5 # Gás
const ataque = 90
const vel = 100
const extra = "Lambe o adversário"


classe cmd_surpreender
herda comando_ataque
const nome = "Surpreender"
const texto = arg0.nomebat + " surpreende " + arg1.nomebat
const tipo = 5 # Gás
const ataque = 90
const vel = 100
const extra = "Dá uma descarga elétrica"


classe cmd_toxico
herda comando_ataque
const nome = "Tóxico"
const texto = arg0.nomebat + " lança gás tóxico em " + arg1.nomebat
const tipo = 5 # Gás
const ataque = 50
const vel = 100
const extra = "Tem grandes chances de envenenar"

func ataca
  se ataca1(arg0, arg1) && rand(100) < 70 && arg1.defvar.5 < 80
    arg1.novoefeito("e_envenenado")
    arg0.dono.msg(arg1.nomebat + " foi envenenado")


classe cmd_confusao
herda comando_ataque
const nome = "Confusão"
const texto = arg0.nomebat + " lança gás tóxico em " + arg1.nomebat
const tipo = 5 # Gás
const ataque = 90
const vel = 100
const extra = "Pode causar confusão"

func ataca
  se ataca1(arg0, arg1) && rand(100) < 20 && arg1.defvar.5 < 80
    arg1.novoefeito("e_confuso")
    arg0.dono.msg(arg1.nomebat + " ficou confuso")


classe cmd_neve
herda comando_ataque
const nome = "Neve"
const texto = arg0.nomebat + " atira bola de neve em " + arg1.nomebat
const tipo = 6 # Gelo
const ataque = 90
const vel = 100
const extra = "Pode congelar"

func ataca
  arg1.apagarefeito("e_queimando")
  se ataca1(arg0, arg1) && rand(100) < 70 && arg1.defvar.6 < 80
    arg1.novoefeito("e_congelado")
    arg0.dono.msg(arg1.nomebat + " congelou")


classe cmd_aurora
herda comando_ataque
const nome = "Aurora"
const texto = arg0.nomebat + " lança aurora em " + arg1.nomebat
const tipo = 6 # Gelo
const ataque = 90
const vel = 100
const extra = "Pode congelar"

func ataca
  arg1.apagarefeito("e_queimando")
  se ataca1(arg0, arg1) && rand(100) < 70 && arg1.defvar.6 < 80
    arg1.novoefeito("e_congelado")
    arg0.dono.msg(arg1.nomebat + " congelou")


classe cmd_pedrada
herda comando_ataque
const nome = "Pedrada"
const texto = arg0.nomebat + " joga pedra em " + arg1.nomebat
const tipo = 7 # Pedra
const ataque = 90
const vel = 100
const extra = "Joga pedra"


classe cmd_pedregulho
herda comando_ataque
const nome = "Pedregulho"
const texto = arg0.nomebat + " joga vários pedegulhos em " + arg1.nomebat
const tipo = 7 # Pedra
const ataque = 90
const vel = 100
const extra = "Joga pedregulho"


classe cmd_onda_solar
herda comando_ataque
const nome = "Onda solar"
const texto = arg0.nomebat + " lança onda solar em " + arg1.nomebat
const tipo = 8 # Planta
const ataque = 90
const vel = 100
const extra = "Onda solar"


classe cmd_dreno
herda comando_ataque
const nome = "Dreno"
const texto = arg0.nomebat + " lança onda solar em " + arg1.nomebat
const tipo = 8 # Planta
const ataque = 60
const vel = 120
const extra = "Rouba aproximadamente metade da energia que tira do adversário"

func ataca
  uint32 total
  total = arg1.energia
  ataca1(arg0, arg1)
  arg0.energia += pos(total - arg1.energia) / 2
  arg0.energia > arg0.enermax && (arg0.energia = arg0.enermax)


classe cmd_lama
herda comando_ataque
const nome = "Lama"
const texto = arg0.nomebat + " joga lama em " + arg1.nomebat
const tipo = 9 # Terra
const ataque = 60
const vel = 120
const extra = "Joga lama, imobilizando o adversário por 4 segundos"

func ataca
  se ataca1(arg0, arg1)
    arg1.esperar(40)


classe cmd_terremoto
herda comando_ataque
const nome = "Terremoto"
const texto = arg0.nomebat + " cria um terremoto, " + arg1.nomebat
const tipo = 9 # Terra
const ataque = 80
const vel = 100
const extra = "Faz o chão tremer"
const ataca = ataca3(arg0, arg1)


classe cmd_picar
herda comando_ataque
const nome = "Picar"
const texto = arg0.nomebat + " pica " + arg1.nomebat
const tipo = 10 # Verme
const ataque = 80
const vel = 100
const extra = "Pica o adversário"


classe cmd_morder
herda comando_ataque
const nome = "Morder"
const texto = arg0.nomebat + " morde " + arg1.nomebat
const tipo = 10 # Verme
const ataque = 90
const vel = 100
const extra = "Morde o adversário"


classe cmd_arranhar
herda comando_ataque
const nome = "Arranhar"
const texto = arg0.nomebat + " arranha " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 70
const vel = 100
const extra = "Arranha o adversário"


classe cmd_cantar
herda comando_ataque
const nome = "Cantar"
const texto = arg0.nomebat + " cria um terremoto, " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 0
const vel = 100
const extra = "Canta durante 13 segundos uma canção que dá sono"

func ataca
  execgolpe(arg0, arg1, 0)
  se result == 0
  senao result == 1
    arg0.dono.msg(arg0.nomebat + " tenta cantar uma canção, mas falhou")
  senao
    arg1.novoefeito("e_dormindo")
    arg0.esperar(130)
    arg0.dono.msg(arg0.nomebat + " canta uma canção e " + arg1.nomebat + " dorme profundamente")


classe cmd_velocidade
herda comando_ataque
const nome = "Velocidade"
const texto = arg0.nomebat + " ataca velozmente " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 50
const vel = 250
const extra = "Um golpe extremamente rápido"


classe cmd_cortar
herda comando_ataque
const nome = "Cortar"
const texto = arg0.nomebat + " corta " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 70
const vel = 250
const extra = "Corta o adversário"


classe cmd_explodir
herda comando_ataque
const nome = "Explodir"
const texto = arg0.nomebat + " explode e se fere, " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 350
const vel = 100
const extra = "Extremamente forte, mas quem aplica esse golpe desmaia"

func ataca
  ataca3(arg0, arg1)
  arg0.energia = 0


classe cmd_descansar
herda comando_ataque
const nome = "Descansar"
const texto = arg0.nomebat + " Descansa durante 14 segundos"
const tipo = 11 # Normal
const ataque = 350
const vel = 100
const extra = "Descansa e se recupera"

func ataca
  arg0.energia = arg0.enermax
  arg0.esperar(140)


classe cmd_arranhatox
herda comando_ataque
const nome = "Arranhatox"
const texto = arg0.nomebat + " arranha " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 70
const vel = 100
const extra = "Arranha o adversário com 40% de chances de intoxicar"

func ataca
  ataca1(arg0, arg1)
  se rand(100) < 40 && arg1.defvar.5 < 80
    arg1.novoefeito("e_envenenado")
    arg0.dono.msg(arg1.nomebat + " foi envenenado")


classe cmd_roer
herda comando_ataque
const nome = "Roer"
const texto = arg0.nomebat + " tenta roer " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 90
const vel = 100
const extra = "Rói o adversário com 40% de chances de intoxicar"

func ataca
  ataca1(arg0, arg1)
  se rand(100) < 40 && arg1.defvar.5 < 80
    arg1.novoefeito("e_envenenado")
    arg0.dono.msg(arg1.nomebat + " foi envenenado")


classe cmd_lancar
herda comando_ataque
const nome = "Lançar"
const texto = arg0.nomebat + " se lança contra " + arg1.nomebat
const tipo = 11 # Normal
const ataque = 85
const vel = 70
const extra = "Se lança contra o adversário"


classe cmd_atirar
herda comando_ataque
const nome = "Atirar"
const tipo = 12 # Perfurante
const ataque = 0
const vel = 20
const extra = "Causa um dano fixo quando acerta o alvo"

func ataca
  se !arg0.var.i_espingarda
    arg0.msg("Você não possui nenhuma espingarda")
    ret
  fimse
  execgolpe(arg0, arg1, 0)
  se result
    result >= 3 && (arg1.energia -= 35)
    arg0.dono.msg(arg0.nomebat + " atira em " + arg1.nomebat + msgefeito(arg1))
    arg1.atkenv.remove(arg0) # Não dá experiência
    arg1.energia == 0 && arg1.morreu # Checa se o alvo morreu


classe cmd_metronomo
herda comando_ataque

func ver
  se obj(arg0)
    arg0.msg("Metrônomo: Um golpe que se transforma em outro golpe")
    ret 1

func ataca
  textotxt t
  t.addfim(lista)
  t.remove(rand(t.total))
  cmd_[t.remove].ataca(arg0, arg1)

const lista = "bolha\n\
bico\n\
choque\n\
onda trovão\n\
chama\n\
chutar\n\
multissoco\n\
lamber\n\
tóxico\n\
confusão\n\
neve\n\
pedrada\n\
dreno\n\
onda solar\n\
lama\n\
terremoto\n\
morder\n\
arranhar\n\
cantar\n\
cortar\n\
explodir\n\
descansar"
