err = 1
classe teste1
# Esse arquivo tem como objetivo testar como o IntMUD
# salva e recupera diferentes tipos de variáveis
ref rx
ref ry
ref rz
func iniclasse
  criar(arg0)
func ini
  arqsav sav
# Cria variáveis
  rx = criar("x")
  ry = criar("y")
  rz = criar("z")
# Variáveis de rx
  rx.num = 1
  rx.varint = -15
  rx.varreal = 3.45
  rx.objetos.addfim(ry)
  rx.objetos.addfim(rx)
  ry.objitem = rx.objetos.addfim(rz)
  rx.objitem.0 = rx.objetos.addfim(ry)
  rx.objitem.1 = rx.objetos.addfim(ry)
  rx.objitem.2 = rx.objetos.addfim(rx)
  rx.tempo.agora
# Variáveis de ry
  ry.bits.0 = 1
  ry.bits.3 = 1
  ry.bits.4 = 1
  ry.refx = rx
  ry.refz = rz
# Variáveis de rz
  rz.texto = "\c1bom\b dia\n"
  rz.varz.0 = 12
  rz.varz.1 = 15
  rz.varz.2 = 18
  rz.refx = rx
  rz.teste1 = $teste1
# Texto de rz
  rz.t.addfim("bom dia\nboa tarde\nboa noite")
  rz.t.addfim("txtproc(<texto>, <texto procurado>)")
  rz.t.addfim("txtproc(<texto>, <texto procurado>, <caracter inicial>)")
  rz.t.addfim("    Procura <texto procurado> em <texto>")
  rz.t.addfim("    Retorna -1 se não encontrou ou o número do caracter se encontrou")
  rz.t.addfim("    <caracter inicial> = a partir de onde procurar no texto")
  rz.t.addfim("            0 = primeiro caracter, 1 = segundo, etc.")
  rz.t.addfim("    Para diferenciar letras minúsculas de maiúsculas e acentuadas de")
  rz.t.addfim("    não acentuadas, escrever txtprocdif ao invés de txtproc.")
  rz.p1 = rz.t.ini
  rz.p2 = rz.t.ini
  rz.p2.depois(4)
  rz.pos.0 = rz.p2
  rz.pos.1 = rz.t.ini
  rz.pos.1.depois(9)
  rz.pos.2 = rz.t.fim
  rz.pos.3 = rz.t.fim
  rz.pos.3.antes
  rz.pos.4 = rz.t.ini
# Anota resultado no log
  $log.anota(rx.objetos)
# Salva arquivo
  $log.msg("------ Salvando: " + sav.salvar("abc.sav", rx.objetos, 10, ""))
# Apaga objetos
  apagar(rx, ry, rz)

classe teste2
func iniclasse
  arqsav sav
  listaobj lista
  $log.msg("Lendo: " + sav.ler("abc.sav", lista))
  $log.anota(lista)
  lista.apagaobj

classe teste3
func iniclasse
  terminar

classe log
arqlog log
func iniclasse
  criar(arg0)
func ini
  log.abrir("abc.log")
  log.msg("------ Novo arquivo")
const msg = log.msg(arg0)
func anota # Anota objetos em arquivo
  listaitem l
  l = arg0.ini
  enquanto l
    l.obj.anota
    l.depois
  efim

classe x
int8 num
sav int8 varint
sav real varreal
sav listaobj objetos
sav listaitem objitem.3
sav datahora tempo
func anota
  listaitem l
  $log.msg("Objeto x")
  $log.msg("  varint = " + varint)
  $log.msg("  varreal = " + varreal)
  $log.msg("  data = " + tempo.dia + "/" + tempo.mes + "/" + tempo.ano)
  $log.msg("  hora = " + tempo.hora + ":" + tempo.min + "." + tempo.seg)
  $log.msg("  objetos =")
  l = objetos.ini
  enquanto l
    $log.msg("    " + l.obj)
    se l = objitem.0
      $log.msg("      >> objitem.0")
    senao l = objitem.1
      $log.msg("      >> objitem.1")
    senao l = objitem.2
      $log.msg("      >> objitem.2")
    fimse
    l.depois
  efim
  $log.msg("  objitem.0.obj = " + objitem.0.obj)
  $log.msg("  objitem.1.obj = " + objitem.1.obj)
  $log.msg("  objitem.2.obj = " + objitem.2.obj)

classe y
sav int1 bits.5
sav ref refx
sav ref refz
sav listaitem objitem
func anota
  $log.msg("Objeto y")
  $log.msg("  bits = " + bits.0 + " " + bits.1 + " " + bits.2 + \
       " " + bits.3 + " " + bits.4)
  $log.msg("  refx = " + refx)
  $log.msg("  refz = " + refz)
  $log.msg("  objitem.obj = " + objitem.obj)
  $log.msg("  objitem.objlista = " + objitem.objlista)

classe z
sav txt20 texto
sav int8 varz.3
sav ref refx
sav ref teste1
sav textotxt t
sav textopos p1
sav textopos p2
sav textopos pos.5
func anota
  textopos p
  int32 x
  $log.msg("Objeto z")
  $log.msg("  texto = " + texto)
  $log.msg("  varz = " + varz.0 + " " + varz.1 + " " + varz.2)
  $log.msg("  refx = " + refx)
  $log.msg("  teste1 = " + teste1)
  p = t.ini
  enquanto p.lin
    $log.msg("  t(" + p.linha + ") = " + p.texto + "\n")
    p.depois
  efim
  $log.msg("  p1 lin=" + p1.linha + " byte=" + p1.byte + "\n")
  $log.msg("  p1 txt=" + p1.texto + "\n")
  $log.msg("  p2 lin=" + p2.linha + " byte=" + p2.byte + "\n")
  $log.msg("  p2 txt=" + p2.texto + "\n")
  enquanto x<5
    $log.msg("  pos." + x + " lin=" + pos.[x].linha + " byte=" + pos.[x].byte + "\n")
    $log.msg("  pos." + x + " txt=" + pos.[x].texto + "\n")
    x += 1
  efim
