classe s_dentro001
herda sala

func segundo2
  arg0.dono = $s_dentro002
  arg0.pvida = 20


classe s_dentro002
herda sala
const area_luta = 0

func entrou
  listaitem i
  txt10 pocoes
  uint8 total
  pocoes = arg0.var.i_pocao
  arg0.var.limpar("i_")
  arg0.var.i_Poção = pocoes
  arg0.recuperar
  enquanto total < config:animal1
    arg0.animal.[total].recuperar, total += 1
  efim
  arg0.pvida = 55
  arg0.msg("Aguarde, restaurando pontos de vida " + arg0.pvida + " de " + arg0.pvidamax)
# func segundo1
# arg0.contleo += 1

func segundo2
  se arg0.jog # Se for jogador
    arg0.pvida += 5
    se arg0.pvida >= arg0.pvidamax
      arg0.pvida = arg0.pvidamax
      arg0.dono = $s_dentro003
    fimse
  senao
    jog.pvida = jog.pvidamax
  fimse

func cmd_escr
  se txt1(arg0) != "d"
    arg0.msg("Aguarde, pontos de vida em " + arg0.pvida + " de " + arg0.pvidamax)
    ret 1
  fimse


classe s_dentro003
herda sala
textovar ent
const area_luta = 0
const limpar = 300
const centro = 1
const dir_l = $s_dentro009
const dir_s = $s_lab014
const dir_n = $s_dentro004

func descr
  textotxt t
  t.addfim("Sala ampla")
  t.addfim("Você está em uma sala bastante ampla de uma casa, dizem ser mal assombrada")
  se misc:obj.var.i_corda
    t.addfim("Há um corredor a leste, uma passagem ao norte e janelas ao sul e, apesar")
    t.addfim("de ser bastante alto, você pode descer com a corda. Porém, a casa")
    t.addfim("está cercada de vampiros.")
  senao
    t.addfim("Há janelas mas é alto demais para pular. Há também um corredor a leste")
    t.addfim("e uma passagem ao norte.")
  fimse
  ret t.remove(100)

func entrou
  ver(arg0)

func cmd_s
  se jog.var.i_corda
    se jog.var.v_capa
      arg0.msg("Você amarrou a corda e começou a descer, mas apareceram vários vampiros")
      arg0.msg("e sugaram todo o seu sangue. Você não resistiu.")
      $mens.p(arg0)
      $mens.mvis2("", "$P foi para o sul")
      arg0.dono = $s_dentro001
      ret 1
    senao
      arg0.msg("Você amarrou a corda e começou a descer, mas apareceram vários vampiros.")
      arg0.msg("Felizmente nenhum deles notou a sua presença.")
    fimse
  fimse
# Sala de recuperação total

func segundo1
# se arg0.contleo < 700 && contleo < 700
  uint8 total
  recupera(arg0, arg0)
  enquanto total < config:animal1
    recupera(arg0, arg0.animal.[total])
    total += 1
  efim

func recupera
  arg1.pvida < arg1.pvidamax && arg0.msg("Recuperado")
  arg1.recuperar


classe s_dentro004
herda sala
const area_luta = 0
const descr = "Passagem\n\
Há uma lápide no centro dizendo 24 de junho de 2005\n\
Sala ampla ao sul e 3 salas do tempo ao norte"
const dir_s = $s_dentro003
const dir_n = $s_dentro005

func entrou
  se config:escolhenivel
    var.i_Capa = 10
    var.i_Óculos = 10
    var.i_Poção = 100
  fimse
  ver(arg0)

func entrou1
  se config:escolhenivel
    arg0.msg("Nesta versão de teste do jogo tecle aqui:")
  fimse

func entrou2
  se config:escolhenivel
    arg0.msg("A para aumentar um nível")
    arg0.msg("D para diminuir um nível")
    arg0.msg("AA para aumentar 10 níveis")
    arg0.msg("DD para diminuir 10 níveis")
  fimse

func cmd_escr
  se config:escolhenivel
    casovar txtmin(arg1)
    casose "aa"
      arg0.nível += 9
      arg0.recalc = 1
    casose "a"
      arg0.nível += 1
      arg0.recalc = 1
      se arg0.nivel > 80
        arg0.msg("Nível máximo é 80")
        arg0.nível = 80
      senao
        arg0.msg(arg0.nome + " é nível " + arg0.nivel)
      fimse
      ret 1
    casose "dd"
      arg0.nível -= 9
      arg0.recalc = 1
    casose "d"
      arg0.nível -= 1
      arg0.recalc = 1
      se arg0.nivel < 1
        arg0.msg("Nível mínimo é 1")
        arg0.nível = 1
      senao
        arg0.msg(arg0.nome + " é nível " + arg0.nivel)
      fimse
      ret 1
    casofim
  fimse


classe s_dentro005
herda sala
const area_luta = 0
const descr = "Sala misteriosa\n\
Você está em uma sala misteriosa. O chão e o teto parecem ser de pedra,\n\
mas não dá para ver direito. Tem uma névoa densa em todo lugar."
const dir_s = $s_dentro004
const dir_n = $s_dentro006
const dir_o = $s_dentro007
const dir_l = $s_dentro008

func entrou
  !dentro1.objini("p_dentro_sacerdote") && criar("p_dentro_sacerdote", este)
  arg0.var.contador_ = 0
  ver(arg0)

func entrou1
  arg0.msg("Sacerdote: Posso ver através das pessoas. Vejamos...")

func entrou4
  uint16 nivel
  uint16 n2
  int1 temfantasma
  uint8 total
  nivel = arg0.nivel
  enquanto total < config:animal1
    n2 = arg0.animal.[total].nivel
    nivel < n2 && (nivel = n2)
    "p_fantasma" == txt(arg0.animal.[total]) && (temfantasma = 1)
    total += 1
  efim
  se !arg0.var.l_hebraico
    se arg0.nivel < 4
      arg0.msg("Sacerdote: Procure uma sala de jantar e espere lá. Quando aparecer")
      arg0.msg("uma múmia, tecle chutar mumia ou socar mumia, só que chutar é mais")
      arg0.msg("forte. Depois, durante a luta, vai teclando apenas chutar ou socar.")
      arg0.msg("A espingarda pode ter suas utilidades, mas cuidado com as múmias.")
    senao arg0.nivel < 7
      arg0.msg("Sacerdote: Procure uma cozinha e mate as múmias de lá. Te darão")
      arg0.msg("mais experiência que as múmias da sala de jantar.")
    senao arg0.nivel < 10
      arg0.msg("Sacerdote: Pegue algumas poções. Vai te fazer bem. Quando estiver com")
      arg0.msg("poucos pontos de vida e não quiser ir até a sala ampla, tecle poção.")
      arg0.msg("Tem poção na cozinha. Vá até lá e tecle pegar poção")
    senao
      arg0.msg("Sacerdote: Procure pelo Catador de insetos. Você já tem capacidade")
      arg0.msg("para vencê-lo.")
    fimse
  senao !arg0.dentro1
    arg0.msg("Sacerdote: Capture pelo menos um animal. Ataque um animal até ele ficar")
    arg0.msg("com poucos pontos de vida, então tecle pegar")
    arg0.msg("Os animais que você pode pegar aparecem na sua agenda (tecle agenda)")
    arg0.msg("Experimente também agenda e o nome do animal.")
  senao !temfantasma
    arg0.msg("Sacerdote: Tenha sempre um fantasma com você. Ele é imune a socos e chutes.")
    arg0.msg("Se quiser pegar um terá de usar um de seus animais. Vá à oeste do corredor")
    arg0.msg("luxuoso, tecle um número de 1 a " + config:animal1 + " e tecle hab para saber que \
golpes pode usar")
  senao nivel < 23
    arg0.msg("Sacerdote: Você precisa ficar mais forte. Se desafiar o Catador de insetos")
    arg0.msg("agora, vai perder, mas pelo menos ganhará experiência se matar algum animal")
    arg0.msg("dele. Ao invés de pegar poção na cozinha,tente des poção em alguns lugares.")
  senao !arg0.var.i_chave
    arg0.msg("Sacerdote: Encontre e pegue uma chave. Existem duas salas de jantar, dois")
    arg0.msg("quartos e dois corredores entre eles. São tão parecidos que algumas pessoas")
    arg0.msg("não percebem isso.")
  senao !arg0.var.i_óculos
    arg0.msg("Sacerdote: Vá até o leopardo. Lá por perto tem óculos especiais, iguais aos")
    arg0.msg("meus. Com eles vai conseguir ver lugares e alguns jogadores, que estariam")
    arg0.msg("invisíveis de outra forma. Não precisa vestir. Só pegar.")
  senao !arg0.var.i_corda
    arg0.msg("Sacerdote: Há uma corda perto de um quarto. Você deve ter passado por lá")
    arg0.msg("várias vezes sem nunca ter notado. Depois de pegar tecle info corda")
    arg0.msg("É bom dar óculos a animais. Eles não perdem itens se você morrer.")
    arg0.msg("Escolha o animal e tecle trocar e o item. Para pegar de volta tecle trocar")
  senao !arg0.var.i_capa
    arg0.msg("Sacerdote: Existe um labirinto no segundo andar. Lá tem uma capa. Mas não")
    arg0.msg("se iluda. Precisei escrever um mapa. Coloquei um livro de um assunto em")
    arg0.msg("cada lugar.")
  senao nivel < 35
    arg0.msg("Sacerdote: Você e seus animais precisam ficar mais fortes. Enfrente")
    arg0.msg("o Catador de insetos ou o leopardo. Não vai resistir se descer no porão.")
  senao nivel < 40
    arg0.msg("Sacerdote: Você e seus animais ainda precisam ficar mais fortes. Enfrente")
    arg0.msg("o Catador de insetos ou o leopardo, ou então desça até o porão.")
  senao !arg0.var.o_gancho
    arg0.msg("Sacerdote: Você está bastante forte. O Catador de insetos não é páreo")
    arg0.msg("para você. Ajude o Eremita. Talvez você precise sair da casa.")
  senao nivel < 60
    arg0.msg("Sacerdote: Você está bastante forte. Vá até os dutos de ar e procure por")
    arg0.msg("um corredor. Lá perto tem leopardos mais fortes.")
  senao
    arg0.msg("Sacerdote: Não precisa mais da minha ajuda.")
  fimse


classe s_dentro009
herda sala
textovar ent
const area_luta = 0
const limpar = 300
const descr = "Corredor\n\
Você está em um corredor que se estende a leste. Há uma sala a oeste\n\
e duas portas, uma ao norte e outra ao sul."
const dir_l = $s_dentro010
const dir_o = $s_dentro003
const dir_n = $s_dentro016
const dir_s = $s_dentro017

func entrou
  ver(arg0)

func cmd_l
  se arg0.var.i_corda
    arg0.msg("A madeira está podre e você caiu. Felizmente conseguiu jogar uma corda")
    arg0.msg("a tempo e está descendo devagar. Tecle subir para voltar.")
  senao
    arg0.msg("A madeira está podre e desprovido de uma corda você caiu,")
    arg0.msg("mas não resistiu à queda.")
    arg0.dono = $s_dentro001
    ret 1
  fimse


classe s_dentro010
herda sala
const dir_c = $s_dentro009

func entrou
  arg0.msg("Descendo...")

func entrou2
  arg0.dono = $s_dentro011


classe s_dentro011
herda sala
const descr = "Porão\n\
A claridade mal chega aqui. Algumas teias negras de aranha embelezam\n\
o lugar. Na verdade não há nenhuma escada para cima, mas você consegue\n\
subir graças à uma corda."
const dir_n = $s_dentro012
const dir_l = $s_dentro013
const dir_s = $s_dentro014
const dir_c = $s_dentro009

func entrou
  ver(arg0)


classe s_dentro012
herda sala
const descr = "Quarto\n\
Você está em um quarto de despejo. Há muito entulho mas nada\n\
de valor. Está difícil ver mais alguma coisa nessa escuridão."
const dir_s = $s_dentro011

func entrou
  ver(arg0)

func segsala2
  se rand(100) < 30
    casovar numperso
    casose "0"
      criar("p_rato", este, 37, "h_roer,h_arranhatox,h_morder")
      ret
    casose "1"
      msg("Entulho 40 começou a se mexer")
      criar("p_entulho", este, 40, "h_surpreender,h_lançar,h_velocidade")
    casofim
  fimse


classe s_dentro013
herda sala
const descr = "Garagem\n\
É uma pequena e antiga garagem. Há uma carruagem, mas nenhum cavalo.\n\
A porta à leste está lacrada com concreto."
int1 travado # Se a porta à leste está lacrada com concreto

func entrou
  ver(arg0)
  msg("Concreto armado lacrou a porta à leste")
  travado = 1

func entrou10
  se !dentro1.objini("p_concreto_armado")
    criar("p_concreto_armado", este, 38, "h_surpreender, h_lançar")
    msg("Concreto armado 38 se desprendeu da porta")
    travado = 0
  fimse

func segsala15
  se rand(100) < 30 && numperso < 2
    se rand(2) == 0
      criar("p_morcego", 33, "h_bico,h_ventania,h_velocidade")
    senao
      criar(sala, rato, 37, "h_roer,h_arranhatox,h_morder")
    fimse
  fimse

const dir_o = $s_dentro011
# const dir_l = -entrar(Rua Estação Sumaré, 1)

func cmd_l
  se travado
    arg0.msg("A porta está lacrada com concreto")
    ret 1
  fimse


classe s_dentro014
herda sala
const descr = "Corredor\n\
Aqui é bastante alto, porém muito estreito. Você se movimenta devagar.\n\
Uma pessoa com claustrofobia certamente não passaria por aqui."
const dir_n = $s_dentro011
const dir_o = $s_dentro015

func entrou
  ver(arg0)


classe s_dentro015
herda sala
const descr = "Sala\n\
Uma sala bastante escura. Parece ser ter sido um quarto de revelação\n\
de fotografias. Melhor voltar à leste."

func segsala8
  se numperso < 4
    criar("p_rato", este, 38, "h_arranhar,h_morder")
  fimse

func entrou
  ver(arg0)

const dir_l = $s_dentro014


classe s_dentro016
herda sala
textovar ent
const area_luta = 0
const limpar = 300
const descr = misc:obj.var.i_chave ? descr1 + "\n" + descr2 : descr1
const descr1 = "Quarto\n\
Há um tapete persa no chão bastante gasto e sujo. Alguém deixou a cama\n\
desarrumada. Há também um ventilador no teto, mas nenhuma janela."
const descr2 = "Há uma passagem à oeste, visível apenas com os óculos especiais."
const dir_s = $s_dentro009
const dir_o = misc:obj.var.i_oculos ? $s_dentro031

func entrou
  var.i_Espingarda = 1
  ver(arg0)


classe s_dentro017
herda sala
textovar ent
const limpar = 300
const descr = "Sala de jantar\n\
Há muita comida servida na mesa, mas ninguém para comê-la. O aroma\n\
agradável indica que está fresca e foi feita há pouco tempo.\n\
Vários quadros antigos estão pendurados na parede."
const dir_n = $s_dentro009
const dir_s = $s_dentro018

func entrou
  ver(arg0)

func segsala15
  se numperso == 0
    criar("p_mumia", este, 1, "h_socar,h_chutar")
  fimse

func cmd_pegar
  casovar arg1
  casose "comida"
    arg0.msg("Você tenta pegar a comida mas quando se aproxima ela desaparece.")
    ret 1
  casose "quadro"
  casose "quadros"
    arg0.msg("Os quadros parecem estar pregados na parede.")
    ret 1
  casofim


classe s_dentro018
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Sala de estar\n\
Você está em uma pequena sala com mesa, poltronas e uma televisão. Pode\n\
ir para a cozinha à oeste ou a sala de jantar ao norte. Há também um\n\
corredor estreito para leste e uma escada para o andar de cima, ao sul."
const dir_o = $s_dentro019
const dir_n = $s_dentro017
const dir_l = $s_dentro025
const dir_s = $s_dentro032

func entrou
  ver(arg0)

func segsala15
  se rand(100) < 65
    criar("p_mumia", este, 2, "h_socar,h_chutar")
  fimse


classe s_dentro019
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Cozinha\n\
Aparentemente tudo está no seu devido lugar. Um fogão de 9 bocas,\n\
uma geladeira e vários armários. O armoma agradável indica que andaram\n\
limpando esse lugar recentemente. O estranho é não haver comida, nem\n\
na geladeira, nem nos armários. A sala de estar fica à leste e há também\n\
um quarto escuro ao sul."
const dir_l = $s_dentro018
const dir_s = $s_dentro020
const dir_c = misc:obj.var.o_gancho ? $s_duto002
const mostra_c = 0 # Esconde saída para cima

func segsala15
  se rand(100) < 65
    criar("p_mumia", este, 3, "h_socar,h_chutar")
  fimse

func entrou
  var.i_Poção = 1
  ver(arg0)

func cmd_s
  arg0.msg("Você sentiu tontura por um momento mas passou. Tudo ficou claro.")


classe s_dentro020
herda sala
textovar ent
const limpar = 300
const area_luta = 0
const descr = "Quarto\n\
Há um tapete persa no chão bastante gasto e sujo. Alguém deixou a cama\n\
desarrumada. Há também um ventilador no teto, mas nenhuma janela."
const dir_s = $s_dentro021

func entrou
  var.i_Espingarda = ""
  ver(arg0)


classe s_dentro021
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Corredor\n\
Você está em um corredor que se estende a leste. Há uma sala a oeste\n\
e duas portas, uma ao norte e outra ao sul."
const dir_o = $s_dentro003
const dir_n = $s_dentro020
const dir_s = $s_dentro022
const dir_l = $s_dentro023

func segsala15
  se rand(100) < 65
    criar("p_mumia", este, 4, "h_socar,h_chutar")
  fimse

func segsala22
  se rand(100) < 50
    criar("p_toupeira", este, 8, "h_lama,h_terremoto,h_arranhar")
  fimse

func entrou
  ver(arg0)


classe s_dentro022
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Sala de jantar\n\
Há muita comida servida na mesa, mas ninguém para comê-la. O aroma\n\
agradável indica que está fresca e foi feita há pouco tempo.\n\
Vários quadros antigos estão pendurados na parede."
const dir_n = $s_dentro021
const dir_s = $s_dentro018

func entrou
  ver(arg0)

func segsala22
  se rand(100) < 50
    criar("p_ima", este, 8, "h_choque,h_onda trovão,h_velocidade")
  fimse

func segsala15
  se rand(100) < 65
    criar("p_mumia", este, 5, "h_socar,h_chutar")
  fimse

func cmd_pegar
  casovar arg1
  casose "comida"
    msg(arg0.nome + " pegou um pouco de comida.")
    arg0.var.i_Comida = 1
    ret 1
  casose "quadro"
  casose "quadros"
    arg0.msg("Os quadros parecem estar pregados na parede.")
    ret 1
  casofim


classe s_dentro023
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Corredor\n\
Você está em um corredor que se estende a oeste. Há uma despensa a leste."
const dir_l = $s_dentro024
const dir_o = $s_dentro021

func entrou
  ver(arg0)

func segsala22
  se rand(100) < 50
    criar("p_lula", este, 8, "h_bolha,h_surf,h_cortar")
  fimse


classe s_dentro024
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Pequena despensa\n\
Aqui está completamente vazio. Não há janelas; apenas uma porta a oeste."
const dir_o = $s_dentro023

func entrou
  var.i_Chave = 1
  ver(arg0)

func segsala15
  se rand(100) < 65
    criar("p_mumia", este, 8, "h_socar,h_chutar")
  fimse

func segsala22
  se rand(100) < 50
    criar("p_morcego", este, 5, "h_bico,h_ventania,h_velocidade")
  fimse


classe s_dentro025
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Corredor\n\
Você está em um corredor bastante estreito e extenso.\n\
Há uma sala de estar a oeste e um corredor mais largo ao norte.\n\
Há uma grande fenda, na parede ao sul."
const dir_o = $s_dentro018
const dir_n = $s_dentro027
const dir_s = $s_dentro026

func entrou
  ver(arg0)

func segsala15
  se rand(100) < 65
    criar("p_mumia", este, 5, "h_socar,h_chutar")
  fimse


classe s_dentro026
herda sala
textovar ent
uint8 passo # Passo da batalha
ref escolhido # Jogador escolhido pelo Catador
ref animal.3 # Animais do catador de insetos
const limpar = 300
const descr = "Caverna\n\
Você está em uma caverna sombria e úmida. Goteiras pingam no chão.\n\
A saída é ao norte, a caverna se estende ao sul."
const dir_n = $s_dentro025
const dir_s = $s_lab018

func entrou
# Se o jogador estiver marcado como escolhido, deixa de ser o escolhido
  arg0 == escolhido && (escolhido = nulo)
# Cria o catador de insetos
  se !dentro1.objini("p_catador_de_insetos")
    criar("p_catador_de_insetos", este, 32)
    passo = 0, escolhido = nulo
  fimse
# Descrição da sala
  ver(arg0)

func segundo40
  arg0.pvida -= 5
  arg0.msg("Uma pedra de gelo cai do teto em você, " + arg0.pvida + " de " + arg0.pvidamax)

func segundo3
# Checa se o Catador de insetos está na sala
  se !dentro1.objini("p_catador_de_insetos")
    escolhido = nulo
    passo = 0
    ret
# Encerra batalha se jogador não estiver na sala
  senao escolhido && escolhido.dono != este
    escolhido = nulo
    passo = 0
  fimse
# Animal do catador acorda
  listaitem i
  ref r
  epara i = dentro1.ini, i, i.depois
    continuar i.obj.jog
    r = i.obj.objefeito("e_dormindo")
    continuar !r
    msg("Catador explode uma bomba perto de " + i.obj.nome)
    apagar(r)
  efim
# Catador de insetos desafia jogador
  se !escolhido && !arg0.var.l_hebraico
    escolhido = arg0
    passo = 1
    ret
  fimse
# Passos da batalha
  casovar passo
  casose "1"
    passo += 1
    msg("Catador de insetos: Eu desafio " + escolhido.nome + " para uma batalha")
    ret
  casose "2"
    passo += 1
    msg("Catador de insetos: Vamos ver se " + escolhido.nome + " pode com meus anões")
    msg("Catador de insetos escolhe Anão 8")
    criar("p_anao", este, 8, "h_socar,h_chutar,z_atk")
    ret
  casose "3"
    se numperso == 1
      passo += 1
      msg("Catador de insetos: Você não é tão mal assim. Até venceu meu anão mais fraco!")
    fimse
    ret
  casose "4"
    passo += 1
    msg("Catador de insetos: Mas não vai ganhar deste outro.")
    msg("Catador de insetos escolhe +Anão 10")
    criar("p_anao", este, 10, "h_chutar,z_atk")
    ret
  casose "5"
    se numperso == 1
      passo += 1
      msg("Catador de insetos: Você venceu meus capangas, desisto.")
      msg("Vou te ensinar hebráico. É isso que você quer, não é?")
    fimse
    ret
  casose "6"
    passo += 1
    msg("Assim poderá trocar itens e animais com quem souber hebráico")
    msg("bastando teclar dar e o número do animal ou nome do item.")
    ret
  casose "7"
    passo += 1
    msg("Para escolher para quem quer dar, tecle alvo e o nome da pessoa")
    msg(arg0.nome + " aprende hebráico")
    arg0.var.l_hebráico = 1
    ret
  casose "8"
    passo += 1
    msg("Catador de insetos: cheque seus idiomas teclando idioma")
    msg("Quando quiser me desafiar novamente tecle catador de insetos")
    ret
  casose "9"
    passo += 1
    msg("Catador de insetos: Mas agora vá até a biblioteca. Vai te fazer bem.")
    escolhido = nulo
    ret
  casose "11"
    passo += 1
    msg("Catador de insetos escolhe +Lula 16 e +Minhocão 14")
    criar("p_lula", este, 16, "h_bolha,h_tóxico,h_morder", contador)
    criar("p_minhocao", este, 14, "h_pedrada,h_pedregulho,h_tóxico,z_atk")
    ret
  casose "12"
    se numperso == 1
      passo += 1
      msg("Catador de insetos escolhe +Ímã 23")
      criar("p_ima", este, 23, "h_choque,h_onda trovão,h_arranhar,z_atk")
    fimse
    ret
  casose "13"
    se numperso == 1
      passo += 1
      msg("Catador de insetos escolhe +Lagarchama 29")
      criar("p_lagarchama", este, 29, "h_chama,h_brasa,h_arranhar,z_atk")
    fimse
    ret
  casose "14"
    se numperso == 1
      passo += 1
      msg("Catador de insetos escolhe +Pinguim 37")
      criar("p_pinguim", este, 37, "h_arranhar,h_neve,h_aurora,z_atk")
    fimse
    ret
  casose "15"
    se numperso == 1
      passo += 1
      msg("Catador de insetos: Parabéns! Você provou que vale alguma coisa")
    fimse
    ret
  casose "16"
    passo += 1
    msg("Catador de insetos: Desta vez não tenho nada a oferecer")
    msg("Mas pode me desafiar sempre que quiser")
    ret
  casose "17"
    passo += 1
    msg("Você sabia que a partir do nível 10 você e qualquer outro ser conseguem")
    msg("se lembrar das habilidades sempre que acordarem naturalmente")
    ret
  casose "18"
    passo += 1
    msg("Catador de insetos: Não, claro que não sabia!")
    ret
  casose "19"
    passo += 1
    msg("Mas é uma pena que você não vai sair vivo daqui")
    ret
  casose "20"
    passo += 1
# passo = 0
    ret
  casofim

func segundo1
  uint16 novonivel
  ref r
  r = dentro1.objini("p_catador_de_insetos")
# Checa se tem catador de insetos
  se !r
    ret
# Checa se catador está lutando
  senao passo < 20
    r.recuperar
    ret
  fimse
# Catador está lutando
  r.var.h_toxico = 1
  r.var.h_onda_trovao = 1
  se r.nivel < 40 && r.pvida < r.pvidamax * 30 / 100
    novonivel = r.nivel + 2
  senao r.e_paralisado || r.e_dormindo || r.e_envenenado
    novonivel = r.nivel
  fimse
  se novonivel
    msg(r.nome + " usa poção e se cura")
    r.nivel = novonivel
    r.morreu(1) # Apenas dar experiência, não apaga o personagem
    r.recuperar
    msg(r.nome + " é nível " + r.nivel)
  fimse

func cmd_escr # Jogador desafia Catador de insetos
  se arg1 == "catador de insetos"
    se !dentro1.objini("p_catador_de_insetos")
      arg0.msg("Catador de insetos não está aqui")
    senao escolhido
      arg0.msg("Catador de insetos: Estou ocupado, espere um pouco até a minha vitória")
    senao
      msg("Catador de insetos: Eu aceito seu desafio, " + arg0.nome)
      msg("Meus animais modificados geneticamente vão te esfolar vivo")
      escolhido = arg0
      passo = 11
    fimse
    ret 1
  fimse

func cmd_ataque
  se arg0.jog && arg1.jog # Jogador não ataca jogador
# arg0.msg("Catador de insetos: Não pode atacar outros jogadores aqui")
    ret 1
  senao !arg0.jog && arg1 != escolhido # Não jogador só ataca jogador escolhido
    ret 1
  fimse

func cmd_n
  se dentro1.objini("p_catador_de_insetos")
    se arg0 == escolhido
      arg0.msg("Catador de insetos: Não vou deixar você fugir da briga, covarde")
      ret 1
    senao !arg0.var.l_hebraico
      arg0.msg("Catador de insetos: Não pode ir embora antes da nossa batalha")
      ret 1
    fimse
  fimse

func cmd_s
  se dentro1.objini("p_catador_de_insetos")
    se arg0 == escolhido
      arg0.msg("Catador de insetos: Não vou deixar você fugir da briga, covarde")
      ret 1
    senao arg0.animal.0.nivel >= 40
      ret 0
    senao
      arg0.msg("Catador de insetos: Não vou deixar que entre na caverna. É perigoso.")
      arg0.msg("Seu primeiro animal precisa ser pelo menos nível 40.")
      ret 1
    fimse
  fimse


classe s_dentro027
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Corredor\n\
O corredor se estende a oeste. Mais adiante há uma sala bastante ampla.\n\
À leste uma escada leva para o andar de baixo."
const dir_s = $s_dentro025
const dir_l = $s_dentro028
const dir_o = $s_dentro010
const dir_c = misc:obj.var.o_gancho ? $s_duto003
const mostra_c = 0 # Esconde saída para cima

func entrou
  ver(arg0)

func segsala15
  casovar rand(4)
  casose "0"
    criar("p_mumia", este, 12, "h_socar,h_chutar")
    sair
  casose "1"
    criar("p_anao", este, 10, "h_socar,h_chutar")
    sair
  casose "2"
    criar("p_mumia", este, 8, "h_socar,h_chutar")
    sair
  casose "3"
    criar("p_anao", este, 6, "h_socar,h_chutar")
  casofim

func segsala22
  se rand(100) < 50
    criar("p_lagarchama", este, 8, "h_chama,h_brasa,h_cortar,h_arranhar")
  fimse

func cmd_o
  se arg0.var.i_corda
    arg0.msg("A madeira está podre e você caiu. Felizmente conseguiu jogar uma corda")
    arg0.msg("a tempo e está descendo devagar. Tecle subir para voltar.")
  senao
    arg0.msg("A madeira está podre e desprovido de uma corda você caiu,")
    arg0.msg("mas não resistiu à queda.")
    arg0.dono = $s_dentro001
    ret 1
  fimse


classe s_dentro028
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Sala de estar\n\
Há uma escada à oeste e uma porta ao sul. Costuma haver um leopardo\n\
faminto aqui."
const dir_o = $s_dentro027
const dir_s = $s_dentro029

func entrou
  ver(arg0)

func entrou1
  se !dentro1.objini("p_leopardo")
    se arg0.contleo < 700
      criar("p_leopardo", este, 20, "h_arranhar,h_morder")
    senao
      criar("p_leopardo", este, 20, "h_morder,z_sinistro")
    fimse
  fimse

func entrou2
  ref r
  r = dentro1.objini("p_leopardo")
  se arg0.var.i_comida && r
    arg0.var.i_comida = ""
    r.esperar(150)
    arg0.msg("Você largou a comida na sala e o leopardo está comendo.")
  fimse


classe s_dentro029
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = misc:obj.var.i_chave ? descr2 : descr1
const descr1 = "Corredor\n\
Há um portão no meio, trancado, interrompendo a passagem.\n\
Você precisa de uma chave para passar."
const descr2 = "Corredor\n\
Há um portão no meio, trancado, interrompendo a passagem.\n\
Felizmente você tem a chave para abrir, e por isso consegue passsar."
const dir_n = $s_dentro028
const dir_s = misc:obj.var.i_chave ? $s_dentro030
const dir_c = misc:obj.var.i_ganco ? $s_duto008
const mostra_c = 0 # Esconde saída para cima

func entrou
  ver(arg0)

func segsala22
  se rand(100) < 50
    criar("p_kickboxer", este, 8, "h_socar,h_chutar,h_multissoco,h_cortar")
  fimse


classe s_dentro030
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Despensa\nHá um pouco de entulho aqui, mas nada que se aproveite."
const dir_n = $s_dentro029

func entrou
  var.i_Óculos = 1
  ver(arg0)

func segsala22
  se rand(100) < 50
    criar("p_pinguim", este, 8, "h_neve,h_aurora,h_cortar,h_arranhar")
  fimse

func cmd_n
  se !arg0.var.i_chave
    arg0.msg("Você precisa da chave para sair.")
    ret 1
  fimse


classe s_dentro031
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Despensa\n\
Você está em uma despensa secreta. Há apenas uma saída, à leste."
const dir_l = $s_dentro016
const dir_c = misc:obj.var.o_gancho ? $s_duto001
const mostra_c = 0 # Esconde saída para cima

func entrou
  var.i_Corda = 1
  ver(arg0)

func segsala15
  criar("p_cogumelo", este, 8, "h_dreno,h_onda solar,h_cortar")


classe s_dentro032
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Corredor\n\
Aqui é grande e luxuoso. Há uma uma escada para baixo ao norte, uma sala\n\
de reunião ao sul, uma biblioteca à leste e um corredor menor a oeste."
const dir_n = $s_dentro018
const dir_s = $s_dentro033
const dir_l = $s_dentro035
const dir_o = $s_lab001

func entrou
  ver(arg0)

func segsala12
  se rand(100) < 50
    criar("p_minhocao", este, 6, "h_pedrada,h_pedregulho,h_explodir,h_cortar")
  fimse


classe s_dentro033
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = "Sala de reuniões.\n\
Há várias janelas, e a vista é bonita. Dá para ver até o monte Chiano.\n\
Mas não dá para sair pela janela. Há uma biblioteca à leste, um escritório\n\
ao sul, e dois corredores ao norte e à oeste."
const dir_n = $s_dentro032
const dir_s = $s_dentro034
const dir_l = $s_dentro035
const dir_o = $s_lab003

func entrou
  ver(arg0)

func segsala22
  se rand(100) < 50
    criar("p_borboleta", este, 8, "h_picar,h_morder,h_cortar")
  fimse


classe s_dentro034
herda sala
textovar ent
const limpar = 300
const area_mumia = 1
const descr = misc:obj.var.i_oculos ? descr2 : descr1
const descr1 = "Escritório\n\
Aqui são guardos os temas das últimas reuniões em um quadro de avisos.\n\
Mas não tem nada de importante."
const descr2 = "Escritório\n\
Aqui são guardos os temas das últimas reuniões em um quadro de avisos.\n\
Há também uma nota, visível apenas com os óculos que você está usando.\n\
Tecle ler para ler."
const dir_n = $s_dentro033
const dir_c = misc:obj.var.o_gancho ? $s_duto007
const mostra_c = 0 # Esconde saída para cima

func entrou
  ver(arg0)

func segsala22
  se rand(100) < 50
    criar("p_balao", este, 10, "h_cantar,h_cortar")
  fimse

func cmd_escr
  se arg1 == "ler" && arg0.var.i_oculos
    arg0.msg("O labirinto perto da biblioteca é muito perigoso e também difícil, porque")
    arg0.msg("todas as salas são iguais. São poucas salas mas você pode andar em círculo")
    arg0.msg("durante horas. Apesar disso tudo, ele esconde um artefato raro em uma")
    arg0.msg("despensa. Espero que algum dia alguém faça um mapa...")
    ret 1
  fimse


classe s_dentro035
herda sala
textovar ent
const limpar = 300
const area_luta = 0
const dir_s = $s_dentro033
const dir_o = $s_dentro032
const dir_c = misc:obj.var.o_gancho ? $s_duto006
const mostra_c = 0 # Esconde saída para cima

func descr
  textotxt t
  t.addfim("Biblioteca")
  t.addfim("Embora tenha uma placa escrito \"Biblioteca\" aqui, há apenas uma pequena")
  t.addfim("estante de livros no centro da sala. Aparentemente aqui nunca foi uma")
  t.addfim("biblioteca. Pode-se pegar qualquer livro pelo assunto. Confira teclando sala")
  se misc:obj.var.l_hebraico
    t.addfim("Aqui na biblioteca tecle salvar para salvar o jogo.")
    t.addfim("Uma vez salvo tecle salvar novamente só quando quiser mudar a senha.")
  fimse
  ret t.remove(100)

func entrou
  cria_livros
  ver(arg0)

func cmd_escr
  se arg1 == "salvar" && arg0.var.l_hebraico
    arg0.mudasenha
    ret 1
  senao
    livros
  fimse

func cria_livros
  var.i_Livro_História = 5
  var.i_Livro_Geografia = 5
  var.i_Livro_Português = 5
  var.i_Livro_Matemática = 5
  var.i_Livro_Física = 5
  var.i_Livro_Química = 5
  var.i_Livro_Biologia = 5
  var.i_Livro_Herbologia = 5
  var.i_Livro_Fenomenologia = 5
  var.i_Livro_Monstros = 5
