classe efeito
# Efeitos que afetam os personagens
sav listaitem idono # Uso interno: personagem afetao pelo efeito
const dono = idono.objlista
# func ini1 # Efeito foi criado
# func ini2 # Efeito foi reforçado
# func ataquerec # Personagem recebeu ataque
# func recalc # Atualizando status (ataque, defesa, velocidade, etc).

func mens # Evia mensagem
# arg0 = mensagem; na mensagem $D=nome completo do personagem
  ref r
  r = idono.objlista.dono
  se r.perso # Se pertence a alguém
    $mens.p(r.dono, r)
  senao # Se não pertence a ninguém
    $mens.p(r, r)
  fimse
  $mens.mvis1(arg0)


classe e_confuso
herda efeito
const nome = " confuso"
inttempo t1 # Para perder pontos de vida
inttempo t2 # Quanto tempo dura o efeito
const ini1 = ini2 # Efeito foi criado

func ini1
  t1 = 10, t2 = 1000

func t1_exec
  t1 = 10

func t2_exec
  apagar(este)
  mens("$D não está mais confuso")


classe e_congelado
herda efeito
const nome = " congelado"
inttempo t1 # Tempo para descongelar
const ini1 = t1 = 1000
const ini2 = t1 = 1000

func cmd_escr
  se arg2.comando_mover
    arg0.msg("Você está congelado!")
    ret 1
  fimse

func cmd_ataque
  arg0.msg("Não pode atacar congelado")
  ret 1

func t1_exec
  apagar(este)
  mens("$D não está mais congelado")


classe e_dormindo
herda efeito
const nome = " dormindo"
inttempo t1 # Tempo para acordar
const ini1 = t1 = 1800
const ini2 = t1 = 1800

func cmd_escr
  se arg2.comando_mover
    arg0.msg("Você está dormindo")
    ret 1
  fimse

func cmd_ataque
  arg0.msg("Não pode atacar dormindo")
  ret 1

func ataquerec # Recebeu ataque: acorda mais rápido
  t1 = intpos(t1 - 250) + 1

func t1_exec
  apagar(este)
  dono.acordou # Indica que acordou naturalmente
  mens("$D acordou")


classe e_paralisado
herda efeito
const nome = " paralisado"

func ataqueexec
  esperar(50)


classe e_envenenado
herda efeito
const nome = " envenenado"
inttempo t1 # Para perder pontos de vida
inttempo t2 # Para avisar que está envenenado
const ini1 = ini2 # Efeito foi criado

func ini2 # Efeito foi reforçado
  t1 = 20, t2 = 100

func t1_exec
  t1 = 20
  pvida -= 1
  pvida <= 0 && morreu

func t2_exec
  t2 = 100
  mens("$D está envenenado")


classe e_queimando
herda efeito
const nome = " queimando"
inttempo t1 # Para perder pontos de vida
inttempo t2 # Para avisar que está envenenado
const ini1 = ini2 # Efeito foi criado

func ini2 # Efeito foi reforçado
  t1 = 20, t2 = 100

func t1_exec
  t1 = 20
  pvida -= 1
  pvida <= 0 && morreu

func t2_exec
  t2 = 100
  dono.msg("Você está queimando")
